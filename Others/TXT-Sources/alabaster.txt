"Alabaster" by "John Cater, Rob Dubbin, Eric Eve, Elizabeth Heller, Jayzee, Kazuki Mishima, Sarah Morayati, Mark Musante, Emily Short, Adam Thornton, and Ziv Wities".
 
Use no scoring.
Use American dialect.
Use the serial comma. 

Use MAX_DICT_ENTRIES of 2000.
Use MAX_STATIC_DATA of 500000.
Use MAX_PROP_TABLE_SIZE of 400000.
Use MAX_OBJECTS of 750. 


Part 0 - Introduction

Release along with cover art, source text, a file of "Conversation Diagram" called "ConversationGraph.pdf", a file of "Plot Structure" called "Choices.pdf ", a file of "Walkthroughs" called "Walkthroughs.pdf", and an "Alabaster" website.

The release number is 1.

Include Plurality by Emily Short.

The story headline is "An Interactive Experiment".
The story genre is "Fairy Tale".
The story description is "The Queen has told you to return with her heart in a box. Snow White has made you promise to make other arrangements. Now that you're alone in the forest, it's hard to know which of the two women to trust. The Queen is certainly a witch — but her stepdaughter may be something even more horrible.

Alabaster is an experiment in open authorship, using a built-in conversation-construction system to allow people to contribute their own content. For more information on the development, see the project blog at http://emshort.wordpress.com/?s=alabaster."
The story creation year is 2008. 

When play begins:
	say "It is the bitterest night you can remember since the dwarrows last marched against men. The sky is too cold, the village too still. There is smoke in the air but no voices from the doorways. The Queen's light burns in the tower window behind you. She is watching, as far as she can.
	
You and the girl move through the narrow lanes without speaking, past the muted inns and shuttered bakeries, until you reach the narrowing path beyond the churchyard. 

Pass in silence the graves of veterans and goodwives, and then at the outskirts the graves of suicides, which are granted no markers. Come to the place where the village ends and the trees begin to grow.

Enter the forest like two spies, one sent to kill the other.

Walk for hours.";
	wait for any key.



Part 1 - Help 

Chapter 1 - Instructions

Include Basic Help Menu by Emily Short.

When play begins:
		choose row 1 in Table of Basic Help Options;
		blank out the whole row;
		change the title entry to "About Alabaster";
		change the description entry to "[about text]";
		choose row 2 in the Table of Basic Help Options;
		blank out the whole row;
		change the title entry to "Instructions for Conversation";
		change the description entry to "[hint-text]"; 


To say about text:
	say "Alabaster is the story of Snow White -- after a fashion. It is up to you to come to your own understanding of what has happened, and to decide what to do next. There are some eighteen possible outcomes to the story. Some of these are considerably happier than others.[paragraph break]";
	say "The hook for Alabaster was written without any knowledge of Neil Gaiman's 'Snow, Glass, Apples'. That story is well worth a read, but was not an inspiration for this one."

To say hint-text:
	say "Much of Alabaster is about conversation. Sometimes your character will have thoughts about where the conversation should go next; in that case, you can type your choice from the listed options in order to move the conversation on.[paragraph break]";
	say "You can also ASK ABOUT any topic, SHOW objects to Snow White, and TELL Snow White ABOUT things. These commands can be further abbreviated as A (ASK ABOUT) and T (TELL ABOUT). 

YES, NO, and MAYBE may also come in handy when you've been asked a question. TOPICS may list some things that you can still think of to say, and THINK will review the facts that you've learned thus far in the game.";


Table of Basic Help Options (continued)
title	subtable	description	
"Instructions for Physical Interaction"	a table-name	"[phys desc]"
"Stopping, Restarting, and Changing the Display"	--	"[endings desc]"
"About the Writing"	--	"[collab desc]"
"About the Illustrations"	--	"[graphics desc]"
"Credits"	--	"[credit list]"
"License"	--	"[license desc]"

To say phys desc:
	say "You will find sometimes that you may want to EXAMINE objects in your surroundings (X for short, as in X TREES); TAKE or DROP things; or CUT, HIT, or BURN objects. LOOK will show you what is around you once again. Other verbs are also known to the game, so it's worth experimenting if there is something else you'd like to try.

If you would like hints each turn about what to do next, you can always type TUTORIAL ON (and turn this mode off again with TUTORIAL OFF).";

To say endings desc:
	say "There are a few simple commands for controlling the game itself. These are: [paragraph break]SAVE saves a snapshot of the game as it is now. [paragraph break]RESTORE puts the game back to a previous saved state. You may keep as many saved games as you like. [paragraph break]RESTART puts the game back to the way it was at the beginning. [paragraph break]QUIT ends the game.

A final note: if you don't care for the way Alabaster is displaying, check your preferences menu. On most operating systems it is possible to adjust the text and background colors. In some it's also possible to add or remove a vertical bar between the graphics and text of the game, or to adjust the font size and margins. While we put some care into Alabaster's appearance, we want the screen to be comfortable to read -- so please adjust it as needed." 

To say collab desc:
	say "Alabaster is the result of an experiment in open authorship.[paragraph break]";
	say "Emily Short wrote and released the introduction to the story; John Cater, Rob Dubbin, Eric Eve, Elizabeth Heller, Jayzee, Kazuki Mishima, Sarah Morayati, Mark Musante, Adam Thornton, and Ziv Wities then contributed conversation text. Every day or two, the game was recompiled and rereleased with waves of new material, allowing contributors to draw on one another's inventions. This period of writing took place around Halloween 2008.

Afterward, the game received further editing to improve continuity and conversation flow, and assorted testing and improvements to the environment. The plot that resulted is thus no single person's invention. (Those interested in the development process will find the whole thing charted in blog entries at http://emshort.wordpress.com/?s=alabaster , back to the initial request for participants.)";

To say graphics desc:
	say "The graphics are also an experiment, this time in procedural illustration. Daniel Allington-Krzysztofiak provided the original idea, the drawing elements, design input, and critique. Emily Short supplied code to place these elements in a collage representing diverse aspects of the game state. 

While some juxtapositions were precisely engineered, others are the result of the particular route the player has taken through the game, and are likely to be unique. Considered singly, the resulting images might be considered found art -- or perhaps the work of the game itself.";

To say credit list:
	say "[second custom style]Design and Code:[roman type][paragraph break]Emily Short[paragraph break]";
	say "[second custom style]Content:[roman type][paragraph break]John Cater[line break]Rob Dubbin[line break]Eric Eve[line break]Elizabeth Heller[line break]Jayzee[line break]Kazuki Mishima[line break]Sarah Morayati[line break]Mark Musante[line break]Emily Short[line break]Adam Thornton[line break]Ziv Wities[paragraph break]";
	say "[second custom style]Illustrations:[roman type][paragraph break]Daniel Allington-Krzysztofiak[paragraph break]";
	say "[second custom style]Alpha Testing:[roman type][paragraph break]Eric Eve[line break]Andrew Hickey[line break]Michael Tenuis[paragraph break]";
	say "[second custom style]Beta Testing:[roman type][paragraph break]Daniel Allington-Krzysztofiak[line break]Sam Kabo Ashwell[line break]Jason Dyer[line break]Eric Eve[line break]Kyle McCowin[line break]Sarah Morayati[line break]Mark Musante[line break]Jason Novak[line break]Greg Pallis[line break]Erik Temple[paragraph break]";
	say "[second custom style]Inform language and compilers:[roman type][paragraph break]Graham Nelson[line break]Andrew Hunter[line break]David Kinder[line break]Adam Thornton[line break]Philip Chimento[paragraph break]";
	say "[second custom style]Special Thanks:[roman type][paragraph break]David Kinder for his work on accelerated Glulx performance and improved interpreters.[paragraph break]";
	say "[second custom style]Extensions:[roman type][paragraph break][list of extension credits]".
	
To say license desc:
	say "Alabaster is released under the Creative Commons Attribution-NonCommercial-No Derivative Works 3.0 United States License. 
	
Simply, that means that you are free to distribute copies of Alabaster as long as you do not remove the work's built-in attribution; do not use the work for commercial purposes; and do not alter, transform, or build on the work. You may charge a reasonable fee for medium and postage if tranferring the work to someone else."

Understand the commands "credits" and "settings" and "license" and "licence" as "help".

Chapter 2 - Banner

Include Extended Banner by Stephen Granade.

Rule for printing the banner text:
     say "[bold type][story title][roman type][line break][extended story headline][if story author is not blank] by [story author][end if][line break]Illustrations by Daniel Allington-Krzysztofiak[line break]Release [release number] / Serial number [story serial number] / Inform 7 build [I7 version number][line break]" instead.

The story rights statement is "Released under the Creative Commons Attribution-NonCommercial-No Derivative Works 3.0 United States License".


Part 2 - Conversation System Preliminaries

[The conversation builder system takes care of many of these tasks, but I've rewritten a few of the rules to make them more clear to people who aren't familiar with or using the full extension, so that means redoing things that most authors would be able to leave well enough alone in the extension.]

Include Threaded Conversation by Emily Short. 
 
Understand "topics" as changing the subject.

The standard report other subjects rule is not listed in any rulebook. [By default, this rule lists available quips; but we can have so many available at once that it may be better just to list subjects that are related to available quips. That also reduces the risk of giving away cool stuff before its time.]

A thing can be flagged-topical or flagged-untopical. A thing is usually flagged-untopical.

Check changing the subject when the current quip is restrictive:
	if every flagged-ready quip in the quip-repository is binary-answering:
		say "[The current interlocutor] seem[s] to be expecting either yes or no. " instead;
	otherwise:
		say "The only reasonable responses you can think of are these: [list of flagged-ready quips that directly-follow the current quip]. " instead.
		
Definition: a quip is binary-answering if it is a negative quip or it is an affirmative quip.

Definition: a thing is subject-relevant if it is mentioned by the current quip.
Definition: a thing is referable:
	 if it is flagged-topical and it is subject-relevant:
		yes;
	if allowing irrelevance is true and it is flagged-topical:
		yes;
	no.
		
Total referables is a number that varies.
Allowing irrelevance is a truth state that varies. Allowing irrelevance is false.

Check changing the subject:
	repeat with item running through listed flagged-ready quips in the quip-repository:
		now every thing which is mentioned by the item is flagged-topical;
	if nothing is flagged-topical:
		say "You are out of ideas." instead.

Carry out changing the subject:
	if a flagged-topical thing is subject-relevant: 
		change allowing irrelevance to false; [now we divide "referables" into two groups, the relevant and the irrelevant; the meaning of "referable" changes depending on whether we are allowing irrelevant items to be included.]
		change total referables to the number of referable things;
		say "You could discuss ";
		pick referable patterns;
		if something is referable:
			say "[the list of referable things]";
		now every referable thing is flagged-untopical;
		if something is flagged-topical:
			change allowing irrelevance to true;
			change total referables to the number of referable things;
			if total referables is greater than 2: 
				say ".[paragraph break]Alternatively, you could change the subject to ";
			otherwise:
				say ", or change the subject to ";
			pick referable patterns;
			if something is referable:
				say "[the list of referable things]";
		say ".";
	otherwise:
		change allowing irrelevance to true;
		change total referables to the number of referable things;
		say "You could discuss ";
		pick referable patterns;
		if something is referable:
			say "[the list of referable things].";
	now every thing is flagged-untopical. [* This could be done in fewer lines but would take longer to run.]

[This is so that we can arrange the topic list into more attractive full paragraphs with related ideas clustered together. This affects not only the order of the topic list with related ideas grouped, but also the language rhythms.]
To pick referable patterns:
	let phrase length be 1;
	if the player knows possible-blood-drinking and Snow White is referable and vampirism is referable:
		say "[Snow White] or (daringly) [vampirism]";
		now phrase length is 2;
		now Snow White is not flagged-topical;
		now vampirism is not flagged-topical;
		if blood is referable:
			say " or (coyly) [blood]";
			now blood is not flagged-topical;
			now phrase length is 3;
		punctuate;
	if Snow White is referable:
		say "[Snow White] herself";
		now phrase length is 1;
		now Snow White is not flagged-topical;
		punctuate;
	if the heart-sized box is referable and the symbols are referable:
		if the corpse is referable and the hart's heart is referable:
			say "[the heart-sized box], [the hart's heart] meant to go in it, the corpse that heart belonged to, the symbols on the box surface";
			now the corpse is flagged-untopical;
			now the hart's heart is flagged-untopical; 
			now phrase length is 1;
		otherwise if phrase length is 2:
			say "the symbols on the box you're carrying for the Queen, the box itself";
			now phrase length is 1;
		otherwise:
			say "[the heart-sized box] or the symbols on it";
			now phrase length is 2;
		now the heart-sized box is not flagged-topical;
		now the symbols are not flagged-topical;
		punctuate;
	if the corpse is referable and the hart's heart is referable:
		say "[the corpse] and its heart";
		now the corpse is flagged-untopical;
		now the hart's heart is flagged-untopical; 
		now phrase length is 2;
	if the magic mirror is referable and the queen is referable:
		say "[the magic mirror] or [the queen] herself";
		now phrase length is 2;
		now the magic mirror is not flagged-topical;
		now the queen is not flagged-topical;
		punctuate;
	if the king is referable and the queen is referable:
		say "[the King] or [the queen]";
		now phrase length is 2;
		now the queen is not flagged-topical;
		now the king is not flagged-topical;
		punctuate;
	if vampirism is referable and witchcraft is referable:
		say "[vampirism] and [witchcraft]";
		now phrase length is 2;
		now vampirism is not flagged-topical;
		now witchcraft is not flagged-topical;
		punctuate;
	if vampirism is referable:
		say "[vampirism]";
		now phrase length is 1;
		if blood is referable:
			say " (or just blood)";
			now phrase length is 1;
			now blood is not flagged-topical;
		now vampirism is not flagged-topical;
		punctuate;
	if safe haven is referable:
		say "[a safe haven]";
		now phrase length is 1;
		if deal is referable:
			say " and [the deal]";
			now phrase length is 2;
			now deal is not flagged-topical;
		now safe haven is not flagged-topical;
		punctuate;
	if death is referable: 
		if murder is referable:
			say "death (abstract) or murder (specific)";
			now phrase length is 2;
			now deal is not flagged-topical;
		otherwise:
			say "death";
			now phrase length is 1;
			now death is not flagged-topical;
		punctuate;
	if the number of referable things is 2:
		let A be a random referable thing;
		let B be a random referable thing which is not A;
		say "[the A], or [the B]";
		now A is not flagged-topical;
		now B is not flagged-topical.
		

To punctuate:
	if the number of referable things is greater than 1:
		say ", ";
	if the number of referable things is 1:
		if total referables is 2:
			say " or ";
		otherwise:
			say ", or ";


Chapter 1 - Listing Quips

A quip can be listed or unlisted.

A plausibility rule for an unlisted quip (this is the don't suggest unlisted quips rule):
	it is implausible.
	
[First plausibility rule for a quip (called target) when the current quip is dead-ended (this is the limit top-level suggestions rule):
	if the target mentions something which is mentioned by the current quip:
		make no decision;
	it is dubious.] [* This kind of sort of worked, but was scattershot and (worse) slow.]

A plausibility rule for a shallowly-buried quip (called target) (this is the pruning mid-level suggestions rule):
	if a quip indirectly-follows the current quip:
		it is dubious; 
	if at least three quips indirectly-follow the previous quip:
		it is dubious;
	unless the current quip is dead-ended and the target mentions something which is mentioned by the current quip: 
		it is dubious.[* These determine what options are listed to the player. As people create increasingly "bushy" conversation trees, sometimes these suggestions are becoming a bit long-winded, so I want to reduce the diversity of the suggestions for this particular game.]

Rule for listing plausible quips when the current quip is dead-ended:
	prepare a list of plausible quips; 
	if the number of filled rows in the Table of Scored Listing is 0:
		rule succeeds;
	if the number of filled rows in the Table of Scored Listing is greater than 4:
		prepare a list of flagged-ready quips which mention every subject-relevant thing;
		if the number of filled rows in the Table of Scored Listing is greater than 4 or the number of filled rows in the Table of Scored Listing is less than 1:
			repeat with item running through listed flagged-ready quips in the quip-repository:
				now every thing which is mentioned by the item is flagged-topical;
			if a flagged-topical thing is subject-relevant:
				prepare a list of subject-relevant flagged-topical things;
				say "[one of]You could discuss[or]The conversation could turn to[at random] [the prepared list delimited in disjunctive style].";
			now every thing is flagged-untopical; [* This will significantly reduce and tidy lists of ideas when there are a large number of plausible related quips; it's like the changing the subject action, but narrower.]
			rule succeeds;
	repeat through the Table of Scored Listing:
		carry out the quip-introducing activity with the output entry;
	[This could have blanked some rows out, so we want to check again:]
	if the number of filled rows in the Table of Scored Listing is greater than 0, 
		say "[quip-suggestion-phrase][the prepared list delimited in disjunctive style].";

Chapter 2 - Quip-Building Questions

[There are several ways of achieving this effect; the current demo is trying out one of them to see how well it works.]
	
[The read all performative quips rule is not listed in any rulebook. The vague asking rule is not listed in any rulebook. The vague telling rule is not listed in any rulebook.

The clean conversation creation rule is not listed in any rulebook.]

[The new source examination rule is listed instead of the source examination rule in the carry out looking at new source rules.

This is the new source examination rule:
	say "Conversation saved to NewConversation.glkdata."]

[Understand "purge file" as purging file.

Purging file is an action out of world.

Carry out purging file:
	purge file called "NewConversation". [* This is really only likely to be useful to me, because it's helpful to be able to wipe the text file from inside the IDE and start over.]

To say implemented:
	say "implemented";
	if the current quip is restrictive:
		say " (or else it is unavailable from here -- the current quip is restrictive and does not allow access to most other quips[unless a quip indirectly-follows the current quip]. [italic type]You've been left by your collaborators with no implemented followups to the current situation. You can either write new dialogue to follow on this strand, or type NO here and UNDO a few turns until you are free of this branch[roman type][end unless])";

Rule for printing a parser error (this is the read new speech acts rule):
	if player-mode is true, make no decision;
	if the parser error is I beg your pardon, make no decision;
	if the current interlocutor is nothing, make no decision;
	change the variable snippet to the player's command; 
	store base quip text;
	say "That speech act is not [implemented]. Draft a new one? [length warning]>";
	if the player consents
	begin; 
		change the currently open reference to the reference after opening file "NewConversation" for writing;
		say "	[message of the sample-quip]";
		close file "NewConversation" for writing; 
		write " is a performative quip." after file "NewConversation";
		if nominal-sample-quip is switched on, escape troubled quip-names;
		carry out the filling in standard quip activity;
	end if.
	
Carry out addressing corpse with something:
	follow the read new speech acts rule.
	
To say length warning:
	let N be indexed text;
	let N be "[current interlocutor]";
	let N be "[N in upper case]"; 
	let copied command be indexed text;
	let copied command be "[the player's command]";
	let word count be the number of words in the player's command;
	if word count is less than 6:
		say "(Note: if your command was vaguely phrased, as in ASK [N] ABOUT WEATHER, you may want to reply NO, then reword the command more specifically as, e.g., ASK [N] ABOUT WHETHER THE RAIN WILL STOP. This will produce more sensibly-named quips in the resulting code file.) [run paragraph on]".

The making interlocutor specific rule is not listed in any rulebook. ]

[A quip-building rule (this is the new making interlocutor specific rule): 
	change the currently open reference to the reference after opening file "NewConversation" for writing;
	say "	It quip-supplies [the current interlocutor]";
	close file "NewConversation" for writing;
	end new quip line. [* This is an invisible feature from the point of view of the collaborator using it: because the hart can become undead and speak, there are a small handful of quips addressed to it instead of to Snow White. However, we want the quip builder to assume that dialogue written to Snow White should be good only for her, and dialogue for the hart is good only for it. In a different kind of game, multiple-person quips might be used if, for instance, we wanted to be able to ask the same questions of anyone we met on the street.]

The spare space rule is listed before the marking repeatable quips rule in the quip-building rules. This is the spare space rule: say "[line break]".]

Instead of asking the player about something (this is the player reticence rule): 
	say "Your secret thoughts are best kept unspoken."
	
Instead of telling the player about something:
	abide by the player reticence rule.

[Instead of asking someone about something (this is the new vague asking rule):
	if player-mode is true, make no decision; 
	if the current interlocutor is nothing, change the current interlocutor to the noun;
	change the variable snippet to the topic understood; 
	store base quip text;
	say "That question is not [implemented]. Draft a new one? [length warning]>";
	if the player consents
	begin;
		change the currently open reference to the reference after opening file "NewConversation" for writing;
		say "	[message of the sample-quip] is a questioning quip. "; 
		close file "NewConversation" for writing; 
		if nominal-sample-quip is switched on, escape troubled quip-names;
		carry out the filling in standard quip activity;
	end if.
	 
Instead of telling someone about something (this is the new vague telling rule):
	if player-mode is true, make no decision;
	if the current interlocutor is nothing, change the current interlocutor to the noun;
	change the variable snippet to the topic understood; 
	store base quip text;
	say "That remark is not [implemented]. Draft a new one? [length warning]>";
	if the player consents
	begin;
		change the currently open reference to the reference after opening file "NewConversation" for writing;
		say "	[message of the sample-quip] is an informative quip. "; 
		close file "NewConversation" for writing; 
		if nominal-sample-quip is switched on, escape troubled quip-names;
		carry out the filling in standard quip activity;
	end if.
	
The new marking restrictive quips rule is listed instead of the marking restrictive quips rule in the quip-building rules.

This is the new marking restrictive quips rule:
	say "Is the player going to be restricted to a small choice of responses to this? [one of](This is most appropriate for questions that the player must answer.) [or][stopping] >";
	if the player consents
	begin;
		change the currently open reference to the reference after opening file "NewConversation" for writing;
		say "	It is restrictive";
		close file "NewConversation" for writing;
		end new quip line;
	end if. 
	

The establish quip-settings rule is not listed in any rulebook.

The first quip-building rule (this is the new establish quip-settings rule):
	now single character setting is q-set-yes;
	now immediacy setting is q-set-no;
	now indirection setting is q-set-no;
	
The setting assumed facts rule is listed after the new marking restrictive quips rule in the quip-building rules.

This is the setting assumed facts rule:
	move quip line forward;
	say "Does this quip require the player to know a fact learned earlier? [one of](If it is sufficient for the player to have seen the conversation you've played through so far, choose NO. If, on the other hand, this quip depends on some learned information that might turn up in multiple choices, choose YES.) [or][stopping]>";
	if the player consents:
		if using the suppressed subject list option, do nothing;
		otherwise say "[line break][if the number of facts is 0]There are no facts yet defined[otherwise]Current facts include [italic type][fact-collection][roman type][end if]. What does the player need to know at this point? ";
		say "[one of](Provide your answer as a list separated by commas.)[or][stopping][line break]";
		write "	It assumes " after file "NewConversation";
		stuff buffer;
		end new quip line.  ]
		


[A procedural rule while filling in standard quip:
	ignore the player learns facts rule. [* Otherwise, building the quip with the facts will set them all to known.]]
	
[To say fact-collection:
	repeat through Table of All Known Facts:
		say "  [fact entry][line break]".
	
The unlisting rule is listed after the setting assumed facts rule in the quip-building rules. 

This is the unlisting rule:
	say "Should this quip be recommended to the player? [one of](If so, it will be included in hints like 'You could say...') [or] [stopping] >";
	if the player consents:
		change the currently open reference to the reference after opening file "NewConversation" for writing;
		say "	It is listed";
		close file "NewConversation" for writing;
		end new quip line;]

Chapter 3 - Affirmative, Negative, and Demonstrative Quips

Section 1 - Counting quips for collaborative purposes

[When play begins:
	change the total no count to the number of negative quips;
	change the total yes count to the number of affirmative quips;
	change the total demo count to the number of demonstrative quips; 
	change right hand status line to "[number of listed-plausible quips]/[number of flagged-ready quips]/[number of quips in the quip-repository]/[number of quips]".]
	
Section 2 - Affirmative quips

An affirmative quip is a kind of performative quip. Understand "yes" as an affirmative quip.

[The total yes count is a number that varies.

Instead of saying yes (this is the new vague approval rule):
	if player-mode is true, make no decision;
	if the current interlocutor is nothing, continue the action;
	increase the total yes count by 1;
	let N be indexed text;
	change N to "yes-[total yes count]";
	change the text of the player's command to N;
	change the variable snippet to the player's command; 
	store base quip text;
	say "That agreement is not [implemented]. Draft a new one? >";
	if the player consents
	begin;
		change the currently open reference to the reference after opening file "NewConversation" for writing;
		say "	[message of the sample-quip] is an affirmative quip. The printed name is 'say yes'. The true-name is '[message of the sample-quip]'. "; 
		close file "NewConversation" for writing; 
		if nominal-sample-quip is switched on, escape troubled quip-names;
		carry out the filling in standard quip activity;
	end if.]

Understand "say yes" or "agree" or "consent" or "concur" as saying yes.
	
Before saying yes when an affirmative quip (called target) is flagged-ready:
	try discussing target instead.
	
Section 3 - Negative quips

A negative quip is a kind of performative quip. Understand "no" or "disagreement" as a negative quip.

[The total no count is a number that varies.]

Understand "disagree" as saying no.  

[Instead of saying no (this is the new vague refusal rule):
	if player-mode is true, make no decision;
	if the current interlocutor is nothing, continue the action;
	increase the total no count by 1;
	let N be indexed text;
	change N to "no-[total no count]";
	change the text of the player's command to N;
	change the variable snippet to the player's command; 
	store base quip text;
	say "That refusal is not [implemented]. Draft a new one? >";
	if the player consents
	begin;
		change the currently open reference to the reference after opening file "NewConversation" for writing;
		say "	[message of the sample-quip] is a negative quip. The printed name is 'say no'. The true-name is '[message of the sample-quip]'. "; 
		close file "NewConversation" for writing; 
		if nominal-sample-quip is switched on, escape troubled quip-names;
		carry out the filling in standard quip activity;
	end if.]

Understand "say no" as saying no.
	
Before saying no when a negative quip (called target) is flagged-ready:
	try discussing target instead.

Section 4 - Demonstrative quips

A demonstrative quip is a kind of performative quip. The printed name of a demonstrative quip is usually "show [the random thing mentioned by the item described]". The true-name of a demonstrative quip is usually "show [the random thing mentioned by the item described]".
		
[The total demo count is a number that varies.
	
Instead of showing something to the current interlocutor (this is the vague demonstrative rule):
	if player-mode is true, make no decision;
	if the current interlocutor is nothing, continue the action;
	increase the total demo count by 1;
	let N be indexed text;
	change N to "demo-[noun]-[total demo count]";
	change the text of the player's command to N;
	change the variable snippet to the player's command; 
	store base quip text;
	say "There is no reaction [implemented]. Draft a new one? >";
	if the player consents:
		change the currently open reference to the reference after opening file "NewConversation" for writing;
		say "	[message of the sample-quip] is a demonstrative quip. It mentions [the noun]. "; 
		close file "NewConversation" for writing; 
		if nominal-sample-quip is switched on, escape troubled quip-names;
		carry out the filling in standard quip activity;]
			
Before showing something to the current interlocutor when a quip (called the target) that mentions the noun is flagged-ready:
	try discussing target instead.
			
[Procedural rule while showing:
	ignore the setting mentions rule. ]
	
Understand "show [a scenery thing] to [someone]" as displaying it to. Understand "show [someone] [a scenery thing]" as displaying it to (with nouns reversed). Understand "point at/to/toward [a scenery thing]" as pointing out.

Pointing out is an action applying to one visible thing.

Check pointing out:
	if the current interlocutor is not a person:
		say "No one is around to react to your gesture." instead.

Carry out pointing out:
	try displaying the noun to the current interlocutor.
	
Displaying it to is an action applying to one visible thing and one thing.

Carry out displaying it to:
	try showing the noun to the second noun. 

Section 5 - Uncertainty

[Here we define some common terminology to be used for any 'maybe' sorts of answers, to make sure that the game is consistent in reading these.]

Understand "maybe" or "perhaps" or "equivocate" or "possibly" or "shrug" or "I don't know" or "don't know" or "dunno" or "do not know" or "I" or "i dont know" as "[maybe]".

A quip has some text called the uncertainty-refusal. The uncertainty-refusal of a quip is usually "blank".

Definition: a quip is yes-no only if the uncertainty-refusal of it is not "blank".

Understand "[maybe]" as a mistake ("You shrug weakly.[paragraph break][uncertainty-refusal of the current quip]") when the current quip is yes-no only.

Section 6 - Testing Yes-No Nodes - Not for release

[Using graphs, it is possible to find broken spots in the conversation flow where a restrictive quip has no proper follow-up. However, it can also be useful to search explicitly for these. Similarly, sometimes it is useful to search just for the yes-no quips to make sure that they have both 'yes' and 'no' answers (or a good reason why not).]

[Carry out testing restrictive nodes:
	repeat with item running through restrictive quips:
		 say "[line break]  [if nothing directly-follows item][bold type][end if][item][roman type]: [list of quips which directly-follow item]".

Carry out testing yes-no nodes:
	repeat with item running through restrictive quips:
		if an affirmative quip directly-follows item or a negative quip directly-follows item:
			say "[line break]  [if exactly two quips directly-follow item and item is not yes-no only][bold type][end if][item][roman type]: [list of quips which directly-follow item]".]


Chapter 4 - Tidying Quip Names for the Conversation Builder 

[Since certain kinds of quip name are not very elegant when printed to the screen, we want to replace those with an actual scrap of dialogue when asking our co-author questions about these quips (instead of writing sentences like "should this quip directly follow no-58" or the like.)]

Discussing something is chatting. Saying yes is chatting. Saying no is chatting. Showing something to someone is chatting.

[Rule for printing the name of an NPC-directed quip (called target) when not listing plausible quips or asking which do you mean or building dot or building subject dot or testing restrictive nodes or testing yes-no nodes:
	say target by text; ]
	
[Rule for printing the name of a negative quip (called target) when building dot or building subject dot:
	say "[true-name of the target]"]
	
[Rule for printing the name of a negative quip (called target) when not listing plausible quips or listing peripheral quips or building dot or building subject dot or asking which do you mean or testing restrictive nodes or testing yes-no nodes:
	say target by text; 
	
Rule for printing the name of a affirmative quip (called target) when building dot or building subject dot:
	say "[true-name of the target]"
	
Rule for printing the name of an affirmative quip (called target) when not listing plausible quips or listing peripheral quips or building dot or building subject dot or asking which do you mean or testing restrictive nodes or testing yes-no nodes:
	say target by text; ]

[To say (target - a quip) by text:
	let N be indexed text;
	if target is NPC-directed:
		let N be "[response of the target]";
	otherwise:
		let N be "[comment of the target]";
	say italic type;
	say the first 5 words in N;
	say "... [run paragraph on][roman type]";]

To say the/-- first (max - a number) word/words in (N - indexed text):
	let top number be the number of words in N;
	if top number is greater than max, change top number to max;
	repeat with Y running from 1 to top number:
		say "[unpunctuated word number Y in N]";
		if Y is less than top number:
			say " ";
		otherwise:
			say "[run paragraph on]".

Chapter 5 - Dot Builder and Node Checking

[Understand "build dot" as building dot. Building dot is an action out of world.

Understand "subject dot" or "build subject dot" as building subject dot. Building subject dot is an action out of world.

Understand "restrictive nodes" as testing restrictive nodes. Testing restrictive nodes is an action out of world.

Understand "yes-no nodes" as testing yes-no nodes. Testing yes-no nodes is an action out of world.]

Chapter 6 - Removable aspect of building dot - Not for release

[A quip can be dotted or undotted. A quip is usually undotted.

Fact-revelation relates various quips to various facts. The verb to fact-reveal (it fact-reveals, they fact-reveal, it fact-revealed, it is fact-revealed) implies the fact-revelation relation.

Establishing relations is an activity.

Rule for establishing relations:
	now generic-quip does not fact-reveal anything;
	now generic-quip does not quip-trigger anything;
	repeat with item running through quips:
		change current quip to item;
		say "[comment of item][response of item][paragraph break]";
		now the player does not know anything. [This is because some quips use if clauses to present facts if the player missed seeing them elsewhere; we want to find all of these connections.]

Rule for printing the name of a fact (called target) while establishing relations:
	now the current quip fact-reveals the target.
	

Carry out building dot:
	carry out the establishing relations activity;
	say "digraph Conversation {[line break]";
	now every quip is undotted;
	pre-color nodes;
	pre-color facts;
	repeat with item running through shallowly-buried quips:
		dot-tree item;
	say "}".
	
A quip has some indexed text called the node-name. 
	
To pre-color nodes:
	repeat with node running through quips: 
		let N be indexed text;
		let N be "[node]";
		let word count be the number of punctuated words in N;
		let line number be the word count divided by 3;
		if line number is greater than 1:
			let source be indexed text;
			let source be "[N]";
			let N be "";
			let running total be 0;
			let current line be 0;
			while running total is not greater than word count:
				repeat with index running from 1 to 3:
					increase running total by 1;
					if running total is not greater than word count: 
						let N be "[N][punctuated word number running total in source]";
						if index is not 3:
							let N be "[N] ";
				if running total is less than word count:
					let N be "[N]\n"; 
		change node-name of the node to "[N]";
		say "'[node-name of node]' ";
		if node is NPC-directed and node is restrictive:
			say "[bracket]color = 'deeppink' fillcolor = 'deeppink' shape = 'box'[close bracket]";
		otherwise if node is NPC-directed:
			say "[bracket]color = 'gray' fillcolor = 'gray' shape = 'box'[close bracket]";
		otherwise if node is restrictive:
			say "[bracket][if the node is not directly-followed by anything]color = 'crimson' fillcolor = 'crimson'[otherwise]color = 'deeppink' fillcolor = 'deeppink'[end if][close bracket]";
		otherwise if node is binary-answering and the node directly-follows a restrictive quip:
			say "[bracket]color = 'gray50' fillcolor = 'gray50'[close bracket]";
		otherwise if node is unlisted:
			say "[bracket]color = 'ghostwhite' fillcolor = 'ghostwhite'[close bracket]";
		otherwise:
			say "[bracket]color = 'gray' fillcolor = 'gray'[close bracket]";
		say "; [line break]";
		
To pre-color facts:
	repeat with relevant fact running through facts:
		say "'[relevant fact]' [bracket]color = 'palegreen' fillcolor = 'palegreen' shape = 'diamond'[close bracket]; [line break]";
		
To pre-color subjects:
	repeat with relevant subject running through things which are mentioned by a quip:
		if the relevant subject is a subject:
			say "'[relevant subject]' [bracket]color = 'palegoldenrod' fillcolor = 'palegoldenrod' shape = 'circle'[close bracket]; [line break]";
		otherwise:
			say "'[relevant subject]' [bracket]color = 'orangered' fillcolor = 'orangered' shape = 'circle'[close bracket]; [line break]"; [* These will be physical objects in the game world; they may be equally numerous but will probably tie into fewer quips.]

To dot-tree (node - a quip):
	if node assumes something:
		repeat with relevant fact running through facts which are assumed by node:
			say "'[relevant fact]' -> '[node-name of node]'; [line break]";
	if node fact-reveals something:
		repeat with relevant fact running through facts which are fact-revealed by node:
			say "'[node-name of node]' -> '[relevant fact]' [bracket]color = 'palegreen'[close bracket]; [line break]";
	if node quip-triggers something:
		repeat with relevant quip running through quips which are quip-triggered by node:
			say "'[node-name of node]' -> '[node-name of relevant quip]' [bracket][if the relevant quip is postponed obligatory or the relevant quip is immediate obligatory]color = 'deeppink'[otherwise]color = 'gray'[end if][close bracket]; [line break]";
	repeat with offspring running through undotted quips which indirectly-follow node:
		say "'[node-name of node]' -> '[node-name of offspring]' [if offspring directly-follows node][bracket]color = 'deeppink'[close bracket][end if]; [line break]";
		if every quip which is indirectly-followed by offspring is dotted:
			now offspring is dotted; [* This is necesssary because it is actually possible (though uncommon) for a quip to follow more than one other quip.]
		dot-tree offspring;

Carry out building subject dot:
	say "digraph Subjects {[line break]";
	now every quip is undotted;
	pre-color nodes; 
	pre-color subjects;
	repeat with node running through quips:
		if node mentions something:
			repeat with relevant subject running through things which are mentioned by the node:
				say "'[node-name of node]' -> '[relevant subject]'; [line break]";		
	say "}".]


Chapter 7 - Player Only Mode

[Player-mode is a truth state that varies.

Understand "player mode on" as requesting player mode on.

Understand "player mode" as requesting player mode on when player-mode is false. Understand "player mode" as requesting player mode off when player-mode is true.

Understand "player mode off" as requesting player mode off.

Requesting player mode on is an action out of world. Requesting player mode off is an action out of world.

Carry out requesting player mode on:
	now player-mode is true.
	
Report requesting player mode on:
	say "Player mode is now on."

Carry out requesting player mode off:
	now player-mode is false.
	
Report requesting player mode off:
	say "Player mode is now off."]
	

Chapter 8 - Optimizations

Section 1 - Limiting the quip repository

[The following tricks optimize the behavior of the system by limiting (initially to about 1/5 for Snow White) the number of quips that the game needs to look at during every turn, intensive loop operations. (For characters other than Snow White -- that is to say, the hart and the true princess -- the speed gain is even greater, because their working selection of quips is so small and we no longer have to search against the full pool.)

In practice, this will not always be the ideal optimization pattern; some games might prefer optimization based on scenes or other features. On consideration, the thing to do might be to create a rulebook and allow the author to turn off optimizations that don't contribute helpfully.]

To start talking to (new person - a person):
	follow the new-conversation rules for the new person.

The new-conversation rules are an object-based rulebook.

First new-conversation rule for someone (called the new person):
	if the current interlocutor is a person:
		delete all queued conversation for the current interlocutor;
	now the current interlocutor is the new person.
	
A new-conversation rule for someone (called the new person):
	now every quip is in the quip-repository;
	repeat with item running through things in the quip-repository: [* This is not strictly necessary (and indeed wouldn't even be desirable if we had a game where you could use the same quips with different NPCs) but it can be used here to speed up performance.]
		if item does not quip-supply the new person or the item is unexposed or the item is an NPC-directed quip:
			remove the item from play;
		if item is one-time and the player recollects the item:
			remove the item from play; 
		if the item is negated by a fact which is known by the player:
			remove the item from play.
			
A new-conversation rule:
	repeat with item running through things in the quip-repository: [* The selection of these facts is determined by the diagnostic in Section 2 below. It would be a drag on the system to have *every* fact move things into the quip repository -- this would require a lot more searching -- but for facts that open numerous new quips it is worth starting with their quips out of play and moving them later]
		if the item assumes snow-white-possessed:
			if the player does not know snow-white-possessed:
				remove the item from play;
		if the item assumes blood-sundering:
			if the player does not know blood-sundering:
				remove the item from play;	
		if the item assumes snow-white-knows-sundering:
			if the player does not know snow-white-knows-sundering:
				remove the item from play;
			
A quip can be exposed or unexposed. A quip is usually unexposed. [More caching hackery.]

When play begins:
	now every shallowly-buried quip is exposed.

Carry out someone discussing a quip: 
	repeat with item running through quips which directly-follow the previous quip:
		remove the item from play;
	repeat with item running through quips which indirectly-follow the noun:
		now the item is exposed;
		now the item is in the quip-repository.

Before printing the name of a fact (called the target):
	repeat with item running through things in the quip-repository:
		if the target negates the item:
			remove the item from play. [* A lot of quips are negated by a few specific facts (vampirism-revealed, snow-white-possessed), so this is a way to winnow quickly] 
				
Before printing the name of snow-white-possessed:
	repeat with item running through quips which quip-supply the current interlocutor:
		if the item assumes snow-white-possessed:
			move the item to the quip-repository.
			
Before printing the name of blood-sundering:
	repeat with item running through quips which quip-supply the current interlocutor:
		if the item assumes blood-sundering:
			move the item to the quip-repository.

Before printing the name of snow-white-knows-sundering:
	repeat with item running through quips which quip-supply the current interlocutor:
		if the item assumes snow-white-knows-sundering:
			move the item to the quip-repository.			
			
Section 2 - Diagnostics 

Finding critical facts is an action out of world. Understand "critical facts" as finding critical facts.

Carry out finding critical facts:
	repeat with item running through facts:
		let A be the number of quips which assume the item;
		let N be the number of quips which are negated by the item;
		let total be A plus N;
		say "[line break][if total is greater than 5][bold type][end if][item][roman type]: assumed by [A], negates [N], total [total]";
		

Section 3 - Cache Assembly

[Currently there is a rule that builds a cache of information about how quips relate at runtime at the beginning of play. This is to avoid having to do these calculations over and over between turns.

That still generates an unacceptably long delay at startup, however. So instead of that, we add a rule that creates a hard-code version of that cache, which then can be recompiled into the game and distributed.

In order to rebuild the cache, we destroy the existing cache. (See the last part of the sourcecode.) This needs to happen every time we add new quip material or change the way quips relate to one another, so it's kind of a bore to have going on during serious development, but once we approach release and the content becomes fairly stable, we're going to want this in place, even if we still have to  rebuild it sometimes.]

Use hard cache.


Chapter 9 - Autopilot Testing Method - Not for release

[Provides a method for getting pseudo-transcripts without beta-testers, by having the game play itself based on what commands are plausibly available at a given moment. This is not a replacement for beta-testing properly, of course, but it does allow capture of certain kinds of errors, especially:

-- places where the name of a quip is not being properly parsed again even if the player types it in verbatim
-- questioning quips misidentified as performative quips (in which case they won't be properly invoked)
-- infelicities in text produced when we move from one topic to another unexpectedly
-- unexpected orderings of quips (where something might have been written with the expectation that it would be used very early in the game, and instead it winds up being selected randomly quite late)]

[Autopilot is a truth state that varies. Autopilot is false.

Setting autopilot is an action applying to nothing. [* We could set it out of world, but that has the effect of turning off NPC initiative, which is not what we want.]

Understand "autopilot" as setting autopilot.

Carry out setting autopilot:
	now player-mode is true;
	now autopilot is true.

Rule for reading a command when autopilot is true:
	let N be indexed text;
	let the select quip be generic-quip;
	if the current quip is restrictive:
		let the select quip be a random flagged-ready quip which directly-follows the current quip;  
	otherwise if a random chance of 4 in 5 succeeds and a quip is listed-plausible:
		let the select quip be a random listed-plausible quip; [* Most of the time, the player will follow your lead.]
	otherwise if a quip is flagged-ready:
		let the select quip be a random flagged-ready quip;
	if the select quip is the generic-quip or the select quip is nothing:
		let N be "wait";
	otherwise:
		if the select quip is an affirmative quip:
			let N be "yes";
		otherwise if the select quip is a negative quip:
			let N be "no";
		otherwise if the select quip is a performative quip:
			let N be "[select quip]";
		otherwise:
			let N be "discuss [select quip] ";
	repeat through the Table of Prearranged Autopilot Acts: [* And this allows us to add in cases where the player is likely to take some action external to the conversation system at some point]
		if the set quip entry is the current quip and a random chance of 2 in 3 succeeds:
			let N be "[suggested outcome entry]";
	change the text of the player's command to N;
	say "[bold type][N][roman type]".
	
Before reading a command when autopilot is true:
	if every flagged-ready quip is repeatable:
		now autopilot is false. [* stop if the player knows everything already: we've probably run out the course of the game]

Table of Prearranged Autopilot Acts
set quip	suggested outcome
explain that you must stop here to collect the heart	"cut heart"
thank-the-hart	"wake hart"
yes-17	"untie her"]

Chapter 10 - Coloring hints

Include Glulx Text Effects by Emily Short.

Table of User Styles (continued)
style name	justification	obliquity	indentation	first-line indentation	boldness	fixed width	relative size	glulx color
special-style-2	left-justified	italic-obliquity	0	0	light-weight	proportional-font	0	g-dark-grey

Before listing plausible quips [when dialogue tinting is true]:
	say "[second custom style][run paragraph on]".
	
After listing plausible quips [when dialogue tinting is true]:
	if a paragraph break is pending:
		say "[roman type]";
	otherwise:
		say "[roman type][run paragraph on]".
		
Before listing matched quips:
	say "[second custom style][run paragraph on]".

After listing matched quips:
	if a paragraph break is pending:
		say "[roman type]";
	otherwise:
		say "[roman type][run paragraph on]".


Part 3 - Subjects

Chapter 1 - Specific Subjects

[Subjects are things the player might ask about that are not present in the world; as in >ASK SNOW WHITE ABOUT DEATH. Any quip that mentions that subject will then be considered as a possible response. A quip can mention more than one subject.]


The Queen is a subject. Understand "stepmother" as the queen.

The King is a subject. Understand "kingdom" as the king.

The daughter is a subject. [* relevant to Snow White as the king's daughter.]

The lost man is a subject. Understand "prince" or "courtier" or "lover" or "princes" or "of neighboring kingdoms" or "neighboring" as the lost man. The printed name is "[if the player recollects what the embroidery on her dress signifies]lost man[otherwise]princes of neighboring kingdoms[end if]".

The magic mirror is a subject. Understand "reflection" or "Queen's magic mirror" or "Queen's mirror" as the magic mirror.

Before printing the name of the magic mirror while changing the subject:
	say "Queen's "

Some vampirism is a proper-named subject. Understand "vampires" or "vampire" or "undead" or "monster" or "monstrous" or "monstrosity" or "my suspicions" or "my suspicions about" or "suspicions" or "suspicions about" as vampirism.

Before printing the name of vampirism while changing the subject:
	if the player does not know possible-blood-drinking:
		say "your suspicions about ".

Some blood is a proper-named subject. 

The safe haven is a subject. Understand "resting place" or "her safe" or "her haven" as the haven. The indefinite article is "her".

Some servants are a subject. Understand "servant" as the servants.

Some hounds are a subject. Understand "dogs" or "dog" or "hound" as hounds.

Some witchcraft is a proper-named subject. Understand "sorcery" or "wizardry" or "magic" or "witch" or "witches" or "my suspicions" or "my suspicions about" or "suspicions" or "suspicions about" as witchcraft. Understand "blood-sundering" or "sundering" or "blood sundering" as witchcraft when the player knows blood-sundering.

The deal is a subject. Understand "pact" or "arrangement" or "agreement" or "between you" or "between us" as the deal.

After printing the name of the deal while changing the subject:
	say " between you"

Some fear is a subject. Understand "terror" as fear.

The night is a subject. 

The temperature is a subject. Understand "cold" or "chill" or "coldness" or "heat" or "warmth" as the temperature.

Huntsmen is a subject. Understand "hunters" or "other huntsmen" or "other hunters" as huntsmen.

Xyzzy is a subject.

The library is a subject. Understand "books" or "book" as the library.

The delay is a subject.

The riddle is a subject.

The apple is a subject.

A snake is a subject.

Some treason is a subject. Understand "treachery" or "betrayal" as treason.

Some dwarrows are a subject. Understand "dwarf" or "dwarves" or "dwarfs" or "dwarrow" as dwarrows. 

After reading a command when dwarrow-warning is false:
	if the player's command includes "dwarves/dwarfs":
		say "[italic type](The echo of a paternal voice comes through from ages past: 'DWARROWS, boy. Men call them dwarves or dwarfs, but their own name for themselves is dwarrows, and it is better if you learn to think of them so.')[roman type][line break]";
		now dwarrow-warning is true.
		
dwarrow-warning is a truth state that varies.

Some dirt is a subject. Understand "earth "or "soil" as dirt.

Some death is a subject. Understand "died" or "dying" or "dead" as death. 

A murder is a subject. Understand "murder" or "killing" or "slaying" as murder. 

Grumpy is a subject. Understand "brassbiter" as Grumpy.

Dwarf-cousins is a privately-named subject.[* This is entirely for in-joke catching purposes, if the player asks Happy about any other famously-named dwarrows from Disney or Tolkien. Bubba is from a live-action movie version of Snow White I saw when I was a kid, so probably I am the only person in the universe who would think of trying it, but hey.] Understand "Dopey" or "Sleepy" or "Doc" or "Bubba" or "Bashful" or "Sneezy" or "Thorin" or "Gloin" or "Oin" or "Gimli" or "Balin" or "Bifur" or "Bofur" or "Bombur" or "Borin" or "Dain" or "Dis" or "Dori" or "Dwalin" or "Farin" or "Fili" or "Floi" or "Frar" or "Frerin" or "fror" or "fundin" or "gamil" or "zirak" or "groin" [* I know it's pronounce Gro-in, but seriously? JRR, you needed a life] or "Gror" or "Ibun" or "Khim" or "Kili" or "loni" or "mim" or "nain" or "nali" or "nar" or "narvi" or "nori" or "ori" or "telchar" or "thrain" or "thror" or "stonehelm" or "oakenshield" as dwarf-cousins.  [* The thing about wikipedia is that you never have an excuse to leave anything out, even when you'd really kind of like to call it a day. See: http://en.wikipedia.org/wiki/Glv=in#Gl.C3.B3in.2C_son_of_Gr.C3.B3in .]

A war is a subject. Understand "fighting" or "battle" or "drums" or "dwarf war" or "war with/against dwarf/dwarrows" as war.

Some souls are a subject.

Some powers are a subject. Understand "power" as some powers. [* This is in the sense of evil powers invoked in witchcraft.]

Some demons are a subject. Understand "demon" or "devil" or "devils" or "tempter" or "satan" as demons.

Lilith is a subject.

An exorcism is a subject. Understand "exorcise" or "casting out" as exorcism.

Some creatures are a subject. Understand "animal" or "animals" or "creature" as creatures.

A plague is a subject. Understand "disease" or "diseases" or "plagues" or "illness" or "sickness" as a plague.

Some mind reading is a subject.

Adam is a subject.

Eve is a subject.

Cain is a subject.

Abel is a subject.

Some wings are a subject. Understand "wing" or "flight" or "flying" as wings.

Shadows are a subject. Understand "shadow" or "shade" as shadows.

The cabin is a subject. Understand "cottage" or "house" or "home" as cabin.

Some family is a subject. Understand "wife" or "husband" or "marriage" or "wedding" as family.

God is a subject. 

The ribald anecdote is a subject. Understand "story" or "tale" or "whore" or "prostitute" or "fat" or "jacob" as ribald anecdote.

Some flame is a subject. Understand "fire" as flame.

Eden is a subject. Understand "garden of eden" or "garden" as eden.

Chapter 2 - Reviewing Subjects

[Understand "review subjects" as reviewing subjects. Reviewing subjects is an action out of world. 

Check reviewing subjects when the number of quips is greater than 40:
	say "The output is likely to be pages long; would you prefer just a summary listing subjects and objects with related quips? >[run paragraph on]";
	if the player consents:
		say "[line break]The following subjects and objects currently have quips: [the list of things which are mentioned by something]." instead.

Carry out reviewing subjects:
	repeat with item running through things which are mentioned by something:
		try reviewing topics about the item.

Definition: a thing is substantial if it is not a quip. [That includes subjects.]

Understand "review [any substantial thing]" as reviewing topics about. Reviewing topics about is an action out of world, applying to one thing.

Check reviewing topics about:
	if nothing mentions the noun:
		say "There are currently no quips about [the noun]." instead.

Carry out reviewing topics about:
	say "The following quips mention [the noun] (* marks quips not yet available, X those already used): [paragraph break]";
	repeat with item running through quips which mention the noun:
		say "  [fixed letter spacing][if item is recollected by the player]X[otherwise if item is flagged-ready] [otherwise]*[end if][variable letter spacing] [item] ([item by text])[line break]".
		
Rule for printing the name of an affirmative quip which directly-follows something (called target) while reviewing topics about:
	say "yes after [target]".
	
Rule for printing the name of a negative quip which directly-follows something (called target) while reviewing topics about:
	say "no after [target]".

Understand "availability speed test" or "ast" as testing availability speed. Testing availability speed is an action out of world.

Carry out testing availability speed:
	repeat with item running through quips:
		if item is available:
			say "Y";
		otherwise:
			say "N". 

Understand "plausibility speed test" or "pst" as testing plausibility speed. Testing plausibility speed is an action out of world.

Carry out testing plausibility speed:
	repeat with item running through quips:
		if item is plausible:
			say "Y";
		otherwise:
			say "N". ]

Part 4 - Facts

Chapter 1 - List of Facts

[Facts allow us to track whether something has been mentioned, even if there are multiple places for that idea to turn up.]

Table of All Known Facts (continued)
fact	summary
possible-blood-drinking	"She might drink blood."
silver-hurts	"The silver chain seems to have caused her pain."
drinks-red-wine	"She drinks red wine."
written-embroidery	"Her dress is embroidered as though with writing."
pact-exists	"You and she have a pact."
haven-location	"Snow White's haven is somewhere to the north."
symbols-seen	"There are strange symbols on the box lid."
library-known	"Snow White has studied in the Queen's magical library."
rock-at-mirror	"Snow White threw a rock at the mirror."
snowWhite-sees-reflections	"That Snow White sees the same thing the Queen sees in the magic mirror."
king-gone	"The King is gone from the court; Snow White never met him."
king-not-king	"The King is perhaps no longer the King."
king-had-stern-father	"The King had a stern father."
not-daughter	"Snow White might not be the daughter of the King."
queen-fears	"The Queen is afraid of Snow White."
servant-dwarrows	"Snow White uses dwarrow servants."
Grumpy-named	"One of the dwarrows who helped Snow White was called Grumpy."
queen-uses-people-up	"The Queen uses up her servants."
hart-thanked	"Snow White did something odd to the hart corpse."
undead-hart	"The hart became undead."
hart-pacified	"The hart is willing to talk to you despite the killing incident."
asked-about-cosmic	"You asked the hart about cosmic meanings."
hart-pact-proposed	"The hart has suggested a pact."
hart-pact	"You have made an agreement with the hart while it is undead."
hart-no-longer-undead	"The hart was undead, but now is no longer."
happy-name-known	"The hart used to be a dwarf named Happy."
hart-omniscience-revealed	"That Happy's soul, embodied in the hart, is now omniscient."
truth-about-souls	"That souls float around for a while after someone dies."
magic-revealed	"That Snow What has some sort of magic at her disposal."
box-is-creepy	"That the heart-sized box has some kind of unpleasant power."
symbols-uncreate	"That the symbols on the lid of the box are related to ripping spirit from flesh and uncreating things."
vampirism-revealed	"That Snow White is, in fact, truly a vampire."
burn-box	"The hart has asked you to burn the box."
will-burn-box	"You have said you will burn the box."
ashes-scattered	"You have scattered the ashes."
Queen-made-mirror	"The Queen made the mirror by putting its voice into it."
bad-soul-in-mirror	"The problem with the mirror is that the Queen used a corrupt soul."
servant-used	"That the Queen used a serving woman."
take-ashes	"The hart recommended you take the ashes of the box to the Queen in order to change the mirror."
winged-shadow	"Snow White's shadow has wings."
lilith-named	"Snow White mentioned a creature called Lilith."
snow-white-possessed	"Snow White is possessed by Lilith."
eve-named	"Snow White mentioned Eve."
adam-named	"Snow White mentioned Adam."
misses-adam	"Lilith is apparently still pining for Adam."
lilith-first-wife	"Lilith was the first wife of Adam."
magic-involves-demons	"Magic requires invocation of demons."
queen-invoked-lilith	"The Queen invoked the demon Lilith in order to make her more beautiful and give her other powers."
seeks-equal	"Lilith seeks her equal among men."
wrong-to-kiss	"You thought to kiss her, but decided that was the wrong approach."
murderous-lilith	"Lilith kills male children."
apples-for-men	"Apples are the food of mortals."
first-vampire-accusation	"You've accused Snow White of being a vampire."
second-vampire-accusation	"You've made it clear you're sure Snow White is a vampire."
riddle-given	"Snow White has revealed her riddle."
riddle-solved	"You've solved the riddle and discovered that Snow White seeks an apple."
cabin-destroyed	"That the Queen destroyed your cabin as a warning not to disobey her."
blood-sundering	"Blood-sundering divides families."
snow-white-knows-sundering	"Snow White knows about blood-sundering."
power-from-sundering	"Blood-sundering provides the power for other tasks."
sundering-method	"That you could restore a sundered person by burning some of that person's blood in the box."
you-were-king	"That you once were king."
swear-word	"That Snow White cultivates some strange swear words."


Assumption relates various quips to various facts. The verb to assume (it assumes, they assume, it assumed, it is assumed) implies the assumption relation.

An availability rule for a quip (called the target):
	if the target assumes a fact which is not known by the player, it is off-limits.

Negation relates various facts to various quips. The verb to negate (it negates, they negate, it negate, it is negated) implies the negation relation.

An availability rule for a quip (called the target):
	if the target is negated by a fact which is known by the player, it is off-limits.

Chapter 2 - Fact Review for Collaborators

[Understand "review facts" as reviewing facts. Reviewing facts is an action out of world. 

[A procedural rule while reviewing facts:
	ignore the player learns facts rule. [* Otherwise, merely reviewing the facts will set them all to known.]]

The player learns facts rule is not listed in any rulebook.

Before printing the name of a fact (called the target):
	if reviewing facts:
		do nothing;
	otherwise:
		now the player knows the target.

Carry out reviewing facts:
	say "The currently-defined facts are as follow (K marks those the player now knows, U those yet unknown): [paragraph break]";
	repeat through the Table of All Known Facts:
		say " [fact entry] ([if the player knows the fact entry]K[otherwise]U[end if]): [summary entry][line break]";
	say paragraph break.]		 

Chapter 3 - Fact Review for Players

Instead of thinking:
	follow the recap of known facts rule.
	
Part 5 - Moods

[I think we want to track a couple of aspects of Snow White's character:

-- how pathetic does she seem? How much pity do we have for her?
-- how scary is she? Do we know frightening facts about her?
-- how angry is she? Does she have reason to be displeased with us?

Pathetic and scary are probably opposite ends of the same spectrum. It is hard to imagine someone being both scary and pitiful at the same time.

On the other hand, she could be pathetic and angry or pathetic and not-angry (when most like a little girl) or scary and angry (when agitated about being called a vampire) or scary and not-angry [Lilith when romantic]

]

Chapter 1 - Moods Defined 

When play begins: 
	change left hand status line to "[Mood of Snow White]:  S [scariness] / A [anger].";
	change the command prompt to "> ".

Scariness is a number that varies. Anger is a number that varies.

To say scary: 
	increase scariness by 1.
	
To say pathetic:
	decrease scariness by 1.
	
To say angry:
	increase anger by 1.

To say friendly:
	decrease anger by 1.
	

[Now, we want to judge into what category her cumulative mood falls.]

Include Mood Variations by Emily Short.

The moods are menacing [pure scariness], aggressive [scary/angry], furious [angry], whiny [angry/pathetic], needy [pure pathetic], harmless [friendly/pathetic], warm [pure friendly], weird [friendly/scary].

The previous mood is a mood that varies.

The moodiness counter is a number that varies.

When play begins:
	now Snow White is harmless.

To adjust the mood of Snow White: 
	change the previous mood to the mood of Snow White;
	if scariness is greater than 0:
		if anger is greater than 0:
			now Snow White is aggressive;
		otherwise if anger is less than 0:
			now Snow White is weird;
		otherwise:
			now Snow White is menacing;
	otherwise if scariness is less than 0:
		if anger is greater than 0:
			now Snow White is whiny;
		otherwise if anger is less than 0:
			now Snow White is harmless;
		otherwise:
			now Snow White is needy;
	otherwise:
		if anger is greater than 0:
			now Snow White is furious;
		otherwise if anger is less than 0:
			now Snow White is warm;
		otherwise:
			now Snow White is harmless;
	if the mood of Snow White is not the previous mood:
		increase the moodiness counter by 1;

Last carry out someone discussing something:
	adjust the mood of Snow White.
	

Chapter 2 - Beats 

To say beat:
	if the mood of Snow White is:
		-- menacing:
			say "[one of]You wait, heart pounding[or]Very little time passes[or]Something rustles in the shrubbery[or][random lampstate][or][random temperature][or][random urgency][or][random sound][or]A bat flits into the lamplight and just as quickly out of it[or]Something — almost certainly a rat — runs over your boot and is gone[as decreasingly likely outcomes].[run paragraph on]";
		-- aggressive:
			say "[one of]You edge a little further away from her[or][random lampstate][or][random temperature][or][random urgency][or]You wait several paces away from her[or][random sound][as decreasingly likely outcomes].[run paragraph on]";
		-- furious:
			say "[one of]You edge a little further away from her[or]You wait, out of reach[or][random sound][or][for eye-gest]She glares at you[or breath-gest]She huffs and mutters something under her breath[or walk-gest]She paces around, kicking at the roots underfoot[end for][as decreasingly likely outcomes].[run paragraph on]";
		-- whiny:
			say "[one of]You are still[or][random lampstate][or][random temperature][or][for eye-gest]She looks at you reproachfully, obviously blaming you for keeping her in this dreadful place[or walk-gest]She stumbles on something — in the lamplight it looks like the corpse of a squirrel — but she immediately kicks it away[or breath-gest]She sighs [one of]gustily[or]heavily[or]loudly[or]pointedly[purely at random][end for][as decreasingly likely outcomes].[run paragraph on]";
		-- needy:
			say "[one of]You wait[or][random lampstate][or][random sound][or]She shivers[or]Her teeth chatter[if a random chance of one in three succeeds] audibly[end if][or]She bends as though she is going to try to curl up on the ground for warmth; then she apparently thinks better of that, and stands up again[as decreasingly likely outcomes].[run paragraph on]";
		-- warm:
			say "[one of]You wait in companionable silence[or]The lamplight dances over her features[or]Her hair falls across her face, and she tosses it back again[or]Neither of you speaks for a little while[or]Half-intentionally, you draw nearer to her[as decreasingly likely outcomes].[run paragraph on]";
		-- harmless:
			say "[one of]Both of you are silent[or]There is a pause[or][random sound][or][random temperature][or]She hums a snatch of a dwarven tune under her breath[or]She squints up through the branches as though looking for stars; but of course none are visible[as decreasingly likely outcomes].[run paragraph on]";
		-- weird: 
			say "[one of]You pace a small circle[or][random lampstate][or][random temperature][or][random urgency][or]Even when you don't look at her, you are conscious of her watching you[as decreasingly likely outcomes].[run paragraph on]"; 
		
Chapter 3 - Address

[One obvious way for mood to come across is through the way people address one another:]

To say vocative huntsman:
	if the mood of Snow White is menacing or the mood of Snow White is aggressive or the mood of Snow White is furious:
		if the player knows Snow-White-possessed:
			say "[one of]mortal[or]man[or]son of Adam[or]Eve-get[as decreasingly likely outcomes]";
		otherwise:
			say "[one of]huntsman[or]woodsman[or]man[or]royal executioner[as decreasingly likely outcomes]";
	otherwise if the mood of Snow White is whiny or the mood of Snow White is needy:
		say "[one of]hunter[or]sir[as decreasingly likely outcomes]";
	otherwise:
		say "[one of]huntsman[or]my friend[as decreasingly likely outcomes]"

To say vocative Snow White:
	if the player knows snow-white-possessed:
		say "Lilith";
		stop;
	if the mood of Snow White is menacing or the mood of Snow White is aggressive or the mood of Snow White is furious:
		say "[one of]princess[or]your highness[or]your royal highness[at random]";
	otherwise if the mood of Snow White is whiny or the mood of Snow White is needy:
		say "[one of]girl[or]child[or]my girl[or]young lady[at random]";
	otherwise if the mood of Snow White is warm:
		say "[one of]Snow[or]Snowy[at random]";
	otherwise if the mood of Snow White is harmless:
		say "[one of]Snow White[or]princess[at random]";
	otherwise:
		say "[one of]Snow White[or]changeling[or]O enigmatic one[or]O princess of the peculiar[as decreasingly likely outcomes]";
		
Chapter 4 - Mood reactions to facts

[Some facts should trigger large mood jumps.]

Before printing the name of snow-white-possessed when the player does not know snow-white-possessed:
	if scariness is less than 0:
		change scariness to 10;
	otherwise:
		increase scariness by 10. [* This is something that really ought to scare you whatever you might have thought of Snow White going in.]
		
Chapter 5 - Mood-related Player gestures

Section 1 - Smiling

Understand "smile" or "smirk" or "grin" as smiling.

Smiling is an action applying to nothing.

Report smiling:
	if the current interlocutor is Snow White:	
		if the mood of Snow White is:
			-- menacing:
				say "[one of]They say smiling at a bear will make it back down, too.[or]You offer a placating smile.[at random]";
			-- aggressive:
				say "[one of]You smile weakly.[or]Your smile feels uncomfortable	 on your own face.[at random]";
			-- furious:
				say "[one of]You try to smile, but it doesn't come off very well.[or]Your attempted smile [for eye-gest]melts under her glare[or breath-gest]fades when she snarls[or walk-gest]meets with a cold shoulder[end for].[at random]";
			-- whiny:
				say "[one of]You smile. It is the same smile you offer when your cousin's children are begging for treats and you would much prefer to be rid of them.[or]You smile without pleasure.[at random]";
			-- needy:
				say "You give a reassuring smile.";
			-- warm:
				say "You grin.";
			-- harmless:
				say "You smile.";
			-- weird: 
				say "You smile uncertainly.";
		casually queue funny-memory;
	otherwise:
		say "You smile."
		
Section 2 - Winking

Understand "wink" as winking. [* Someone tried this because the hart's eye seems to wink early on.]

Winking is an action applying to nothing.

Check winking:
	if the corpse is knowing:
		try winking at the corpse instead;
	if the current interlocutor is a person:
		try winking at the current interlocutor instead;
	otherwise:
		say "There's no point, since you're unobserved." instead. [* This should never happen.]

Understand "wink at [someone]" as winking at. 

Winking at is an action applying to one thing.

Check winking at the player:
	say "You wouldn't really be able to appreciate the gesture, coming from yourself and all." instead.
			
Carry out winking at someone when Snow White is the current interlocutor: [* she should see you wink, even if you're not winking at her.]
	if the mood of Snow White is weird:
		casually queue smirk;
	otherwise:
		casually queue something-in-your-eye.

Report winking at the corpse when the corpse is not the current interlocutor:
	if the corpse is not undead:
		say "You wink, but it's unlikely to rouse him." instead;
	otherwise:
		say "He doesn't move. Maybe you need a more aggressive approach. Maybe you're going mad." instead. [* This should only happen if the hart is in a wakeable-but-not-yet-woken state.]

Report winking at the corpse:
	say "You wink at [the corpse]. He doesn't respond — if it deserves a response." instead.

Report winking at Snow White:
	if the mood of Snow White is:
		-- menacing:
			say "You wink at [one of]her[or]Snow White[at random].";
		-- aggressive:
			say "You wink at [one of]her[or]Snow White[at random].";
		-- furious:
			say "In your experience, winking at infuriated women does not come out well. But you go ahead anyway.";
		-- whiny:
			say "You wink at [one of]her[or]Snow White[at random], as though she were a small child.";
		-- needy:
			say "You wink at [one of]her[or]Snow White[at random]. It's meant to say 'hey, don't worry, I'll handle this', but it probably just looks strange..";
		-- warm:
			say "You wink at [one of]her[or]Snow White[at random].";
		-- harmless:
			say "You try winking at [one of]her[or]Snow White[at random].";
		-- weird: 
			say "You wink broadly.".
		
Understand "wink at [body part]" as a mistake ("Your winks aren't so precisely targeted."). [* In case you try winking at the hart's eye. Yeah, it's not that likely.]

Section 3 - Frowning

Understand "frown" or "glare" as frowning.

Frowning is an action applying to nothing.

Report frowning:
	say "You frown."

Section 4 - Threats, Tears, Etc

Understand "threaten [someone]" as a mistake ("It is not entirely clear what [the noun] would find threatening.").

Understand "pester [someone]" or "annoy [someone]" or "irritate [someone]" or "bother [someone]" or "anger [someone]" or "tease [someone]" or "insult [someone]" or "tickle [someone]" or "mock [someone]" or "taunt [someone]" as a mistake ("[taunting]").

To say taunting:
	if the noun is the current interlocutor:
		say "[one of]You are past the point of beer-hall antics.[or]That seems unlikely to end well.[or][The noun] is likely in no mood for village-idiot japes.[at random]";
	otherwise if the noun is the corpse:
		say "Mocking the dead is a less-than-graceful sort of thing to do.";
	otherwise:
		say "You make a face at [the noun], and immediately sense that it was a silly thing to do."

Understand "poo" or "poop" or "pee" or "urinate" or "piss" or "relieve myself" as a mistake ("You stopped sometime earlier, set Snow White down, and relieved yourself behind a tree. Consequently you have no present need.").

Understand "fart" or "break wind" or "pass gas" as a mistake ("You have had little appetite for the past few days: consequently your insides are empty even of wind.").

Understand "cough" or "sneeze" or "sniffle" as a mistake ("You may well be catching cold in this weather, but you have not succumbed yet.").

Understand "cry" or "sob" or "weep" as a mistake ("You have shed no tears for many years. They would not come easily now.").

Understand "laugh" or "chuckle" or "chortle" or "giggle" or "cackle" as a mistake ("With the weight on your heart? Laughter is far away.").

Understand "scream" or "holler" as a mistake ("[one of]No one would hear you so deep in the woods.[or]Well, perhaps someone would hear, but no one who heard would come to your side.[or]Perhaps someone would come, but no one who came to your side would be a person you wanted to meet.[stopping]").

Section 5 - Moods and the Status Window
 

Include Simple Graphical Window by Emily Short. 
Include Collage Tools by Emily Short.  


The graphics window proportion is 25. The graphics window position is g-left.

Figure of neutral expression is the file "ambiguous.png".
Figure of sympathetic expression is the file "sympa.png".
Figure of very sympathetic expression is the file "slicedambiguous.png".
Figure of pathetic expression is the file "pathetic.png".
Figure of very pathetic expression is the file "slicedpathetic.png".
Figure of angry expression is the file "angry.png".
Figure of very angry expression is the file "slicedangry.png".

Figure of close post-exorcism  is the file "exorcisedclose.png". 

Figure of post-exorcism is the file "exorcised3.png". 
Figure of lilith eyes is the file "lilith eyes.png".
Figure of single eye is the file "right eye.png".

Figure of frame is the file "frame.png".
Figure of overlay is the file "overlay.png".

Figure of downward scribble is the file "backg1 (lefth).png".
Figure of upward scribble is the file "backg2 (righth).png".
Figure of vertical scribble is the file "backg3 (vert).png".
Figure of horizontal scribble is the file "backg4 (horiz).png".

Figure of simple tree is the file "backg6 (tree2).png".
Figure of large simple tree is the file "bigtree.png".
Figure of small simple tree is the file "tinytree.png".

Figure of rich tree is the file "backg5 (tree).png".
Figure of large rich tree is the file "bigscribbly.png".
Figure of cropped rich tree is the file "croppedscribbly.png".
Figure of tight rich tree is the file "tightscribbly.png".
 
Before starting the virtual machine:
	change graphics background color to g-black;
	now the current graphics drawing rule is the bland graphics drawing rule.

When play begins:
	change graphics background color to g-white;
	change the currently shown picture to the figure of neutral expression; 
 

To decide what figure-name is the interlocutor expression:
	if the current interlocutor is:
		-- corpse:
			if the player does not know will-burn-box:
				if the player knows hart-pact:
					decide on figure of tight rich tree;
				decide on figure of rich tree;
			if the player knows hart-omniscience-revealed:
				decide on figure of cropped rich tree;
			if the player knows happy-name-known:
				decide on figure of large rich tree;
			decide on figure of rich tree;
		-- new-Snow-White:
			decide on figure of post-exorcism;
		-- Snow White:
			if the player knows Snow-white-possessed:
				decide on the figure of lilith eyes; 
			if the mood of Snow White is:
				-- menacing:
					decide on figure of angry expression;
				-- aggressive:
					decide on figure of angry expression;
				-- furious:
					decide on figure of very angry expression;
				-- needy:
					decide on figure of pathetic expression;
				-- whiny:
					decide on figure of very pathetic expression;
				-- harmless:
					decide on figure of neutral expression;
				-- warm:
					decide on figure of very sympathetic expression;
				-- weird:
					decide on figure of sympathetic expression;
	
A last background drawing rule when the player recollects whether you could be such a man:
	if anger is greater than 3:
		center the figure of horizontal scribble vertically;
	if anger is greater than 2:
		center the figure of upward scribble vertically;
	if anger is greater than 1:
		center the figure of vertical scribble vertically;
	if anger is greater than 0:
		center the figure of downward scribble vertically;
	rule succeeds.
	
A last background drawing rule:
	if the current interlocutor is Snow White:
		if the player does not know snow-white-possessed:
			follow the knowledge background rules for Snow White;
		otherwise if the player does not recollect what Lilith wants again:
			follow the knowledge background rules for Lilith;
	if the current interlocutor is corpse:
		follow the knowledge background rules for corpse; 
	if the current interlocutor is new-snow-white:
		follow the knowledge background rules for new-snow-White; 
			
The knowledge background rules are an object-based rulebook.
	
A knowledge background rule for Snow White when the player knows first-vampire-accusation:
	center the figure of vertical scribble vertically.
	
A knowledge background rule for Snow White when the player knows second-vampire-accusation:
	center the figure of horizontal scribble vertically.

A knowledge background rule for Snow White when the player knows possible-blood-drinking:
	center the figure of downward scribble vertically.
	
A knowledge background rule for Snow White when the player knows blood-sundering:
	center the figure of upward scribble vertically.

A knowledge background rule for Snow White when the player knows pact-exists and player does not know haven-location:
	plant forest.
	
To plant forest:
	if the player does not know servant-dwarrows:
		truncate collage list to 0 entries; 
		repeat with N running from 1 to 3:
			add figure of simple tree to collage list; 
		draw collage list at frame edge, top;
	truncate collage list to 0 entries; 
	repeat with N running from 1 to 2:
		add figure of rich tree to collage list; 
	draw collage list at frame edge, top.
	
Definition: a quip is player-recollected:
	if it is NPC-directed:
		no;
	if the player recollects it:
		yes;
	no.

To plant narrow-forest:
	truncate collage list to 0 entries; 
	let total be the number of unrestrictive player-recollected quips which quip-supply the corpse;
	if total is 0:
		make no decision;
	collage total trees;
	[let total be total - 1;
	collage total trees.]

To collage (total - a number) trees:
	repeat with N running from 1 to total:
		if total is greater than 10:
			add figure of small simple tree to collage list; 
		otherwise if total is 1:
			add figure of large simple tree to collage list;
		otherwise:
			add figure of simple tree to collage list;
	stutter collage list in graphics window 3 times, horizontally.

A knowledge background rule for Snow White when the player knows apples-for-men:
	center the figure of large simple tree vertically.

A knowledge background rule for new-snow-white when the player does not know haven-location: 
	plant forest.
	

A knowledge background rule for corpse when the player knows you-were-king:
	repeat with N running from 1 to 20:
		add figure of small simple tree to collage list;
	inconsistently randomize collage list in graphics window;
	rule succeeds.

A knowledge background rule for corpse when the player knows truth-about-souls:
	center the figure of upward scribble vertically.
	
[A knowledge background rule for corpse when the player knows hart-omniscience-revealed:
	center the figure of horizontal scribble vertically.]
	
A knowledge background rule for corpse when the player knows vampirism-revealed:
	center the figure of downward scribble vertically.  
	
[A knowledge background rule for corpse when the player knows blood-sundering:
	center the figure of upward scribble vertically.]
	
	
A knowledge background rule for corpse when the player knows happy-name-known:
	if the player knows hart-pact-proposed and the player does not know will-burn-box:
		make no decision;
	plant narrow-forest. 
	
	
A knowledge background rule for Lilith when the player knows murderous-lilith:
	center the figure of large simple tree vertically.

A knowledge background rule for Lilith when the player knows misses-adam:
	center the figure of rich tree vertically.

[A knowledge background rule when the current quip mentions the roots:
	center the figure of simple tree vertically.]

A main drawing rule when the current interlocutor is corpse: 
	if the player knows will-burn-box:
		do nothing;
	otherwise:
		center the interlocutor expression vertically;
	rule succeeds.
	
A main drawing rule when the current interlocutor is new-Snow-White and insanity count > 0 (this is the madness drawing rule): 
	truncate collage list to 0 entries;
	repeat with N running from 1 to insanity count:
		add interlocutor expression to collage list; 
	draw collage list at frame edge, bottom;
	rule succeeds. 
	
A main drawing rule when the player does not know snow-white-possessed: 
	if the player knows demon-possession or the player knows queen-invoked-lilith:
		justify figure of very angry expression.
			
			
A main drawing rule when the current interlocutor is Snow White: 
	if Snow White is warm or Snow White is weird or Snow White is furious:
		justify the interlocutor expression;
		rule succeeds.

A main drawing rule (this is the moody drawing rule):
	let depth be 40 - (scariness * 5);
	if depth > 65:
		let depth be 65;
	if depth < 45:
		let depth be 45;
	inset interlocutor expression in graphics window at depth percent vertical by 0 percent horizontal.
	
A border drawing rule (this is the framing rule):	
	if the player knows hart-pact-proposed and the player does not know will-burn-box:
		make no decision; 
	if the player knows snow-white-possessed and the player recollects what Lilith wants again and the current interlocutor is snow white:
		make no decision;
	if the current interlocutor is not snow white or the mood of Snow White is not harmless or the player recollects you-promised-me:
		center the figure of frame vertically with g-white padding.
	
[A last border drawing rule when the player knows hart-pact-proposed and the player does not know will-burn-box:
	center the figure of overlay vertically. ]

A last border drawing rule when the current quip mentions mirror or the current quip mentions Lilith:
	if the current interlocutor is Snow White and the player does not know snow-white-possessed:
		center the figure of overlay vertically.
	
A last border drawing rule when the current quip mentions demons or the current quip mentions Lilith:
	if the player recollects what powers the Queen dabbles and the player does not know snow-white-possessed:
		center the figure of single eye at top of frame.
	
To center (image - a figure-name) at top of frame:
	let image width be the centered width of the image;
	let image height be the centered height of the image; 
	let w be (width of the graphics window - centered width of the image) / 2; 
	let h be height of the graphics window - (centered height of the image / 3);
	let h be (h - centered height of the figure of frame) / 2;
	draw image in graphics window at w from the left by h from the top with width image width by height image height; 
		
To draw (L - a list of figure-names) at frame edge, top or bottom:
	if top:
		let H be the horizontal scaling factor of L;
		let index width be 0;
		repeat with image running through L:
			let new width be the width of the image * H;
			let new width be new width / 100;
			let new height be the height of the image * H;
			let new height be new height / 100;
			let height offset be the (height of the graphics window - centered height of the figure of frame) / 2;  
			draw image in graphics window at index width from the left by height offset from top with width new width by height new height;
			increase index width by new width;
	otherwise:
		let H be the horizontal scaling factor of L;
		let index width be 0;
		repeat with image running through L:
			let new width be the width of the image * H;
			let new width be new width / 100;
			let new height be the height of the image * H;
			let new height be new height / 100;
			let height offset be the (height of the graphics window + centered height of the figure of frame) / 2; 
			let height offset be the height offset - the new height;
			draw image in graphics window at index width from the left by height offset from top with width new width by height new height;
			increase index width by new width.
			
This is the sundering-ended collage rule:
	follow the bland graphics drawing rule;
	center the figure of close post-exorcism vertically;
	center the figure of frame vertically with g-white padding.
	
	
[A border drawing rule when the player knows possible-blood-drinking (this is the shading for sin rule):
	if the current interlocutor is not Snow White:
		make no decision;
	if the player knows snow-white-possessed:
		make no decision;
	center the figure of overlay vertically. ]

[A main drawing rule when the current interlocutor is Snow White and the current quip mentions the mirror (this is the mirrored drawing rule):
	let vertical midpoint be the height of the graphics window divided by 2;
	let image be mirrored interlocutor expression;
	let image width be the width of the image;
	let image height be the height of the image; 
	let w be 0;
	let h be the vertical midpoint;
	let total width be (w + image width);
	if total width > the width of the graphics window:
		let former image width be image width;
		let image width be the width of the graphics window - w;
		let image height be image width * image height;
		let image height be image height / former image width;
	let total height be (h + image height);
	if total height > the height of the graphics window:
		let former image height be image height;
		let image height be the height of the graphics window - h;
		let image width be image height * image width;
		let image width be image width / former image height; 
	draw image in graphics window at w from the left by h from the top with width image width by height image height;
	fog window; 
	let the image be interlocutor expression;
	let h be the vertical midpoint - image height;
	draw image in graphics window at w from the left by h from the top with width image width by height image height;
	rule succeeds.]
	
[A main drawing rule when the current interlocutor is Snow White and moodiness counter is greater than 1 (this is the creepy drawing rule):
	if a random chance of 1 in 3 succeeds:
		add interlocutor expression to collage list;
	otherwise:
		add intense expression to collage list;
	randomize collage list in graphics window;
	let fallback be the remainder after dividing the turn count by 5;
	if fallback is 0:
		decrease moodiness counter by 1;
	rule succeeds. 
	
A main drawing rule when the current interlocutor is Snow White and anger is greater than 2 (this is the furious drawing rule): 
	blank graphics window to g-black;
	inset figure of angry slice in graphics window at 0 from the left by 0 from the top;
	rule succeeds.  
	
To fog window:
	let temporary picture be the currently shown picture;
	change the currently shown picture to the figure of white gradient;
	follow the fully scaled drawing rule;
	change the currently shown picture to the temporary picture. 
	
This is the ripped left rule: 
	loop figure of ripped left side in graphics window, at left.
	
This is the ripped right rule: 
	loop figure of ripped right side in graphics window, at right.
	
A first border drawing rule when the current interlocutor is Snow White:
	follow the spatter rules.
	
The spatter rules are a rulebook.

A spatter rule when the player knows possible-blood-drinking: 
	inset figure of ink splatter in graphics window at 60 percent vertical by 0 percent horizontal;  

A spatter rule when the player knows blood-sundering: 
	inset figure of ink blot in graphics window at 20 percent vertical by 20 percent horizontal;  ]
	
[A border drawing rule when the player knows snow-white-possessed and the current interlocutor is Snow White:
	follow the ripped left rule. 
	
A new-conversation rule: [clear out the collage when we talk to someone new] 
	change the last mood stare to the intense expression;
	change the last mood image to the interlocutor expression;]

Part 6 - Conversation Flow

Section 1 - Combining Elements

[The essential elements of conversation flow, following a conversational command, are these:

1. PC's comment (which can be multiple paragraphs, but usually isn't)

2. NPC's response (which can be multiple paragraphs, but usually isn't)

3. (If step 4 is going to happen) A grounding beat produced by the rule for avoiding talking heads. This reminds us of where we are; the exact text of the beat is dependent on Snow White's mood, with scarier beats set for scarier moods. 

4. (Optionally) voluntary further comment by NPC.

I've actually left out a hook -- between 1 and 2 the NPC can challenge the PC for changing the subject abruptly. In this game we mostly see that used with the riddle, where you aren't restricted as to answer but Snow White will be disappointed if you change the subject without trying to guess. It's not something we need most of the time, though. 

From a functional interactive perspective, relying on the structure in steps 1-4 work out pretty well. The transitional beat means that if the NPC changes the subject at 4, we get an implied pause -- just as in real life conversations, your interlocutor will often introduce a change of subject only when a longer-than-usual silence signifies the end of the current line of thought.

This mechanism is not so great from a prose style perspective, though. Over many turns, the player soon becomes used to the response-beat-followup pattern, and the rhythm gets boring, no matter how carefully the components are written.

So we want the freedom to change it up a little, by using context-specific beats some of the time, and by connecting paragraphs 2&3, or 3&4, or even 2&3&4 in a single paragraph if the response in 2 is short and doesn't preclude a mid-paragraph change of subject. This latter decision is one that has to be taken on a case by case basis, so we create a strong vs. weak distinction in quips and apply it by hand.

The result is a more supple flow of conversation in which the automated breaks and joins are (we hope) less apparent to the reader.]
		
A quip can be strong or weak. A quip is usually strong. [* A quip may be designated "weak" if we want to allow the interlocutor to transition smoothly into another line of conversation without a paragraph break.]

Rule for avoiding talking heads:
	if the current interlocutor is ready for transition:
		if the current quip is strong and a random chance of 1 in 2 succeeds:
			say "[beat][line break][paragraph break]"; [* If the current quip from which we are building is weak, we want to fold it into one continuous paragraph with one intervening beat.]
	otherwise:
		say "[beat][if a random chance of 1 in 2 succeeds] [run paragraph on][otherwise][line break][paragraph break][end if]". [* This generates text that is printed between lines of conversation when the conversation is supposed to pause for a bit. The complexity of the structure is so that it can produce not-completely-predictable text structures. Specifically, an NPC's comment can either be q-transitional (an ugly term, I know) or not. If it is, that indicates that the comment begins with its own special, handwritten beat; in that case, we don't need to generate a grounding beat every time before we print it. If, however, the NPC's comment begins with quoted text, we do want a beat to separate it from the quoted text that preceded.]

Report someone discussing a weak dead-ended quip when the current interlocutor is likely to continue:
	if the noun provides the property response:
		say "[response of the noun] [run paragraph on]" instead.
		
Section 2 - Prose Variation Control

[Some words are used quite a bit in the source text, and there is a risk of combining two (apparently) unrelated bits, such as a grounding beat and a bit of dialogue, that repeats this vocabulary in awkward ways. If, however, we always print these words algorithmically we can assure that they are not repeated in the same section.]

To say comforting:
	say "[one of]comforting[or]reassuring[or]encouraging[at random]".
	
Section 3 - Gestures

Include Gesture Variations by Emily Short.

The gestures are eye-gest, breath-gest, and walk-gest.

[This is the same idea as the foregoing, except more abstract. The idea is, we don't want to describe her looking funny at you twice in a row, or walking somehow twice in a row, or... etc. So we keep track of what kind of gesture we last used and pick a different one. Text can be tagged with text variations of the form

   "[for eye-gest]She glares[or face-gest]She frowns[or walk-gest]She kicks the tree[end for]"

and this will both select the correct piece of text for the present and record for next time what to avoid in future iterations.

A handful of such texts are not marked up, on the grounds that the specific gesture is crucial to the passage in which it appears and sufficiently distinctive not to run on with the others.

]




Part 7 - Commands to NPCs

[This game is not designed to involve a lot of asking or telling Snow White to do things. She is, after all, bound up.

Nonetheless, there is some chance that the player will try these commands anyway, which means that for thoroughness we ought to provide sensible responses.]

Chapter 1 - Parsing Commands and Reprinting

Section 1 - Clarifying Directed Commands
 

After reading a command: 
    let N be indexed text; 
	let N be "[the player's command]"; 
    replace the regular expression "(ask|tell|order|instruct|beg|request|encourage) (him|her) to (.+)" in N with "\2, \3"; 
    replace the regular expression "(ask|tell|order|instruct|beg|request|encourage) (snow white|white|girl|princess) to (.+)" in N with "\2, \3"; 
    replace the regular expression "(ask|tell|order|instruct|beg|request|encourage) (happy|hart corpse|corpse|decomposing hart corpse) to (.+)" in N with "\2, \3";  
    change the text of the player's command to N.

After reading a command: [* This is to handle things like SNOW WHITE, SHOW ME YOUR TEETH. NB that the options in the regular expression must be in lower case because the player's command will have been flattened to LC before inspection, *but* regular expressions are case-sensitive by default. Rather than futz around with that, we just use lower case versions of the name.]
	let N be indexed text;
	let N be "[the player's command]"; 
	if N matches the regular expression ".*snow$": 
		change N to "[N] white";
		change the text of the player's command to N; 
	if N matches the regular expression "^(snow white|snow|white|princess|girl|vampire|lilith)\," :
		while the player's command includes "yourself":
			replace the matched text with "Snow White";
		while the player's command includes "your":
			replace the matched text with "Snow White's"; 
		[replace the regular expression "^(snow white|snow|white|princess|girl|vampire|lilith)\, " in N with "";
		now the direct command is N;]
	otherwise if N matches the regular expression "^(hart|happy|corpse|decomposing corpse|hart corpse|decomposing hart corpse)\," :
		while the player's command includes "yourself":
			replace the matched text with "corpse";
		while the player's command includes "your":
			replace the matched text with "corpse's"; 
			

Section 2 - Generating the Player's Command Line

The direct command is indexed text that varies. 

[The idea here is that we want to be consistent in having the game present both player and NPC dialogue -- even when that dialogue is a command that might be interpreted as direct speech. Moderating for mood, we can use this as an opportunity to sketch in the characterization a little more, too.]
 
Rule for printing the name of the player while setting up the player's command:
	say "me".
	
[Rule for printing the name of the current interlocutor while setting up the player's command:
	say "yourself".
	
Rule for printing the name of Snow White's heart while setting up the player's command:
	if the current interlocutor is the hart:
		say "her heart";
	otherwise:
		say "your heart". 
	
Rule for printing the name of Snow White while setting up the player's command:
	if the current interlocutor is the hart:
		say "[italic type]her[roman type]";
	otherwise:
		say "yourself". ]

Setting up the player's command is an activity.

For setting up the player's command:
	change the direct command to "[recap of command]"; [* This lets us tap into the same tricks the parser uses to recap the player's command]
	let N be word number 1 in direct command;
	if N is "d" or N is "down":
		change direct command to "dig a hole and descend into the earth";
	if N is "e" or N is "east":
		change direct command to "turn east";
	if N is "go" or N is "leave" or N is "run" or N is "walk":
		replace the regular expression " d$" in direct command with " down";
		replace the regular expression " e$" in direct command with " east";
		replace the regular expression " n$" in direct command with " North";
		replace the regular expression " ne$" in direct command with " Northeast";
		replace the regular expression " nw$" in direct command with " Northeast";
		replace the regular expression " s$" in direct command with " south";
		replace the regular expression " se$" in direct command with " southeast";
		replace the regular expression " sw$" in direct command with " southwest";
		replace the regular expression " u$" in direct command with " up";
	if N is "take":
		replace the regular expression "^take inventory" in direct command with "look through your things";
	if N is "n" or N is "north":
		change direct command to "continue north";
	if N is "ne" or N is "northeast":
		change direct command to "angle Northeast";
	if N is "nw" or N is "northwest":
		change direct command to "angle Northwest";
	if N is "s" or N is "south":
		change direct command to "turn back south";
	if N is "se" or N is "southeast":
		change direct command to "turn southeast";
	if N is "sw" or N is "southwest":
		change direct command to "turn southwest";
	if N is "u" or N is "up":
		change direct command to "climb something";
	if N is "w" or N is "west":
		change direct command to "turn west"; 
	if the current interlocutor is the corpse:
		replace the regular expression "your" in direct command with "my";
		replace the text "Snow White's" in direct command with "her";
		replace the text "snow white" in direct command with "her";
		replace the regular expression "the corpse's" in direct command with "your";
	otherwise:
		replace the regular expression "your" in direct command with "my";
		replace the text "Snow White's" in direct command with "your";
		replace the regular expression "the corpse's" in direct command with "its";
	replace the regular expression "myself's" in direct command with "your";
	[if the noun is enclosed by the current interlocutor:
		let Y be "[the noun]";
		replace the text Y in the direct command with "your [noun]";
	if the second noun is enclosed by the current interlocutor:
		let Y be "[the second noun]";
		replace the text Y in the direct command with "your [second noun]".]

A first persuasion rule (this is the producing instruction rule):
	carry out the setting up the player's command activity;
	if asking someone to try swearing mildly or asking someone to try swearing obscenely:
		say "'[direct command in sentence case]!'";
		make no decision;
		if the current quip is restrictive:
			refuse to change the subject for the current interlocutor;
			persuasion fails;
	issue command to the current interlocutor;		
	if the current quip is restrictive:
		refuse to change the subject for the current interlocutor;
		persuasion fails;
	make no decision.
	
To issue a/-- command to (target - Snow White): [* I'm using phrases rather than activities for this one because there is lower overhead, and I don't anticipate needing to do a lot of special-case overrides or before/after hooks on this particular thing.]
	if scariness is greater than 1 and a random chance of 1 in 3 succeeds:
		change direct command to "[one of]I'd like you to[or]please[or]do me a favor and[as decreasingly likely outcomes] [direct command]";
	if anger is greater than 1 and a random chance of 1 in 3 succeeds:
		change direct command to "[direct command], if [one of]you would[or]you don't mind[or]you'd be so kind[at random]";
	say "'[direct command in sentence case],' you [when warm]suggest[or weird]say[or menacing]suggest[or aggressive]beg[or furious]plead[or harmless]order[or needy]instruct her[or whiny]command, in your best hound-training voice[end when].";
	
To issue a/-- command to (target - new-snow-White): 
	change direct command to "[one of]why don't you[or]would you[or]how about if you[or]do you think you could[as decreasingly likely outcomes] [direct command]";
	say "'[direct command in sentence case]?' you ask[one of][or] gently[or] in your most soothing tone[as decreasingly likely outcomes].";
	
To issue a/-- command to (target - corpse): 
	change direct command to "[one of]how about if you[or]would you[or]do you think you could[or]er, could you[as decreasingly likely outcomes] [direct command]";
	say "'[direct command in sentence case]?' you ask.";
	
To issue a/-- command to (target - Snow White): [* I'm using phrases rather than activities for this one because there is lower overhead, and I don't anticipate needing to do a lot of special-case overrides or before/after hooks on this particular thing.]
	if scariness is greater than 1 and a random chance of 1 in 3 succeeds:
		change direct command to "[one of]I'd like you to[or]please[or]do me a favor and[as decreasingly likely outcomes] [direct command]";
	if anger is greater than 1 and a random chance of 1 in 3 succeeds:
		change direct command to "[direct command], if [one of]you would[or]you don't mind[or]you'd be so kind[at random]";
	say "'[direct command in sentence case],' you [when warm]suggest[or weird]say[or menacing]suggest[or aggressive]beg[or furious]plead[or harmless]order[or needy]instruct her[or whiny]command, in your best hound-training voice[end when].";


To refuse to change the subject for (target - new-snow-white):
	if asking someone to try swearing mildly or asking someone to try swearing obscenely:
		say "'Um.'";
	otherwise:
		say "[line break]'Don't change the topic.'".

To refuse to change the subject for (target - Snow White):
	now Snow White is already-talking; [This will prevent her also printing a nag line in this turn, because some of those juxtapositions sound redundant.]
	if asking someone to try swearing mildly or asking someone to try swearing obscenely:
		say "[line break][one of]She just stares coolly back at you. 'The question stands,' she says.[or]'I'm shocked,' she says, without being so at all. 'Now can we get back to the question?'[at random]";
	otherwise:
		say "[line break][one of]She just stares coolly back at you. 'The question stands,' she says.[or]'Don't try to change the subject.'[or]'One thing at a time,' she says.[at random]".

To refuse to change the subject for (target - corpse):
	say "[line break][one of][The current interlocutor] just snorts rudely.[or]'One thing at a time!' says [the current interlocutor].[at random]".	

Chapter 2 - Convertible Commands


Section 1 - For Snow White

[These are commands that can be converted to other, sensible actions and which we should therefore allow.]

Instead of asking Snow White to try kissing the player:
	try kissing Snow White.

Instead of asking Snow White to try examining something which is not the player:
	if an available quip mentions the noun:    
		try showing the noun to Snow White;
	otherwise:
		continue the action.   

Rule for implicitly taking Snow White's teeth when the person asked is Snow White:
	if Snow White recollects whether her teeth seem normal:
		follow the producing instruction rule; 
		say "[line break]'[one of]You have seen them once[or]I am not a horse for sale[or]Not again[at random],' she replies.";
		stop the action;
	otherwise:
		try discussing whether her teeth seem normal instead. 
	
[And since that should only happen once:] 
	
A persuasion rule for asking Snow White to try showing something which is part of Snow White to the player:
	persuasion succeeds.
	
Check Snow White showing something to the player: [* Because of the instead rule above, she'll show you the teeth anyway, regardless of mood.]
	if Snow White is furious or Snow White is aggressive:
		say "'[one of]Look from a distance[or]You can see well enough from there[or]You hardly need my help to use your eyes[or]See from where you are[at random],' she [one of]says[or]replies[or]retorts[or]snaps[or]says ungraciously[as decreasingly likely outcomes]." instead.
		
Report Snow White showing Snow White's wrists to the player:
	say "She turns so that (for the moment) her back is to you and you can see more easily.";
	try examining the noun instead.
	
Report Snow White showing Snow White's eyes to the player:
	say "She opens them very wide.";
	try examining the noun instead.
	
Report Snow White showing something to the player:
	say "She [one of]moves into the lamplight so you can see[or]lets you get a look[at random].";
	try examining the noun instead.

A persuasion rule for asking Snow White to try smiling:
	persuasion succeeds.
	
Report Snow White smiling:
	say "[Snow White smiles].[paragraph break]" instead.
	
A persuasion rule for asking Snow White to try listening to a room:
	persuasion succeeds.

Instead of Snow White listening to a room:
	say "She cocks her head for a moment as though attending to something very distant; then shrugs. Whatever she has heard, she doesn't think it worth comment.";
	rule succeeds.
	
	
Instead of asking Snow White to try walking properly:
	if Snow White recollects to walk properly:
		say "You've already tried that; if she's not cooperating, she probably won't.";
	otherwise if to walk properly is not flagged-ready:
		say "This doesn't seem the moment to lecture her about her style of motion.";
	otherwise:
		try discussing to walk properly.
		
Unsuccessful attempt by Snow White doing something: 
	repeat through Table of Retorts: 
		if the reason the action failed is the cause entry: 
			say "[response entry][paragraph break]"; 
			rule succeeds; 
	say "'I don't see how.'"

Table of Retorts 
cause	response   
can't take yourself rule	"'An absurd command.'" 
can't take what's fixed in place rule	"[physical impossibility]"   
can't take scenery rule	"[physical impossibility]"   
can't take what's already taken rule	"[already done]"   
can't drop what's already dropped rule	"[already done]"   
can't wear what's already worn rule   	"[already done]"   
can't take off what's not worn rule   	"[already done]"   
can't close what's already closed rule   	"[already done]"   
can't open what's already open rule   	"[already done]"   
can't switch off what's already off rule   	"[already done]"   
can't switch on what's already on rule   	"[already done]"   
can't unlock what's already unlocked rule   	"[already done]"   
can't lock what's already locked rule   	"[already done]"    
carrying requirements rule	"[not-free]"

To say physical impossibility:
	say "She eyes [the noun]. 'Even if my hands were untied, I'd need the strength of a [one of]demoness[or]giantess[or]monster[at random].'";

To say already done:
	say "'Redundant.'"
	
To say not-free:
	say "'You speak as though I were free to move and pick up whatever I liked.'";
	casually queue pleading-to-be-untied.
	
Section 2 - For the hart

Instead of asking hart to try kissing the player:
	try kissing hart.
	

	

		
Instead of asking the corpse to try walking properly:
	if the corpse is the current interlocutor:
		say "Considering that you are responsible for his partial dismemberment, you're hardly in a position to comment on any irregularities of gait he might now display.";
	otherwise:
		say "Mostly he seems inclined to remain very still on the ground."
	
Section 3 - New Snow White

Instead of asking new-Snow-White to try doing something:
	say "She just looks at you, exhausted."
	
Instead of asking new-snow-white to try walking properly:
	say "It was probably the demon causing all the trouble in the first place."


Chapter 3 - Refusals

Section 1 - Snow White

[These are commands that don't make sense or don't appeal: Snow White should have some reaction to these.]
	
A persuasion rule for asking Snow White to try swearing obscenely or swearing mildly: 
	say "[one of]She looks coy. 'I know [italic type]much[roman type] better ones than that.'[or]'If you're curious,' she says, 'my favorite is ?afa??d??[swear-word].'[or]'Sorry,' she says. 'One per customer.'[stopping]";
	persuasion fails.

rhafanido is a questioning quip.  
 Understand "rhafanidou" or "rhaphanidou" or "rafanidou" or "raphanidou" as rhafanido.
 The comment is "[one of]'What was that word? ?afa??d??;'[or]'?afa??d??,' you repeat with gusto.[or]'Yes, all right, but what does the word mean?'[or]'Tell me again about your naughty word.'[stopping]".
 The response is "[one of]'Yes, that's more or less right,' she says.[or]'Excellent.'[or]'It invites the listener to forcibly apply a radish,' she says. 'Up his own, er, fundament. A popular punishment for adultery, or so I am told.'[or]'I've said enough.'[stopping]".
 It quip-supplies Snow White.
 It is repeatable.
 It assumes swear-word.

Understand "rhafanidou" or "rhaphanidou" or "rafanidou" or "raphanidou" as a mistake ("[unless the current interlocutor is Snow White]Nothing happens.[otherwise]You give it your best try.[paragraph break]'You need to work on the pronunciation.'[end if]") when the player knows swear-word.

A persuasion rule for asking Snow White to try dropping something which is not enclosed by Snow White:
	say "'Not mine to start with,' she points out.";
	persuasion fails.

A persuasion rule for asking Snow White to try kissing the corpse:
	if the player knows hart-no-longer-undead:
		say "[one of]'Once per customer,' she replies gaily. 'We can't have the townfolk casting doubt on my virtue.'[or]It looks like you're out of luck when it comes to reviving [the corpse].[stopping]";
	otherwise if the quip-next-queued is thank-the-hart:
		say "'Be careful what you ask for.'";   [In fact I'm pretty sure this branch is not reachable]
	otherwise:
		say "[one of]'I wonder, do you also take in the Three-Dwarf Peepshow at the fair? I'm told that for people of elastic tastes it is well worth the five coppers... No, don't answer; I'd prefer not to dwell on the topic. Let us rather pretend that you never asked.'[or]She feigns oblivion.[stopping]";
	persuasion fails;

A persuasion rule for asking Snow White to try waking the corpse:
	say "[one of]'I don't know what you mean.'[or]'It is obviously dead.'[or]'It's not [italic type]asleep[roman type]. A hunter should recognize death.'[at random]";
	persuasion fails.

Rule for printing the name of Snow White's heart when asking Snow White to try doing something:
	say "heart".

A persuasion rule for asking Snow White to try taking or dropping or pulling or pushing or turning or swinging or waving something:
	if the noun is part of Snow White:
		say "'[one of][Cap That-those of noun][or]My [short version of noun][or][Cap it-they of noun][at random]";
		say " [one of][if the noun acts plural]don't[otherwise]doesn't[end if] come off.'[or][is-are]n't removable.'[or][if the noun acts plural]don't[otherwise]doesn't[end if] unhook from the rest of me.'[at random]";
	otherwise if Snow White wears the noun:
		say "'I already have [the noun],' she says. 'But I'm not giving it to anyone.'";
	otherwise if the noun is far-off:
		say "'That would be a nice trick, wouldn't it?'";
	otherwise if the noun is scenery or the noun is fixed in place:
		if the player knows snow-white-possessed:
			say "She eyes you with unnerving amusement[scary]. 'No.'";
		otherwise:
			say "[physical impossibility]";
	otherwise:
		say "[impossible-due-to-bonds]";
	persuasion fails.
	
A persuasion rule for asking Snow White to try smelling something which is part of Snow White:
	say "'You're a fine one to talk about bathing standards.'";
	persuasion fails.
	
A persuasion rule for asking Snow White to try smelling something which is worn by Snow White:
	say "'My laundry maid has been inattentive of late.'";
	persuasion fails.
	
A persuasion rule for asking Snow White to try searching or looking under the green dress:
	say "'Thanks, but I know what I will find.'";
	persuasion fails.
	
A persuasion rule for asking Snow White to try closing Snow White's eyes:
	say "[when warm]'I'd rather be able to see if anyone is coming.'[or weird]'Thanks, but no surprises just now.'[at other times]'I am not in a trusting mood.'[end when]";
	persuasion fails.
	
A persuasion rule for asking Snow White to try opening Snow White's eyes:
	say "'They're wide open.'";
	persuasion fails.
	
A persuasion rule for asking Snow White to try tasting or consuming an inedible thing:
	say "[one of]'A meal of [noun with selected length] isn't to my taste, frankly.'[or]'I only eat [noun with selected length] with [one of]plenty of salt[or]honey[or]strawberry jam[at random].'[or]'I see why you're a huntsman and not a chef.'[or]'I've seen small boys dare each other to eat worms, but I would have thought you were past that age.'[or]She merely sighs, bored with this exchange.[stopping]";
	persuasion fails.
	
To say (N - a thing) with selected length:
	say "[if N is a body part][short version of N][otherwise][N][end if]";
	
A persuasion rule for asking Snow White to try doing something to the silver chain:
	say "All you get for your suggestion is an inarticulate growl[angry].";
	persuasion fails.
	
A persuasion rule for asking Snow White to try doing something to the breath:
	say "She pretends not to hear you. Perhaps she's sensitive about her odor.";
	persuasion fails.
	
A persuasion rule for asking Snow White to try drinking veins:
	if the player knows vampirism-revealed:
		say "[first-vampire-accusation]";
	if Snow White is likely to continue:
		say "[angry][one of]'If you suspect a hole of being a dwarrow shaft, do you jump in to test it? You are making an idiot of yourself and seem not even to realize it.'[or]'You are making a fool of yourself.'[stopping]";
	otherwise:
		say "[angry][for eye-gest]She looks at you from under heavy lidded eyes and does not say anything[or walk-gest]She rocks back on her heels but says nothing[or breath-gest]She exhales unpleasant breath at you but says nothing[end for]. After a moment you are uncomfortable enough to turn away.";
	persuasion fails.
	
A persuasion rule for asking Snow White to try drinking a limited-use thing which is not veins:
	if the noun is the hart's heart:
		make no decision;
	say "[one of]'You may be mad, but I am not.'[or]'Is that an idea or just random words?'[or]'If you're going to waste your breath giving me orders, they might as well at least be orders that make sense,' she says [when whiny]petulantly[or needy]sulkily[at other times]bluntly[end when].[or]'That doesn't even make sense.'[at random]";
	persuasion fails.
	
A persuasion rule for asking Snow White to try opening or closing or locking or unlocking or switching on or switching off or burning or cutting or consuming or attacking or entering or wearing or getting off or taking off or searching or climbing or looking under a limited-use thing:
	if the noun is the hart's heart:
		make no decision;
	say "[one of]'You may be mad, but I am not.'[or]'Is that an idea or just random words?'[or]'If you're going to waste your breath giving me orders, they might as well at least be orders that make sense,' she says [when whiny]petulantly[or needy]sulkily[at other times]bluntly[end when].[or]'That doesn't even make sense.'[at random]";
	persuasion fails.

A persuasion rule for asking Snow White to try doing something to Snow White:
	say "She sticks her tongue out at you.";
	persuasion fails.
	
A persuasion rule for asking Snow White to try taking off something which is not worn by Snow White:
	say "'I'm not wearing [the noun],' she says. '[if the noun is part of a person]But if I were, I'd certainly consider removing [it-them] again[otherwise if the noun is not wearable]And frankly I don't think [it-they] would make much of a hat[otherwise]Not really my style[end if].'";
	persuasion fails.
	
Definition: a thing is limited-use:
	if it is covered:
		yes;
	if it is part of a person:
		yes;
	if it is far-off:
		yes.
	
Definition: a thing is general-use if it is not limited-use.

A persuasion rule for asking Snow White to try listening to or searching or opening or closing Snow White's heart:
	say "'If you have [one of]a question for me, just ask[or]advice, say so (not that I promise to take it)[at random]. But don't try to be poetic.'";
	persuasion fails.
	
A persuasion rule for asking Snow White to try buying the corpse:
	say "'[one of]I have no use for a dead stag.'[or]I am a choosy eater.'[at random]";
	persuasion fails.
	
A persuasion rule for asking Snow White to try buying something which is part of the corpse:
	say "[one of]'Sorry — if you're looking to sell your hunting prize, you'll have to take it to one of the villagers.'[or]The Queen might like a dead stag, I suppose.'[stopping]";
	persuasion fails.
	
A persuasion rule for asking Snow White to try buying Snow White:
	say "'I like to think I am already my own.'";
	persuasion fails.
	
A persuasion rule for asking Snow White to try buying something which is part of Snow White:
	say "'I like to think I am already my own.'";
	persuasion fails.
	
A persuasion rule for asking Snow White to try buying a scenery thing:
	say "'All this will be mine by right when the Queen passes it on,' she says. 'So there's no need to buy anything.'";
	persuasion fails.
	
A persuasion rule for asking Snow White to try taking off the dress:
	say "She gives you a very dubious look[casually queue no-rape].";
	persuasion fails.

A persuasion rule for asking Snow White to try listening to the player:
	say "[if anger is less than 0]'I have [one of]been listening[or]heard every word you said[at random],' she says.[otherwise]'I don't have much choice, do I? Though you have yet to say anything of very great interest.'[end if]";
	persuasion fails.
	
A persuasion rule for asking Snow White to try climbing the trees:
	say "[impossible-due-to-bonds]";
	persuasion fails.
	
A persuasion rule for asking Snow White to try jumping or waving hands:
	say "[impossible-due-to-bonds]";
	persuasion fails.
	
A persuasion rule for asking Snow White to try switching on or switching off or drinking something:
	persuasion succeeds. [* It'll just fall through to an 'impossible' refusal since none of these are possible. ]
	
A persuasion rule for asking Snow White to try tying something to something:
	if the noun is limited-use or the second noun is limited-use:
		let A be a random limited-use thing which is part of yourself;
		let B be a random limited-use thing which is part of the hart;
		let C be a random limited-use thing which is part of Snow White; 
		say "'Why don't I just juggle your [short version of A], the hart's [short version of B], and my own [short version of C] while I'm at it?' she retorts.";
	otherwise:
		say "'I am not in favor of tying things up just at the moment.'";
	persuasion fails.

A persuasion rule for asking Snow White to try tying the gold embroidery to the green dress:
	say "'It's hard to see how it could be more securely tied than it is right now,' she says.";
	persuasion fails.

A persuasion rule for asking Snow White to try touching or rubbing or swinging or waving or removing or squeezing or cutting or burning or attacking or consuming or tasting something:
	if the noun is far-off:
		persuasion succeeds; [* The attempt won't work, but she'll announce its impossibility in a moment.]
	otherwise:
		say "[impossible-due-to-bonds]";
		persuasion fails.
		
A last persuasion rule when the person asked is Snow White:
	if the mood of Snow White is:
		-- furious:
			say "[one of]She doesn't look very cooperative[or]She looks downright mutinous[or]She fumes silently, and doesn't grace this with a reply[at random].";
		-- menacing:
			say "[for eye-gest]One look from her reminds you that you are not really in a position to give her orders.[or breath-gest]She growls.[or walk-gest]She simply turns away from you[end for]";
		-- aggressive:
			say "[for breath-gest]She growls in the back of her throat. The sound is uncannily like that of your hounds, when provoked.[or eye-gest]Her eyes flash. 'No.'[or walk-gest]She leans against a tree as though to say, 'watch me not doing what you asked.'[end for]";
		-- whiny:
			say "[one of]'I'm tired,' she says.[or]She pouts.[or]'If you can't be any help, leave me alone.'[or]She fakes a few sobs. 'I am sooo tired.'[or]'You can't be serious. All I want to do is sleep. And eat something. But mostly sleep.'[at random]";
		-- needy:
			say "[one of]'I'm too tired,' she says.[or]'Hmf!' She turns a shoulder on you[or]'I just want to sleep.'[or]'I'm exhausted and I haven't had anything to eat,' she says. 'I'm not up for doing tricks.'[at random]";
		-- warm:
			say "[one of]'I doubt it would help our situation,' she remarks.[or]'Sorry,' she replies.[at random]";
		-- weird:
			say "[one of]'Do you suppose it will be effective, giving me orders?' she asks rhetorically.[or]She raises her eyebrows.[stopping]";
		-- otherwise:
			say "[one of]She shakes her head.[or]'I think not.'[or]'No.'[or]'I'd prefer not to.'[at random]";
	persuasion fails.

To say impossible-due-to-bonds:
	say "[one of][conditionally queue pleading-to-be-untied]She [for eye-gest]looks at you incredulously[or breath-gest]snorts[or walk-gest]spins half around to show you her bonds[end for]: there is of course no way she could do this with her hands tied behind her back.[or]'Again, not possible, for reasons that are not my fault.'[or]She [one of]rattles her chain pointedly[or]glares at you[or]motions to indicate her pinioned arms[or]makes a pantomime of trying to struggle free[at random].[conditionally queue stop-taunting][stopping]";
	
Unsuccessful attempt by Snow White doing something:
	say "'[one of]Impossible[or]Improbable[or]Unlikely[or]Inconceivable[as decreasingly likely outcomes],' she says [when warm]with amusement[or whiny]bitterly[or needy]with a bit of a pout[at other times]coldly[end when]."
	
Section 2 - Hart

A persuasion rule for asking corpse to try examining something which is not the player:
	say "'Seen [it-them of noun], thanks.'";
	persuasion fails.
	
A persuasion rule for asking corpse to try listening to a room:
	say "[one of]'There's nothing moving out there, bucko,' he says. 'We're out of touch with the rest of time.'[or]'Let me break this down for you: there's nothing to hear. If I hadn't exempted this space around us, we wouldn't even be able to hear one another, or see that lantern of yours, because no sound or light would be moving around.'[or]He just rolls his eyes.[stopping]";
	persuasion fails.

To say nonsense-lead:
	say "'Look, I know you're the forgetful type, but that request suggests full-blown senility.'"

A persuasion rule for asking corpse to try dropping something which is not enclosed by corpse:
	say "[nonsense-lead]";
	persuasion fails.

A persuasion rule for asking corpse to try waking the corpse:
	say "'I'm awake, already.'";
	persuasion fails.

A persuasion rule for asking corpse to try taking or dropping or pulling or pushing or turning or swinging or waving something:
	if the noun is part of corpse:
		say "[nonsense-lead]";
	otherwise if the noun is far-off:
		say "[nonsense-lead]";
	otherwise if the noun is scenery or the noun is fixed in place:
		say "[nonsense-lead]";
	otherwise:
		say "[one of]'In case you hadn't noticed, this body doesn't come with opposable thumbs.'[or]'This body isn't up to that.'[or]'How would I do that? Balance [the noun] on my nose?'[at random]";
	persuasion fails.
	
A persuasion rule for asking corpse to try doing something to Snow White:
	say "'Don't worry about her. She's not going anywhere.'";
	persuasion fails.
	
A persuasion rule for asking corpse to try doing something to something which is part of Snow White:
	say "'My power keeps her completely contained,' he says. 'But we can't reach in, either.'";
	persuasion fails.  
	
A persuasion rule for asking corpse to try doing something to something which is part of something which is part of Snow White:
	say "'My power keeps her completely contained,' he says. 'But we can't reach in, either.'";
	persuasion fails.  
	
A persuasion rule for asking corpse to try doing something to something which is worn by Snow White:
	say "'That light-box you see keeps her completely contained,' he says. 'But we can't reach in, either.'";
	persuasion fails.  
	
A persuasion rule for asking corpse to try tasting or consuming or drinking something:
	say "'Ever tried eating when most of your innards have become outards?' he asks conversationally. 'It's not attractive.'";
	persuasion fails.
	
A persuasion rule for asking corpse to try opening or closing or locking or unlocking or switching on or switching off or burning or cutting or consuming or drinking or attacking or entering or wearing or getting off or taking off or searching or climbing or looking under a limited-use thing:
	say "[nonsense-lead]";
	persuasion fails.
	
Definition: a thing is limited-use:
	if it is covered:
		yes;
	if it is part of a person:
		yes;
	if it is far-off:
		yes.
	
Definition: a thing is general-use if it is not limited-use.

A persuasion rule for asking corpse to try taking the corpse:
	say "'How would that work?'";
	persuasion fails.

A persuasion rule for asking corpse to try looking under the antlers:
	say "He rolls his eyes all the way back up in his head. 'Nope, nothing interesting up there.'";
	persuasion fails.

A persuasion rule for asking corpse to try taking off the cavity:
	say "'Your sense of humor leaves something to be desired sometimes, you know that?'";
	persuasion fails.

A persuasion rule for asking corpse to try wearing the cavity:
	say "'I already seem to be doing that. I don't suppose you'd care to swap outfits.'";
	persuasion fails.

A persuasion rule for asking corpse to try taking off the antlers:
	say "'I know you'd like some of your own, but it just isn't going to work out. I recommend you get a nice wig instead.'";
	persuasion fails.

A persuasion rule for asking corpse to try wearing the antlers:
	say "'They're already a permanent fashion accessory. Try not to be too jealous.'";
	persuasion fails.

A persuasion rule for asking corpse to try listening to or searching or opening or closing hart's heart:
	say "'I'm going to pretend that was just a really tasteless joke.'";
	persuasion fails.
	
A persuasion rule for asking corpse to try buying the corpse:
	say "[nonsense-lead]";
	persuasion fails.
	
A persuasion rule for asking corpse to try buying something which is part of the corpse:
	say "'[The noun] belong[s] to me, friend.'";
	persuasion fails.
	
A persuasion rule for asking corpse to try buying corpse:
	say "'I like to think I am already my own.'";
	persuasion fails.
	
A persuasion rule for asking corpse to try buying a scenery thing:
	say "[one of]'This obsession with owning the landscape is more of a human thing.'[or]'Was my previous answer not spiritual enough? Let's say that I'm at one with the cosmos now.'[or]'I can see you're hard up for cash, but it's not as though there's anywhere to spend it here anyway. Let it go.'[or]'This conversation is turning out to be much less interesting than I hoped.'[or]He just sighs.[stopping]";
	persuasion fails. 
	
A persuasion rule for asking corpse to try listening to the player:
	say "[if anger is less than 0]'I have [one of]been listening[or]heard every word you said[at random],' she says.[otherwise]'I don't have much choice, do I? Though you have yet to say anything of very great interest.'[end if]";
	persuasion fails.
	
A persuasion rule for asking corpse to try climbing the trees:
	say "'If this body could climb trees, I don't think it would be in this position to start with.'";
	persuasion fails.
	
A persuasion rule for asking corpse to try jumping or waving hands:
	say "'I'm not big on the cavorting.'";
	persuasion fails.
	
A persuasion rule for asking corpse to try switching on or switching off or touching or rubbing or swinging or waving or removing or squeezing or cutting or burning or attacking something:
	say "[nonsense-lead]";
	persuasion fails.
	
A persuasion rule for asking corpse to try tying something to something:
	say "[one of]He holds up one hoof. 'I'll just tie a nice bow with this, shall I?'[or][nonsense-lead][stopping]";
	persuasion fails. 
		
A last persuasion rule when the person asked is corpse:
	say "'Maybe you haven't grasped this,' he says, 'but I'm past the point of wanting to do tricks for anyone.'"
	
Unsuccessful attempt by the corpse trying doing something:
	say "'That's not happening.'"

Chapter 4 - Nonsense Commands

[Currently through a bug in the Standard Rules NPCs do not offer any response to being given instructions that are actions out of world. We want to fix that.]

Check asking someone to try quitting the game:
	say "[out of world refusal]" instead.

To say out of world refusal:
	if the current quip is restrictive:
		say "[The current interlocutor] ignores your attempt to change the subject.";
	otherwise:
		if the current interlocutor is the hart:
			say "(You invite her to speak a magic incantation. 'I've already put us in stasis,' says the hart. 'You have something more in mind, you'll have to do it yourself.')[line break]";
		otherwise if the current interlocutor is new-Snow-White:
			say "(You invite her to speak a magic incantation. She just looks back at you with large eyes.)[line break]";
		otherwise:
			say "(You invite her to speak a magic incantation. Her eyes flash — a challenge! 'I could, you know,' she says. 'But then where would you be?')[line break]".
	
Check asking someone to try restoring the game:
	say "[out of world refusal]" instead.
	
Check asking someone to try saving the game:
	say "[out of world refusal]" instead.
	
Check asking someone to try restarting the game:
	say "[out of world refusal]" instead.
	
Check asking someone to try requesting the score:
	if the current quip is restrictive:
		say "[The current interlocutor] ignores your attempt to change the subject." instead;
	if the current interlocutor is the hart:
		say "('What's my score so far?' you ask. 'Surely you know.'
	
'There is no score,' he says. 'But you're closer to the end of your life than you were a minute ago, I can tell you that.')[line break]" instead;
	otherwise if the current interlocutor is new-Snow-White:
		say "('What's my score so far?' you ask.
	
'Score?' she repeats, confused.)[line break]";
	otherwise:
		say "('What's my score so far?' you ask. 'Surely you know.'
	
'Life doesn't work that way,' she says, in a drippingly condescending tone of voice.)[line break]" instead.
	
Check asking Snow White to try requesting the story file version:
	if the current quip is restrictive:
		say "She ignores your attempt to change the subject." instead;
	if the player knows snow-white-possessed:
		say "('What version are we in?'

'This is my 10,458th body,' she says. 'I think. It becomes difficult to keep them straight.')[line break]" instead;
	otherwise:
		say "('What version are we in?'

'That would be telling,' she says.)[line break]" instead.

Check asking someone to try requesting the story file version:
	if the current quip is restrictive:
		say "[The current interlocutor] ignores your attempt to change the subject." instead;
	if the current interlocutor is new-Snow-White:
		say "[out of world refusal]" instead;
	otherwise:
		say "('What version are we in?'

'Currently [release number in words],' he says.)[line break]" instead.

Check asking someone to try verifying the story file:
	if the current quip is restrictive:
		say "She ignores your attempt to change the subject." instead;
	otherwise:
		if the current interlocutor is the hart:
			say "('Is the cosmos properly constructed?' you ask.
			
'Does that question even have meaning?' he retorts.)[line break]" instead;
		otherwise if the current interlocutor is new-snow-white:
			say "('Is the cosmos properly constructed?' you ask.

'Considering what has happened to me so far, I'd say no.')[line break]" instead;
		otherwise:
			say "('Is the cosmos properly constructed?' you ask.

'I've always thought there was something a little off about the universe,' she remarks. 'But we shouldn't be discussing this, so let's pretend it never happened.')[line break]" instead.
		
Check asking someone to try switching the story transcript on:
	if the current quip is restrictive:
		say "[The current interlocutor] ignores your attempt to change the subject." instead;
	otherwise:
		if the current interlocutor is Snow White:
			say "('Keep track of what we're saying and doing,' you suggest. 'I'd find it handy.'

'I'm a little tied up,' she points out. 'It hampers my abilities as amanuensis.')[line break]" instead;
		otherwise:
			say "[out of world refusal]" instead;
		
Check asking someone to try switching the story transcript off:
	if the current quip is restrictive:
		say "[The current interlocutor] ignores your attempt to change the subject." instead;
	otherwise:
		if the current interlocutor is Snow White:
			say "('I've had enough of transcribing all my deeds,' you say. 'I'd prefer to be forgotten by history. Could it be arranged?'

'You'll have to work that spell yourself.')[line break]" instead;
		otherwise:
			say "[out of world refusal]" instead;
		
Check asking someone to try switching score notification on: 
	try asking the person asked to try requesting the score instead.
		
Check asking someone to try switching score notification off: 
	try asking the person asked to try requesting the score instead.
	
Check asking someone to try preferring abbreviated room descriptions:
	if the current quip is restrictive:
		say "[The current interlocutor] ignores your attempt to change the subject." instead;
	otherwise:
		say "('I'd like to see the world through different words.'
	
[if the current interlocutor is new-Snow-White]'Eh?'[otherwise]'You'll have to work that spell yourself.'[run paragraph on][end if])[line break]" instead.
		
Check asking someone to try preferring unabbreviated room descriptions:
	try asking the person asked to try preferring abbreviated room descriptions.

Check asking someone to try preferring sometimes abbreviated room descriptions:
	try asking the person asked to try preferring abbreviated room descriptions.
	
Check asking someone to try requesting the pronoun meanings:
	if the current quip is restrictive:
		say "[The current interlocutor] ignores your attempt to change the subject." instead;
	otherwise:
		if the current interlocutor is Snow White:
			say "('Do you ever get confused about what we were just talking about, and who is he and she and it, and all of that?'

She makes a face. 'No more than is ordinary, I suppose.')[line break]" instead;
		otherwise:
			say "[out of world refusal]" instead.

[Check asking someone to try turning on dialogue hinting:
	try asking the person asked to try preferring abbreviated room descriptions.

Check asking someone to try turning off dialogue hinting:
	try asking the person asked to try preferring abbreviated room descriptions.]
	
Check asking someone to try asking for help:
	if the current quip is restrictive:
		say "She ignores your attempt to change the subject." instead;
	otherwise: 
		if the current interlocutor is:
			-- Snow White:
				say "('I'd like guidance,' you say.

'Pity.')[line break]" instead;
			-- corpse:
				say "('I'd like guidance,' you say.

'I'm trying to help you,' he says. 'But you're not turning out very easy to guide.')[line break]" instead;
			-- new-Snow-White:
				say "('I'd like guidance,' you say.

'I can't help you.')[line break]" instead.

Part 8 - Other Preliminaries

[Include Implicit Actions by Eric Eve. ] [* This is a cool extension, but probably overkill for the actual amount of implicit activity this game needs. Also, it incorrectly overrides the Standard Rules' handling of NPC implicit actions.]

Chapter 1 - Distant Things

A thing can be far-off or nearby. A thing is usually nearby.

This is the distant-reach rule: 
	if asking someone to try doing something:
		make no decision;
	[if an actor analyzing:
		make no decision;]
	if the action requires a touchable noun and the noun is far-off:
		if the actor is the player:
			say "You can't possibly reach [the noun]." instead; 
		rule fails;
	if the action requires a touchable second noun and the second noun is far-off:
		if the actor is the player:
			say "You can't possibly reach [the second noun]." instead;
		rule fails;

The distant-reach rule is listed before the basic accessibility rule in the action-processing rules.


Chapter 2 - Concealed Things

A thing can be covered or uncovered. A thing is usually uncovered. [These are for Snow White's internal organs]

This is the robed-reach rule: 
	if cutting something:
		make no decision;
	if cutting something with something: 
		make no decision;
	if asking someone to try doing something:
		make no decision;
	if the action requires a touchable noun and the noun is covered:
		if the actor is the player:
			say "[The noun] [is-are] not kept on the surface." instead; 
		rule fails;
	if the action requires a touchable second noun and the second noun is covered:
		if the actor is the player:
			say "[The second noun] [is-are] not kept on the surface." instead;
		rule fails.

The robed-reach rule is listed before the basic accessibility rule in the action-processing rules.



Chapter 3 - Acting on characters during the stopped time

The stopped-time-effects rule is listed before the basic accessibility rule in the action-processing rules.

This is the stopped-time-effects rule: 
	if Stopped-time is not happening, make no decision;
	if igniting something with the lantern:
		say "The lantern is glowing but the flame does not leap or flicker: you cannot burn anything, with time held in this way." instead;
	if the action requires a touchable noun: 
		if the noun is Snow White or the noun is part of Snow White or the noun is enclosed by Snow White:
			say "Snow White is impossible to approach at the moment." instead; 
		if the noun is part of the corpse:
			if the actor is the player:
				say "You only get as far as reaching for [the noun]. [corpse-objection]." instead;
			otherwise:
				say "'I don't want anything to fall apart more than it has,' he says." instead;
	if the action requires a touchable second noun:
		if the second noun is Snow White or the second noun is part of Snow White or the second noun is enclosed by Snow White:
			say "Snow White is impossible to approach at the moment." instead;
		if the second noun is part of the corpse:
			if the actor is the player:
				say "You only get as far as reaching for [the second noun]. [corpse-objection]." instead;
			otherwise:
				say "'I don't want anything to fall apart more than it has,' he says." instead.
			
To say corpse-objection:
	say "[one of]'Hey!' he says, knocking you away[or]'Keep your hands to yourself, mister,' he says[or]'Someone here was brought up in a barn,' he observes[at random]".


Instead of saying hello to Snow White during Stopped-time:
	say "You try waving to get her attention. [one of]She doesn't respond[or]Her expression remains fixed[stopping][casually queue not-talking-now]."


Chapter 4 - The Power of Observation

A thing can be unexamined or examined. A thing is usually unexamined.

Carry out examining something:
	now the noun is examined.
	

Chapter 5 - Protruding 

A thing can be protruding or flush. A thing is usually protruding.

Instead of tying a flush thing to something:
	say "[The noun] [if the noun acts plural]don't[otherwise]doesn't[end if] offer much scope for tying."

Instead of tying something to a flush thing:
	say "[The second noun] [if the second noun acts plural]don't[otherwise]doesn't[end if] offer much scope for tying."
	
Instead of swinging or waving a flush thing:
	say "[The noun] [is-are]n't really the kind of thing you can get a satisfying swing out of."
	
Check pulling a flush thing:
	say "[The noun] [if the noun acts plural]don't[otherwise]doesn't[end if] offer much of a handle for pulling." instead.
	
Check pushing a flush thing:
	say "[The noun] [if the noun acts plural]don't[otherwise]doesn't[end if] offer much leverage for pushing." instead.

Check turning a flush thing:
	say "[The noun] [if the noun acts plural]don't[otherwise]doesn't[end if] really twist as such." instead.
	
Check climbing a flush thing:
	if the noun is part of a person:
		say "[going-crazy]" instead;
	say "[The noun] [is-are] not rich in [one of]toe-holds[or]finger-grips[at random]." instead.

Chapter 6 - Body Parts

A body part is a kind of thing.

A nose is a kind of body part. A nose is part of every person. A nose is usually flush. Understand "nostril" or "nostrils" as a nose. Understand "her nose/nostril/nostrils" as nose when the item described is part of Snow White or the item described is part of new-snow-white.

A face is a kind of body part. A face is part of every person. A face is usually flush. Understand "head" or "expression"or "cheek" or "cheeks" or "temple" or "temples" or "bone structure" as a face. Understand "her head/expression/face/cheek/cheeks/temple/temples" as face when the item described is part of Snow White or the item described is part of new-snow-white.

A chin is a kind of body part. A chin is part of every person. A chin is usually flush. Understand "jaw" or "jawbone" as a chin. Understand "her chin/jawbone/jaw" as chin when the item described is part of Snow White or the item described is part of new-snow-white.

[A head is a kind of body part. A head is part of every person.]
	
A skin is a kind of body part. A skin is part of every person. The indefinite article is "some". Skin is usually flush. Understand "her skin" as skin when the item described is part of Snow White or the item described is part of new-snow-white.

Some ears are a kind of body part. Ears are a part of every person. Understand "ear" and "right ear" and "left ear" as ears.  Understand "her ear/ears" or "her left/right ear" as ears when the item described is part of Snow White or the item described is part of new-snow-white.

A hair is a kind of body part. Hair is part of every person. The indefinite article is "some". Understand "locks" or "lock" or "lock of hair" as hair.  Understand "her hair/locks/lock" or "lock of her hair" as hair when the item described is part of Snow White or the item described is part of new-snow-white.

Some wrists are a kind of body part. Wrists are a part of every person. Understand "wrist" and "left hand" and "right hand" and "left wrist" and "right wrist" and "hands" and "fists" and "hand" and "fist" and "arm" and "arms" and "forearm" and "forearms" as wrists. [* In this game, it is not really worth implementing separate individual wrists.]   

Understand "her wrist/hand/hands/wrists/arm/arms/fist/fists/forearm/forearms" or "her left/right wrist/hand/wrists/hands" as wrists when the item described is part of Snow White or the item described is part of new-snow-white.

A neck is a kind of body part. A neck is part of every person. Understand "throat" or "collarbone" or "collarbones" as a neck. Understand "her neck/throat/collarbone/collarbones" as a neck when  the item described is part of Snow White or the item described is part of new-snow-white.

Some veins are a kind of body part. The printed name is "[if the item described is part of the corpse]hart's[otherwise if the item described is part of yourself]my[otherwise][random person who incorporates the item described]'s[end if] [if not mentioning blood]veins[otherwise]blood supplies[end if]". Veins are usually flush and covered. Veins are part of every person. Understand "vein" or "arteries" or "artery" or "blue-green" or "blue" or "green" or "jugular" as veins. Understand "blood" as veins when the wound is off-stage. [* We're doing a bit of juggling here, depending on what the player is most likely to be referring to as "blood" at any given time.]
 

 Understand "her veins/vein/artery/arteries/jugular/blood" as a neck when  the item described is part of Snow White or the item described is part of new-snow-white.

To decide whether mentioning blood:
	if the player's command includes "blood":
		yes;
	no.

Some lips are a kind of body part. Lips are usually flush. Lips are part of every person. Understand "mouth" or "tongue" or "lip" as lips. Understand "her mouth/tongue/lip/lips" as lips when the item described is part of Snow White. 

Some teeth are a kind of body part. The teeth are flush. Teeth are part of every person. Understand "tooth" as the teeth. Understand "her tooth/teeth" as teeth when  the item described is part of Snow White or the item described is part of new-snow-white.

Some eyes are a kind of body part. Some eyes are part of every person. Understand "eye" or "right eye" or "left eye" or "iris" or "pupil" or "irises" or "pupils" or "eyeball" or "eyeballs" as eyes. Understand "her eye/eyes/iris/pupil/eyeball/eyeballs" or "her left/right eye" as teeth when  the item described is part of Snow White or the item described is part of new-snow-white.


Understand "Snow's" or "princess'" or "princess's" or "girl's" as a body part when the item described is part of Snow White or the item described is part of new-snow-white.
Understand "her" as Snow White. Understand "her" as new-snow-white.

Understand "his" or "hart's" or "hart corpse's" or "decomposing hart corpse's" as a body part when the item described is part of the corpse.

Understand "my" as a body part when the item described is part of the player.

[And because sometimes we'll want to print 'neck,' 'veins', 'wrists,' etc. without the owner's name glued on first:]

To say short version of (sale item - a thing):
	let N be indexed text;
	let N be "[sale item]";
	let Y be the number of words in N;
	change N to "[word number Y in N]";
	say N.

Body-context is a thing that varies. The body-context is Snow White.

Before doing something to someone:
	now the body-context is the noun.
	
Before doing something to a body part which is part of someone (called owner):
	now the body-context is the noun.
	
To say set body-context to (target - a person):
	now the body-context is the target.

[This will be used for disambiguation (see the Disambiguation section below) so that the game will assume whenever you're looking at someone and then look at individual body parts, you are interested in the parts of the person to whom your attention turned.]

Chapter 7 - Replacing some standard rules

Section 1 - Replacement texts

Understand the command "lock" as something new.

Understand the command "unlock" as something new.

Understand "unlock [text]" as a mistake ("There is nothing about that needs locking and unlocking.").

Understand "lock [text]" as a mistake ("There is nothing about that needs locking and unlocking."). 

Understand the command "eat" as something new.

Understand "eat [something]" as consuming. [* This is because we want to get around the carrying requirements rule without using procedural rules, which work fine but have to fire too many times.] Consuming is an action applying to one thing.

Check an actor consuming the hart's heart:
	if the actor does not carry the noun:
		try the actor taking the hart's heart;
		if the actor does not carry the noun:
			stop the action.

Check an actor consuming a far-off thing:
	if the actor is not the player:
		stop the action;
	say "You couldn't possibly reach." instead.

Check an actor consuming a covered thing:
	if the actor is not the player:
		stop the action;
	say "You can't get at [the noun] to eat." instead. 

Check an actor consuming an inedible thing:
	if the actor is not the player:
		stop the action;
	say "[The noun] cannot be eaten." instead.
	
Carry out an actor consuming something:
	remove the noun from play.
	
Report someone consuming something:
	say "[The actor] eat[s] [the noun]."

Report consuming something:
	say "You eat [the noun]. Not bad."

Check buying a person:
	say "Slavery is not the custom of this kingdom." instead.

Check buying something which is part of a person (called the owner):
	say "[one of]It is unlikely [the owner] would care to sell.[or]You heard of a man with [unless the noun acts plural]a [end if][one of]wooden[or]copper[or]silver[or]golden[or]gem-encrusted[stopping] [short version of noun], but [unless the noun acts plural]that is[otherwise]those are[end if] the only [short version of noun] you have ever seen to have much worth separate from the owner.[or]No one has any market value for [noun] but [the owner], who might miss [it-them of the noun] very much.[or]It is improbable that you would be able to resell [the noun] for any very significant price.[at random]" instead. 

This is the new block buying rule:
	if the actor is the player:
		say "That is the lovely thing about the woods: there is no buying or selling here, no bickering or haggling, no politics.";
	stop the action.
	
The new block buying rule is listed instead of the block buying rule in the check buying rules.
	
This is the new block attacking rule:
	if the player is the actor:
		say "You prefer your violence to be directed and purposeful.";
	stop the action.
	
The new block attacking rule is listed instead of the block attacking rule in the check attacking rules.
	
Instead of switching on or switching off something:
	say "There is nothing about made of clockwork, such as could be switched on or off."
	
The new block climbing rule is listed instead of the block climbing rule in the check climbing rules.

This is the new block climbing rule:
	if the player is the actor:
		say "The relative sizes and stabilities of the components rule out that option.";
	stop the action.
	
Check dropping a scenery thing:
	say "Easily done, considering you were never carrying [the noun] in the first place." instead.

Check dropping something which is part of a person:
	say "[going-crazy]" instead.
	
This is the new block drinking rule:
	if the actor is the player:
		say "If only [the noun] were [one of][one of]pureed[or]juiced[or]pressed[or]melted[purely at random] into a tasty beverage[or]made of [one of]ale[or]wine[or]rainwater[or]small beer[or]cider[or]goat's milk[or]sweet, refreshing blood... Aie, that thought was not your own[purely at random][at random]." instead;
	otherwise:
		stop the action.
	
The new block drinking rule is listed instead of the block drinking rule in the check drinking rulebook.

Before wearing something which is part of a person (called the owner):
	say "Just for a moment you imagine [noun] grafted onto your own body. [one of][going-crazy][or]Now you think of it, that could be rather fetching.[or][going-crazy][stopping]" instead. 
	
Check opening something which is part of a person:
	say "[going-crazy]" instead.
	
Check closing something which is part of a person:
	say "[going-crazy]" instead.

Check entering something which is part of a person:
	say "[going-crazy]" instead.
	
To say going-crazy:
	say "[one of]You do realize that fear and sleeplessness and the long march without food are affecting your mind?[or]The idea is mad.[or][one of]Sheer[or]Complete[or]Utter[or]Total[or]Barking[or]Baying[or]Howling-at-the-moon[at random] [one of]lunacy[or]insanity[or]madness[at random].[stopping]".
	

Check tying something to the noun:
	if the noun is a person:
		say "No need for such bondage." instead;
	otherwise:
		say "No doubt it would make [the noun] feel very secure to be tied to [if the noun acts plural]themselves[otherwise]itself[end if], but it's hard to see what you would get out of it." instead.

Instead of swearing mildly or swearing obscenely:
	if the current interlocutor is Snow White:
		change the direct command to "[the player's command]";
		try asking Snow White to try swearing obscenely;
	otherwise if the current interlocutor is new-Snow-White:
		say "This Snow White might actually be offended. And you don't want to add to her troubles for the evening.";
	otherwise:
		say "There's just no satisfaction in it when you don't have a shockable audience."
		
Instead of waving hands:
	say "You wave[if the current interlocutor is a person], but [the current interlocutor] just looks confused about why you are waving when you are already in the middle of conversation[otherwise]at nothing[end if]."
	
[We also want to revise the handling of inventory, given that the individual items are important and not likely to change much during the game.]
	
The print empty inventory rule is not listed in any rulebook.

The print standard inventory rule is not listed in any rulebook.

Carry out taking inventory:
	if the player carries exactly 0 things:
		say "You are empty-handed." instead;
	say "You have with you [a list of things carried by the player]. ";
	if the player carries the heart-sized box:
		if the heart-sized box is open:
			if the heart-sized box contains exactly 0 things:
				say "[The box] is suggestively open. ";
			otherwise:
				say "In [the box] [is-are a list of things in the heart-sized box]. ";
		otherwise:
			say "[The box] is closed[if something is in the box], concealing for the moment [the list of things which are in the box][end if]. ";
	if the player does not carry the dagger:
		say "You are feeling a little naked without your dagger. ";
	say paragraph break instead.

Instead of singing or jumping:
	say "You have not the heart."

Understand "dance" as a mistake ("You once knew how, so long ago you can barely recollect it.").

The block vaguely going rule is not listed in any rulebook. [* instead, we will translate all attempts to leave to "north", since moving towards the haven should be the default rather than moving towards the castle.]

Rule for supplying a missing noun while going:
	change the noun to north;
	rule succeeds.
	
[We also need to get rid of "that was a rhetorical question", since the player *should* be encouraged to type YES and NO on a regular basis. Our replacement default should indicate that the response to these commands will be dependent on conversational context. Likewise, "Oh, don't apologize" doesn't work when the player might try having the protagonist genuinely be sorry for something.]
	
The block saying yes rule is not listed in the check saying yes rulebook.
The block saying no rule is not listed in the check saying no rulebook.
The block saying sorry rule is not listed in the check saying sorry rulebook.

Report saying yes:
	say "You nod silently, though there wasn't really any question that needed answering."
	
Report saying no:
	say "You shake your head silently, though there wasn't really any question that needed answering."
	
Report saying sorry:
	say "You give an apologetic little shrug, though there isn't really anything to say."

Instead of asking someone to try saying yes:
	say "You've always found it best to stick to your own side of the conversation."
Instead of asking someone to try saying no:
	say "You've always found it best to stick to your own side of the conversation."
Instead of asking someone to try saying sorry:
	say "You've always found it best to stick to your own side of the conversation."

Understand "ask/tell/a/t/discuss/talk/say/answer" as "[say]".

Rule for printing a parser error when parser error is noun did not make sense in that context: 
	if the player's command includes "[say]": 
		say "[roman type][run paragraph on]";
		carry out the expressing ignorance for activity with the current interlocutor; [* This lets the game reply that there's nothing to say.]
	otherwise if the player's command includes "maybe/perhaps":
		if a flagged-ready quip in the quip-repository is binary-answering:
			say "This doesn't seem like something you can waffle about.";
		otherwise:
			carry out the expressing ignorance for activity with the current interlocutor; [* This lets the game reply that there's nothing to say.]
	otherwise: 
		say "That's not a verb I recognize."

Rule for expressing ignorance for someone when the current quip is restrictive:
	try changing the subject. [* This will give topics information letting the player know which of a limited set of options are available; in particular, will tip off the player about yes/no questions if he didn't realize that's why he wasn't allowed to change the subject.]

Rule for expressing ignorance for someone when the current quip is not restrictive:
	say "You can't think of anything appropriate to say on that topic, but you can always CHANGE THE SUBJECT."

	
Section 2 - Prying apart words

[And here because Snow looks longingly at the heart, we might imagine trying to give it to her:]

Understand the command "feed" as something new. Understand "feed [something preferably held] to [someone]" as feeding it to. Feeding it to is an action applying to one carried thing and one thing.

Check feeding something to the player:
	try consuming the second noun instead.
	
Check feeding something to someone:
	say "You rather doubt that [the second noun] would appreciate that." instead.
	

[And because violence is so essential to the game:]

Understand "poison [something]" as a mistake ("[if the noun is inanimate][The noun] lack[s] digestion; it follows that poisoning is impossible.[otherwise][one of]The Queen has assassins; you are not among them[or]There are probably poisonous toadstools in these woods, but it would be a trifle obvious to go hunting for one now[at random].[end if]").

Understand "drown [something]" as a mistake ("[if the noun is inanimate][The noun] do[es] not breathe to begin with.[otherwise]You are lacking the requisite equipment, viz. a millpond, a cesspool, or a butt of malmsey.[end if]"). 

Definition: a thing is inanimate if it is not a person.

Understand the command "punch" as something new. Understand the command "hit" as something new.

Understand "smack [something]" as bullying. bullying is an action applying to one thing.

Understand the commands "punch" or "hit" or "slap" or "strangle" or "choke" or "suffocate" or "smother" as "smack".

Understand "step on [something]" as bullying.
	
Check bullying the player:
	say "And wouldn't you just look fit for tying up, then?" instead.

Check bullying someone:
	say "Your training is to fight with weapons. [one of]Anything less shows contempt for your opponent, as your father used to say.[or][scolding][stopping]" instead.
	
To say scolding:
	repeat through the Table of Disgraceful Behavior:
		if the player's command includes topic entry:
			say "[reason-why-not entry][line break]";
			stop.

Table of Disgraceful Behavior
topic	reason-why-not
"punch/hit"	"Fist-fights are for taverns."
"slap/smack"	"Only women hit with an open hand."
"kick"	"Kicking is suitable to children who haven't yet learned to hold their water."
"strangle/choke"	"Strangulation is an assassin's trick."
"suffocate/smother"	"Your father said suffocation was best used only on those who were already dying in pain. Nonetheless he himself did not accept this form of mercy, if mercy it is."
"step on"	"Step on nothing larger than an insect, your father always said; anything larger might bite your heel."
	
Check bullying a body part:
	let owner be a random person who incorporates the noun;
	try bullying the owner instead.
	
Check bullying:
	say "[The noun] [has-have] done nothing to you." instead.
	

Understand "kick [something]" as kicking. Kicking is an action applying to one thing.

Check kicking the player:
	say "Metaphorically, no doubt." instead.
	
Check kicking something which is part of the player:
	say "You lack the flexibility." instead.
	
Check kicking something which is part of Snow White:
	say "You can't kick higher than a dwarrow's head, as the old saying goes." instead. [* I know, I know. But part of the point is: unconsciously, you retain a bit of the bigotry of your kind; in fact, you've reverted a bit since your sundering; but that didn't, in the crunch, prevent you from making a choice on the dwarrows' behalf.]
	
Check kicking:
	try bullying the noun instead. [if we fall through to this point we just want to convert the action.]


Understand the command "hug" as something new. Understand "hug [someone]" as hugging. Hugging is an action applying to one thing.

Check hugging the player:
	say "That doesn't warm you up for long." instead.
	
Check hugging the corpse:
	say "But the corpse is... leaking." instead.
	
Check hugging Snow White:
	if Snow White is warm:
		say "It's true she seems more than usually approachable, and yet that is only a relative measure." instead;
	otherwise:
		say "The time you spent half-carrying her through the woods was embrace enough and more." instead.
		
Report hugging someone: [* Presumably this will happen only with new-snow-white]
	say "You put your arms around [the noun] for a [comforting] moment."
	
Section 3 - Extra Action Names

Understand  the command "collect" as "take". [* at some point the game refers to collecting the hart's heart, so we want >COLLECT HART'S HEART to work.]

Understand the command "brandish" as "wave". [* Similarly, some flavor text says "You brandish the knife...", so we want to make sure that we can in fact brandish it.]

Understand the command "lick" as taste.


Understand "walk properly/normally/right" as walking properly. Walking properly is an action applying to nothing. [ This is an add-in so that we can say "Snow White, walk properly" when the game prompts you to tell her to do this.]

Instead of walking properly:
	try going.

	
Chapter 8 - Disambiguation 
 

Does the player mean climbing the trees:
	it is very likely.
	
Understand "kiss [something]" as kissing.
	
Does the player mean kissing or attacking the current interlocutor:
	it is likely.
	
Does the player mean showing something to the current interlocutor:
	it is likely.
	
Does the player mean giving something to the current interlocutor:
	it is likely.
	
Does the player mean opening or closing an openable thing:
	it is likely.

Does the player mean giving the hart's heart to the current interlocutor:
	it is very likely.

Does the player mean feeding the hart's heart to the current interlocutor:
	it is very likely.
	
Does the player mean examining or taking or dropping the hart's heart:
	it is very likely.
	
Does the player mean cutting the hart's heart when the hart's heart is part of the corpse:
	it is very likely.
	
Does the player mean cutting the hart's heart with the dagger when the hart's heart is part of the corpse:
	it is very likely.
	
Does the player mean cutting Snow White's heart when the hart's heart is not part of the corpse: 
	it is very likely.
	
Does the player mean cutting Snow White's heart with the dagger when the hart's heart is not part of the corpse: 
	it is very likely.

Does the player mean doing something with the hart's heart:
	it is likely.
	
[Does the player mean doing something with something which is part of the current interlocutor:
	it is likely.]
	
[Definition:
	 a person is parse-matched:
		say "FOO";
		yes.]

Does the player mean doing something with something (called direct item) which is part of someone (called the target):  
	if the direct item is the hart's heart or the direct item is Snow White's heart: 
		make no decision;
	if the target is the body-context: 
		it is very likely;
	if the target is the current interlocutor: 
		it is likely; 
	it is very unlikely. 

Does the player mean asking corpse to try doing something with something (called direct item) which is part of someone (called the target):
	if the target is corpse:
		it is likely;
	it is unlikely.
	
Does the player mean asking Snow White to try doing something with something (called direct item) which is part of someone (called the target):
	if the target is Snow White:
		it is likely;
	it is unlikely.


Rule for deciding whether all includes scenery:
	it does not.
	
Rule for deciding whether all includes a person when taking:
	it does not.
	
Rule for clarifying the parser's choice of a body part:
	do nothing instead.

Rule for clarifying the parser's choice of the hart's heart:
	do nothing instead.

Test disambiguation with "x hair / x hart's / x heart / t haven / no / a queen / a what the queen thinks / t kill / t witch / x my wrists / x her wrists / x snow white's wrists / snow white, x your wrists / snow white, touch wrists / z / snow white, touch my wrists / snow white, touch your wrists / x / climb / cut heart / cut heart / kiss / kill / feed heart / x hart / x eyes / wink / x snow white / x eyes".


Chapter 9 - General Parsing Improvements

[As much as I like the concept of Smarter Parser, it proved to be throwing Glk errors for reasons I couldn't work out; it also made for a longer response time for negative results on commands, which I didn't like in the context of the already slow runtime here.]

[Test parse-broken with "test hart-chat / ask why / yes / no / a snow white / smile / a terms / yes / yes / yes / yes / a where / say I'm sorry". [testing for a specific bug that turned up with the inclusion of these extensions -- now solved by stopping the apostrophe stripping.]

Include Smarter Parser by Aaron Reed.
Include Poor Man's Mistype by Aaron Reed.

When play begins:
	choose a row with a therule of signs of confusion rule in the Table of Smarter Parser Messages;
	change the message entry to "Try typing ASK ABOUT (something) or TELL ABOUT (something) to direct the conversation, and YES, NO, and MAYBE to answer questions. To interact with your surroundings you can LOOK and EXAMINE specific objects. You may also HIT, CUT, or BURN objects, or PUT things IN other things.";
	choose a row with a therule of asking who are you rule in the Table of Smarter Parser Messages;
	change the message entry to "Alabaster responds to your instructions by telling you more of the story you're inside. Try typing HELP for more information.";
	choose a row with a therule of unnecessary movement rule in the Table of Smarter Parser Messages;
	change the message entry to "You needn't move within a single location, but if you want to go to a new place, try GO TO (wherever).";
	choose a row with a therule of stripping body parts rule in the Table of Smarter Parser Messages;
	change the message entry to "You do not have to specify the use of body parts in that much detail.";
	choose a row with a therule of signs of frustration rule in the Table of Smarter Parser Messages;
	change the message entry to "I'm sorry you're feeling frustrated. If you like, you can type SAVE to store your progress to a file, then RESTORE to come back to it later."; 
	choose a row with a therule of the asking random questions rule in the Table of Smarter Parser Messages;
	change the message entry to "No such question is currently available. Try TOPICS if you are at a loss for things to say."; 


A first smarter parser rule:
	say "[second custom style][run paragraph on]".
	
Before printing a parser error: 
	say "[second custom style][run paragraph on]".
	
After printing a parser error:
	say "[roman type]".
	
A last smarter parser rule:
	say "[roman type]".
	
The too many words rule is not listed in any rulebook.
The standardize apostrophes rule is not listed in any rulebook.
The Smarter Parser simplify punctuation rule is not listed in any rulebook.

After reading a command (this is the new Smarter Parser simplify punctuation rule):
	let tmpcmd be indexed text;
	now tmpcmd is the player's command;
	replace the regular expression "<;:>" in tmpcmd with ". "; [new players have sometimes tried to seperate commands using these punctuation marks.]
	replace the regular expression "<!?\/()\{\}[quotation mark]>" in tmpcmd with "";
	change the text of the player's command to tmpcmd. ]
	
Chapter 10 - Tutorial Mode

First when play begins:
	if File of Alabaster Settings exists:
		read File of Alabaster Settings into the Table of Setting-Info;
		choose row 1 in the Table of Setting-Info;
		if there is a mode-setting entry:
			change the tutorial mode to mode-setting entry;
			make no decision;
	say "Have you played interactive fiction before? >> ";
	if the player consents:
		now tutorial mode is false;
		say "[second custom style][line break]Welcome. If you have any questions about Alabaster specifically, type HELP at any prompt.[roman type]"; 
	otherwise:
		now tutorial mode is true;
		say "[second custom style][line break]Welcome. To give you a bit of guidance, a tutorial mode is turned on, but if at any point you wish to get rid of it, type TUTORIAL OFF. Overall instructions are always available if you type HELP.[roman type]";
	choose row 1 in the Table of Setting-Info;
	change mode-setting entry to the tutorial mode;
	write File of Alabaster Settings from the Table of Setting-Info;
	pause the game.
	
The File of Alabaster Settings is called "alabstersettings".

Table of Setting-Info
mode-setting
a truth state
		
Section 1 - Creating tutorial mode and controls

Tutorial mode is a truth state that varies. 

Understand "tutorial mode off" or "tutorial off" as turning off tutorial mode.
Understand "tutorial mode" or "tutorial on" or "tutorial mode on" as turning on tutorial mode.

Turning off tutorial mode is an action out of world.

Check turning off tutorial mode:
	if tutorial mode is false, say "Tutorial mode is already off." instead.

Carry out turning off tutorial mode:
	now tutorial mode is false.
	
Report turning off tutorial mode:
	say "Tutorial mode is now off."

Turning on tutorial mode is an action out of world.

Check turning on tutorial mode:
	if tutorial mode is true, say "Tutorial mode is already on." instead.

Carry out turning on tutorial mode:
	now tutorial mode is true.
	
Report turning on tutorial mode:
	say "Tutorial mode is now on."
	
Section 2 - Tutorial Help

Before reading a command when tutorial mode is true:
	follow the suggestion rules.
	
The suggestion rules is a rulebook. 

Quip clue given is a truth state that varies. Quip clue given is false.

A suggestion rule when a quip (called target quip) is listed-plausible:
	if quip clue given is true:
		rule succeeds;
	now quip clue given is true;
	let N be indexed text;
	let N be "[target quip]"; 
	say "[second custom style]At various points in the conversation, your character will think of things he could say. You can choose to say one of them, with a command like SAY [N in upper case] (or, if you prefer, just the first few words of it). 
	
But if you don't like those options, you don't have to use them. You could CHANGE THE SUBJECT, or WAIT, or try another command you already know.";
	say "[roman type]";
	rule succeeds.

A suggestion rule when the turn count is 1:
	say "[second custom style]At this point, you can learn more about any of the things around you with EXAMINE -- for instance, you could type EXAMINE SNOW WHITE or EXAMINE THE HART (and then press return).";
	say "[roman type]";
	rule succeeds.

A suggestion rule when changing the subject:
	say "[second custom style]DISCUSS any one of these topics to move the conversation along in your own direction.";
	say "[roman type]";
	rule succeeds.

A suggestion rule when pleading-to-be-untied is the current quip:
	say "[second custom style]If you want to obey her request, you can UNTIE SNOW WHITE.";
	say "[roman type]";
	rule succeeds.
	
A suggestion rule when a flagged-ready quip in the quip-repository is binary-answering:
	say "[second custom style]You can answer questions with YES, NO, and MAYBE -- though of course sometimes not all of those answers are appropriate.";
	say "[roman type]";
	rule succeeds.
	
A last suggestion rule when discussing:
	say "[second custom style][next chatty hint]";
	say "[roman type]";
	rule succeeds.
	
A last suggestion rule:
	say "[second custom style][next hint]";
	say "[roman type]";
	rule succeeds.
	
To say next chatty hint:
	say "[one of]If you are at a loss for things to say, CHANGE THE SUBJECT or (equivalently) TOPICS will reveal new possibilities for discussion.[or]Besides conversation with other characters, you can under the right circumstances also ATTACK, KISS, or HIT them.[or]Your character can also SMILE, FROWN, and CRY.[or]If you want to remember what you've discovered so far, the THINK command will give you a brief overview.[cycling]"
	
To say next hint:
	say "[one of]You can always investigate what you're carrying by typing INVENTORY.[or]If you want to move physical objects, you can TAKE them (as in TAKE THE HEART).[or]To get rid of things you're holding, you can DROP them, or PUT them INTO another container.[or]OPEN and CLOSE have their expected effect on objects (as in OPEN THE BOX).[or]To explore objects, you can LISTEN TO or SMELL or TOUCH or TASTE them as well as using EXAMINE.[or]You may also find a use for CUT, BURN, or ATTACK.[cycling]"
 
Part 9 - The Scenario
 [To avoid confusion in this system, it is usually best to declare subjects first, then objects in the game world, then conversation quips. Here we have all the physical things currently written into the game.]

Chapter 1 - The Woods and Scenery

The Dark Woods is a dark room. "It is a moonless night[if stopped-time is happening], though it is hard to pay much attention to the sky when all around you the woods are silenced by magic[otherwise if the player carries the lantern]. The lantern light does not reach far[end if][one of]. You are seldom frightened in these woods, but tonight is unusual[or][stopping]."

The sky is far-off flush scenery in the dark woods. Understand "stars" and "star" and "starlight" and "moon" and "moonlight" and "sun" and "sunlight" or "clouds" or "haze" or "cloud" or "thick haze" as the sky. The description is "The sky is blotted out by pine branches, and even if you could see it, there would be only a few stars out, the rest shrouded by a thick haze. The moon and the sun are looking down on some other part of the world."

Instead of looking under the sky:
	say "And what would that signify?"

Some roots are flush scenery in the dark woods. Understand "root" as roots. The description is "You cannot see much at the moment, but they are down there, twisting under the soil, tripping you when you walk."

Instead of looking under the roots:
	say "They descend, no doubt, into the soil and then perhaps below they form the ceiling of the land of mines, where the dwarrows are said to work. But you have never seen those wonders, because to have earth over your head is not an idea that pleases you. There will be time enough when you are dead."
	
Instead of listening to the roots:
	say "If you expect to be able to hear them grow, you are disappointed."

Instead of searching the roots:
	say "There is nothing there but plant and soil."
	
Instead of smelling the roots:
	say "Ah, damp earth."
	
Instead of climbing the roots:
	say "They would offer significant climbing challenge only to an ant."
	
Instead of touching or rubbing or squeezing the roots:
	say "They are cool and soil-damp."

Instead of taking or pushing or pulling or turning or cutting the roots:
	say "You always did find the clearing of land a tedious chore, but it works better if you are at least equipped for the task."
	
Instead of igniting the roots with the lantern:
	say "They are live and well-watered: there's little hope of making them catch."
	
Instead of tasting or consuming the roots:
	say "They're tree roots, not turnips or carrots or beets."

Some trees are scenery in the dark woods. Understand "woods" or "tree" or "forest" or "woods" or "wood" or "foliage" or "leaves" or "leaf" or "branch"  or "branches" or "shadows" or "bark" or "pine bark" or "aspen bark" or "around us" or "around me" as trees. The description is "You know the trees in these parts well: a mix of pine and aspen. But all you can see in this light are the ghost-white trunks, and the shadows cast by the pine trees."

After printing the name of the trees while changing the subject:
	say " around you"

Some rubs are part of the trees. The description is "Rubs are where the stags wear the velvet from their horns, before season. There would be some if they regularly used this part of the wood; but you caught your hart in an unaccustomed place. Bad luck for him; convenient for you.". 
	
Instead of doing something other than examining when rubs are the noun or rubs are the second noun:
	say "This is the wrong part of the forest for any such sign."

Understand "count leaves" or "count the leaves" as a mistake ("Pointless: some are already sodden and disintegrating as autumn ends.").


Instead of inserting something into the trees: [ This will catch THROW FOO INTO WOODS.]
	say "You are uncomfortably aware that losing something is not the same as getting rid of it forever. If you want something destroyed, you'll destroy it, but otherwise it is best if everything remains under your control."
	
Instead of looking under the trees:
	say "Some scattered undergrowth grows here and there — less around the base of the pines."

Instead of touching or rubbing or squeezing the trees:
	say "The pine bark is rough; the aspen smooth. There are no conspicuous rubs about. You caught this hart outside his normal territory."
	
Instead of waving or swinging the trees:
	say "There is an old wives['] tale of a demoness who used a pine tree for a broom. You, however, are not in her league."

Instead of listening to the trees:
	say "When the wind picks up, the trees sigh and rustle. It is a little melancholy, but usual for these parts."
	
Instead of taking or pushing or pulling or turning or cutting the trees:
	say "The harvesting of lumber is a task you gladly leave to [nitwits].";
	
To say nitwits:
	say "[one of]men of less speed and more patience[or]men of less stealth and more strength[or]men of less wit and more bluster[or]your idiot cousins[stopping]"

The undergrowth is scenery in the dark woods. The indefinite article is "some". Understand "bush" or "bushes" or "shrubbery" or "plant" or "ground cover" or "plants" as the undergrowth.  The description is "The undergrowth is sparse — pine does not encourage plantlife around its base — but there are bushes here and there nonetheless."

Instead of touching or rubbing or squeezing the undergrowth:
	say "There's just too much chance of touching spines or stinging plants that you do not care to go feeling the undergrowth in the dark."
	
Instead of tying something to the undergrowth:
	say "The ground cover is hardly sturdy enough to anchor anything. And in any case the only tying-up that needed to be done this evening has already been accomplished."

Instead of listening to the undergrowth:
	say "There is only a very occasional rustle of small animals moving through the plants. They will be able to hear and smell you and Snow White, and see your light, so they are not very daring."

Instead of searching or looking under the undergrowth when the dagger is in the location:
	say "Your dagger remains where it fell earlier."
	
Instead of searching or looking under the undergrowth:
	say "There is nothing extraordinary here."
	
Instead of smelling the undergrowth:
	say "It has no special smell other than plant life and rotting leaves and dankness."

Instead of taking or pushing or pulling or turning or cutting the undergrowth:
	say "You always did find the clearing of land a tedious chore[one of], but it works better if you are at least equipped for the task[or], and you're happy to leave it to [nitwits][stopping]."

The aspen is scenery in the dark woods. Understand "aspen" or "aspen tree/trees" or "trunk" or "trunks"  or "ghost-white trunks" as the aspen. The description is "In daylight, they are beautiful. This time of year the aspen is shedding and the leaves lie deep and golden on the ground."

The pine is scenery in the dark woods. Understand "pines" or "pine trees/tree" as the pine. The description is "They blot out even the little light that might otherwise come from the stars."

Instead of tasting the trees:
	say "Sometimes in spring the freshest pine leaf growth is suitable to eat; otherwise, you do not care to test yourself."

Instead of smelling the trees:
	say "The dominant scent is that of damp pine needles."
	
Instead of climbing or entering the trees:
	say "These are not good trees for climbing, even with free hands and plenty of light."
	
Instead of tasting or consuming something inedible:
	if the current interlocutor is new-Snow-White:
		say "[The noun] won't serve as much of a breakfast — for you or for her. [one of]You both need to get to where you'll be safe and able to eat and sleep[or][stopping].";
	otherwise if the player knows vampirism-revealed:
		say "You prefer food more suitable for humans. This makes you perhaps unusual in the court. The Queen is always brewing her drugs and tinctures, and [SW-or-Lilith]'s preferences are apparently even more disturbing.";
	otherwise:
		say "You prefer food more suitable for humans. This makes you perhaps unusual in the court. The Queen is always brewing her drugs and tinctures, and [SW-or-Lilith]... who knows?"
		
To say SW-or-Lilith:
	say "[if the player knows snow-white-possessed]Lilith[otherwise]Snow White[end if]"

Before doing something other than examining when the noun is the pine or the second noun is the pine:
	if the noun is the pine, change the noun to the trees;
	if the second noun is the pine, change the second noun to the trees;
	try the current action instead.
	
Before doing something other than examining when the noun is the aspen or the second noun is the aspen:
	if the noun is the aspen, change the noun to the trees;
	if the second noun is the aspen, change the second noun to the trees;
	try the current action instead.

Instead of searching the trees:
	say "No need: the most frightening thing for miles around is the slip of girl beside you."
	
Understand "shiver" as a mistake ("You already are.").

Instead of listening to the location, say "[random sound]."

To say random urgency:
	say "[one of]You cannot be all night about this[or]You should get back by dawn[or]The Queen may already be wondering where you are[or]The later you get back, the more awful the Queen's questions will be[or]The delay is making you ever less comfortable[stopping]".

To say random lampstate:
	say "The lantern [one of]flickers[or]dims[or]brightens again[cycling]"

To say random temperature:
	say "[one of]It is getting colder[or]You would be warmer if only you were still moving[or]Your ears are growing numb[or]You stomp your feet to warm them[or]It is so cold that you are barely aware of it any longer[stopping]".

To say random sound:
	say "[one of]All around you is silence[or]A breeze stirs the aspens, sending down a few more leaves[or]The branches nearby rustle[or][one of]In the distance, an owl screeches[or]The screech-owl calls again[or]Wings beat in the dark[or]A small animal squeaks: most likely a mouse in the act of being caught by the owl[cycling][or]She shifts her weight, breaking a twig[or]You hear something very far off that sounds like a wolf[as decreasingly likely outcomes]". [* Added the bit about the screech-owl because this animal is associated with Lilith in some Biblical translations. (At least, if Wikipedia is to be trusted.)]


Chapter 2 - Snow White and Her Attire

Snow White is a woman in the Dark Woods. Understand "princess" or "girl" as Snow White. "[if stopped-time is happening]Snow White hangs motionless in midair, silenced and unapproachable[otherwise]Snow White is with you[one of]. Her wrists are bound behind her back. She has made as much of a nuisance of herself as she could, deliberately stumbling over every root in the dark, until you had to half-carry her this far[or][stopping][end if]." 

The description of Snow White is "Her skin is whiter than the surface of the moon and her eyes are black and her breath is [if scariness is not greater than 0]unappealing[otherwise]the breath of the grave[end if]. At night she dances and she drinks red wine[drinks-red-wine]; she sleeps by day.

[if the player does not know rock-at-mirror]She alone of all the court refuses to be brought before the Queen's magic mirror. She threw a rock at it once, [rock-at-mirror]and almost broke it. But she bites and scratches and will not be dragged in front of its face[otherwise][mood-based description][end if]."

To say mood-based description:
	if the player knows Snow-white-possessed:
		say "Knowing what you know about her, you can barely take in the surface appearance, because you are trying too hard to make out the demon beneath that skin";
		stop;
	otherwise if the player knows vampirism-revealed:
		say "This must be how vampires appear";
		stop;
	if the mood of Snow White is:
		-- menacing:
			say "The Queen has suggested she is a monster. That may be so";
		-- aggressive:
			say "What she sees when she looks back, you don't know, but it doesn't appear to be entirely favorable";
		-- furious:
			say "She looks back at you, too, and her stare is wholly hostile";
		-- needy:
			say "At the moment she is childlike — like a child treated badly, and very confused — not a healthy child, perhaps. But vulnerable all the same";
		-- whiny:
			say "If there is beauty in her face, it is ruined by the petulance: she pouts, and sighs, and gives you reproachful looks. Someone has spoiled her, that is certain";
		-- harmless:
			say "All the same there is more ordinary than strange about her, just now. Who wouldn't be pale and cold after a journey like she has had? Who would not have a similar expression of raw exhaustion? You may look much the same yourself";
		-- warm:
			say "And yet you feel a little kinship toward her, especially when she smiles her least menacing smile, and shows her glancing wit";
		-- weird:
			say "But you find yourself more tolerant of her strange looks than you have ever been before, for she seems — if not exactly human — at least better disposed towards you";


Instead of dropping Snow White:
	say "You've already set her back on her own feet. Thank goodness: your arm was getting sore."

Instead of consuming or tasting Snow White:
	say "[italic type]Your[roman type] food preferences do not run in that direction."
	
Instead of consuming or tasting something which is part of Snow White:
	try consuming Snow White.
	
Understand "bite [something]" as consuming.

Instead of looking under Snow White:
	if Stopped-time is happening:
		say "She is indeed suspended in mid-air. There appear to be no levers nor stands nor strings nor other apparatus of false magic.";
	otherwise:
		say "Beneath her is ordinary ground."

Instead of searching Snow White:
	say "She has no hidden possessions: the Queen saw to that, before you led her away."

Instead of pulling or pushing or taking Snow White:
	say "[lethal-blow]".
	
Understand "screw [someone]" as entering.

Instead of turning Snow White:
	say "The Queen has, in her chambers, a large, jointed doll that can be dressed in gowns and posed in various positions.

Snow White is not such a doll."
	
Instead of pulling or pushing or turning or taking something which is part of Snow White:
	change the noun to Snow White;
	try the current action.
	
Instead of entering or getting off Snow White:
	say "Her person is sacrosanct — at least against violations of that sort."
	
Instead of squeezing Snow White:
	try rubbing Snow White.
	
Instead of rubbing or squeezing something that is part of Snow White:
	if the noun is flush:
		say "Your cousin's wife has sometimes hinted that she wishes your cousin would perform such acts, but you yourself have never seen the appeal.";
	otherwise:
		try rubbing Snow White.
	
Instead of rubbing Snow White:
	say "She is cold, to be certain, but you'd rather not go close enough to warm her."
	
Instead of throwing something at Snow White:
	say "[lethal-blow]".
	
Instead of throwing something at something which is part of Snow White:
	try throwing the noun at Snow White.
	
Instead of waking Snow White:
	say "She might not be entirely herself these days, but you doubt that sleep-walking is the cause."

Instead of tying something which is part of Snow White to Snow White:
	if the noun is flush:
		say "Hard to imagine how that would work.";
	otherwise:
		say "Is there some risk [it-them of noun] might otherwise come off? No."
	
Instead of tying Snow White to something:
	say "Snow White is already bound as much as she needs to be."
	
Instead of tying something which is part of Snow White to something:
	try tying Snow White to the second noun.
	
To say lethal-blow:
	if scariness is greater than 0:
		say "Dangerous animals should be killed instantly. Wounding them is very risky.";
	otherwise if anger is greater than 0:
		say "This is not a game, and you are not a favored uncle. If you attempt violence on her person, it will have to be forceful and permanent.";
	otherwise:
		say "Any violence you attempt should be lethal.".
	
Instead of throwing the dagger at something:
	say "It is sharp, but it isn't balanced as a throwing knife — and that was never your best skill anyway."
	
Instead of climbing something which is part of Snow White:
	try climbing Snow White.
	
Instead of climbing Snow White:
	say "[if the player knows snow-white-possessed]Whatever her true form, she is currently[otherwise]She is[end if] a normal-sized girl, not a giantess from the stories of the early days of the earth."

Instead of swinging or waving Snow White:
	say "She isn't an errant cat!"

Instead of swinging or waving something which is part of Snow White:
	if the noun is flush:
		say "[The noun] [is-are] not exactly susceptible to being wielded in that fashion.";
	otherwise:
		say "If she wishes to make any gestures, no doubt she will do so on her own."

Snow White wears a green dress. Understand "clothing" or "clothes" or "outfit" or "gown" or "Snow White's" as the green dress. The description of the green dress is "She wears a green dress, meant for court dances, not for late walks in the woods. The gold embroidery around the neckline glints in the lantern light."

Instead of touching or rubbing or squeezing the green dress:
	say "From your earlier contact, you know it to be a very expensive but not at all warm sort of cloth."

Instead of opening or taking or attacking or entering or cutting the green dress:
	say "The Queen is happy to have you murder her step-daughter, but any defilements you perform must be strictly according to instruction."
	
Instead of closing the green dress:
	say "It is already buttoned up as high as it will go. Even so, it can't be very warm."
	
Instead of buying or wearing the green dress:
	say "It wouldn't go with your eyes."

A gold embroidery is part of the green dress. It is flush. The indefinite article is "some". Understand "neckline" or "Snow White's" as the gold embroidery. Understand "gold embroidery on [something related by reversed incorporation]" or "embroidery on [something related by reversed incorporation]" as the embroidery. The description is "She sewed it herself, you believe[written-embroidery]: it is an intricate pattern that does not repeat, almost like writing."

Instead of rubbing or squeezing or touching or listening to or smelling the gold embroidery:
	say "Snow White might be unable to tell that your interest was in the embroidery and not in what lies beneath her gown." 

Instead of looking under or searching the gold embroidery:
	say "It lies flat and conceals nothing."

Instead of taking or cutting or pulling or pushing or turning or opening the gold embroidery:
	say "The embroidery is done in hundreds of small, neat stitches: it would take you hours of work to cut all the threads and pick them free of the cloth, even if she were minded to permit it."

Understand "chafing" or "chafed" or "raw" as Snow White's wrists. Understand "wound" or "wounds" as Snow White's wrists when the wound is off-stage. 

The description of Snow White's wrists is "Her wrists are bound with a silver chain. [silver-hurts]It has chafed her, oh, much more than you would expect possible in such a short time. But the Queen insisted that no mere rope would suffice."

Instead of closing Snow White's wrists:
	say "You do not have a doctor's ability to close wounds."

Instead of taking or cutting Snow White's wrists:
	if Snow White's wrists are part of Snow White:
		say "The cutting off of hands is a punishment reserves to thieves and poachers: the Queen did not tell you to do any such thing, and you would rather leave such gruesome tasks to the royal executioners.";
	otherwise if Snow White's wrists are part of new-Snow-White:
		try attacking new-Snow-White;
	otherwise:
		say "*** BUG: FREE-FLOATING Snow White's wrists ***"

To decide whether the chain is misunderstood:
	if the player's command includes "bonds/rope/ropes":
		yes;
	no.

The silver chain is part of Snow White's wrists. The description of the silver chain is "[if the chain is misunderstood]What binds Snow White's hands is not an ordinary rope, but a strong silver chain made for the purpose.[otherwise]Strong links: this is no mere piece of jewelry, but something the Queen had specially forged.[end if]". Understand "rope/ropes/bonds" or "links" or "link" as the silver chain. 

Instead of opening or taking or cutting or attacking or pulling or pushing or turning the chain:
	if the silver chain is part of Snow White's wrists:
		if Snow White's wrists are part of Snow White:
			try freeing Snow White;
		otherwise if Snow White's wrists are part of new-Snow-White:
			try freeing new-Snow-White.
	
Instead of searching or looking under the silver chain:
	say "You glance without actually touching, but yes, it appears that the wrists beneath have worn very raw."
	
Instead of closing the silver chain:
	say "The links are already forged closed by the Queen's magic."
	
Instead of tying the silver chain to Snow White:
	say "She is already well-bound."
	
Instead of tying the silver chain to something which is part of Snow White:
	try tying the silver chain to Snow White.
	
Instead of tying the silver chain to something which is not part of Snow White:
	say "You would first have to remove the chain from its present owner."

Instead of rubbing or touching or squeezing the silver chain:
	say "You felt the links earlier. They were like ice, and tingled when you touched them."

Instead of listening to the silver chain:
	say "It makes no sound; does not even clink much when she struggles, so finely made are the links."
	
Instead of climbing the silver chain when the silver chain is part of Snow White's wrists and Snow White's wrists are part of Snow White:
	try climbing Snow White.
	
Instead of buying the silver chain:
	say "No doubt she would gladly give it to you, if you [if the current interlocutor is new-Snow-White]wanted it[otherwise]had the nerve to take it[end if]."
	
Instead of smelling the silver chain:
	say "You don't fancy putting your face where the gesture could be so completely misinterpreted by your companion."

The description of Snow White's hair is "Her hair is long, black, and shining. It hangs past her waist."

Instead of taking Snow White's hair:
	say "The Queen has had one or two of her court rivals plucked bald. Her feelings against Snow White go much deeper than that."
	
Instead of touching Snow White's hair:
	say "You know from prior contact — though you are nervous of repeating the gesture — that it is like a river of silk."
	
Instead of pulling Snow White's hair:
	say "You are not Snow White's rival in love, and this is not a matter to be settled with hair-pulling and scratches."
	
Instead of squeezing or rubbing or waving or swinging or turning or pushing Snow White's hair:
	say "No doubt some such activity enters into her hair-care regimen, but the intricate braiding techniques of the court ladies are a mystery to you."

Before wearing Snow White's hair:
	try buying Snow White's hair instead.

Instead of buying Snow White's hair:
	say "Such a wig would not be comely on your head."
	
Instead of cutting Snow White's hair:
	if the player knows magic-revealed:
		say "No, you'd prefer not to; who knows what she might do.";
	otherwise if the hair is part of new-Snow-White:
		say "She deserves no further indignities.";
	otherwise if the current interlocutor is Snow White:
		say "You boldly go to Snow White and cut a lock of her hair. It is silky in your fingers.

[magic-revealed]Her eyes flash with anger, and she speaks a word you cannot understand. At once the hair turns to a dusting of powdery snow in your fingers. A moment later it has melted away[queue do-not-try].";
	otherwise:
		say "That would make a strange sort of trophy."

The description of Snow White's skin is "Fair and smooth, almost like wax or marble or alabaster."

Instead of taking or cutting or looking under or searching or opening Snow White's skin:
	say "The Queen stopped short of ordering you to flay your victim, and you have no taste for torturing things."
	
Instead of closing Snow White's skin:
	say "She has no visible wounds save on her wrists." 

The description of Snow White's face is "The Queen told you to cut it off and so now you regard her head with an executioner's professional curiosity, whether you wish to or not.

The eyes and the lips are her most exceptional features. All the rest — bone structure, cheeks, temples, nose — is finely formed, attractive, but neither striking nor frightening."

Instead of taking or cutting or attacking Snow White's face when the face is part of Snow White:
	try attacking Snow White.
	
Instead of taking Snow White's face:
	try cutting Snow White's skin.

Instead of looking under Snow White's face:
	say "If there is anything beneath but bone and flesh, you do not wish to know it."
	
Instead of squeezing Snow White's face:
	say "Your cousin's children are amused when you give them fish-lips, but you doubt that Snow White would have the same reaction."

Instead of searching a face:
	try examining the noun.


Snow White's heart is a flush covered part of Snow White. Understand "her heart" as Snow White's heart.
	The description is "You can hardly see it from outside her body."

Instead of listening to Snow White's heart:
	say "Earlier, when you carried her, you heard her heart beat. It was fast and light like a bird's."
	
Instead of taking or cutting Snow White's heart when Snow White's heart is part of Snow White:
	try attacking Snow White.
	
The description of Snow White's neck is "Through the pale skin the veins of blue-green are sometimes visible — but not now, not in this inferior light."
	
Instead of attacking or cutting or turning or squeezing Snow White's neck when Snow White's neck is part of Snow White:
	try attacking Snow White. [Because this might be an attempt to break her neck or choke her or slit her throat.]
	
Instead of drinking Snow White's neck:
	try drinking Snow White's veins.

The description of Snow White's ears is "Small, round, close to her head. Her hair falls across them sometimes, and sometimes they are visible."
	
Instead of listening to ears:
	say "They return the favor."

Instead of inserting something into Snow White's ears:
	say "Rendering her deaf was not the gist of your assignment."

Instead of taking or cutting Snow White's ears:
	say "You have heard of some barbarians that take such trophies. You do not count yourself among them."
	
Instead of pushing or pulling something which is part of Snow White:
	say "[lethal-blow]"

The description of Snow White's nose is "Her nose is long and straight, perhaps the least extraordinary feature of her face."
	
Instead of taking or attacking or cutting Snow White's nose when the nose is part of Snow White:
	say "The nostrils (you have heard) can be split to create a particularly unattractive form of disfigurement. But that is not the fate the Queen has ordained for Snow White."

Instead of inserting something into Snow White's nose when the nose is part of Snow White:
	say "Though the Queen encouraged you to mutilate Snow White, she did not suggest doing anything quite of that nature."
	 
Understand "pinch [something]" as squeezing.

Instead of squeezing or closing Snow White's nose:
	say "There are less vile and less dangerous ways of killing than to suffocate your victim."


The description of Snow White's veins is "Her delicate veins you have seen before, but they are not now visible."
	
Instead of looking under a covered thing:
	say "You cannot see [the noun] clearly, let alone what might lie beneath."

Before drinking Snow White's veins:
	say "You are not yourself a vampire." instead. [* Since the veins are covered, we need a before rule in order to skip the rule about touchability.]
	
Instead of attacking or cutting or opening  Snow White's veins when Snow White's veins are part of Snow White:
	try attacking Snow White. 
	
The description of Snow White's chin is "Her chin is small and pointed, her jawbone angular and proportionate for her face."
	
Instead of cutting or taking Snow White's chin:
	say "[lethal-blow]".
	
Instead of looking under Snow White's chin:
	say "In an elegant bit of divine engineering, her neck begins just where the chin leaves off."

The description of Snow White's lips is "[if the lips are part of Snow White]Red-black[otherwise]Rose-red[end if] in the lamplight."
	
Instead of tasting lips when lips are part of someone (called the owner) who is not the player:
	try kissing the owner.
	
Instead of consuming lips:
	say "The thought reminds you of an unproductive romantic interlude from your early youth. An episode you do not intend to repeat."

Instead of cutting or attacking or taking Snow White's lips:
	say "Once the Queen had a lady so disfigured when she (that is, the lady) was caught in a certain adulterous act. But surely that is not the source of the Queen's anger against Snow White."

Instead of squeezing lips which are part of someone (called the owner):
	let the chosen face be a random face which is part of the owner;
	try squeezing the chosen face.
	
Instead of opening or closing Snow White's lips:
	say "She opens them herself, and closes them again too. At the moment they are just parted."

The description of Snow White's teeth is "Her teeth are even, white, and sharp. She has incisors; everyone does. You can see nothing absolutely abnormal." Understand "fangs" or "fang" as Snow White's teeth. 

Instead of squeezing or rubbing or touching Snow White's teeth:
	say "You are especially nervous of the teeth, and have no desire to make their closer acquaintance."

Instead of taking or cutting Snow White's teeth:
	say "If Snow White is what the Queen says she is, impromptu dentistry could be extremely dangerous."

Instead of tasting or consuming Snow White's teeth:
	say "In the old fables, there is a demon that makes its bread out of a meal of crushed teeth and ground bones. [one of]But demon diets are notoriously unsuited for humans[or]But demons have apparently discovered a form of leavening unknown to man[at random]."

The description of Snow White's eyes is "[if Snow White's eyes are part of Snow White]They are flawless, deep black, the pupil and the iris indistinguishable except in the brightest lamplight. (Or perhaps there is a color that sunlight would reveal, but you have never seen her so.)[otherwise]They are a deep warm brown, like walnut, with lighter flecks in the iris.[end if]"

Instead of opening or closing Snow White's eyes:
	say "If she were a corpse, she might hold still for that."

Instead of taking or cutting or pulling or pushing or turning or igniting or squeezing or looking under Snow White's eyes:
	if Snow White's eyes are part of Snow White:
		say "The removal of the eyes, the Queen prefers to inflict on spies and those she believes to have been snooping on her magic. 
	
Besides, as horrifying as Snow White is now, imagine how much more frightening she would be staring at you with two empty eye sockets.";
	otherwise:
		say "Unpardonably cruel."

Instead of touching or searching or rubbing Snow White's eyes:
	say "If she has something in them, she hasn't said so."

The breath is part of Snow White. The indefinite article is "some". The description is "It is not quite cold enough for her breath to cloud the air."

Instead of smelling the breath:
	say "She is not near enough for you to do so now, but when you were dragging her along you got plenty of opportunity. It is sickly, sweet and fetid."

Instead of doing something other than examining or taking or smelling to the breath:
	say "It is hardly possible."

Instead of taking the breath when the breath is part of someone (called the owner):
	try attacking the owner.

Instead of smelling something which is part of Snow White:
	if the noun is the breath:
		continue the action;
	otherwise:
		try smelling Snow White.
		
Instead of smelling Snow White:
	say "Her breath is sickly; you have smelled nothing extraordinary about the rest of her. She wears (unlike the Queen) no perfumes."
	
Instead of listening to or looking under or searching something which is part of Snow White:
	if the noun is covered:
		say "You would first have to peel back her skin — an idea that does not please you.";
	otherwise:
		say "That seems offensively intimate."
	
Instead of listening to or smelling or looking under or searching something which is worn by Snow White:
	 say "That seems offensively intimate."

The hypothetical vampire is a far-off flush scenery thing in the Woods. It is privately-named. Understand "vampire" as the hypothetical vampire.
	
Before doing something to the hypothetical vampire:
	if the player knows vampirism-revealed:
		say "Perhaps you'd better not think of her in those terms so loudly. She might pick up on it." instead;
	otherwise:
		say "You're not certain you can see any such thing." instead.

Chapter 3 - Starting Inventory

The player carries a dagger, a lantern, and a heart-sized box. The carrying capacity of the heart-sized box is 1.

Carry out taking inventory for the first time:
	casually queue watching-inventory.
	
implicitly acting is a truth state that varies.

Before inserting something into the heart-sized box when the heart-sized box is closed:
	if something is in the heart-sized box:
		say "[The heart-sized box] already contains [the list of things in the heart-sized box], as you know even though you can't see inside at the moment. There's no room for extra contents." instead;
	now implicitly acting is true;
	try opening the heart-sized box;
	now implicitly acting is false;
	if the heart-sized box is closed:
		stop the action.
		
Before inserting the hart's heart into the heart-sized box when the player is not carrying the hart's heart:
	now implicitly acting is true;
	try taking the hart's heart;
	now implicitly acting is false;

The heart-sized box is a closed openable container. Understand "heart sized" or "ornamented" or "wood" or "wooden" or "I'm carrying" or "you're carrying" or "for the queen" as the heart-sized box. 

The description of the heart-sized box is "It is made of deep black wood, with a sequence of symbols[symbols-seen] burned across the top[if the player knows symbols-uncreate]. If she is to be trusted, they have something to do with uncreating life[otherwise]. You have no idea what they do. Your part is only to bring it back full[end if]."

After printing the name of the heart-sized box while changing the subject: 
	if we have not taken inventory and we have not examined the heart-sized box and we have not opened the heart-sized box:
		say " you're carrying for the Queen".
		
The heart-sized box can be introduced or unintroduced. The heart-sized box is unintroduced.

Carry out opening the heart-sized box when the heart-sized box had not been open:
	casually queue watching-box-opened.
	
Report opening the heart-sized box:
	say "[one of]The lid [if implicitly acting is false]of [the heart-sized box] [end if]is stiff on its hinges, but you get [if the prior named noun is the heart-sized box]it[otherwise][the heart-sized box][end if] open with a little effort[or]Once again you pry the box open[stopping][if implicitly acting is true]. [run paragraph on][otherwise].[end if]" instead.
	
Report closing the heart-sized box:
	if the heart-sized box contains the hart's heart or the heart-sized box contains the dripped blood:
		say "You snap the lid shut on the gory contents[if the current quip is pleased-watching and the current interlocutor is Snow White]. She sighs ever so faintly[end if]." instead;
	otherwise:
		say "You push the lid and it snaps shut." instead.
		

	
Instead of bullying the heart-sized box:
	say "It doesn't look easily crushed. Not to mention that you might need it for something."

Instead of touching or rubbing or squeezing the heart-sized box:
	say "The surface is smoothly oiled except for the symbols burnt into it. It feels warmer than the night."

Instead of smelling the heart-sized box:
	say "It smells like cedar, and something else expensive and nameless. Some perfume or spice from a distant place."
	
Instead of listening to the heart-sized box:
	if the heart-sized box is open:
		say "When you put your ear to the mouth of the box, it sounds like someone is whispering within; but not in a language that you know. Then again, perhaps you are going mad.";
	otherwise:
		say "It is silent on the outside."
	
Instead of attacking or cutting the heart-sized box:
	if the player is not carrying the dagger:
		say "(retrieving the dagger first)[line break]";
		silently try taking the dagger;
	if the player is not carrying the dagger:
		stop the action;
	say "[one of]When you drag the point of your dagger along the side of the box, it shrieks dreadfully, like a baby being killed. You stop at once.[or]Absolutely not.[stopping]".

Instead of entering the heart-sized box:
	say "You are larger than your heart: ergo, you will not fit."

Instead of wearing the heart-sized box:
	say "It would make a nice [one of]hat, if you had a little cubical head[or]glove, if you had no fingers[or]shoe, if your foot could fold in half[or]codpiece, if— never mind[cycling]."

Some ashes are a flush thing. Understand "ash" or "pile" or "gray" or "grey" or "fleck" or "flecks" or "black" or "fleck/flecks of black" as the ashes. "A pile of ashes is all that remains from the heart-sized box." [* These will come in handy later when you burn the box.] Instead of examining the ashes: say "Grey with flecks of black inside."

Before dropping the ashes when the player does not carry the ashes:
	try taking the ashes;
	if the player does not carry the ashes:
		stop the action.

Instead of searching the ashes:
	say "All that remains of the box are occasional black flecks of wood that were not completely destroyed."
	
Instead of listening to the ashes:
	say "Silent as the tomb."

Instead of pulling or pushing or turning or touching or rubbing or swinging the ashes:
	say "If you wish to scatter them, you may do so, but there is not a sufficiently substantial pile to make into interesting ash pictures."
	
Instead of wearing the ashes:
	say "This would certainly make a statement, but you don't have the coordinating sackcloth."

Instead of cutting the ashes:
	say "It would be hard to divide the powder further."

Instead of squeezing the ashes:
	say "Unlike snowflakes, they do not cohere."

Instead of smelling the ashes:
	say "They smell like burnt flesh and wood and incense."
	
Instead of tasting or consuming the ashes:
	say "It's less of a taste and more of a feeling.  It's like your tongue has dried up and forgotten how to taste, and your mouth is full of powder, and it's eating away at your memory of any taste you've ever had before and has collapsed any hope of tasting anything different ever again.";
	the huntsman spits in four turns from now;
	rule succeeds. [* Description courtesy of Justin de Vesine aka vimes, who, as he puts it, "used to do a fire-eating thing in a sideshow".]
	
At the time when the huntsman spits:
	say "(The taste of ash is still on your tongue.)"
	
Understand "spit" as a mistake ("You try to spit decorously[if we have tasted the ashes]. It doesn't help[otherwise]. It can't be done[end if].").
	
Before tasting something when we have tasted the ashes:
	say "You can't really take in any other tastes. Your mouth is full of ash-flavor." instead.

Report taking the ashes:
	say "You scoop up a grubby handful of ashes." instead.
	
Understand "scatter [something]" as dropping.

Some symbols are part of the heart-sized box. The symbols are flush. Understand "symbol" or "impressions" or "blackened" or "writing" or "carved on the heart-sized box" or "carved on the box" as the symbols. The description is "Blackened impressions in near-black wood." Understand "impressions/writing/symbols on [something related by reversed incorporation]" or "blackened impressions/writing/symbols on [something related by reversed incorporation]" as the symbols.


The symbols can be introduced or unintroduced. The symbols are unintroduced.
		
After printing the name of the symbols while changing the subject:
	if we have not examined the heart-sized box:
		say " carved on the heart-sized box".

Instead of attacking or cutting the symbols:
	try attacking the heart-sized box.
	
Instead of smelling the symbols:
	try smelling the heart-sized box.
	
Instead of listening to the symbols:
	try listening to the heart-sized box.

Instead of searching or looking under the symbols:
	say "They are part of the surface of the box."
	
Instead of pulling or pushing or turning the symbols:
	say "They cannot be manipulated separate from the box itself."
	
Instead of touching or rubbing or squeezing the symbols:
	try touching the heart-sized box.

Definition: a thing is boxable:
	if it is the hart's heart:
		yes;
	if it is Snow White's heart:
		yes;
	if it is the dripped blood:
		yes;
	no. 

Instead of inserting something which is not boxable into the heart-sized box:
	say "[The second noun] is too small to accept [the noun]."

The lantern is lit. Understand "oil" or "battered" or "old" or "lamp" as the lantern. The description is "An old, battered oil lantern. It should be good for another couple of hours."

The lamplight is part of the lantern. The description is "A weak circle of gold light, but still sufficient to discourage animals."

Before doing something other than examining to the lamplight:
	change the noun to the lantern;
	try the current action instead.

Instead of touching or rubbing or squeezing the lantern:
	say "The handle is cool enough, but the rest is hot to the touch."
	
Instead of searching the lantern:
	say "There is enough oil to last you the rest of the evening."
	
Instead of looking under the lantern:
	say "The space beneath the lantern is, necessarily, in shadow. The problem is akin to cutting your dagger with itself, or climbing up yourself to reach a second-story window."
	
Instead of drinking the lantern:
	say "Burning oil is your next-to-least favorite beverage.[one of][or] Once in childhood you had the misfortune to stand in the splash range of a urinating donkey.[or][stopping]".

Instead of waving or swinging the lantern:
	say "The shadows move back and forth in response. The effect is a little unsettling."
	
After dropping the lantern:
	say "You set the lantern down carefully on a (mostly) even patch of ground."
	
Instead of smelling the lantern:
	say "It smells... hot."

Instead of opening or closing the lantern:
	say "It is well-fed with oil, and unless you mean to burn something, it is probably wisest to leave it closed lest a gust of wind put it out."
	
Instead of throwing the lantern at something:
	say "It would be safer to burn something with the lantern without breaking the glass."
	
Instead of attacking the lantern:
	say "You can scarcely go without a light source."

Instead of entering the lantern:
	say "There is an old story of a woman who made her home in a birdcage, too, but she first had to eat a magic birdseed to make her small enough[one of][or]. 
	
Just to be clear, you haven't got a magic birdseed[stopping]."
	
Understand "eat a magic birdseed" or "eat magic birdseed" or "eat birdseed" as a mistake ("If only you'd thought to bring along the contents of the feeders from the royal garden.").
	
Instead of cutting the lantern:
	say "It is made of glass and metal and cannot be cut."
	
Rule for writing a paragraph about the lantern:
	say "[The lantern] sits on the ground, lighting [Snow White] and [the hart] in the most garish fashion imaginable[if the dagger is in the location and the dagger is not mentioned], and making the hilt of your [dagger] glint from where it lies in [the undergrowth][end if]."

Understand "knife" or "blade" or "hilt" as the dagger.

The description of the dagger is "[if the hart's heart is unused]It's... well, sharp enough[otherwise]It remains sharp. You cleaned the blade as a matter of instinct when you cut the heart out of the corpse, so it doesn't even look much worse for wear[end if]."

The dagger can be unused or besmirched.

Carry out cutting something with the dagger:
	now the dagger is besmirched.

Instead of rubbing the dagger: [* This will respond also to actions like WIPE DAGGER and CLEAN DAGGER, so we want it to print something sensible]
	if the dagger is besmirched:
		say "You already have, you realize. You keep it clean of blood, instinctively, and never use it but you wipe it afterward as a matter of course.";
	otherwise:
		say "It's not been used yet tonight."

Instead of cutting the dagger:
	say "The idea reminds you of a village jest — something about a witch girl with a teat long enough to feed herself — but if it works with witches, it doesn't work with daggers."

Instead of attacking the dagger:
	say "You need this. More than anything else — even the lantern."

Instead of wearing the dagger:
	say "You did not bring a sheath tonight because you never wanted the dagger more distant than your own hand."
	
Instead of waving or swinging the dagger:
	say "When you use your dagger, you use it precisely and for a purpose."

Instead of touching the dagger:
	say "You draw a little reassurance from its cool blade and familiar weight."

Instead of squeezing the dagger:
	try cutting the player.

After dropping the dagger:
	say "You toss the dagger aside. It lands some distance away, in the undergrowth."
	
Rule for writing a paragraph about the dagger:
	say "You can just tell where your [dagger] landed, from the way [the undergrowth] is bent a few feet away; otherwise, you'd be hopeless to find it again in this murk."

Instead of looking under something which is carried by the player:
	say "You can shift [the noun] from hand to hand, but discover nothing of interest."


Chapter 4 - Yourself


Instead of examining or looking under something which is part of the player:
	if the noun is your wrists:
		continue the action;
	say "When you have a mirror in front of you — which is not now — you find [noun] merely tolerable."
	
Instead of smelling or listening to or tasting or searching something which is part of the player:
	say "You had a hound once that displayed that sort of fascination with itself. It is, however, unmanly."
	
Instead of buying something which is part of the player:
	try buying the player.
	
Instead of dropping something which is part of the player:
	say "What a grotesque idea."
	
Instead of throwing something which is part of the player at something:
	say "You had a dream like that once. You were in a garden with dwarrows climbing over the wall — they were all bloodied up and wounded — and the only thing you could think to do was pelt them with pieces of your own body."
	
Instead of throwing something at something which is part of the player:
	try throwing the noun at yourself.
	
Instead of wearing something which is part of yourself:
	say "Fortunately, you already are."
	
Instead of cutting something which is part of yourself:
	if the noun is not your wrists and the wound is not part of the player:
		unless the noun is your skin: 
			say "...on second thought, better to use the forearm; that's easier to control...";
	try cutting yourself.
	
Instead of pushing or pulling or turning or taking or touching or rubbing or squeezing something which is part of the player:
	say "[The noun] still seem[s] to be in place." [* We lavish much less attention on the player character's implementation than on Snow White's, because we need him in character to be more other-directed. So we discourage exploration of the PC by making him not very interesting in comparison.]

The description of your wrists is "[if the wound is part of the player]The wound is still there.[otherwise]You are muscled and tan from much time spent out of doors, but so would any man be in your station.[end if]".

Instead of looking under yourself:
	say "There are some roots at your feet. A little undergrowth. That is all."	

Instead of searching yourself:
	try taking inventory.

Instead of smelling the player:
	say "Manly."
		
Instead of buying the player:
	say "You're your own man. Sort of, anyway."
	
Instead of pulling or pushing or turning or climbing or taking yourself:
	say "You would look like the jester at the fair if you were to try. And that would be if it went well."
	
Instead of throwing something at yourself:
	say "Juggling is not your art."

Instead of opening yourself:
	say "Even in a metaphorical sense, that is a bad idea."
	
Before showing something to yourself:
	try examining the noun instead.
	
Before giving something to yourself:
	try taking the noun instead.

Instead of entering yourself:
	say "You have never been out of yourself."
	
Instead of waving yourself:
	try waving hands.

Instead of touching or rubbing or squeezing yourself:
	say "You rub your hands together for warmth. It helps, but only for a moment."
	
Instead of attacking yourself:
	try cutting yourself with the dagger.
	
Instead of cutting yourself for the first time:
	say "If you drew blood, would it attract her to feed? Something to think about."

Check cutting yourself when the wound is part of the player:
	say "You don't need multiple wounds." instead.

Carry out cutting yourself:
	now the wound is part of the player;

Report cutting yourself:
	say "You slice across your forearm — deep enough to bleed, not deep enough to damage any muscle. You wouldn't want to impair your strength." instead.
	
Instead of tying yourself to something:
	say "You have seen such performances from troupes of traveling fools, but you were never very impressed."

Instead of tying something to yourself:
	if the noun is yourself:
		say "Family legend has it that your great-great-grandfather once freed himself from dwarf captors by chewing his way through a 3-inch rope. You've never had much urge to follow on his example, though.";
	otherwise:
		try tying the second noun to the noun.
	
Instead of kissing yourself:
	say "In the event you grow prehensile lips, you have better ideas about what to do with them."
	
Instead of consuming or tasting yourself:
	say "As the story goes, that's how the first dwarf became a dwarf, during a particularly bad famine. [if the player knows happy-name-known]It's not a very kind story, from the point of view of the dwarf population.[otherwise][going-crazy][end if]".
	
Instead of drinking yourself:
	say "If you could be any beverage at all, it would likely be your third cousin's windfall cider, the one too sour for anyone but the hounds."
	
Before throwing yourself at something:
	say "You're more of a think before acting sort." instead.
	

Instead of cutting your hair:
	say "You leave this task to a nosy female relative when you cannot put her off any longer."
	
Instead of squeezing or taking your nose:
	say "That won't work for long: you will need to breathe sooner or later."
	
Understand "breathe" or "exhale" or "inhale" as a mistake ("[if stopped-time is not happening]When you exhale, a little cloud forms in front of your mouth[otherwise]You exhale with conscious effort, but it does not feel as though the air moves normally[end if].").

Instead of swinging your chin:
	say "People have, from time to time, and when you've been discourteous with drink, taken a swing [italic type]at[roman type] your chin. But that is another matter."
	
Instead of pushing or pulling or turning your lips:
	say "You make faces[if the current interlocutor is a person]. If [the current interlocutor] finds these expressions alluring, [it-they] suppresses the reaction[end if]."
	
Does the player mean tasting your lips:
	it is likely.

Instead of tasting your lips:
	say "You moisten your lips."
	
Instead of buying your teeth:
	say "You don't need the wooden sort just yet, thanks be."
	
Instead of rubbing your eyes:
	say "You rub your eyes, but it makes you no less tired."


The wound is a thing. Understand "bloody" or "bleeding" or "cut" as the wound. The description is "The cut is neat and clean, for now; not too deep. No danger unless it becomes inflamed; but if all goes well you will be able to dress it later." Understand "blood"  as the wound when the dripped blood is not visible.

After examining the player when the wound is part of the player:
	say "There is a shallow cut on your left forearm."

Understand "stop [wound]" as a mistake ("Not having anticipated this eventually, you did not bring bandages.").

Understand the command "staunch" as "stop".

Instead of rubbing the wound:
	say "You can't make it any cleaner than it is, given the present conditions. It's not as though your hands are especially dirt-free. And you have no cloths."

Instead of tasting or consuming the wound:
	say "Animals aside, it is not healthy to lick wounds."
	
Instead of smelling the wound:
	say "It has no especially strong smell, beyond the faint coppery tang of blood. But then there hasn't been time for it to become fetid."

Instead of inserting the wound into the heart-sized box when the dripped blood is off-stage:
	if the player is not carrying the heart-sized box:
		if the heart-sized box is closed:
			say "(first taking and opening the box)[line break]";
			silently try taking the heart-sized box;
			silently try opening the heart-sized box;
		otherwise:
			say "(first taking the box)[line break]";
			silently try taking the heart-sized box;
	otherwise if the heart-sized box is closed:
		say "(first opening the box)[line break]";
		silently try opening the heart-sized box;
	if the heart-sized box is closed:
		stop the action;
	if the player is not carrying the heart-sized box:
		stop the action;
	move the dripped blood to the heart-sized box;
	say "You squeeze some blood into the box. It may be your imagination, but the box seems to grow warmer in your hand[if the player recollects  how the blood-sundering could be done][casually queue really-you][otherwise][casually queue how-strange][end if]."
	
Instead of giving the dripped blood to Snow White:
	try giving the wound to Snow White.

Instead of giving the wound to Snow White:
	if the player knows vampirism-revealed:
		say "Knowing what you know, you can't quite bring yourself to taunt her openly.";
	otherwise:
		say "You offer her the blood, but she merely [for eye-gest]looks back at you with cold amusement[or walk-gest]takes a neat dance-step backward[or breath-gest]laughs softly under her breath[end for]."

Instead of inserting the dripped blood into the heart-sized box when the dripped blood is in the heart-sized box:
	say "There is already blood in the box; you don't need to give any more."

Instead of inserting the wound into the heart-sized box when the dripped blood is in the heart-sized box:
	say "There is already blood in the box; you don't need to give any more."
	
Understand "pour [wound] in/into [something]" as inserting it into. Understand "pour [dripped blood] in/into [something]" as inserting it into.
Understand "fill [something] with [something]" as inserting it into (with nouns reversed). 

Instead of taking the dripped blood:
	say "It's not something you can just pick up."

The dripped blood is a thing. The indefinite article is "some". The description is "It looks reddish-black in this light." 

Instead of smelling the dripped blood:
	say "Coppery."

Instead of tasting or consuming the dripped blood:
	try tasting the wound.

Understand "empty [something]" as emptying. Emptying is an action applying to one thing.

Understand "pour out [something]" as emptying. Understand the command "dump" as "pour".

Check emptying something:
	if the noun is not a container:
		say "[The noun] [is-are] not something that can be emptied, not having contents in the normal sense to start with." instead;
	if nothing is in the noun:
		say "[The noun] [is-are] already empty." instead.
		
Carry out emptying something:
	if the noun contains something (called target) which is not the dripped blood:
		silently try taking the target;
		silently try dropping the target;
	otherwise if the noun contains the dripped blood:
		remove the dripped blood from play.	

Check someone emptying something:
	stop the action. 
	
Report emptying something:
	say "[The noun] [is-are] now empty."


Chapter 5 - The Corpse

The corpse is an animal in the Dark Woods. The printed name is "decomposing hart corpse". [We've made it animate so that we can address it later, though most of the time this won't go anywhere.] Understand "hart" or "decomposing" or "dead" or "deer" or "animal" or "body" or "beast" or "stag" or "noble" as the corpse. Understand "undead" or "glowing" as the corpse when stopped-time is happening. "[if stopped-time is happening]You are faced, rather uncomfortably, with one undead and glowing hart[otherwise]Earlier, you killed a hart and left it here. It was a preparation: you didn't want to have to hunt such an animal in the darkness[one of].

Now, you can't help wondering whether you should have spared the noble beast[or][stopping][end if]." 

Rule for clarifying the parser's choice of the corpse:
	do nothing instead.

Before giving or showing something to the corpse when the player does not know hart-thanked or the player knows hart-no-longer-undead:
	say "There is, unsurprisingly, no reaction." instead.
	
The corpse's hair is privately-named. Understand "fur" or "pelt" as the corpse's hair. The printed name of the corpse's hair is "fur". The description is "It is dark and matted with dirt and blood: not a savory thing to look at."

The description of the corpse's eyes is "They are fixed and glassy."

The description of the corpse's nose is "It looks damp and snuffly."

The description of the corpse's chin is "The hart doesn't exactly have a chin in the human sense. Especially not after what the corpse has been through."

The description of the corpse's face is "It was a handsome creature, once. The antlers are still attached."

The description of the corpse's skin is "The skin is mostly still covered with fur."

The description of the corpse's ears is "They are mostly intact — intricate clever structures. You've always admired the ears of animals. Cats, most of all."

The corpse's wrists are privately-named. Understand "hooves" or "hoof" or "leg" or "legs" as the corpse's wrists. The printed name is "hooves". The description is "They are in good condition, and would have left tracks as long as your thumb (you had a look earlier). The hart was perhaps three years old when you killed it."

The description of the corpse's neck is "The neck is the most elegant part of any animal. But seldom useful for much."

The description of the corpse's  veins is "The blood will have long since stopped flowing." Understand "hart's" or "neck veins" as the corpse's  veins.

The description of the corpse's lips is "Somewhat... frayed."

The description of the corpse's teeth is "They're a bit worn: he's not the youngest buck you've taken."

Instead of cutting or attacking something which is part of the corpse:
	if the noun is the hart's heart:
		continue the action;
	say "You've probably done enough harm already."

Instead of looking under the corpse:
	say "There's nothing there but crushed undergrowth."

Instead of smelling the corpse:
	say "You know a fine recipe for meat of advanced age: to bury it in cold ground for ten days, then to dig it up again and cut away the green portions, and then to prepare the remainder, stewing in an iron pot, all day long; and then to serve it with nutmegs if they can be gotten, and if not, with strong peppers from the south. It is an extravagant meal, but —
	
—but in any case the hart has not advanced to such a state as to need such treatment. It could be eaten in the ordinary way, yet, and not make a man ill."

Instead of smelling something which is part of the corpse:
	try smelling the corpse.

Instead of attacking the corpse:
	if the hart's heart is part of the corpse:
		try taking the hart's heart;
	otherwise:
		say "Haven't you done enough?"

Instead of touching or rubbing or squeezing the corpse:
	if the corpse is the current interlocutor:
		say "That idea makes you feel ill.";
	otherwise if the player knows hart-thanked:
		try waking the corpse;
	otherwise:
		say "You lay a hand on its flank. The flesh is cold."

Instead of pushing or pulling or turning the corpse:
	if the corpse is the current interlocutor:
		say "It might not appreciate such a gesture.";
	otherwise if the player knows hart-thanked:
		try saying hello to the corpse;
	otherwise:
		try taking the corpse.

Instead of waking the corpse:
	if the corpse is the current interlocutor:
		say "It is already unnervingly awake.";
	otherwise if the player knows hart-thanked:
		try saying hello to the corpse;
	otherwise:
		say "You'd need Gabriel's trump: the unfortunate creature is dead."

Instead of taking the corpse:
	say "You don't want to encumber yourself with such a large load; you wouldn't be able to move it far, and you have [italic type]her[roman type] to deal with."
	
Instead of buying the corpse:
	say "According to the law of the land, the hart's flesh is already yours, since you were the one who slaughtered him, and were on territory where you have license to hunt[if the corpse is the current interlocutor]. 

At the moment, however, [the corpse] appears to be exercising squatter's rights[end if]."

Instead of climbing the corpse:
	if the corpse is the current interlocutor:
		say "What a notion.";
	otherwise:
		say "It is hardly large enough to provide much scope for climbing."

Instead of consuming or tasting the corpse:
	if the corpse is the current interlocutor:
		say "Not (you rather think) while it is reanimated and holding a conversation with you.";
	otherwise:
		say "Not raw, you don't." 
		
Instead of kissing the corpse:
	say "Contrary to a certain scurrilous rumor started by your second cousin, you are not attracted to animals of the non-human persuasion."
	
Instead of waving or swinging the corpse:
	say "Even if there were any point in that, it would prove rather bulky."

The hart's heart is part of the corpse. The description of the hart's heart is "It hasn't yet been too much eaten by animals. It should do." 

Report dropping the hart's heart:
	say "You set [the noun] down carefully where you'll be able to find it again." instead.
	
Instead of giving or showing the hart's heart to the corpse when the corpse is the current interlocutor:
	say "[one of]'I am well aware that you have it,' he says austerely. 'And as it cannot be returned, I suggest we try to pass over the incident with as much tact as possible.'[or]He doesn't seem to want it back himself.[stopping]"

Instead of searching the hart's heart:
	say "If you cut it open, it will be ruined for your current purposes."

Instead of wearing the hart's heart:
	say "As a trophy, on a string? You're not a man for much jewelry."

Instead of buying something which is part of the corpse:
	try buying the corpse.
	
Instead of buying the hart's heart:
	try buying the corpse.

Instead of opening the hart's heart:
	say "There will only be blood within."
	
Instead of smelling the hart's heart:
	say "Smells like copper."

Instead of listening to the hart's heart:
	say "It is cold and still."

Instead of squeezing or rubbing or touching the hart's heart:
	say "You are unable to make it beat again."
	
Instead of tasting or consuming the hart's heart:
	say "Even to taunt your prisoner, you can't bring yourself to take a bite of the raw lump."
	
Instead of pulling or pushing or turning the hart's heart:
	if the hart's heart is part of the corpse:
		try taking the hart's heart;
	otherwise:
		try squeezing the hart's heart.
	
Instead of searching or looking under the hart's heart when the hart's heart is part of the corpse:
	say "It is surrounded by ordinary flesh."

Instead of feeding the hart's heart to Snow White:
	if the player knows vampirism-revealed:
		say "'Like a bite of heart?' you ask, holding up the one you severed from the stag[first-vampire-accusation]. She pretends not to hear you.";
	otherwise:
		say "[one of]You hold it up to Snow White's mouth. She looks at it and her lips[set body-context to Snow White] part, but she does not— well, what were you expecting? For her to take a bite out, as though it were a piece of fruit?[or]You have already tried that experiment. It was inconclusive.[stopping]"

Some antlers are part of the corpse. The description is "They are sizable, and firmly attached: this is a deer at the peak of his development, killed in mating season."

Instead of touching or rubbing the antlers:
	say "You run your fingers over the antlers for luck."

Instead of searching the antlers:
	say "While they might, domesticated, make a suitable rack for hats or cloaks, they are currently unadorned."
	
Instead of listening to the antlers:
	say "Unsurprising silence."

Instead of smelling the antlers:
	say "They haven't much scent of their own on this cold night."

Instead of pulling or pushing or turning or taking or cutting or buying the antlers:
	if the current interlocutor is the corpse:
		say "He would probably take exception to that." instead;
	say "[one of]This is no occasion for collecting trophies. Besides, you have three larger sets at home. No: had. Before your cabin was burned by the Queen[cabin-destroyed].

Well, you can collect them another time.[or]You are not currently in the mood for antler-collection.[stopping]"
	
Instead of squeezing the antlers:
	say "They are firm."

The chest cavity is part of the corpse. It is a flush container. The description is "The hart has been only very incompletely butchered. Most of its organs remain intact[if stopped-time is happening]. It is also glowing from the inside, which is not an effect you've ever noticed down at the village butcher shop[end if]." Understand "viscera" or "organs" or "liver" or "guts" or "gut" or "intestine" or "lung" or "lungs" or "kidney" or "stomach" as the chest cavity. 

Instead of searching the chest cavity when the hart's heart is part of the corpse:
	say "The heart is still partially attached to the body of the corpse."

Instead of inserting something into the chest cavity:
	if the noun is the hart's heart:
		say "Putting the heart back would not make it work again.";
	otherwise if the corpse is the current interlocutor:
		say "The woods lack the restrictions of court, but inserting random objects into your interlocutors probably still qualifies as unacceptable.";
	otherwise:
		say "You have attended many a feast on roast hart, but [the noun] do[es] not constitute suitable stuffing.".

Instead of pulling or pushing or turning or taking or touching or rubbing or squeezing the chest cavity:
	say "Being up to your elbow in stag viscera holds no appeal at present."

Instead of looking under or searching the chest cavity:
	say "You are not one of those who claim to read the future from the inspection of a creature's organs, but you see nothing to amaze you here."

Instead of cutting or opening the chest cavity:
	try cutting the corpse.
	
Instead of closing the chest cavity:
	say "Much, much too late."

Instead of consuming the chest cavity:
	try consuming the corpse.

Instead of listening to the chest cavity:
	say "What you won't hear is any sort of heartbeat."

Instead of searching the corpse:
	try examining the corpse.
	
The description of the corpse is "[if the hart's heart is part of the corpse]The heart is still there[notice hart's heart] — exposed by your neat cuts, but not damaged yet[otherwise]The heart has been cut out[end if]."

Instead of examining the corpse when the player knows hart-thanked and the player does not know undead-hart:
	say "[one of]You peer more closely at it. The heart is gone, the chest cavity lies open, it is plainly dead. And yet it seems to be looking at you with those glassy eyes.[or]Maybe it will speak soon.[or]Perhaps if you touched it it would wake up.[stopping]"

Instead of examining the corpse when the corpse is the current interlocutor:
	say "His body still gapes; his fur is still bloody and tattered; but his face is animated by an expression that no animal would have."

Understand "glassy" as the corpse's eyes.

Instead of examining the corpse's eyes when the player knows hart-thanked and the player does not know undead-hart:
	say "It winks. Or the lamplight glints off its eyeball. One of those.";
	now the corpse is knowing.
	
The corpse can be knowing or unknowing. The corpse is unknowing. [* This is so that we can prefer to wink at the corpse if it has seemed to wink at us; but otherwise not.]

To say notice (noticed item - an object):
	have the parser notice the noticed item. [* This is to deal with the fact that the player may well type "TAKE IT" after being told that the heart is still in the deer's body, even though the pronoun at that point is set to the corpse as a whole.]

Understand "extinguish [something]" or "put out [something]" as extinguishing. Extinguishing is an action applying to one thing.

Check extinguishing:
	if the noun is not lit:
		say "[The noun] cannot be extinguished." instead.
	
Instead of extinguishing the lantern:
	say "Without it, even you will never find your way back to the castle."

Instead of examining yourself:
	say "[one of]You haven't slept well in days. And today: the hunt, the death of your dogs, the Queen enraged, the court on edge. An evening of the Queen locked away in her chamber, while the magic mirror talked on and on, its shrieking voice and its laughter audible even if its counsel was not. The uneasy sleep that fell. The more uneasy waking, with the Queen herself in your chamber, bending over your bed. The urgent demand that you take Snow White at once, and bring her here, and end her life.

You are exhausted. Your other feelings are even worse and you do not dare dwell on them.[or]You are so tired and your mind so full of horrible things that you barely know yourself any longer.[stopping]".

Understand "dwell on my feelings" or "dwell on feelings" or "dare to dwell on feelings" or "dare to dwell on my feelings" as a mistake ("Dread; cold; the knowledge of corruption.").

Instead of waiting:
	if the nag of the current quip is not "":
		stop the action; [* In this case, we're already going to have a grounding beat and a comment, so we want beat-comment, not beat-beat-comment, which looks clumsy and in the worst case scenario will give, say, two flickering-lantern messages on a single turn.]
	otherwise:
		say "[beat][line break][paragraph break]". 
		

	
Part 10 - Conversation
 

Chapter 1 - Emily Short  



When play begins: 
	now the quip-suggestion-phrase is "You [one of]could[or]might[or]can[or]could now[or]could always[or]consider whether to[as decreasingly likely outcomes] "; [* By default this is just "You could "; here we change it up a little just for variety's sake, because this is a game where it is destined to appear a great deal.]
	start talking to Snow White;
	queue you-promised-me as postponed obligatory.
	
[An availability rule for what she seems:
	if the player knows magic-revealed:
		it is off-limits; [* The question at that point becomes a bit mundane.]
	if the player knows vampirism-revealed or the player knows snow-white-possessed:
		it is off-limits.]
	
Does the player mean discussing what she seems: it is very likely.

 what she seems is a questioning quip. 
 The printed name is "what she is". The true-name is "what she seems". 
 Understand "is" as what she seems.
 It mentions Snow White.
 The comment is "'What are you?' you demand. '[if scariness is greater than 0]No ordinary girl[otherwise]No other girl your age[end if] would have lasted a night march into the dark woods.'".
 The response is "'Oh, I dare say she would, if she were tied up and taken along by force! It is not as though you stopped to ask how I was feeling[pathetic].' ".
 It quip-supplies Snow White.
 It is negated by magic-revealed, vampirism-revealed, snow-white-possessed.
	
you-promised-me is a q-transitional NPC-directed quip. 
	The response is "[for eye-gest]She's looking at you with an expression very reminiscent of her stepmother's[or walk-gest]She paces around to where she has the upper ground and can face you eye-to-eye — very much like her stepmother[or breath-gest]She takes a deep breath, just as her stepmother does when about to give a command[end for]. '[if the previous quip does not mention safe haven]We had a deal,[pact-exists]' she says. 'I have made a safe resting place. [otherwise]Concerning my haven, I mean to hold you to our deal[pact-exists]. [end if]You promised to take me there if the Queen turned against me.'"
	
[ The following rule gets a little dense, but what we're about to do is allow Snow White to interrupt anything you say to her (assuming it's more than three words of continuous quoted dialogue) if you change the subject when she first challenges you about your deal.

This is frankly gratuitous, but I had the idea and it seemed to fit the mood and feel of the exchange better than having her grumble at the subject-changing entry-point, after the player has finished his comment. 

In my defense (1) this is a prominent quip near the beginning of the game, so lavishing a little extra care on the transitions here is worthwhile; (2) this is another feature that breaks the standard conversation flow pattern, and anything that does that reduces the apparent monotony and automation of the prose.

Aperture science: we do what we must because we can.]

After discussing something when the previous quip is you-promised-me:
	if the noun indirectly-follows you-promised-me:
		continue the action;
	let N be indexed text;
	let N be "[comment of the current quip]";
	if N matches the regular expression "(.?\'\w*\s\w*\s\w*\s)(.*)": [splits the comment field after the first three words of the player's direct speech]
		replace the regular expression "(.?\'\w*\s\w*\s\w*\s)(.*)" in N with "\1--'[paragraph break]She sets her jaw as she realizes you are changing the subject.[paragraph break]'--\2";
	queue irritated-promise as postponed obligatory;
	say "[angry][angry][angry][N][line break]".
	
Rule for listing plausible quips when the current quip is you-promised-me: [* The standard "You could..." line gets thuddingly dull after a short time.]
	prepare a list of plausible quips; 
	if the number of filled rows in the Table of Scored Listing is 0:
		rule succeeds;
	otherwise:
		repeat through the Table of Scored Listing:
			carry out the quip-introducing activity with the output entry;
		[This could have blanked some rows out, so we want to check again:]
		if the number of filled rows in the Table of Scored Listing is greater than 0:
			if the number of filled rows in the Table of Scored Listing is 1:
				say "[roman type]Only one explanation comes to mind: you could [the prepared list delimited in sequential style].";
			otherwise: 
				say "[roman type][one of]Your instinct is to put her off. [or][stopping]You consider explanations for your stop: [the prepared list delimited in questioning style]?";
			
Table of List Style Assignments (continued) 
list style	first delimiter	second delimiter	alternate second delimiter	indefinite name phrase	definite name phrase
questioning	"? "	"? "	"? "	"[a current listed object]"	"[the current listed object]"
				
Understand "put her/snow/white/princess off" or "put snow white off" as a mistake ("Yes, but how?") when the current quip is you-promised-me.

[Availability rule for that the safe haven seems too far away to reach tonight:
	if the player recollects explain that you must stop here to collect the heart:
		it is off-limits.]
		
Understand "excuse" or "excuses" or "an excuse" or "some excuse" as "[excuses]".

irritated-promise is an NPC-directed quip.
	It mentions deal.
	The response is "'Talk and meander all you like, but sooner or later I expect you to honor our deal[if the previous quip is weak]. [angry][otherwise],' she reminds you[angry]. '[end if]Or if you aren't brave enough for that, then to set me free and let me go on alone.'".
	It quip-supplies Snow White.

Instead of saying yes or saying no when the current quip is irritated-promise:
	say "She's obviously annoyed; better just to do what you have to do, and let her discern your intentions from your actions."
	
 that the safe haven seems too far away to reach tonight is an informative quip. 
 The printed name is "that the safe haven is too far away to reach tonight". The true-name is "that the safe haven seems too far away to reach tonight". 
 Understand "is" or "put her off" or "make [excuses]" or "put Snow White off" as that the safe haven seems too far away to reach tonight. 
 It mentions safe haven.
 The comment is "'[if immediately]It[otherwise]The haven[end if] is too far away to reach tonight.' The Queen sent you out long after midnight. There are probably only a few hours remaining before dawn.".
 The response is "She shivers. [if the player does not recollect pleading-to-be-untied]'I suppose the Queen will expect you to return quickly.' You do not say anything, which is a kind of answer. [queue pleading-to-be-untied as immediate obligatory][otherwise]'If you had more wit, you'd let me go to find my own way,' she comments, though her tone suggests that she doesn't expect to convince you this time.[end if]".
 It quip-supplies Snow White.
 It is listed.
 It indirectly-follows you-promised-me.      
                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                   
Availability rule for explain that you must stop here to collect the heart:
	if the hart's heart is not part of the corpse:
		it is off-limits;
	[if the player recollects that the safe haven seems too far away to reach tonight:
		it is off-limits.]
		
Rule for listing plausible quips when the current quip is explain that you must stop here:
	do nothing instead.

 explain that you must stop here to collect the heart is a performative quip.
 Understand "I" or "we" or "put her off" or "put Snow White off" or "make [excuses]" as explain that you must stop here to collect the heart.
 The printed name is "explain that you must stop here to collect the[if we have not examined the corpse] hart's[end if] heart".
 It mentions hart's heart, corpse.
 The comment is "'We are stopping to collect the hart's heart[queue go-ahead-and-cut as postponed optional]. I cannot return to the Queen empty-handed.' In fact you have some doubts as to whether this scheme will work. Surely the magic mirror will know that the heart is a fraud? But you could think of no better way. You will just have to take comfort in the fact that the mirror doesn't seem to know about anything that hasn't been reflected in its surface.".
 The response is "She nods curtly.".
 It quip-supplies Snow White.
 It indirectly-follows you-promised-me.
 It is listed.

 what seems at the safe haven is a questioning quip. 
 The printed name is "what is at the safe haven". The true-name is "what seems at the safe haven". 
 Understand "is" as what seems at the safe haven. 
 It mentions safe haven.
 The comment is "'What sort of haven is this?' [if anger is greater than 1]Perhaps it is futile to hope for an answer[otherwise]It is astonishing that she has managed to provide herself with even that much protection[end if].".
 The response is "'It has the necessities of life only,' she says. 'A very modest house. A small number of servants — or perhaps I should say a number of small servants.'".
 It quip-supplies Snow White.
 It indirectly-follows you-promised-me.

[Availability rule for what she means about the servants being small:
	if the player knows servant-dwarrows:
		it is off-limits.]

 what she means about the servants being small is a questioning quip. The comment is "'[if immediately]Small[otherwise]What did you mean about your servants being small[end if]?'".
 Understand "little" or "short" or "tiny" or "number of small servants" as what she means about the servants being small.
 It mentions dwarrows, servants.
 The response is "'I went to the dwarrows[servant-dwarrows],' she replies. 'None of the human subjects of the Queen could be trusted to protect me for long. The danger would be too great.'".
 It indirectly-follows what seems at the safe haven.
 It quip-supplies Snow White.
 It is negated by servant-dwarrows.

 why she avoids the magic mirror is a questioning quip. 
 It mentions magic mirror.
 The comment is "'[if scariness is greater than 1]Tell me, why exactly[otherwise if anger is less than 0]I am curious: why[otherwise]Why[end if] do you avoid the Queen's mirror? Many of us have stood before it, and taken no harm.'".
 The response is "It is hard to tell expressions by the lantern light, but hers seems full of [if anger is greater than 1]pity and scorn[angry][otherwise]pity[end if]. 'You see only an ordinary reflection in it, do you not? You do not see how the mirror reflects you to the Queen. If you did, you would not expose yourself.'"
 It quip-supplies Snow White.

 what she sees in the mirror is a questioning quip. 
 It mentions snow white, magic mirror.
 The comment is "'[if the previous quip is why she avoids the magic mirror]And you? [end if]What do you see when you stand before the mirror?'".
 The response is "'I do not stand there. I know better.' [if the player does not know rock-at-mirror]Certainly the last time someone tried to bring her before it, she kicked and scratched and threw a rock at it[rock-at-mirror], and could not be controlled[otherwise]It was not a happy time[end if].".
 It quip-supplies Snow White.
 It is negated by snow-white-possessed.
 It indirectly-follows why she avoids the magic mirror.
 It is listed.

 what you look like in the mirror is a questioning quip. 
 The printed name is "what you look like in the mirror". The true-name is "what you look like in the mirror". 
 Understand "I" as what you look like in the mirror. 
 It mentions magic mirror, yourself.
 The comment is "'What do I look like[if the previous quip mentions the magic mirror], then[otherwise] in the magic mirror[end if]?' you ask. You had always assumed that the mirror showed the Queen what it showed you; that if you stood before the mirror with no bad intentions in your heart, without treason, then your innocent reflection would look back. [paragraph break]True enough, you've avoided passing in front of it for the last day or two, since you and Snow White made your pact.".
 The response is "'You stoop. You have a hunchback. [scary][angry]Your reflection leers at the Queen. Once, it made an obscene suggestion.' [if the player knows snow-white-possessed][queue how the mirror reflects you to the queen][end if]She licks the tips of her teeth daintily, the way she does when she's thinking. 'I think she was pleased[snowWhite-sees-reflections].'".
 It quip-supplies Snow White.
 It indirectly-follows why she avoids the magic mirror.
 It is listed.


 no-3 is a negative quip. The printed name is "say no". The true-name is "no-3". 
 It mentions yourself.
 The comment is "'I do not lust after the Queen.'".
 The response is "'Not much, anyway; no more than a man can help.' It is as though she can read your thoughts[scary]. 'The mirror is enchanted. That doesn't make it honest.'".
 It quip-supplies Snow White.
 It directly-follows what you look like in the mirror


thank-the-hart is a q-transitional NPC-directed quip.
 The response is "She goes awkwardly over to the corpse and goes to one knee beside it. 'Thank you,' she whispers — to it, not to you. Then she leans down and— what, exactly? You can't see. Maybe she kisses it. Maybe she licks its blood[scary][possible-blood-drinking][hart-thanked]. Maybe neither.

When she's done she struggles back to her feet and looks at you defiantly[queue hart-stirs as postponed obligatory]."
 It quip-supplies Snow White.

[Just a possibly-needless refinement:]

Instead of waiting when the current quip is thank-the-hart and Snow White is likely to continue:
	if Snow White is ready for transition:
		say "There's an awkward mutual silence. [run paragraph on]";
	otherwise:
		say "You keep silent about this weird behavior and wait for her to change the subject. [run paragraph on]"

hart-stirs is an NPC-directed quip.
	The response is "It might be just a trick of the light, but it almost looks as though the hart [notice corpse]moved."
	
whether the hart moved is a weak questioning quip.  The comment is "'Did you see the hart move[if immediately] just now[otherwise] a little while ago[end if]?'".
 It mentions corpse.
 The response is "'No[scary]. It is dead.'".
 It indirectly-follows hart-stirs.
 It quip-supplies Snow White.

Rule for quip-introducing what she was doing alongside the hart:
	say "[roman type]Of course you could ask what [if the current quip is hart-stirs]she did to the hart[otherwise]she was doing[end if], but who is to say that she would answer?"; [* This replaces the usual 'You could...' prompt with custom text.]
	list no other quips. 

 what she was doing alongside the hart is a questioning quip. 
 The printed name is "what she was doing with the hart". The true-name is "what she was doing alongside the hart". 
 Understand "with" or "did" or "doing/did to" or "snow white" or "snow" or "white" or "princess" or "girl" as what she was doing alongside the hart. 
 It mentions corpse.
 The comment is "'What was that?' you ask. 'What did you [if the previous quip is hart-stirs]just do[otherwise]do to the hart[end if]?'".
 The response is "'It died for my sake; I repaid a little of the debt.' Her lips look dark, almost black[set body-context to Snow White][scary].".
 It quip-supplies Snow White.
 It directly-follows thank-the-hart, hart-stirs.
 It is listed.

 how such a debt could be repaid is a weak questioning quip.  The comment is "'Such a debt is beyond repayment.'".
Understand "can" as  how such a debt could be repaid.
 It mentions corpse.
 The response is "[unless Snow White is likely to continue]Her expression is mysterious. [end unless]'[if the player knows snow-white-possessed]A little manipulation of the souls involved. Nothing He would approve of. But a favor to the hart, and -- well, never mind[otherwise]I do not expect you to understand[end if].'".
 It directly-follows what she was doing alongside the hart.
 It quip-supplies Snow White.

 demo-dagger-1 is a demonstrative quip. It mentions the dagger. The true-name is "demo-dagger-1".  The comment is "You hold out the dagger for her to look at. 'Bear this in mind,' you tell her. 'I'm the one who is armed[angry].'".
 It quip-supplies Snow White.
 The response is "If she is frightened, she doesn't show it."

 whether she kens who killed your dogs is a weak questioning quip. 
 Understand "if" or "my" or "snow white knows/kens" or "knows" or "kens" or "know" or "ken" as whether she kens who killed your dogs. 
 The true-name is "whether she kens who killed your dogs". 
 The printed name is "whether she knows who killed your dogs".
 It mentions blood, hounds.
 The comment is "'Someone killed my dogs,' you say. 'I found them in a pile outside my door. They had no blood left in them[possible-blood-drinking]. Do you know anything about that?'".
 The response is "'No. But my condolences. I know you are close to your animals[pathetic].'"
 It quip-supplies Snow White.

Chapter 2 - John Cater Oct 28

 what she did to anger the Queen is a questioning quip.
 Understand "snow white" or "you" as what she did to anger the queen.
 It mentions Queen.
 The comment is "'What did you do to anger the Queen?'".
 The response is "'I did nothing, it was my existence that bothered her. I would not let her examine your existence too closely, were I you.'".
 It is negated by snow-white-possessed.
 It quip-supplies Snow White.

[Moved the quip about servants to the section of repeatable quips at the end]

Chapter 3 -  Eric Eve Oct 29

 if she seems a witch is a questioning quip. 
 The printed name is "if she is a witch". The true-name is "if she seems a witch". 
 Understand "is" or "whether" as if she seems a witch. 
 It mentions snow white, witchcraft.
 The comment is "'Are you a witch?' you ask, 'Is that what you don't want the Queen to know?'".
 The response is "She ponders your question for a moment, then replies, ''Witch' is not a good description of what I am; [scary]but then, so few people understand witches.'".
 It quip-supplies Snow White.
 It is negated by vampirism-revealed, snow-white-possessed. 
 It is listed.

 whether she drinks blood is a questioning quip. 
 Understand "if" as whether she drinks blood.
 It mentions blood, vampirism.
 It assumes possible-blood-drinking. [* This is an edit made per Eric's request: the initial version of Alabaster did not provide any way to check facts (as opposed to the relation to other quips). Now that that has been added, I went back and revised Eric's quip.]
 The comment is "[one of]'Tell me frankly, do you drink blood, Snow White?' you demand.[or]'Let me ask again: do you drink blood?'[stopping]".
 The response is "[one of]'I drink what I need to drink, like every other living being,' she replies coolly[scary].[or]'Do you suppose the answer will change with more questioning?'[or]She merely smiles, as enigmatic as always.[stopping]". [* Added some alternate answers here for repeated questioning.]
 It is negated by snow-white-possessed.
 It quip-supplies Snow White.
 It is repeatable.
 It is listed.

Chapter 4 - Jayzee Oct 29

the heart seems dead is an informative quip. 
 The printed name is "heart is dead". The true-name is "heart seems dead". 
 Understand "is" as the heart seems dead.
 It mentions corpse, hart's heart.
 The comment is "'The heart of the hart beats no longer,' you say. 'It's almost as cold as yours, Snow White.'".
 The response is "Snow White [for eye-gest]chills you with a look[or walk-gest]paces[or breath-gest]huffs out a breath[end for][angry][scary]. 'If a killer has a heart, is that a better killer, or a worse, Soldier?'".
 It quip-supplies Snow White..
 It is negated by snow-white-possessed. [This seems too strange a thing to say if you know who she is.]
 It is restrictive.
 It indirectly-follows explain that you must stop here to collect the heart.

 if she seems a living being is a questioning quip. 
 The printed name is "if she is a living being". The true-name is "if she seems a living being". 
 Understand "is" or "whether" or "alive" as if she seems a living being. 
 It mentions vampirism.
 The comment is "'You include yourself[if immediately] then,[end if] among the living?' you say.".
 The response is "[one of]Her lips twitch[set body-context to Snow White]. 'Certainly I am among the living, still.' she replies.[or]'As I said before: yes.'[stopping]".
 It directly-follows whether she drinks blood.
 It is negated by snow-white-possessed.
 It quip-supplies Snow White.
 It is repeatable.

[Availability rule for whether she seems getting colder:
	if the player knows snow-white-possessed:
		it is off-limits.] [* Just too weird asking Lilith this.]

whether she seems getting colder is a questioning quip. 
 The printed name is "whether she is getting colder". The true-name is "whether she seems getting colder". 
 Understand "is" or "if" or "shiver" or "shivering" as  whether she seems getting colder. 
 It mentions night, temperature, vampirism, Snow White.
 The comment is "[one of]You shiver. 'The night grows colder, and I with it,' you say. 'Is this true for you also?'[or]'Colder and colder, isn't it?'[stopping]".
 The response is "'[one of]I am no colder than you[or]As before[or]Asking will not make you warmer[stopping],' she replies indifferently.".
 It quip-supplies Snow White.
 It is negated by snow-white-possessed.
 It is repeatable.
 [It is listed.]

whether she will keep the pact is a questioning quip. 
 Understand "snow white" or "if" as whether she will keep the pact.
 It mentions deal, Snow White.
 It assumes pact-exists.
 The comment is "[if scariness is greater than 1]You swallow nervously[otherwise if anger is greater than 1]Her current mood is a little disquieting[otherwise]You take a step toward her[end if]. '[if the previous quip mentions deal]And you will keep your side of our pact[otherwise]You remember the terms of our pact? You will abide by it[end if]?'".
 The response is "[one of][Snow White smiles]. 'But of course,' she replies softly[scary]. 'I will never reveal your treachery to the Queen while you live.' [line break][or]'I said that I will. Do not question my word.'[stopping]".
 It quip-supplies Snow White.
 It is repeatable.
 It is negated by snow-white-possessed.
 It is listed.

why the Queen fears her is a questioning quip. 
 It mentions queen, fear, snow white.
 It assumes queen-fears.
 The comment is "'Why does the Queen fear you?'".
 The response is "'For the same reason you fear me.' [Snow White smiles][scary]. 'Perhaps.' [line break]".
 It quip-supplies Snow White. [* This is an unusual case, in that this quip was added and then, quite a bit later, Eric Eve added a question about *whether* the queen fears her. My solution is to make this quip dependent on a fact, which will be set by the later quip but also perhaps by some implications in the text.]
 It is negated by snow-white-possessed.

Chapter 5 - Rob Dubbin Oct 29

 why you should avoid the mirror is a questioning quip.
 Understand "I" as why you should avoid the mirror.
 It mentions Queen, magic mirror.
 The comment is "'I am a huntsman,' you say proudly. 'I respect her authority but serve my own whims.'".
 The response is "Her eyes narrow[set body-context to Snow White]. 'You are a man,' she corrects. 'and her authority, as you call it, extends more deeply than you perhaps suspect[angry][snowWhite-sees-reflections].'".
 It quip-supplies Snow White.
 It is restrictive.
 It directly-follows why she avoids the magic mirror.
 It is listed.

what authority seems is a questioning quip.
 The printed name is "what authority is". The true-name is "what authority seems". 
 Understand "is" as what authority seems.
 It mentions Queen, magic mirror, Witchcraft.
 The comment is "'It sounds like you're talking about more than the power to issue land grants.'  You attempt a laugh that rings hollow the moment it leaves your mouth and hits the air.".
 The response is "'It is amusing to see you enjoy your joke, in much the same way that it must have been amusing to watch the final, desperate moments of this hart,' responds Snow White. You can almost see your attempt at levity plummet to the ground and shatter[angry].".
 It quip-supplies Snow White.
 It directly-follows why you should avoid the mirror.
 It is listed.

A plausibility rule for what the symbols mean:
	if the player does not know symbols-seen:
		it is dubious. [* This is a refinement of my own: Rob designated that this quip should be listed, which is fine, but it doesn't seem to make a lot of sense to list it before the player has looked at the box. On the other hand, the player should be able to trigger it with SHOW BOX TO SNOW WHITE even before he examines the box himself. We can control this with a 'plausibility rule', which exists to determine how *likely* the player is to want to say something, and therefore whether it should be explicitly listed as an option. (This is not something that the collaboration interface is currently set up to handle.)]

[An availability rule for what the symbols mean:
	if the player knows sundering-method:
		it is off-limits.]
	
what the symbols mean is a questioning quip.
 It mentions box, symbols.
 The comment is "You tilt the ornamented box in Snow White's direction. 'Any insight into these symbols[symbols-seen]? I can't make heads or tails of them.'".
 The response is "Her eyes[set body-context to Snow White] flash, and for a moment you could swear the box felt colder in your hands. 'No,' comes the reply[angry].".
 It quip-supplies Snow White.
 It is negated by sundering-method.
[ It is restrictive.] [* I think we do want to allow the player to go another direction from here.]
 It is listed.


Rule for quip-introducing what the symbols mean again:
	say "[roman type]But she is lying. Perhaps if you asked again.";
	list no other quips.

[An availability rule for what the symbols mean again:
	if the player knows sundering-method:
		it is off-limits.]
		
what the symbols mean again is a questioning quip.
 It mentions box, symbols.
 The comment is "You won't give up that easily. 'I can tell you know something, and I have you in something of a compromising position,' you say, asserting your authority. 'Tell me what these symbols mean.'".
 The response is "'Very well,' she says, conceding the point. 

Before you can respond, the forest goes silent, the wind seems to swirl, and the white skin of Snow White's face takes on an otherworldly glow. Her lips curl[set body-context to Snow White] into a sneer and she speaks toward the box in a language you do not recognize, but whose abrasive consonants assault your ears like driving hail. The box burns cold in your hand, binding your skin to wood that suddenly feels alive, and hateful. 

Just as the first desperate scream dies soundlessly in your throat, the box, the forest, and your hands return to normal. Your mind, however, affords you no such courtesy. 'That is what I know about the box,' says Snow White[scary][angry][box-is-creepy][magic-revealed].".
 It quip-supplies Snow White.
 It is negated by sundering-method.
 It is listed.
 It directly-follows what symbols mean.

 what just happened is a questioning quip. 
 It mentions snow white, witchcraft, box, symbols.
 The comment is "'W-what just happened?' you manage to stammer, flexing fingers that only a moment ago felt ready to snap off your hand like icicles.".
 The response is "'You pushed me too far,' she answers calmly[angry].".
 It quip-supplies Snow White.
 It directly-follows what the symbols mean again.
 It is listed.

[ how the mirror reflects you to the queen is a questioning quip.
 It mentions magic mirror, Queen.
 The comment is "'And how does the magic mirror reflect me to the Queen?' You can't quite suppress the edge of skepticism in your voice. ".
 The response is "'Your question betrays your ignorance. [snowWhite-sees-reflections]Suffice it to say you are not the first huntsman Her Majesty ' — the title is practically spit — 'has dispatched into the woods.'  Thoughtfully she adds, 'Though you would be the first to return.'   ".
 It is restrictive.
 It directly-follows why she avoids the magic mirror.
 It is listed.] [* It's been pointed out that this seems to overlap and conflict with the other quip about how you see yourself in the mirror. Following a general principle of minimal-editing-sufficient-to-retain-continuity, I think the thing to do is rewrite this as an NPC-directed follow-up to the previous quip about how the mirror displays you: that way Snow White's creepy hints about other huntsmen are preserved but we don't lose the original description of what you actually look like. So: ]

how the mirror reflects you to the Queen is an NPC-directed quip.
	The response is "'Your question [if the previous quip is not what you look like in the mirror]about the mirror [end if]betrays your ignorance. [snowWhite-sees-reflections]Suffice it to say you are not the first huntsman Her Majesty ' — the title is practically spit — 'has dispatched into the woods.'  Thoughtfully she adds, 'Though you would be the first to return.'"
 It quip-supplies Snow White.

Instead of Snow White discussing what happened to other huntsmen when the player does not recollect pleading-to-be-untied:
	try Snow White discussing pleading-to-be-untied.

 what happened to other huntsmen is a questioning quip.
 It mentions huntsmen, Queen.
 The comment is "This piques your curiosity. 'What other huntsmen?', you ask, cautiously. These are close woods, and had there been some manner of... unpleasantness, you would have heard about it.".
 The response is "'Not everything that happens in these woods comes to your ears,' she says, answering your thought more than your speech. 'Some of those the Queen disposes of do not merely die: they are forgotten even by those who loved them[pathetic].'".
 It quip-supplies Snow White.
 [It is restrictive.] 
 It directly-follows how the mirror reflects you to the queen.
 It is listed.

To say Snow White smiles:
	say "[one of][first smile][or]You brace as Snow White inflicts another smile upon you[or][one of]Her teeth glint in the darkness[or]Her smile is white in the dimness[or]Snow White gives a smile bleaker than death[or]Snow White smiles, teeth glinting[at random][stopping][run paragraph on]" [* This is my addition, designed to handle a pseudo-code condition that Rob Dubbin incorporated into the previous quip. Once introduced, it needs to be applied elsewhere in the game as well. It's interesting how later contributors' material implicitly revises that of earlier contributors...]

To say first smile:
	say "[set body-context to Snow White]";
	say "[if scariness is greater than 0]For the first time, Snow White smiles. The expression is not [comforting]; in fact, the temperature seems to dive[scary] and your lantern nearly extinguishes itself[otherwise if anger is greater than 0]For the first time, she smiles. Her eyes glitter in the lamplight[otherwise]Snow White smiles — a small, sad smile[end if]"

Chapter 6 - Kazuki Mishima Oct 29

 [whether she feels cold is a questioning quip. 
 It mentions cold.
 The comment is "'Do you feel cold?' you ask.".
 The response is "A little of that fury she shows when refusing to show herself to the mirror rises in her now. 'I'll feel warmer when I'm moving again,' she says. Then, seeming to notice something in your face, she adds 'Thank you for your concern.'".
 It is repeatable. ] [* This comment was submitted, but overlaps one offered by a previous submitter, so I've commented it out for the time being. (If this turns out to be a common problem, it might be worth going to some kind of check-out system, but I didn't really expect there to be enough traffic on this project to make it worth letting only one person look at it at a time. For a different kind of collaboration I imagine serial rather than parallel input would be a better idea.)]

what the obscene suggestion was is a questioning quip. 
 It mentions magic mirror.
 The comment is "'Just what do you consider obscene, Snow White?' you ask.".
 The response is "'[if the player knows Snow-White-possessed]You expressed your desire to use the Queen like one of your hounds uses another,' she says. 

The memory of a secret dream comes into your head.

'Don't blush! It is a common desire in Adam's sons. Adam himself never cared to take a less than commanding position.[otherwise]I would not know about obscenity,' she says without a trace of sincerity. 'It can not touch the ears of the snow-white maiden. She possesses an aura that repells it.[end if]'". [* The original reply seemed wrong somehow after Lilith has been revealed — she seems like the type who would be frank.]
 It quip-supplies Snow White.
 It indirectly-follows what you look like in the mirror.
 It is listed.

 how to deal alongside the Queen is a questioning quip. 
 The printed name is "how to deal with the Queen". The true-name is "how to deal alongside the queen". 
 Understand "with" as how to deal alongside the queen. 
 It mentions Queen.
 The comment is "'How would you suggest I deal with the Queen now?' you ask.".
 The response is "'Just as you did before,' Snow White says, 'but avoiding the mirror. Run away like I have, if you can do it safely. Better yet, kill her.' She pauses, evaluates your face, then asks 'Could you?'".
 The uncertainty-refusal is "'Then think about it.'"
 The nag is "[one of]'I am curious to hear your answer,' she says again. 'Could you kill the Queen?'[or]She watches your face.[or]'Coward,' she says at last[queue giving-up-question].[stopping]". [* This property gives her a way to pester you for an answer if you remain silent for too many turns.]
 It quip-supplies Snow White.
 It is restrictive.
 It is listed.

 yes-2 is an affirmative quip. 
 The printed name is "say yes". The true-name is "yes-2".
 It mentions Queen.
 The comment is "'Yes,' you say. 'I could.'".
 The response is "'Ha! Brave words,' she says. She indicates the hart. 'It would be a different thing from that, wouldn't it? The Queen would only say 'Stop!', and you would melt.'".
 It quip-supplies Snow White.
 It directly-follows how to deal alongside the queen and  if you could kill the queen.

Chapter 7 - Elizabeth Heller Oct 29

 no-4 is a negative quip. The printed name is "say no". The true-name is "no-4". 
 It mentions Queen, fear.
 The comment is "You look at Snow White, and think of the Queen. For a moment, the princess seems nearly as frightening as her stepmother[scary].".
 The response is "No. Snow White is only a young girl, after all. And the Queen... well, you have seen her many times. You've heard her speak to her mirror. 'No,' you say, shivering. 'I don't think I could.'".
 It quip-supplies Snow White.
 It is negated by snow-white-possessed. [* These thoughts certainly don't make sense in light of knowing she is what she is; see no-36 for the alternative version.]
 It directly-follows how to deal alongside the queen and  if you could kill the queen.

 say xyzzy is a weak performative quip. [* This is a slight variation on the original submission, which had "xyzzy" as the name of both the quip and the subject it mentioned; this tweak lets it compile and will also accept SAY XYZZY as input alongside just XYZZY.]
 It mentions xyzzy.
 The comment is "You whisper an old magic word, pulled from some distant recess of your memory. The syllables are lost in the darkness, muffled by the trees.".
 The response is "[one of]Snow White [for eye-gest]looks at you with an amused expression[or breath-gest]gives a little surprised huff[or walk-gest]turns when she hears it[end for][friendly]. Her red lips[set body-context to Snow White] quirk in the merest hint of a smile. For only a moment — then she looks serious as before[or]This time she merely [for eye-gest]rolls her eyes[or breath-gest]yawns[or walk-gest]strolls a little away from you and looks into the woods[end for][stopping].".
 It quip-supplies Snow White.
 It is repeatable.

Chapter 8 - Ziv Wities Oct 30

 what the Queen sees in the mirror is a questioning quip. 
 It mentions magic mirror, queen.
 The comment is "'What does the Queen see, then, when she looks in her mirror?'".
 The response is "Snow White tilts her head at you, as though trying to divine your intention in asking. 'When she looks at her reflection,' she says at last, 'her reflection looks like me[pathetic].'".
 It quip-supplies Snow White.
 It indirectly-follows why she avoids the magic mirror.

why she sees the mirror's reflections as the Queen does is a questioning quip.
 It mentions magic mirror, queen, witchcraft.
 The comment is "'I wonder, fair maiden, how it is that you see the mirror's reflections as clearly as the Queen does.'".
 The response is "She stares at you, and you can sense the disdain this pale, captive waif of a girl feels for you. 'I am not as helpless as the commonfolk,' she says, 'and I've studied well. Mark my words, [vocative huntsman] — anything the Queen can do, I am capable of as well.' You find this statement less than [comforting][scary].".
 It quip-supplies Snow White.
 It is negated by snow-white-possessed.
 It assumes snowWhite-sees-reflections.
 It is listed.

[question about studies removed to the repeatable quip section]

 to walk properly is an informative quip. 
 It mentions snow white, delay, roots.
 Understand "stumbling" or "stumble" or "stumbles" or "stumbled" or "dragged" or "her" or "dragging" or "feet" or "tripping" or "falling" or "trip" or "fall" or "trips" or "falls" or "nuisance" as to walk properly.
 The comment is "'[if turn count is less than 4]Enough already!' you growl. 'If you want my help tonight, stop pestering me, and start following without forcing me to drag you every step of the way.[otherwise]I meant to mention earlier,' you say, 'you're going to have to start doing your walking on your own, [vocative snow white], and start watching where you're going.[end if] You may be willing to risk your own neck with this deliberate clumsiness, but I assure you, I'm not risking mine.'".
 The response is "Snow White gazes at you coldly[angry]. 'I see. You think I'm being difficult on purpose.[paragraph break]'It didn't occur to you that the Queen might have wounded me, or wearied me with her spells. Nor have you considered that a palace girl with her hands bound might have difficulty making her way through the forest at night. No; it's quite clear that I'm intentionally delaying, risking my only chance to escape, isn't it now.[paragraph break]'Be honest now, [vocative huntsman]: which of these sounds most likely to you?'".
 The nag is "[one of]'Well?'[or]'I await your reading of my motives,' she says[angry].[or]'Tick tock[angry]!'[stopping]".
 It quip-supplies Snow White.
 It is negated by snow-white-possessed.
 It is restrictive. 

Rule for listing plausible quips when the current quip is to walk properly:
	say roman type;
	if scariness is greater than 0:
		say "It seems unlikely that the Queen hurt her or that a creature like her is having real trouble (though you could ask). But you still have the feeling that she is delaying on purpose.";
	otherwise if anger is greater than 0:
		say "It galls you to admit to yourself, but it is possible that the Queen hurt her or that she is having understandable trouble walking (though you could ask). You'd prefer to assume she is delaying on purpose.";
	otherwise:
		say "The Queen might indeed have hurt her — perhaps you should ask."

 whether the Queen hurt her is a questioning quip.
 Understand "you" or "injured" or "injury" or "pain" or "injuries" or "if" as whether the queen hurt her.
 It mentions Queen, snow white, delay.
 The comment is "'My apologies,' you stammer, 'I did not think. Are you well? Has the Queen hurt you?'".
 The response is "'She has tried often enough,' the pale girl answers, 'and even now, ordering my death, she would far rather land the blow with her own hand[pathetic]. [paragraph break]'But no, she has not hurt me... physically. I am as well as I may ever be said to be. No,' she says. [Snow White smiles]. 'You were right the first time[wants-delay].'[line break]".
 It quip-supplies Snow White.
 It is negated by snow-white-possessed.
 It directly-follows to walk properly.
 It is listed.

whether she hath trouble walking is a questioning quip. 
 The printed name is "whether she has trouble walking". The true-name is "whether she hath trouble walking". 
 Understand "if/whether" or "is" or "creature" or "like her" or "real" or "having" or "walking" or "she" or "snow white" or "has" as whether she hath trouble walking. 
 It mentions snow white, delay.
 The comment is "'I suppose that is more plausible,' you say, 'Perhaps I should not have expected you to move as quickly as I would like.'".
 The response is "'Far more plausible,' the pale girl answers, 'but false. No, I am at ease in the forest, and the darkness I find comfortable. No. You were right the first time[wants-delay].'".
 It quip-supplies Snow White.
 It is negated by snow-white-possessed.
 It directly-follows to walk properly.
 It is listed.

 she seems delaying on purpose is an informative quip. 
 The printed name is "she is delaying on purpose". The true-name is "she seems delaying on purpose". 
 Understand "is" or "intention" or "intentionally" as she seems delaying on purpose. 
 It mentions snow white, delay.
 The comment is "'The last one,' you say darkly[angry]. 'I'll be damned if I know why, but you're deliberately slowing us down every step of the way.'".
 The response is "[Snow White smiles]. 'Correct,' she says. 'You are learning, [vocative huntsman]. I do nothing that is not deliberate, mark my words[wants-delay].'".
 It quip-supplies Snow White.
 It is negated by snow-white-possessed.
 It directly-follows to walk properly.
 It is listed.

 why she seems trying to delay the escape is a questioning quip.
 The printed name is "why she is trying to delay the escape". The true-name is "why she seems trying to delay the escape". 
 Understand "is" or "delaying" as why she seems trying to delay the escape. 
 It mentions snow white, delay, apple.
 The comment is "'Am I permitted to know, [vocative snow white], why it is you're so carefully tripping over roots instead of fleeing for your life?'".
 The response is "She considers before speaking. Then, 'I am searching for something,' she says, 'which is as necessary to my safety as reaching my haven[pathetic]. I'd hoped, though, to make my find far earlier, before you noticed.'[paragraph break]She muses for a moment. 'I should not tell you. But you would hardly let me stay silent, would you?'".
 The nag is "Her question hangs between you, unanswered[queue apparently-you-would]."
 The uncertainty-refusal is "'Come, make a decision.'"
 It quip-supplies Snow White.
 It is restrictive. 
 It assumes wants-delay.
 It is negated by snow-white-possessed. 

 yes-3 is an affirmative quip. The printed name is "say yes". The true-name is "yes-3". 
 It mentions snow white, delay, apple.
 The comment is "'Keep your secrets, if you prefer. I won't force you.'".
 The response is "You've surprised her. 'Really?' she asks. Then she [for eye-gest]looks at you warily[or walk-gest]turns toward you more fully[or breath-gest]takes a deep breath[end for]. 'Are you trying to get something in return[friendly][pathetic]?'".
 It quip-supplies Snow White.
 It is restrictive. 
 It is negated by snow-white-possessed.
 It directly-follows why she seems trying to delay the escape.

 no-5 is a negative quip. The printed name is "say no". The true-name is "no-5". 
 It mentions snow white, delay, apple.
 The comment is "'No, I wouldn't. I'd be a fool to, and well you know it.'".
 The response is "'Fair enough. Since we have no bond, though, I cannot tell you directly. A riddle, then:[paragraph break]'Red and sweet - the perfect meal;[line break]Sustenance with such appeal[line break]With a bite release its charm[line break]Thus shall ye be safe from harm[line break]Lilith [lilith-named]had none, yet her man[line break]Could not share his - no one can.'[riddle-given]".
 It quip-supplies Snow White.
 It is negated by snow-white-possessed.
 It directly-follows why she seems trying to delay the escape.

[An availability rule for request a repetition of the riddle:
	if the player knows riddle-solved:
		it is off-limits.]

 request a repetition of the riddle is a performative quip.
 It mentions snow white, riddle.
 The comment is "'Could you repeat the riddle, please?'".
 The response is "'Of course[friendly].[paragraph break]'Red and sweet - the perfect meal;[line break]Sustenance with such appeal[line break]With a bite release its charm[line break]Thus shall ye be safe from harm[line break]Lilith had none, yet her man[line break]Could not share his - no one can.'".
 It quip-supplies Snow White.
 It is repeatable.
 It assumes riddle-given.
 It is negated by riddle-solved, snow-white-possessed.
 It is listed.

Chapter 9 - Eric Eve Oct 30

 if the queen seems evil is a questioning quip.
 The printed name is "if the Queen is evil". The true-name is "if the queen seems evil". 
 Understand "is" or "whether" as if the queen seems evil. 
 It mentions queen.
 The comment is "'Do you believe the Queen to be evil?' you ask.".
 The response is "'I do not think she was born evil,' she replies, 'But she is fearful, and fear breeds evil in otherwise good people[friendly].'".
 It quip-supplies Snow White.
 It is negated by snow-white-possessed.
 It directly-follows what queen sees in the mirror.
 It is listed.

 does the Queen fear Snow White is a questioning quip.
 It mentions queen, fear.
 The comment is "'Does the Queen fear you, then?' you inquire.".
 The response is "She [for eye-gest]looks you straight in the eye[or walk-gest]turns toward you[or breath-gest]chuckles[end for] as she replies, 'Oh [queen-fears]yes, she fears me. At least, that's what she imagines; but in truth it may be herself she fears.'".
 It quip-supplies Snow White. 
 It is negated by snow-white-possessed.
 It indirectly-follows what queen sees in the mirror.
 It is listed.

 why she made a pact alongside you is a weak questioning quip.
 The printed name is "why she made a pact with you". The true-name is "why she made a pact alongside you". 
 Understand "with" and "me" as why she made a pact alongside you. 
 It mentions deal.
 The comment is "'Why did you make a pact with me?' you want to know.".
 The response is "[one of]'I had little choice,' she replies, [or][stopping]'There was no one else available.'".
 It quip-supplies Snow White.
 It is repeatable.
 It assumes pact-exists.
 It is listed.

 [whether she will keep the pact is a questioning quip.
 It mentions deal, pact.
 The comment is "'Can I trust you to keep our pact?' you ask.".
 The response is "'I will keep it if you do,' she assures you, 'but if you betray me...' She leaves the unfinished sentence hanging on the cold night air.".
 It directly-follows why she made a pact alongside you.
 It assumes pact-exists.
 It is listed.] [* Omitted for overlap with another similar quip.]

 if she met the king is a questioning quip. 
 Understand "whether" or "had" or "has" as if she met the king. [* In testing, it proved easy to read and retype this as 'if she has met the king'.]
 It mentions king.
 The comment is "'Did you ever meet the King?' you ask[if the previous quip does not mention the king] suddenly[end if].".
 The response is "'No, he had already gone[king-gone] before I came to court,' she replies.".
 It quip-supplies Snow White.
 It is listed.

Rule for quip-introducing why the king left:
	strip why the king left from table;
	say "[roman type][one of]It occurs to you that she might know[or]She might know[stopping] something you've been wondering a long time: why the king left[if any other quips remain]. [run paragraph on][otherwise].[end if]"; [* This construction allows us a special phrasing in this one case but will list other quips as well if they're currently active.]

Availability rule for why the king left:
	if the player knows blood-sundering and the player knows you-were-king:
		it is off-limits.

 why the king left is a questioning quip.
 It mentions king.
 The comment is "'Why did the king leave — do you know?' you ask.".
 The response is "'He left when the moon was full — the rest is surmise,' she replies flatly.".
 It quip-supplies Snow White.
 It indirectly-follows if she met the king.
 It assumes king-gone.
 It is listed.

[Availability rule for if she seems the king's daughter:
	if the player knows not-daughter:
		it is off-limits.]

 if she seems the king's daughter is a questioning quip.
 The printed name is "if she is the King's daughter". The true-name is "if she seems the king's daughter". 
 Understand "is" or "whether" as if she seems the king's daughter. 
 It mentions king, daughter, snow white.
 The comment is "A sudden flash of would-be insight makes you ask, 'Are you the king's daughter?'".
 The response is "'Do I look like the daughter of a king?' she shoots back.".
 The nag is "Then, when you do not answer, like a mother prompting a child, she opens her [set body-context to Snow White]eyes wide and shakes her head slowly[casually queue answer-is-no]."
 It quip-supplies Snow White.
 It is restrictive.
 It indirectly-follows why the king left.
 It is negated by not-daughter.
[ It assumes snowWhite-sees-reflections.]
 It is listed.

Instead of examining Snow White when the current quip is if she seems the king's daughter:
	say "You look at the blanched skin and the black eyes, the hair like silk, the delicate structure of bone, the fine green gown with its gold embroidery, the red-black lips, the teeth. She does not look like a village girl, at any rate."

 yes-4 is an affirmative quip. The true-name is "yes-4". The printed name is "say yes".
 It mentions snow white, king.
 The comment is "'Yes, I'd say you have a certain regal bearing - and the looks to match,' you reply.".
 The response is "'Perhaps you are too quick with your good opinion, sir,' she answers[not-daughter].".
 It quip-supplies Snow White.
 It directly-follows if she seems the king's daughter.
[ It is listed.]

 no-6 is a negative quip. The true-name is "no-6". The printed name is "say no".
 It mentions king, snow white.
 The comment is "'Not obviously,' you admit, 'at least, not like any king's daughter I've ever seen.'".
 The response is "'Well, then, if I have not the appearance of a king's daughter[not-daughter]...' she says with a little shrug. For a moment she turns away to stare into the night, then turns back to you with the ghost of an enigmatic smile fading from her lips[set body-context to Snow White][friendly][pathetic].".
 It quip-supplies Snow White.
 It directly-follows if she seems the king's daughter.
[ It is listed.]

answer-is-no is an NPC-directed quip.
	The response is "'I don't look like the daughter of any other [not-daughter]King, that's certain,' she says, when you still don't answer. 'But I suppose you have not met sufficient royalty to make much of a comparison.'"
	It quip-supplies Snow White.

Chapter 10 - Mark Musante Oct 30

what the Queen did to her is a questioning quip. 
 It mentions queen.
 It assumes pact-exists.
 The comment is "'What is it that the Queen did? How do you know she turned against you?' Might as well find out what she knows.". [* I've edited slightly so this works with the "examine me" output, where it says that (as per the traditional tale) the huntsman was sent to take Snow White into the woods and kill her; Mark clarified that he meant this as the huntsman exploring Snow White's state of knowledge, even though he himself is aware that the Queen ordered Snow White killed.]
 The response is "'My word what an impertinent question. Your part of the deal is to get me to the safe haven when I need you to, and I have a need right NOW. I ask you to accept that.'". 
 It quip-supplies Snow White.
 It is negated by snow-white-possessed.

where the safe haven seems is a questioning quip.
 The printed name is "where the safe haven is". The true-name is "where the safe haven seems". 
 Understand "is" or "direction/directions" or "exact directions" or "for directions/direction/exact" as where the safe haven seems. 
 It mentions safe haven.
 The comment is "'You told me before that the safe haven would take about an hour to reach[one of], but you never said which way to go.[or][if the player guessed right] — north, I think you said?[otherwise] — which way is it, again?[end if][stopping]'". [* Since this quip is repeatable, I've edited so he doesn't re-ask the question with 'you never said' the subsequent times.]
 The response is "'[if the player guessed right]Yes[otherwise]North[end if]. Safety is to the north, and the sooner we start, the better[haven-location].'".  
 It quip-supplies Snow White.
 It indirectly-follows you-promised-me.
 It is repeatable.

Understand "north" as where the safe haven seems when the player knows haven-location.

To decide whether the player guessed right:
	if the player's command includes "north":
		yes;
	no.

To say list (chosen quip - a quip):
	now the chosen quip is listed.

Chapter 11 - Ziv Wities Oct 31

 that the Queen will kill you is an informative quip. The comment is "'This pact of ours will mean my death. If I stand before the mirror, my treason is revealed; if I avoid it, the Queen will take note, and by her suspicion I'll be doomed.'".
 Understand "me" as that the Queen will kill you.
 It mentions Queen, magic mirror.
 It assumes pact-exists.
 The response is "'Our agreement is hardly to blame, [vocative huntsman]. You've been guilty of treason three weeks hence; I've observed that since then you've stayed well away from the mirror. Had you not been asking your oh-so-covert questions about the King, I never would have spoken to you. I am not heartless, to ask a man to die to protect me[friendly].'[paragraph break]True enough. You have already put yourself in harm's way. Yet you have an unpleasant suspicion that the Queen will be far less wroth at an attempt to restore the King to his rightful throne, than at your failure to eviscerate her young stepdaughter.".
 It quip-supplies Snow White.
 It is listed.

Rule for quip-introducing it was not treachery:
	strip it was not treachery from table;
	say "[roman type]Of course, it was not treachery[if other quips remain]. [run paragraph on][otherwise].[end if]"

 it was not treachery is an informative quip. The comment is "'It was not treachery, [vocative snow white],' you [if anger is greater than 1]growl[otherwise]explain patiently[end if]. 'How can it be treachery to wish to see the King returned home?'".
 It mentions treason, king.
 The response is "'Call it what you will,' she answers unconcernedly. 'The Queen has no love for her husband, no more than she has for me. Rest assured, she will call it what [italic type]she[roman type] will, and you will suffer greatly if she ever finds you out.'".
 It quip-supplies Snow White.
 It directly-follows whether she will keep the pact.
 It is listed.

Chapter 12 - Sarah Morayati Oct 31 

 if she kens her way around the woods is a questioning quip. The true-name is "if she kens her way around the woods". The printed name is "if she knows her way around the woods". 
 Understand "whether" or "knows" or "know" as if she kens her way around the woods.
 It mentions trees.
 The comment is "'[one of]Do [or]And you said [stopping]you know your way around these woods?'".
 The response is "'[one of]Yes, very well. Possibly [or]As I said: most likely [stopping]better than you do.' She almost smiles[friendly].".
 It quip-supplies Snow White.
 It is repeatable.
 It is listed.

Rule for quip-introducing how she kens her way around the woods:
	say "How?";
	list no other quips.

 how she kens her way around the woods is a questioning quip. The true-name is "how she kens her way around the woods". The printed name is "how she knows her way around the woods". 
 Understand "know" or "knows" as how she kens her way around the woods.
 It mentions trees.
 The comment is "'How? This isn't the most inviting place.' You glance at the trees, which seem to have grown darker.".
 The response is "'I take walks.' Strange; you expected something more elaborate. She continues: 'At night. It's much more pleasant then. Everything looks lovelier out of harsh lights. It's much more private.'".
 It quip-supplies Snow White.
 It directly-follows if she kens her way around the woods.
 It is listed.

 why she needs privacy is a weak questioning quip. 
 It mentions Snow White.
 The comment is "'Why do you need privacy?' ".
 The response is "[one of]'I have my reasons.' At least she's consistent on the matter.[or]This time she merely shrugs.[stopping]".
 It quip-supplies Snow White.
 It is repeatable.
 It directly-follows how she kens her way around the woods.
 It is negated by snow-white-possessed.
 It is listed.

 what the Queen thinks of you is a questioning quip. 
 Understand "me" as what the queen thinks of you.
 It mentions queen.
 The comment is "'What does the Queen think of me?'".
 The response is "Snow White laughs softly. [friendly][scary]The sound unnerves you. 'I don't know. Perhaps you should ask her, instead.'".
 It quip-supplies Snow White.
 It is listed.

 why so few people understand witches is a weak questioning quip. 
 It mentions witchcraft.
 The comment is "'Why don't they understand them?'".
 The response is "'There's so much superstition and fancy going around. People are so taken by it. More so,' she adds, 'than by real witches.'".
 It quip-supplies Snow White.
 It directly-follows if she seems a witch.
 It is negated by snow-white-possessed.
 It is listed.

what the symbols mean yet again is a weak questioning quip.
 It mentions symbols, heart-sized box.
 The comment is "No. Not again, never again.".
 The response is "Snow White - maddeningly - says no more.".
 It quip-supplies Snow White.
 It directly-follows what the symbols mean again.
[ It assumes symbols-seen.] [* This belongs to a thread ('directly follows what the symbols mean again') that guarantees that that fact is known — so we don't need to reiterate it. There's no harm to the gameplay in doing so, as it happens, but it may just be tidier to leave it out for the clarity of the conversation chart.]

 why she drinks red wine is a weak questioning quip. 
 It mentions snow white.
 The comment is "'Why do you insist on drinking red wine?'".
 The response is "'I prefer it. Am I now forbidden from having preferences?'".
 The nag is "She watches you, waiting for an answer[queue thought-not]."
 It quip-supplies Snow White.
 It assumes drinks-red-wine. 
 It is negated by snow-white-possessed.
 It is listed.

Before discussing something which does not directly-follow why she drinks red wine when the previous quip is why she drinks red wine:
	say "You ignore the sarcastic question, which does not really deserve an answer. [run paragraph on]".

 yes-6 is an affirmative quip. The printed name is "say yes". The true-name is "yes-6". 
 It mentions snow white.
 The comment is "'Yes.' You may as well play along.".
 The response is "Snow White [for breath-gest]laughs - a throaty sound, nothing like her speaking voice[or eye-gest]looks poniards at you[or walk-gest]twists her wrists in their chain[end for]. 'So presumptuous. [angry]But not surprising from a man.'".
 It quip-supplies Snow White.
 It directly-follows why she drinks red wine.

 no-7 is a weak negative quip. The printed name is "say no". The true-name is "no-7". 
 It mentions snow white.
 The comment is "'Of course not.'".
 The response is "'Then do not question my preferences, for I daresay you have a few of your own.'".
 It quip-supplies Snow White.
 It directly-follows why she drinks red wine.

Chapter 13 - Rob Dubbin Oct 31

Understand "ask [corpse] [text]" or "tell [corpse] [text]" as a mistake ("It's a dead hart, not an undead hart. It's not exactly capable of scintillating conversation.") when the player does not recollect thank-the-hart.

Understand "ask [corpse] [text]" or "tell [corpse] [text]" as a mistake ("The spirit no longer seems to be animating the hart; it says nothing more.") when the player knows hart-no-longer-undead.

Understand "ask [undead corpse] about/that [text]" or  "tell [undead corpse] about/that [text]" as addressing it with.

Understand "ask [undead corpse] [text]" or  "tell [undead corpse] [text]" as addressing it with.

Definition: a corpse is undead rather than really dead if the player recollects thank-the-hart and the player does not know hart-no-longer-undead.

Before giving or showing something to the corpse when the corpse is undead:
	if the corpse is the current interlocutor, continue the action;
	try addressing the corpse with "hello" instead.

Addressing it with is an action applying to one thing and one topic. [* We're creating this action only for this one effect, so we're not going to write it out in full: it can only *ever* match the corpse, and we don't care about the text the player types afterward.]

To say player's own phrase:
	let N be indexed text;
	let N be "[topic understood]";
	let X be 1; [This is the number of words we're going to print from N]
	if the number of words in N is 1: [This likely means the player typed ASK HART ABOUT BLOOD or something, rather than ASK HART ABOUT WHAT HAPPENED]
		if N matches the text "Hello", case insensitively:
			do nothing;
		otherwise:
			if the player's command includes "tell":
				let N be "About [N]";
				increase X by 1;
			otherwise:
				let N be "Tell me about [N]";
				increase X by 3;
	let N be "[N in sentence case]";
	say "[first X words in N]";
	if N matches the text "Hello":
		say "?[run paragraph on]";
	otherwise:
		say "—[run paragraph on]". 

Instead of saying hello to the corpse when the corpse is undead:
	try addressing the corpse with "Hello"
	
Instead of saying hello to the corpse when the corpse is really dead:
	say "It doesn't answer."

Before printing the name of happy-name-known:
	now corpse is proper-named;
	change the printed name of corpse to "Happy".
	
[Rule for printing the name of corpse:
	say "corpse". ]

Understand "Happy" or "blue" as corpse when the player knows happy-name-known. 

Instead of addressing the corpse with something when the player does not know undead-hart:
	let player-asks be true;
	if the player's command includes "tell", change player-asks to false;
	say "You wait for Snow White to become tired of glaring at you and after a few moments she obliges, turning toward a scurrying sound in the underbrush with a hungry glint in her eye.

Seizing the opportunity, you kneel before the heartless hart and [if player-asks is true]ask[otherwise]say[end if], '[player's own phrase]'

Several things happen at once. Firstly, Snow White whirls toward you, her face livid. 'YOU CAN'T —'

But before she can finish her sentence, in point of fact as she's starting it, the once-dark forest flashes with frantic, ice-blue energy, the air fills with a steady, ethereal hum, and you see the pallid princess lifted before you, suspended in the thick, vibrating air, her rage caught in her throat.

The immediate impulse is to taunt your suddenly riposte-incapable companion, but petty vengeance quickly dissolves in the face of a final sudden development.

Namely, the hart — which now stands proudly upright, its eyes[set body-context to corpse] and empty chest cavity glowing an incongruous silver.[paragraph break]";
	now the corpse is unknowing; [* this turns off a trivial flag to do with whether you've seen it "wink" at you; winking disambiguation should go back to normal.]
	start talking to the corpse;
	queue hart-thanks-you as immediate obligatory;
	rule succeeds. [* The original suggested implementation here involved a quip named 'ask the corpse what Snow White was doing', but that would only have worked if the player guessed precisely the right wording. This implementation handles the possibility that the player asks the hart something other than the right question, or types TALK TO CORPSE. But then that meant we also needed to be more flexible about what we show the player saying to the hart: it should reflect what was actually typed in some way. Hence the shenanigans to print part of the player's command.]
	
Rule for expressing ignorance for the corpse when the player does not recollect hart-thanks-you:
	do nothing instead.
	
Stopped-time is a scene.

Stopped-time begins when the current interlocutor is the corpse. Stopped-time ends when the player knows hart-no-longer-undead.

When Stopped-time ends:
	start talking to Snow White;
	say "Snow White returns to earth. She looks around her, blinking: she seems a little confused. Perhaps she does not remember you speaking to the hart."

Instead of listening to the location during Stopped-time:
	say "The woods are eerily still."

Instead of waiting during Stopped-time:
	say "No time passes at all."
	
Instead of examining the lantern during Stopped-time:
	say "It gives off light, but it does so without any of the liveliness of burning. It does not flicker or leap."
	

Rule for avoiding talking heads during stopped-time:
	if the current interlocutor is ready for transition:
		if the current quip is strong and a random chance of 1 in 2 succeeds:
			say "[timeless beat][line break][paragraph break]"; [* If the current quip from which we are building is weak, we want to fold it into one continuous paragraph with one intervening beat.]
	otherwise:
		say "[timeless beat][if a random chance of 1 in 2 succeeds] [run paragraph on][otherwise][line break][paragraph break][end if]". 

To say timeless beat:
	say "[one of]The woods are eerily silent[or]No one speaks, though only the two of you experience any pause[or]The light from your lantern casts an unusually steady glow over the scene[or]You are less cold than you were earlier[or]Even the clouds seem to have stopped[or]The wind has dropped completely[at random].[run paragraph on]". [* The woods should presumably fall silent.]

[Instead of doing anything when Snow White is the noun or Snow White is the second noun during Stopped-time:
	say "She is stilled, unaware."]
	
[what snow white was doing is a questioning quip.
It mentions snow white, corpse.
It assumes possible-blood-drinking.
It is restrictive.
The comment is ""
The response is "".
 It quip-supplies the corpse.]

hart-thanks-you is an NPC-directed quip.
	The response is "[line break]It has what can only be described as a wry expression on its face[set body-context to corpse]. 'I suppose I should begin by thanking you[queue annoyed-offer].[undead-hart]'". [* if the player doesn't follow up on this, we still want the hart to speak on, because he has a definite agenda to pursue and it doesn't make sense that he would wait indefinitely for the player to talk.]
	It assumes hart-thanked.

why he thanks you is a questioning quip.
Understand "me" as why he thanks you.
It mentions corpse.
The comment is "You've hunted and bested many harts through the years, but this is the first to comment on the exchange afterward. Awestruck, you manage to stammer 'Th-thanking me?'"
The response is "[The corpse] glances into the canyon that used to power its circulatory system. 'You've been through a lot. Please forgive my sarcasm.'[queue annoyed-offer]".
 It quip-supplies corpse.
It directly-follows hart-thanks-you.
It is listed. 

that he's welcome is an informative quip.
Understand "you're" or "he's" or "you are" or "he is" as he's welcome.
It mentions corpse.
The comment is "Blindsided, you manage to stammer, 'Y-you're welcome?'"
The response is "'Welcome, am I. Excellent - I shall have to take you up on that sometime, perhaps once I've acquired another heart.'[queue annoyed-offer]".
 It quip-supplies corpse.
It directly-follows hart-thanks-you.
It is restrictive.
It is listed.

annoyed-offer is a q-transitional NPC-directed quip.
	The response is "[The corpse] peers at you sardonically. 'Before we go any further, is there anything else you'd like to carve out of what's left of my body?'".
	The nag is "[one of]The hart clears its throat, sort of.[or][queue no-9]'I guess I'll accept that as an abashed silence.'[stopping]".
	It is restrictive.
	It quip-supplies corpse.

yes-7 is an affirmative quip.
The printed name is "say yes". The true-name is "yes-7".
it mentions corpse, snow white. 
The comment is "You are beginning to find the resplendent hart corpse to be less agreeable company than even Snow White. 'I can't make any promises,' you offer, with a meaningful glance at its flank."
The response is "[The corpse]'s eyes[set body-context to corpse] narrow, peering at you with scorn. 'I'm sorry, Am I bothering you? I can just die again, you know.'"
 It quip-supplies corpse.
It directly-follows annoyed-offer.
It is restrictive.
[It is listed.]

yes-8 is an affirmative quip.
The printed name is "say yes". The true-name is "yes-8".
Understand "to" or "die" or "for" or "real" as yes-8.
it mentions corpse, snow white.
The comment is "The frustrations of the past 24 hours stream out of you angrily. 'You know what? That would be great, if you could just do what you're supposed to do when someone cuts out your heart, and die.'"
The response is "[The corpse] shrugs. 'Very well, but this time it's forever, unless you can get her,' gesturing toward Snow White with its antlers, 'to drink my blood again.'[hart-no-longer-undead]".
 It quip-supplies corpse.
It directly-follows yes-7, maybe-need.
[It is listed.]

 maybe-need is a performative quip. The comment is "'I don't know what else I'll need.'".
 Understand "[maybe]" as maybe-need. The true-name is "maybe-need". The printed name is "say you don't know".
 It mentions corpse.
 The response is "'Impertinent!' it (he?) roars. 'I could just die again, you know.'".
 It is restrictive.
 It directly-follows annoyed-offer.
 It quip-supplies the corpse.

Understand "[maybe]" as a mistake ("It doesn't seem as though further beating around the bush is going to help this conversation. Better to apologize, or else tell him to die for real and end this odd encounter.") when the current quip is maybe-need.

Instead of saying sorry when no-8 is flagged-ready:
	try discussing no-8.
	
no-8 is a negative quip. 
The printed name is "say no". The true-name is "no-8". 
Understand "sorry" or "apologize" as no-8.
it mentions corpse.
The comment is "You realize that you've put the hart through more in the past 24 hours than it's put you through in the past 10 minutes. 'I apologize,' you say, nodding deferentially at the corpse."
The response is "[The corpse] inclines its head in acceptance. 'I forgive you. [hart-pacified]Now let's get down to business.'"
 It quip-supplies corpse.
It directly-follows yes-7, maybe-need.
[It is listed.]

Instead of saying sorry when no-9 is flagged-ready:
	try discussing no-9.

no-9 is a negative quip.
The printed name is "say no". The true-name is "no-9". 
it mentions corpse.
The comment is "'No,' you reply sheepishly."
The response is "'Very well,' says the hart corpse, '[hart-pacified]then let us get down to business[casually queue what Snow White did].'".
 It quip-supplies corpse.
It directly-follows annoyed-offer.
[It is listed.]

Rule for quip-introducing what Snow White did:
	say "[roman type][one of]The first obvious question is what Snow White did — she herself isn't likely to answer, but maybe the hart can[or]You contemplate asking what Snow White did[stopping].";
	list no other quips.

what Snow White did is a questioning quip.
It mentions corpse, Witchcraft, Snow White.
It assumes hart-pacified.
The comment is "You feel justified in asking the hart corpse how exactly it has managed to return to life and stop time. 'What in the underworld just happened?'"
The response is "'[if prompted]Apt choice of words,' compliments the hart corpse. '[end if]I will tell you exactly what just happened, and indeed provide enough information to turn the tables on your know-it-all prisoner over there, if you agree to my terms.[hart-pact-proposed][conditionally queue hart-smile]'".
It quip-supplies corpse.
It is listed.

Instead of saying yes or saying no when the current quip is what Snow White did or the current quip is hart-smile:
	say "Perhaps you had better find out what the terms are before making any rash decisions."

what Snow White did can be prompted or unprompted. what Snow White did is unprompted. Carry out discussing what Snow White did: now what Snow White did is prompted. [* This is just a way of determining whether we get to the quip via the player asking about it or through the hart introducing it himself. IF we were doing a lot more of this kind of thing it would be worth adding it to the system, but for the most part we aren't.]

Instead of the corpse discussing hart-smile when the player recollects what the terms seem:
	say "He makes another grimace that might be a smile or might be something else entirely, it being hard to read facial expressions when the lips are partially missing.";
	now the player recollects hart-smile. [* What we don't want to do is queue hart-smile, have the player leapfrog ahead of it through questioning, and then have the hart bring the conversation *back* into that sequence when it has already been used once.]

hart-smile is a q-transitional NPC-directed quip.
	The response is "As though to add to the disturbing qualities of the moment, the hart gives you something that might be intended as a reassuring smile[conditionally queue what the terms seem]."
	It quip-supplies corpse.
	
Instead of smiling when the current quip is hart-smile:
	say "You try smiling back, but it doesn't feel at all sincere."
	
[Availability rule for what the terms seem:
	if the player knows hart-pact:
		it is off-limits.]

what the terms seem is a questioning quip.
The printed name is "what the terms are". The true-name is "what the terms seem".
Understand "are" as what the terms seem.
It mentions corpse, deal.
The comment is "'What kind of terms?' you ask, wary of entering into any more pacts."
The response is "The hart corpse paws the ground innocently. 'It's very simple, really. I want my heart back[conditionally queue what exactly he intends by cosmic].'"
It assumes hart-pact-proposed.
It is negated by hart-pact.
It quip-supplies corpse.
[It is listed.]

Rule for quip-introducing how that would be possible:
	say "[roman type]It's hard to imagine how that would be possible.";
	list no other quips.

how that would be possible is a questioning quip.
It mentions corpse, hart's heart, cavity.
Understand "terms" or "pact" as how that would be possible.
 It mentions deal.
The comment is "For what you realize is, incredibly, the first time, you feel disgusted. 'Back... in there?' you ask, gesturing at [the corpse]'s glowing rib cage."
The response is "The hart corpse rears upward, clattering back to the ground with enough force to jar a few loose gobbets from its body cavity. 'Don't be grotesque. I mean that I want it back in a more cosmic sense.'[conditionally queue what exactly he intends by cosmic]". [* Even if the player doesn't ask the logical next question, the hart should be interested enough to continue the conversation.]
 It quip-supplies corpse.
It directly-follows what the terms seem.
It is restrictive.
It is listed.

Rule for quip-introducing what exactly he intends by cosmic:
	say "[roman type]Which raises the obvious question of what he means by 'cosmic'.";
	list no other quips.

what exactly he intends by cosmic is a questioning quip.
The printed name is "what exactly he means by cosmic". The true-name is "what exactly he intends by cosmic".
Understand "means" as what exactly he intends by cosmic.
It mentions corpse.
The comment is "'What exactly do you mean, [']in a more cosmic sense[asked-about-cosmic]?[']' You can't believe you're negotiating with a decomposing hart corpse."
The response is "The hart blinks, heedless of the fact that silver light continues to escape from its torn left eyelid. '[if the player knows asked-about-cosmic]Unfortunately, I can't answer that question without revealing too much of my hand, such as it is. [end if]I am proposing you a pact. Do you say yes or do you say no?'".
The nag is "[one of]He waits impatiently.[or]'It is nothing difficult.'[or]He is becoming an increasingly anxious and disconcerting companion.[or]'You should really think about this. I'm the only ally you have in these woods.'[or]He waggles his head from side to side as though trying to remind you of your diverse options, namely YES and NO.[stopping]".
The uncertainty-refusal is "'It isn't much of a pact unless you give a definite answer.'".
 It quip-supplies corpse.
It directly-follows how that would be possible.
It is restrictive.
It is listed.

yes-9 is an affirmative quip.
The printed name is "say yes". The true-name is "yes-9".
It mentions corpse, Snow White.
The comment is "You figure there's no harm in collecting pacts. 'I accept,' you intone solemnly."
The response is "[The corpse] clatters excitedly, and its silver glow momentarily surges while the urgent blue prison surrounding Snow White flickers and dulls. 'Excellent. It is done. [hart-pact]I believe you are in possession of a small wooden box?'"
 It quip-supplies corpse.
It directly-follows what exactly he intends by cosmic.
The nag is "[one of]The hart appears able to wait indefinitely for an answer[or]'Box?' he prompts[or]'Do you have the box?'[at random]."
It is restrictive.
[It is listed.] [* I am commenting these out as a matter of style, since the hart explicitly asks us to say yes or say no in the previous line, which makes the "You could..." bit look particularly silly.]

no-10 is a negative quip.
The printed name is "say no". The true-name is "no-10". 
It mentions corpse, Snow White.
The comment is "You just can't stomach the idea of adding another pact to your docket, least of all with a shambling, reanimated hart corpse. 'Sorry, er, sir. I must decline.'"
The response is "[The corpse] sighs, its silver glow fading to nearly half of what it was. 'Very well. I will say that I expected more from this conversation. Thank you for time, and farewell — forever. Or at least until you can get her to kiss me again.' With that, the glow fades entirely, and the hart collapses to the ground in exactly the same position you found it in.[hart-no-longer-undead]".
 It quip-supplies corpse.
It directly-follows what exactly he intends by cosmic.
[It is listed.]

what his name seems is a questioning quip.
The printed name is "what his name is". The true-name is "what his name seems".
Understand "is" or "its" or "corpse's" or "hart's" as what his name seems.
It mentions corpse.
The comment is "'Before we go any further,' you ask, 'might I ask you your name?'"
The response is "[unless the player knows hart-pact]If a decomposing hart corpse can be said to smile, this one does. 'How rude of me. [otherwise]'I should have introduced myself. [end if]The name's Happy Blue, professional juggler and itinerant dwarf-about-town,' declares the hart corpse proudly, crooking a fractured forelimb and dipping its matted shoulder in salute.[happy-name-known]".
 It quip-supplies corpse.
It indirectly-follows hart-thanks-you.
It is listed.

where its name came from is a questioning quip.
It mentions corpse.
The comment is "[if immediately]If you were puzzled before, now you stand completely at a loss. 'I'm sorry - did you just refer to yourself as a dwarf[otherwise]'I'm sorry, but I'm still trying to understand - you were a dwarf[end if]?'" [* Edited to allow for the fact that it's now possible to go several directions with the conversation at this point, so the player might come back to this quip after talking on other subjects.]
The response is "The decomposing hart cor... er, Happy, capers testily and revoltingly in place. 'This body,' Happy inveighs, 'is but a shell, a container, a temporary lodging for my eternal soul. In fact, when I woke up this morning, I was 100% dwarf, with the large bushy beard and twinkling eyes[set body-context to corpse] typical of my kind. Then, sad to say, I died.'"
 It quip-supplies corpse.
It indirectly-follows what his name seems.
It is listed.

you're sorry to hear is an informative quip.
Understand "I'm" or "I" or "am" or "are" or "you" as you're sorry to hear.
It mentions corpse.
The comment is "You clear your throat. 'My, er, condolences.'"
The response is "'Think nothing of it,' say [the corpse]. 'I have to say, the non-corporeality and omniscience of the deceased [hart-omniscience-revealed]compares quite favorably to the piteous renown and staggering debt of my previous engagement.'"
 It quip-supplies corpse.
It directly-follows where its name came from.
It is listed.

what dwarrows do is a questioning quip.
It mentions corpse, dwarrows.
The comment is "'And what of the dwarrows?', you ask, genuinely curious. Dwarrow folk, after all, have not visited human settlements for some time, preferring to live and work among their own kind."
The response is "'Oh, dwarrows and humans are more alike than many think. We become embarrassed, lose our temper, and catch cold just as humans do. There are those of us who aspire to great knowledge, and others who would just as soon sleep through the day. I myself was considered something of a rube, but of course all of that changes with the infinite knowledge dwarrows inherit upon their demise.[hart-omniscience-revealed]'".
 It quip-supplies corpse.
It indirectly-follows where its name came from.
It is listed.

how its soul got there is a questioning quip.
It mentions corpse, dwarrows, souls.
The comment is "Still mildly skeptical, you press the issue of [The corpse]'s supposed immortality. 'How exactly does the soul of a dwarf come to take up residence in the decomposing body of a hart?'"
The response is "[The corpse] clears what remains of its throat. 'Excellent question. As you may or may not know, all living beings in this world have souls. When mortals die, these souls are flung into the heavens, where they fritter about for a day or two learning the complete and staggering truth of the reality they've recently vacated, before ultimately dissipating into nothing.'  [The corpse] snorts ghoulishly. 'They dissipate, that is, unless they find themselves a host.'[casually queue how its soul got there again][truth-about-souls]".
 It quip-supplies corpse.
It indirectly-follows where its name came from.
It is listed.

Instead of someone discussing how its soul got there when the player does not know hart-pact:
	say "[The corpse] stamps defiantly. 'If you want the answer to that question, and indeed to the many others I know you itch to ask, you must agree to my terms[hart-pact-proposed].'". [* We need to do this, rather than just putting a condition into the response of "how its soul got there", because we only want to count this quip as successfully discussed if Happy answers it. Interrupting the action with an instead should do the trick.]

how its soul got there again is a questioning quip.
It mentions corpse, souls, Snow White.
The comment is "Somewhere in the back of your mind, the absurdity of talking to the soul of a dwarf in the decomposing body of a hart registers. 'And how, exactly, did you come to occupy your current residence?'".
The response is "[The corpse] smiles through rapidly mouldering teeth. 'Oddly enough, I had just learned of my existential peril when I was quite literally sucked into this body, by your companion over there.' Here, the reanimated stag angles its antlers toward Snow White, who, truth be told, you had nearly forgotten about until this moment."
 It quip-supplies corpse.
It assumes truth-about-souls.
It is listed.

[An availability rule for whether snow white seems a vampire:
	if the player knows vampirism-revealed:
		it is off-limits.]

whether snow white seems a vampire is a questioning quip.
The printed name is "whether Snow White is a vampire". The true-name is "whether snow white seems a vampire".
Understand "is" or "if" as whether snow white seems a vampire.
It mentions corpse, Snow White, vampirism.
The comment is "[if the previous quip is how its soul got there again]You're so close to the truth, you can almost taste it. 'Are you saying that Snow White is a vampire?[otherwise]'Tell me, then: is Snow White a vampire?'"
The response is "[The corpse] pauses. '[one of]Knowing the answer to that question is going to change things for you, huntsman. Snow White cannot know that you've learned what you are about to learn. Are you sure you want to know the answer to that question?[or]I warned you before — are you sure you want to know?[stopping]'". [* Originally you could only get to this by asking about the hart's soul, which is a somewhat roundabout way to a question the player is bound to want to know in most play-throughs.] 
 It quip-supplies corpse.
 It assumes hart-pact.
 It is negated by vampirism-revealed and Snow-White-possessed.
[It indirectly-follows how its soul got there again.]
It is repeatable. [* this way we can come back to the question if the player changes his mind and still wants to know]
It is restrictive.
[It is listed.] [* I thought it more interesting to unlist this and let the player discover it for himself.]

no-11 is a negative quip.
Understand "[maybe]" as no-11.
The printed name is "say no". The true-name is "no-11". 
It mentions corpse, Snow White, vampirism.
The comment is "You quail at the menace lurking behind the hart's words. 'I'd like some more time to think about that,' comes your tentative response."
The response is "'Very well,' intones [the corpse]."
 It quip-supplies corpse.
It directly-follows whether snow white seems a vampire.
It is repeatable.
[It is listed.] 

yes-10 is an affirmative quip.
The printed name is "say yes". The true-name is "yes-10".
It mentions corpse, Snow White, vampirism.
The comment is "You steel yourself, certain that you will be better off with this knowledge than without it. 'Yes, I'm sure. Tell me the truth.'"
The response is "He blinks. 'Yes, of course she's a vampire. [vampirism-revealed]A deuced dangerous one, as a matter of fact. I'd avoid any further mention of her condition if I were you — she'll be able to discern the surety behind your question in an instant.'"
 It quip-supplies corpse.
It directly-follows whether snow white seems a vampire.
[It is listed.]

Chapter 14 - Eric Eve Nov 1

 
 why her existence bothered the Queen is a weak questioning quip.
 Understand "Snow" or "White's" as why her existence bothered the queen. The comment is "'Why should your existence bother the Queen?' you want to know.".
 It indirectly-follows what she did to anger the queen.
 It mentions queen, snow white.
 The response is "[if the player knows Snow-White-Possessed]'She has never quite wanted to admit to herself what she did to me.'[otherwise]'Perhaps she thinks we are too much alike.'[end if]".
 It quip-supplies Snow White.
 It is listed.
 
if she believes in vampires is a questioning quip. The comment is "'Do you believe there are such things as vampires?' you ask[if scariness is greater than 1], a little nervously[otherwise] conversationally[end if].".
 Understand "whether" as if she believes in vampires.
 It mentions vampirism.
 The response is "She flashes a cold smile at you, revealing her cruel, sharp teeth[scary]. 'Do you?' she asks.".
 It is restrictive.
 It is negated by snow-white-possessed.
 It quip-supplies Snow White.
 It assumes possible-blood-drinking.
 It is listed. 
 
Availability rule for if she believes in vampires:
	if the player knows vampirism-revealed:
		it is off-limits;
	if the player recollects whether snow white seems a vampire:
		it is off-limits.
 
 yes-11 is a weak affirmative quip. The true-name is "yes-11". The printed name is "say yes".
 The comment is "'Yes, I do now,' you shiver."
 The response is "'You are wise to be open-minded about such things,' she replies."
 It mentions vampirism.
 It quip-supplies Snow White.
 It directly-follows if she believes in vampires. 
 It is negated by snow-white-possessed.
[ It is listed.]
 
 no-12 is a negative quip. The true-name is "no-12". The printed name is "say no".
 The comment is "'No of course not,' you reply hastily, 'That would just be foolish 
   superstition.'"
 The response is "'It must be [comforting] to be so certain,' she replies evenly[scary]. 'But then
   I imagine your world must be quite limited.'"
 It mentions vampirism.
 It quip-supplies Snow White.
 It directly-follows if she believes in vampires. 
 It is negated by snow-white-possessed.
[ It is listed. ]

 yes-12 is an affirmative quip. The printed name is "say yes". The true-name is "yes-12". The comment is "'Yes, I have such a box,' you admit.".
 It directly-follows yes-9.
 It mentions heart-sized box.
 The response is "'Very well,' says the undead hart, 'Then you must burn the box with my heart inside, and scatter the ashes round this spot[casually queue why you must burn the box][burn-box].'".
 It quip-supplies the corpse.

 no-13 is a negative quip. The printed name is "say no". The true-name is "no-13". The comment is "'No - what would I want with a small wooden box?' you reply.".
 It directly-follows yes-9.
 It mentions heart-sized box.
 The response is "'You will need one,' he informs you, 'You must place my heart inside and burn it, then scatter the ashes round this place[casually queue why you must burn the box][burn-box].'".
 It quip-supplies the corpse.

Availability rule for whether the heart will fool the Queen:
	if the player carries the hart's heart:
		make no decision;
	otherwise:
		it is off-limits.

whether the heart will fool the Queen is a questioning quip.
	Understand "if" as whether the heart will fool the Queen.
	It mentions hart's heart.
	The comment is "'Here; do you think the Queen will be fooled by this?' you ask, holding out the heart."
	The response is "For a moment, a strange eagerness enters her eyes[set body-context to Snow White] as she stares at the heart. Then she mutters, 'Let us hope so.'"
	It quip-supplies Snow White.
	
Chapter 15 - Emily Short Nov 1

whether he was a servant of Snow White is a questioning quip. The comment is "'Were you one of Snow White's servants?'".
 It mentions dwarrows, Snow White, servants.
 The response is "'If you'd asked while I was living, I would have said no way. We don't much care for being servants. Looking back, though — now and then I served her purposes without knowing what I was doing.'".
 It quip-supplies corpse.
 It indirectly-follows what his name seems.
 It is listed.

 what he did for Snow White is a questioning quip. The comment is "'[if immediately]How?[otherwise]You said you sometimes served Snow White's purposes without knowing what you were doing — what did you mean?[end if]'".
 It mentions Snow White, servants.
 The response is "'She wanted a lot of dirt dug up,' he says. 'I didn't know why at the time. She put around a rumor about buried gold in a certain area, and soon enough there was plenty of loose dirt for the taking.'".
 It quip-supplies corpse.
 It indirectly-follows whether he was a servant of Snow White.
It is listed.

what the dirt was for is a questioning quip. The comment is "'What was the dirt for[unless immediately] — the dirt you dug up for Snow White, I mean[end unless]?'".
 It mentions dirt.
 The response is "'She brought a big wooden box and [grumpy-named]Grumpy lined the bottom of it with the dirt. She told him it was for a practical joke. Grumpy was always willing to help make someone else uncomfortable.' Happy raises his foreleg at an odd angle, as though trying to stroke a beard that isn't there. He gives up when he discovers he can't reach his chin. 'She doesn't share her plans easily.'".
 It indirectly-follows what he did for Snow White.
 It quip-supplies corpse.
 It indirectly-follows what he did for Snow White.
It is listed.

what the big wooden box was for is a weak questioning quip. The comment is "'Why did [if immediately]she[otherwise]Snow White[end if] need a box of dirt?'".
 Understand "coffin" or "wood" as what the big wooden box was for.
 It mentions Snow White.
 The response is "'She didn't tell us[if the player knows vampirism-revealed], but vampires use such things as beds[end if].'".
 It indirectly-follows what the dirt was for.
 It quip-supplies corpse.

ask-away is an NPC-directed quip.
	The response is "'[if the player knows vampirism-revealed]Is there anything else that you wished to know? I cannot remain forever.[otherwise]Now our pact is made, you may ask me whatever you like in exchange. Those who have died have access to all knowledge, and you look as though you could use some[hart-omniscience-revealed].[end if]'".
	It quip-supplies corpse.
	
no-25 is a negative quip. The printed name is "say no". The true-name is "no-25".  The comment is "'There is nothing.'".
 It mentions yourself.
 The response is "'Very well.'".
 It directly-follows ask-away.
 It quip-supplies corpse.
	
Chapter 16 - Eric Eve Nov 1

Rule for quip-introducing why you must burn the box:
	say "[roman type]Always people are telling you to do things; no one ever explains why.";
	list no other quips.

 why you must burn the box is a questioning quip. The comment is "'Why do you want me to burn the box?' you ask.".
 It mentions heart-sized box.
 The response is "'Only that way can I be made whole - and set free,' he tells you, 'So, will you do that for me?'".
 The nag is "[one of]The corpse frowns at you, waiting for an answer[or]Clearly time itself is not going to be allowed to go on until you answer the hart's question[or]'It is not a difficult request, man!' says the hart[or]'Shall I just go for a canter around the woods and return when you've come to a decision?' he asks irritably[or]'Why' (the hart asks itself rhetorically) 'are all the living such imbeciles?'[or]His sarcasm exhausted, the hart can only wait[or]You somehow continue to hold out against the hart's impatience[or]You still give no answer to the hart's question[or]'In case you've completely forgotten,' the hart says, 'some time ago I asked whether you would be willing to burn the box for me.' He glares at you[or]You continue not to answer[stopping]."
 The uncertainty-refusal is "'It's no pact unless you commit yourself. Don't be such a weakling.'".
 It is restrictive.
 It quip-supplies corpse.
 It assumes burn-box.
 It is listed.

 yes-13 is an affirmative quip. The printed name is "say yes". The true-name is "yes-13". The comment is "'Very well, if that is what you wish,' you agree.[will-burn-box]".
 It mentions heart-sized box, deal.
 The response is "'Good,' the hart replies, 'And in return, I will aid you against your prisoner. But do not delay, the deed must be done [']ere the night is o'er[if the player does not know happy-name-known][queue what his name seems as immediate obligatory][end if][queue ask-away as postponed optional].'".
 It directly-follows why you must burn the box.
 It quip-supplies corpse.

 no-14 is a negative quip. The printed name is "say no". The true-name is "no-14". The comment is "'No, I cannot do ask you ask,' you say.".
 It mentions heart-sized box, deal.
 The response is "The undead hart regards you with sad eyes[set body-context to corpse]. 'Then we are both undone - I had hoped better of you,' he sighs. His eyes slowly close, his legs sag, and he sinks back to the ground, inert once more.[hart-no-longer-undead]".
 It directly-follows why you must burn the box.
 It quip-supplies corpse. 

Every turn during Stopped-Time:
	follow the Happy planning rules.
	
The Happy planning rules are a rulebook.

First Happy planning rule:
	if the player does not know will-burn-box:
		rule fails. [* We don't want to go to the more open-ended portion of the interview until we have made the pact.]
		
A Happy planning rule when the player knows Snow-White-possessed:
	if the player does not recollect banish-her-too:
		queue banish-her-too as postponed obligatory;
		rule succeeds.

A Happy planning rule when the player does not know vampirism-revealed and the player does not know Snow-White-possessed:
	if the player recollects no-11:
		make no decision;
	if the player knows you-were-king:
		make no decision;
	if the player does not recollect ask-away:
		queue ask-away as postponed optional;
		rule succeeds.

A Happy planning rule when the player does not know happy-name-known:
	queue what his name seems as postponed obligatory;
	rule succeeds.
	
The last Happy planning rule:
	queue hart-expires as postponed obligatory. [* We can think of nothing valuable to do with this conversation, so we will end the scene as soon as the player runs out of his current conversational thread.]

Hart-expires is an NPC-directed quip. 
    The response is "The silver glow surrounding the hart begins to fade.
'Alas, I can stay no longer,' he tells you, 'Farewell - and beware!'

He sinks to the ground, a final sigh escaping his decaying throat, 'Do
not delay - box - heart - burn.'

Then the glow is gone, and the hart is still once more, with nothing to
show he ever moved.[hart-no-longer-undead]." 
	It quip-supplies corpse.
	
Chapter 17 - Emily Short Nov 1

 whether the Queen herself made the magic mirror is a questioning quip. 
 Understand "if" or "who" as whether the Queen herself made the magic mirror. The comment is "'Did the Queen make the magic mirror herself? Or did it come to her from elsewhere?'".
 It mentions witchcraft, magic mirror, queen.
 The response is "'Yes, and yes.' She shivers. 'It came to her as an empty vessel; she filled it. [scary][Queen-made-mirror]It is she who chose a suitable voice and soul for the mirror, and made the sacrifice of their living donor.'".
 It quip-supplies Snow White. 
 It is listed.

 whose voice the mirror hath is a questioning quip. 
 The printed name is "whose voice the mirror has". The true-name is "whose voice the mirror hath". 
 Understand "has" as whose voice the mirror hath. The comment is "'[if immediately]Whose voice is it?[otherwise]Did the Queen put someone into the mirror? Who was it?[end if]'".
 It mentions magic mirror, Queen.
 The response is "She shakes her head[if the player does not know snow-white-possessed] — you can't tell whether because she doesn't choose to tell you, or because she doesn't know[end if][friendly]. '[if the player knows queen-uses-people-up]As I said, the[otherwise]The[end if] Queen consumes and corrupts the people around her[queen-uses-people-up]. Some she transforms into monsters. Some she simply uses up and discards.'".
 It indirectly-follows whether the Queen herself made the magic mirror.
 It quip-supplies Snow White.
 It is listed.

 what the Queen wants to achieve is a questioning quip. The comment is "'Why would the Queen harm her own servants and companions? What would be her purpose?'[unless the player knows snow-white-possessed] Snow White has, no doubt, many reasons to lie to you, but there is something entrancing about her answers all the same.[end unless]".
 Understand "wishes" or "desires" as what the Queen wants to achieve.
 It mentions queen, servants.
 The response is "'She has long ago ceased to know what she wants to accomplish. Now she is obsessed with protecting herself — everyone seems to her a threat. Even when there is no true threat to her, she often destroys out of habit.' [Snow White smiles]. [scary][friendly]'It is partly because of the mirror. If you ever have a magic mirror of your own to endow, choose carefully what sort of advisor you make of it.' [line break]".
 It indirectly-follows whose voice the mirror hath.
 It quip-supplies Snow White.
 It is listed.

 suggest that you seem not likely to make any mirrors is a weak performative quip.
 The printed name is "suggest that you are not likely to make any mirrors". The true-name is "suggest that you seem not likely to make any mirrors". 
 Understand "I" or "am" or "are" as suggest that you seem not likely to make any mirrors. The comment is "'I am hardly likely to make any mirrors.'".
 It mentions witchcraft, yourself.
 The response is "Her amusement fades[angry]. 'Naturally not[if Snow White is likely to continue]. [otherwise],' she says. '[end if]At the moment, and being what you are.'".
 It directly-follows what the Queen wants to achieve.
 It quip-supplies Snow White.
 It is listed.

where the Queen got the mirror is a questioning quip. The comment is "'[if immediately]Where did the empty vessel come from, then[otherwise]If the Queen did not make the mirror originally, where did it come from[end if]?'".
 It mentions magic mirror.
 The response is "'I am not sure. She has not confided in me in many years. But it is old; I would not be surprised if it has had some dozens of voices before[friendly][scary]; its value is greater than all the rest of the castle and everything inside.'".
 It indirectly-follows whether the Queen herself made the magic mirror.
 It quip-supplies Snow White.
 It is listed.

 that the Queen ought to guard the mirror is an informative quip. The comment is "'If the mirror is of such great value, I am surprised that she does not guard it better. She keeps it hanging openly. Anyone might take it away.' ".
 It mentions magic mirror.
 The response is "'Would you choose to touch it?' [paragraph break]Snow White [for eye-gest]looks amused[or breath-gest]chuckles[or walk-gest]leans toward you[end for] as you flinch involuntarily[friendly][scary]. 'Just so,' she says. 'No one who fears it would be able to steal it; and what sane person would not fear it? She is safe in her wealth.'".
 It directly-follows where the Queen got the mirror.
 It quip-supplies Snow White.
 It is listed

whether the Queen killed someone for the mirror is a questioning quip. The comment is "'Did the Queen kill someone to make the mirror?'".
 Understand "soul" or "soul in mirror" as whether the Queen killed someone for the mirror.
 It mentions death, Queen, murder.
 The response is "'Power comes at a sacrifice,' Snow White remarks. 'The curious thing is: the wiser and more innocent the soul you place in the mirror, the more charitable and humane its advice. But if you cannot bring yourself to perform such cruelty, and instead you endow the mirror with some embittered wretch who is a joy to no one — the sort of person you can easily live without — then you have a lifetime of its corrupting company[bad-soul-in-mirror].'".
 It indirectly-follows whether the Queen herself made the magic mirror.
 It quip-supplies Snow White.
 It is listed.

 whether such magic can ever be used for good is a questioning quip. The comment is "'[if immediately]Then such magic[otherwise]It sounds as though such magic as made the mirror[end if] can never be used for good; only by monsters and by those who will become monsters.'".
 It mentions witchcraft, magic mirror.
 The response is "'That is not [italic type]quite[roman type] true,' she says slowly. 'But the exceptions are rare.'".
 It indirectly-follows whether the Queen killed someone for the mirror.
 It quip-supplies Snow White.
 It is listed

Chapter 18 - Eric Eve Nov 2 
 
if the queen seems a witch is a questioning quip.
 The printed name is "if the Queen is a witch". The true-name is "if the queen seems a witch". 
 Understand "has" or "whether" as if the queen seems a witch. 
 Understand "is" as if the queen seems a witch. The comment is "'[if the player knows queen-made-mirror]Do I take it that the Queen is a witch[otherwise]Is the Queen a witch[end if]?' you ask.".
 It mentions queen, witchcraft.
 The response is "'She may fancy herself as one,' Snow White sneers. 'In truth she dabbles with powers she does not comprehend.'".
 It quip-supplies Snow White.
 It is negated by snow-white-possessed.
[ It assumes Queen-made-mirror.] [* Since the player can ask this question about Snow White more or less at any time, it seems only fair if you can also ask it about the Queen.]
 It is listed.

 what powers the Queen dabbles alongside is a questioning quip.
 The printed name is "what powers the Queen dabbles with". The true-name is "what powers the Queen dabbles alongside". 
 Understand "with" as what powers the Queen dabbles alongside. The comment is "'What are these powers the Queen dabbles with?' you want to know.".
 It mentions queen, witchcraft, powers.
 The response is "She [for eye-gest]gives you a pitying look that sends ice down your spine[or walk-gest]kicks with irritable energy at the base of the nearest aspen[or breath-gest]mumbles something deeply exasperated[end for][angry][scary]. 'Even for a huntsman your ignorance is remarkable. All sorcery involves the manipulation of demons[magic-involves-demons]. I thought everyone knew that.'".
 [It indirectly-follows why so few people understand witches.] [* This doesn't seem to make sense as a bit of conversation flow — I can change it back if it's really what was intended.]
It indirectly-follows if the queen seems a witch.
 It is negated by snow-white-possessed.
 It quip-supplies Snow White.
 It is listed.

 if the Queen seems possessed is a questioning quip.
 The printed name is "if the Queen is possessed". The true-name is "if the Queen seems possessed". 
 Understand "is" or "whether" or "possession" as if the Queen seems possessed. The comment is "'Is the Queen demon-possessed?' you ask.".
 It mentions queen, witchcraft, demons, souls.
 The response is "[Snow White smiles]. 'No, she managed to avoid that.'[line break]". [* For consistency, we're using the smile-printing token here again, but in a later version we may want to refine this more so that Snow White's expressions are more tightly tied to the mood of the current occasion. I suspect that editing will ental some tidying of gestures and facial expressions in general, in order to get a greater consistency of emotional feedback.]
 It indirectly-follows what powers the Queen dabbles alongside.
 It is negated by snow-white-possessed.
 It quip-supplies Snow White.
 It is listed.

how the Queen avoided possession is a weak questioning quip. The comment is "'How did the Queen avoid possession?' you inquire.".
 It mentions demons, witchcraft, souls, Queen.
 The response is "'By making a bargain with the demon that wishes to possess her, of course,' she replies.".
 It indirectly-follows if the Queen seems possessed.
 It is negated by snow-white-possessed.
 It quip-supplies Snow White.
 It is listed.

what bargain the Queen made is a questioning quip. The comment is "'What manner of bargain did the Queen make to avoid possession?' you ask.".
 It mentions Queen, demons, witchcraft, souls, deal.
 The response is "'She provided another victim,' [demon-possession]she shrugs.".
 It indirectly-follows how the Queen avoided possession.
 It is negated by snow-white-possessed.
 It quip-supplies Snow White.
 It is listed. 

 if Snow White seems possessed is a questioning quip.
 The printed name is "if Snow White is possessed". The true-name is "if Snow White seems possessed". 
 Understand "is" or "victim" or "whether" or "possession" as if Snow White seems possessed. The comment is "'Is that it?' you ask, with a flash of terrifying insight. 'Are [italic type]you[roman type] possessed? Is that why you have developed this taste for blood, and why...'".
 It mentions Snow White, demons, souls, witchcraft.
 The response is "Snow White's glance suffices to silence your babbling[angry][scary], but the look she gives you convinces you that you have hit upon the truth of the [snow-white-possessed]matter.".
 It quip-supplies Snow White.
 It assumes possible-blood-drinking, demon-possession.
 It is negated by snow-white-possessed.
 It is listed.

 what demons the Queen invoked is a questioning quip. The comment is "'So, what demons did the Queen try to invoke for her witchcraft?' you ask.".
 It indirectly-follows what powers the Queen dabbles alongside.
 It mentions demons, Queen, witchcraft.
 The response is "'Lilith, for one,' she tells you, 'She wanted great beauty, and foolishly thought Lilith could endow it.[lilith-named][queen-invoked-lilith]'".
 It quip-supplies Snow White.
 It is negated by snow-white-possessed.
 It is listed.

 whether Lilith made the Queen beautiful is a questioning quip. The comment is "'[if immediately]And d[otherwise]D[end if]id Lilith make the Queen more beautiful?' you wonder.".
 It mentions Queen, demons, Lilith.
 The response is "'No, it didn't work like that,' Snow White tells you. 'The Queen was unwilling to pay the price demanded. Besides, as well you know the Queen has beauty enough already. So they came to another arrangement - hindering the competition, you could say.'".
 It is negated by snow-white-possessed.
 It indirectly-follows what demons the Queen invoked.
 It quip-supplies Snow White.
 It is listed.

[An availability rule for where Lilith Seems now:
	if the player knows snow-white-possessed:
		it is off-limits.]
		

 where Lilith seems now is a questioning quip.
 The printed name is "where Lilith is now". The true-name is "where Lilith seems now". 
 Understand "is" as where Lilith seems now. The comment is "'[if immediately]So w[otherwise]W[end if]here is Lilith now?' you ask.".
 It mentions demons, Lilith.
 The response is "She purses her lips[set body-context to Snow White], as if pondering a deeply puzzling question[scary]. 'I can't imagine,' she finally replies, 'It's not as if the Queen would tell [italic type]me[roman type] such things.'".
 It indirectly-follows whether Lilith made the Queen beautiful.
 It is negated by snow-white-possessed.
 It quip-supplies Snow White.
 It is listed. [* Originally she put her hand up to her lips, but her hand is supposed to be chained behind her back.]

[availability rule for Lilith likes drinking blood:
	if the player knows murderous-Lilith:
		it is off-limits.]

 whether Lilith likes drinking blood is a questioning quip. The comment is "'[if the player knows snow-white-possessed]Do you[otherwise]Does Lilith[end if] like drinking blood[if the player knows vampirism-revealed][first-vampire-accusation][end if]?' you ask.".
 It mentions Lilith, demons, vampirism, blood.
 Understand "is" or "vampire" or "a" or "drinks" or "drink" or "does" as whether Lilith likes drinking blood.
 The response is "'Whatever gave you that idea?' she demands sharply. '[if the player knows snow-white-possessed]Night-demons do not need blood to survive; they are eternal on their own. Should they drink blood on occasion, it is only because they want to do so[casually queue what Lilith wants].[otherwise]Lilith is a night-demon; why should you suppose she's a vampire?[end if]'".
 The nag is "When you don't answer, she relaxes slightly[queue weird-laugh]."
 It quip-supplies Snow White.
It is negated by murderous-lilith.
 It assumes queen-invoked-lilith.
 It is listed.

Rule for quip-introducing it would explain a few things:
	say "[roman type]Certainly it would explain a few things.";
	list no other quips.

 it would explain a few things is an informative quip.
 Understand "I" or "certainly" as it would explain a few things. The comment is "'It would explain a few things, like other acts of vampirism I suspect,' you tell her.".
 It mentions blood, vampirism, Lilith.
 The response is "'I see,' she replies, 'You think [italic type]I[roman type] am Lilith - is that it?'".
 The uncertainty-refusal is "[one of]She snickers rudely[angry]. 'If you're going to accuse me, then do it. Do you think I am possessed?'[or]'Stop playing.'[stopping]".
 It is restrictive.
 It is negated by snow-white-possessed.
 It directly-follows whether Lilith likes drinking blood.
 It quip-supplies Snow White.
 It is listed. 

 it would explain a few things is an informative quip.
 Understand "I" as it would explain a few things. The comment is "'It would explain a few things, like other acts of vampirism I suspect,' you tell her.".
 It mentions blood, vampirism.
 The response is "'I see,' she replies, 'You think [italic type]I[roman type] am Lilith - is that it?'".
 The uncertainty-refusal is "[one of]She snickers rudely[angry]. 'If you're going to accuse me, then do it. Do you think I am possessed?'[or]'Stop playing.'[stopping]".
 It is restrictive.
 It is negated by snow-white-possessed.
 It directly-follows whether Lilith likes drinking blood.
 It quip-supplies Snow White.
 It is listed.

 yes-14 is an affirmative quip. The printed name is "say yes". The true-name is "yes-14". The comment is "'Yes, that's precisely what I think,' you declare, 'Come out of her, Lilith! Leave poor Snow White alone!'".
 It mentions demons, Lilith, exorcism.
 The response is "Snow White gives a chilling laugh[scary]. 'If I were Lilith you'd have to do better than that, [vocative huntsman]! So you'd better pray that I'm not.'[snow-white-possessed] Something - not her words alone, nor just her tone of voice, nor even the cold glare in her eye, but perhaps all three taken together - convinces you that you have hit upon the truth.".
 It directly-follows it would explain a few things.
 It quip-supplies Snow White.
[ It is listed.]

 no-15 is a negative quip. The printed name is "say no". The true-name is "no-15". The comment is "'No, of course I don't think that,' you reply hastily, 'otherwise I shouldn't dare to suggest it!'".
 It directly-follows it would explain a few things.
 It mentions demons, Lilith.
 The response is "'No, of course. Quite.' She regards you steadily[angry], but there's more than a hint of mockery in her eyes[set body-context to Snow White].".
 It quip-supplies Snow White.
[ It is listed.]

Chapter 19 - Emily Short Nov 2

 what she does on her walks is a questioning quip. The comment is "'[if immediately]And you just walk around[otherwise]When you go for walks in the woods, you just wander around in the dark[end if]?'".
 It mentions Snow White, night, trees.
 The response is "There is a pause, almost as though you've confused her[friendly][pathetic]. Then she says, 'Usually. And sometimes speak to the animals. [queue solace-in-desert]What else?'".
 It indirectly-follows how she kens her way around the woods.
 It quip-supplies Snow White.
 It is listed.

 whether the animals ever answer is a questioning quip. The comment is "'Do they ever answer you?'".
 It mentions creatures.
 The response is "[Snow White smiles]. 'No.'[line break]".
 It directly-follows what she does on her walks.
 It quip-supplies Snow White.

solace-in-desert is an NPC-directed quip. [* Lilith is supposed in some stories to have been cast out into the desert and found solace there. But this suits Snow White as well.]
	The response is "'I would expect you would understand this[friendly],' she says. 'You spend your days in the woods, away from the company of others. Is it not because you find solace in being alone?'"
	It is restrictive.
	It quip-supplies Snow White.
	
that your reasons do not concern her is a weak informative quip.
	it mentions yourself.
	Understand "my" and "[maybe]" and "lilith" and "snow white" as that your reasons do not concern her.
	The comment is "'My reasons are my own business.'"
	The response is "'You see? We are two of a kind.'".
	It directly-follows solace-in-desert.
	It quip-supplies Snow White.

 how she hired servants without the Queen knowing is a questioning quip. The comment is "'How did you hire [if immediately]them[otherwise]servants for your haven[end if] without the Queen noticing?'".
 It mentions servants.
 The response is "'There are times when she does not watch what I do. Sometimes, when she is most occupied with other affairs, I am free to take messages[pathetic]. Sometimes, I have arranged matters when I was out walking. It is very hard to watch every prisoner closely, and the Queen regards all her subjects as her prisoners.'".
 It indirectly-follows who are the servants.
 It quip-supplies Snow White.
 It is listed.

 where such magic originates is a questioning quip. The comment is "'I have never heard of anyone making [if immediately]such a thing[otherwise]any form of magic mirror[end if]. Is it from some distant country, in the far past?'".
 It mentions magic mirror, witchcraft.
 The response is "'There were a people long ago, who lived away to the southeast. This was when the world was very young and men had not forgotten how to speak to the great powers.' Her voice has gone distant, as though she is telling a story she heard when she was young, or like a grandmother recounting a courtship from her girlhood. 'They suffered from a terrible plague, which came in spring every seventeeth year. So many people died that their skills and crafts were being lost, their knowledge destroyed. So they made a bargain with the demons[magic-involves-demons]: they learned to make vessels that would hold souls at the moment of their passing, and so retain what would otherwise be lost.'".
 It indirectly-follows where the Queen got the mirror.
 It quip-supplies Snow White.
 It is listed.

whether vampires made the mirror is a weak questioning quip. The comment is "'Is the mirror the work of vampires?'".
 Understand "if" or "constructed" or "fabricated" or "work" as whether vampires made the mirror.
 It mentions vampires, magic mirror.
 The response is "'No.'".
 It indirectly-follows where the Queen got the mirror.
 It quip-supplies Snow White

 how she kens about the plague is a weak questioning quip. The comment is "'How do you know [if immediately]all this[otherwise]about the people who suffered the plague[end if]?'".
 The true-name is "how she kens about the plague".
 The printed name is "how she knows about the plague".
 Understand "knows" as how she kens about the plague.
 It mentions plague, demons, powers.
 The response is "'I suppose I read it somewhere.' [Snow White smiles].[line break]".
 It indirectly-follows where such magic originates.
 It is negated by snow-white-possessed. [Because maybe she lived through it.]
 It quip-supplies Snow White.
 It is listed.

 whether the Queen hath such books in her library is a weak questioning quip. 
 The printed name is "whether the Queen has such books in her library". The true-name is "whether the Queen hath such books in her library". 
 Understand "has" as whether the Queen hath such books in her library.  The comment is "'Does the Queen have such books in her library?'".
 It mentions library.
 The response is "'When refugees come out of the east with their tattered volumes in trade for food and house, she pays without even looking inside. She has more books than she knows.'".
 It directly-follows how she kens about the plague.
 It quip-supplies Snow White.
 It is listed.

 what happened to the people who made the mirror is a questioning quip. The comment is "'What happened to [if immediately]those people[otherwise]the people who made the mirror[end if]?'".
 It mentions plague.
 The response is "She lifts one shoulder dismissively. 'They could preserve the memories of the past in their mirrors and scrying glasses; but they could not make enough children quickly enough; they could not keep the dying alive. In each generation the plague became more virulent. It killed men more rapidly than women. In the end there were only a few girls left. They lived in long rooms hung with mirror-glass and singing with the voices of the dead, and knew that they were the last of their kind, and that there would be no one to preserve what [italic type]they[roman type] knew. So they wrote their history down in books, and marked off years on a calendar, and waited.'".
 It indirectly-follows where such magic originates.
 It quip-supplies Snow White.
 It is listed.

 what became of the history books is a weak questioning quip.  The comment is "'What became of [if immediately]their books[otherwise]the books written by the plague sufferers[end if]? Can they still be found?'".
 It mentions plague, library.
 The response is "'Perhaps a few. No one would now believe what is written.'".
 It indirectly-follows what happened to the people who made the mirror.
 It quip-supplies Snow White.

Chapter 20 - Sarah Morayati Nov 2

Rule for quip-introducing a quip which mentions vampirism when the player knows first-vampire-accusation:
	if the current quip is how vampires came to be:
		make no decision;
	say "[roman type]If you [one of]want to live dangerously[or]really cannot stop yourself from goading her[or]feel like pushing your luck[or]have a death-wish[at random], you could [list of flagged-ready quips which mention vampirism][if other quips remain]. It might be safer to pretend this line of inquiry was going somewhere else, however. [run paragraph on][otherwise].[end if]";

 what she knows about vampires is a questioning quip. The comment is "'Tell me what you know about vampires,' you say, more sharp than you intended. 'Everything you know[if the player knows vampirism-revealed][first-vampire-accusation][end if].'".
 It mentions vampirism.
 The response is "'They drink blood,' she says. 'And prefer the nighttime, and are not to be trifled with.' There is a glint in her pupils that you never noticed before: faintly red, a bit too bright for her delicate features[scary].".
 It is negated by snow-white-possessed.
 It quip-supplies Snow White.
 It is listed.

 how vampires came to be is a questioning quip. The comment is "'Where did the vampires come from? Were they always there?'[if the player knows vampirism-revealed] you ask, more keenly than you ought.[first-vampire-accusation][otherwise] you ask.[end if]".
 It mentions vampirism.
 The response is "'That depends on who you believe,' she says simply. 'Some say they only recently came to be. They evolved, you could say.' A small laugh escapes her. 'But others say they have always existed, in one form or another; it is merely an opportune time for them to walk the earth.' She looks at you, straight on. 'Who do [italic type]you[roman type] believe?'".
 It is restrictive.
 It quip-supplies Snow White.
 It is negated by snow-white-possessed.
 It is listed.

 vampires only recently came to be is an informative quip. The comment is "'They evolved, of course,' you say. 'Anything else would be foolish superstition.'".
 It mentions vampirism.
 The response is "'Very well,' Snow White responds. 'Perhaps you would think differently if you were around for the beginning times[scary].'".
 It directly-follows how vampires came to be.
 It quip-supplies Snow White.
 It is listed.

 vampires always existed is an informative quip. The comment is "'They must have been here all along,' you say[if anger is greater than 1]. Hopefully that's the answer she wants[otherwise if scariness is greater than 1], hesitantly[end if].".
 It mentions vampirism.
 The response is "'You are astute,' Snow White answers. 'The first step, in everything, is knowing who you are up against[friendly].'".
 It directly-follows how vampires came to be.
 It quip-supplies Snow White.
 It is listed.

 why she threw a rock at the mirror is a questioning quip. The comment is "'Why did you throw a rock at the mirror?'".
 It mentions magic mirror.
 It assumes rock-at-mirror.
 The response is "'Some things should not exist,' she says simply[scary]. 'At least not in the hands of the unworthy.'".
 It quip-supplies Snow White.
 It is listed.

Chapter 21 - Emily Short Nov 2

[This edit is mostly refactoring to make the conversation flow more robustly, and to give Snow White some more proactive behavior especially at the beginning of the game.]

go-ahead-and-cut is a q-transitional NPC-directed quip.
	The response is "She shivers and looks pointedly from you to the corpse." [* This is an optional followup that appears if you tell her you're here to collect the heart but then don't do anything about it.]
	It quip-supplies Snow White.

 remark that she isn't very comforting is a performative quip. The comment is "'You aren't a very [comforting] ally,' you mutter.".
 Understand "is" and "not" as remark that she isn't very comforting.
 It mentions Snow White.
 The response is "'Nor are you. But the strongest pacts are built on distrusting someone else more.' [pathetic][Snow White smiles].[line break]".
 It directly-follows the heart seems dead.
 It quip-supplies Snow White.
 It is listed.

 whether other huntsmen were sent to kill her is a questioning quip. The comment is "'[unless immediately]You mentioned other huntsmen. [end unless]Has the Queen attempted to — send you away — before?' This is as close as you can bring yourself to explaining the nature of your commission, though surely she knows what is happening.".
 It mentions huntsmen.
 The response is "'Not in exactly this way,' she says. [queen-uses-people-up]'But she finds reason, sooner or later, to destroy her servants, or send them on tasks they cannot bear, and then punish them for their failure. If you stay in her service, it will go the same way for you. Her requirements will grow stranger and more horrible. If you do not die, or disobey her, you will be corrupted[friendly].'".
 It quip-supplies Snow White.
 It indirectly-follows what happened to other huntsmen.
 It is listed

stop-taunting is an NPC-directed quip.
	The response is "'[angry]If you won't untie me, you might at least stop taunting me about the fact,' she says."
	It quip-supplies Snow White.

pleading-to-be-untied is an NPC-directed quip.
	The response is "[if the previous quip is the safe haven seems too far away to reach tonight]'Untie me, and I'll go on alone.'[otherwise if the previous quip is how the mirror reflects you to the queen][Snow White smiles]. 'I'm sorry,' she says, with unapologetic flatness. 'I seem to have drawn a blank. Perhaps if my hands were freed, it might jog my memory.' [line break][otherwise]'Untie me,' she says. 'It will make everything easier for both of us.'[end if]".
	The nag is "[one of]She twists around to give you access to her bound wrists, just in case you want to set her free[or]'Come, we're allies,' she says[queue no-1][stopping]."
	It quip-supplies Snow White. [* This used to be distributed over several quips, with different possible responses, but it is more coherent if the player has the same set of response options no matter when/how she asks to be untied.]
	
Before discussing something which does not directly-follow pleading-to-be-untied when the previous quip is pleading-to-be-untied:
	say "[one of]It is not the first time she has made the request, and you are tired of refusing. This time you do not give any answer.[or][run paragraph on][stopping]"


maybe-later is a performative quip. The printed name is "say maybe". The true-name is "maybe-later".
 Understand "[maybe]" as maybe-later. 
 The comment is "'Perhaps in a little while.'".
 The response is "She stamps her foot on the cold ground[pathetic] and flounces toward one of the trees — as much as a girl can flounce with her arms tied behind her, that is.".
 It quip-supplies Snow White.
 It is negated by snow-white-possessed.
 It directly-follows pleading-to-be-untied. 

 no-1 is a negative quip. The printed name is "say no". The true-name is "no-1". 
 The comment is "'I don't mean to untie you quite yet. I don't know you won't bite.'".
 The response is "'I'm tired,' she says. 'My feet hurt. My wrists hurt. I am a third your size. Do you really think there is any harm I could do to you[pathetic]?'".
 It quip-supplies Snow White.
 It is negated by snow-white-possessed.
 It directly-follows pleading-to-be-untied. 

no-30 is a negative quip. The printed name is "say no". The true-name is "no-30". 
 The comment is "'I think not, Lilith.'".
 The response is "'I wouldn't harm you,' she promises. But who is to say that she is telling the truth?".
 It quip-supplies Snow White.
 It assumes snow-white-possessed.
 It directly-follows pleading-to-be-untied.

maybe-so is a performative quip. The printed name is "say perhaps". The true-name is "maybe-so". Understand "[maybe]" as maybe-so.
The comment is "'If you are dangerous, I would not expect physical size to be much of an impediment,' you say.".
 It mentions Snow White.
 The response is "She slumps[pathetic]. 'It's easier to demonstrate strength than to prove you really are weak.'".
 It directly-follows no-1.
 It is negated by snow-white-possessed.
 It quip-supplies Snow White

After examining Snow White when the current quip is no-1:
	say "But she catches you looking at her and she looks back at you, and in that moment seems no more or less than human, exhausted, small. You drop your eyes. [run paragraph on]";
	queue no-2 as immediate obligatory. [We want her to react to your looking her over this way in reaction to her question.]

 no-2 is a negative quip. The printed name is "say no". The true-name is "no-2". 
 The comment is "'Probably not.' She is a small thing, after all. Delicate, like chicken bones. Funny how when you think of her you always think of blood and bones and dead parts of things. 

'I am not taking chances, all the same,' you add.".
 The response is "She grimaces[pathetic]. 'The Queen must have been very persuasive.'".
 It is negated by snow-white-possessed.
 It quip-supplies Snow White.
 It directly-follows no-1. 

yes-1 is an affirmative quip. 
 The printed name is "say yes". The true-name is "yes-1".
 Understand "have" as yes-1. 
 Understand "is" as yes-1. 
 It mentions vampirism.
 The comment is "'If you are truly a—' [if scariness is less than 5]You falter before the look in her eyes[set body-context to Snow White], which is hurt[pathetic][otherwise]Suddenly you can't bring yourself to name it[scary][end if]. '— what the Queen says you are[first-vampire-accusation], then you could bleed a man dry in an instant.'".
 The response is "'Superstition,' she says. 'I sleep late. I avoid the sun. My skin is delicate: it's something I inherited from my real mother. Everything else is spite. She is, after all, the one who communes every night with a mirror that speaks!'".
 It is negated by snow-white-possessed.
 It quip-supplies Snow White.
 It directly-follows no-1. 

she-sees-hesitation is an NPC-directed quip.
	The response is "From the expression on her face[set body-context to Snow White], she was able to read most of your thoughts just now[casually queue no-1]."
	It quip-supplies Snow White. [* This quip is queued if you try to obey her request to be untied, then hesitate and change your mind. If you don't change the subject, she'll push on why you're afraid of her.]
	
Chapter 22 - Adam Thornton Nov 2

Rule for quip-introducing if you could kill the queen:
	say "[roman type][one of]That is a good question, actually. The witch might be impervious to normal methods of dispatch[or]You're tempted to parrot the question back, ask her whether the queen is killable[or]The more you think about the question the more uncomfortable you are[stopping].";
	list no other quips.

 if you could kill the queen is a questioning quip. 
 The printed name is "if you could kill the queen". The true-name is "if you could kill the queen". 
 Understand "I" or "whether" or "can" or "impervious" or "witch" or "normal" or "methods" or "dispatch" or "of dispatch" or "might" or "be" or "good" or "question" or "is" or "killable" as if you could kill the queen. The comment is "'Could I? Could I kill the queen?'".
 It mentions queen, death, murder.
 The response is "'My question. Could you?'".
 The nag is "[one of]She continues to watch you keenly[or]The longer you delay, the more amused she appears[at random]."
 The uncertainty-refusal is "'No, you can equivocate no longer. Consider what you believe and commit to it.'".
 It directly-follows how to deal alongside the queen.
 It is restrictive.
 It is listed.



whether the Queen seems unworthy of the mirror is a weak questioning quip. 
 The printed name is "whether the Queen is unworthy of the mirror". The true-name is "whether the Queen seems unworthy of the mirror". 
 Understand "is" as whether the Queen seems unworthy of the mirror.  The comment is "'I take it[unless immediately] from your earlier comments[end unless] that the Queen is unworthy of the mirror.'".
 It mentions Queen, magic mirror.
 The response is "[if the player recollects who seems worthy of the mirror]'Both her intentions and her knowledge have been muddled from the beginning. It was always likely that she would become dangerous, trying to control such a thing.'[otherwise]'She does not have the self-knowledge to control such an object. Few mortals do.'[end if]".
 It indirectly-follows why she threw a rock at the mirror.
 It quip-supplies Snow White.
 It is listed.

 who seems worthy of the mirror is a questioning quip. 
 The printed name is "who is worthy of the mirror". The true-name is "who seems worthy of the mirror". 
 Understand "is" as who seems worthy of the mirror.  The comment is "'Who ought to have the mirror, in your opinion?'".
 It mentions magic mirror.
 The response is "'It is an unanswerable question,' she says — but she seems drawn, as though you had posed an interesting philosophical conundrum. 'Those who wish to use magic for virtuous ends are usually disappointed, or destroyed. Good intentions are not sufficient. For some people[scary], they are even a hindrance. Evil intentions that succeed are rarely so dangerous as good ones that go awry. But most of the good magic the world has ever seen was performed by a magician who died in the act or shortly afterward. Death curtails the options for further mischief, you see.'".
 It indirectly-follows why she threw a rock at the mirror.
 It quip-supplies Snow White.
 It is listed.

Chapter 23 - Rob Dubbin Nov 3

Rule for quip-introducing whether she can read your thoughts while freeing Snow White:
	say "[roman type]Thinking about asking her about whether she can read your thoughts is almost like asking her. And thinking about thinking about asking... No, stop.";
	list no other quips. [* Added because currently the text reads "From the expression on her face, she was able to read most of your thoughts just now... You could ask whether she can read your thoughts." Which is tediously repetitive.]

whether she can read your thoughts is a questioning quip. The comment is "'I don't like that look,' you say. 'In fact I can't help but feel like you just took a glance into my mind, which may I remind you belongs to me.'".
 Understand "my" or "Snow White" or "thinking" or "mind" or "could" as whether she can read your thoughts.
 It mentions mind reading, snow white.
 The response is "'There are some things I cannot help,' she says. 'Call it intuition. Besides, you are hardly in a position to take a moral stance on privacy[pathetic].' Snow White's chin gestures toward her chained wrists.".
 It directly-follows she-sees-hesitation, no-21, maybe-2. [* There is other thought-reading elsewhere.]
 It is negated by snow-white-possessed.
 It quip-supplies Snow White.
 It is listed.

 yes-15 is an affirmative quip. The printed name is "say yes". The true-name is "yes-15". The comment is "'The Queen has a way with words, and even more of a way with leverage.' With bitterness, you recall arriving home to find your cabin[cabin-destroyed] razed — its once-pristine roofbeams now twisted and charred beyond not only recognition, but also physical force.".
 It mentions witchcraft, Queen. [* reduced the number of mentions here because "treason" and "hounds" seemed like false positives for this one — it mentions neither explicitly.]
 The response is "It is as though she feels your pain. 'Yes... I understand[friendly][pathetic].'  For a moment there is a connection, but then nothing.".
 It directly-follows no-2.
 It quip-supplies Snow White.

 no-16 is a negative quip. The printed name is "say no". The true-name is "no-16". The comment is "You are hesitant to reveal too much. 'I make my own decisions,' you say, with bravado you do not feel.".
 It mentions Queen, snow white, powers, witchcraft.
 The response is "She sees through the ruse[angry]. 'Yes, I am sure you do. Let us discuss the matter no further; I can see it gives you great discomfort.'".
 It directly-follows no-2.
 It quip-supplies Snow White.

 what will happen if she kens you ken she's a vampire is a questioning quip.
The true-name is "what will happen if she kens you ken she's a vampire".
The printed name is "what will happen if she knows you know she's a vampire".
 Understand "is" or "know" or "knows" as what will happen if she kens you ken she's a vampire. The comment is "'And if she does... discern the surety?' You repeat the corpse's words back to him, unwilling even to picture the scenario.".
 It mentions corpse, vampirism, snow white.
 The response is "'Well, you know how vampires are. They anger easily and don't like to leave much behind. Since I got here I've heard all kinds of tales. There was Count Anthrax, a horribly cursed being who enjoyed puncturing his victims['] arms slowly, because for him it was less about drinking the blood than it was about witnessing the transition. Oh, and there was this one vampire who possessed the body of a vagrant, actually not too far from here, who would sneak into open windows at night and nibble the feet of infants, because he liked the way they would turn against their parents the next morning, and suddenly there would be a whole family of vampires. Then there was[queue tell the corpse to stop it] —'". [Since your concern at the moment is that he'll keep going, we want, in fact, to guarantee that he does keep going if you don't first intervene.]
 It directly-follows yes-10.
 It quip-supplies corpse.
 It is listed.

Rule for quip-introducing tell the corpse to stop it:
	say "[roman type]You want to tell him to stop it.";
	list no other quips.

 tell the corpse to stop it is a performative quip. The comment is "'Stop it! Stop telling me these things.'  ".
 It mentions snow white, vampirism, death.
 The response is "'— Don't eat my ears! I need those! But the vampire ate them anyway. And you haven't lived until you've seen the spirit imprint of a canine vampire attack. I swear to you, huntsman, it's like nothing I've ever seen. Mauled Beyond Recognition is an understatement[queue more-disturbance].'".
 It directly-follows what will happen if she kens you ken she's a vampire.
 It quip-supplies corpse.
 It is listed.

more-disturbance is an NPC-directed quip.
	The response is "'They say that if the vampires become common in the area, all the animals of the forest turn vampire as well, and devour any traveler fool enough to pass through. Only their dried white corpses are found afterward, all twisted and wrung-out.'"
	It quip-supplies corpse.
	
Availability rule for seriously please stop telling me these things:
	if the player recollects more-disturbance:
		it is off-limits.

 seriously please stop telling me these things is an informative quip. The comment is "You [if the player carries the dagger]brandish your knife[otherwise]wave frantically[end if] in a desperate attempt to get [corpse] to stop describing grotesquely fatal scenarios. 'I'm not kidding. Stop telling me these things.'".
 It mentions vampirism, snow white, death, corpse.
 The response is "'Oh I'm sorry, am I disturbing you? Please don't hurt me with that knife, I'd hate to lose a vital organ OTHER THAN THE HEART YOU ALREADY STOLE.'".
 It directly-follows tell the corpse to stop.
 It quip-supplies corpse.
 It is listed.

 if she needs privacy because she's a vampire is a questioning quip. The comment is "'Is the main reason that you're a vampire?'[first-vampire-accusation]".
 Understand "whether" as if she needs privacy because she's a vampire.
 It mentions vampirism, snow white.
 The response is "It was tempting, and you regret giving in to the temptation. The diminutive Snow White suddenly looks larger than she did before, and more dangerous[scary][angry]. The look in her eyes[set body-context to Snow White] makes you want to dash into the nearest tree. She responds: 'I do not like the tone behind your words. In fact... I think it dangerous. I do not respond well to danger.'".
 It directly-follows why she needs privacy.
 It quip-supplies Snow White.
 It assumes vampirism-revealed.
 It is listed.

 if she doesn't stand there because she's a vampire is a weak performative quip. The comment is "In a flash of insight, you remember an obscure fact from the tales of your youth. 'Do you avoid the mirror because you're a vampire?'[first-vampire-accusation]".
 Understand "does" and "not" and "whether" as if she doesn't stand there because she's a vampire.
 It mentions vampirism, snow white, magic mirror.
 The response is "'That isn't why,' she says ambiguously."
 It directly-follows what she sees in the mirror.
 It quip-supplies Snow White.
 It is negated by snow-white-possessed.
 It assumes vampirism-revealed.
 It is listed.

Instead of Snow White discussing something when accusation-count is greater than 1:
	say "'Interesting [if the noun is a questioning quip]question[otherwise]comment[end if],' she says, before leaping at your throat and tearing it out with her teeth. The last sensation you feel is one of floating away, as your soul drains from your body.

At the very end, it occurs to you to be amused that you thought binding her hands would keep you safe.";
	record "Provoking Snow White to murder through incessant questions about vampires" as an ending;
	end the game in death.
	
accusation-count is a number that varies.

Before printing the name of first-vampire-accusation: [* This is a slightly tricky way of counting how many times the player has tipped his hand]
	say "[scary][angry]";
	increase accusation-count by 1.

To say end the game in death:
	end the game in death.	
	
Chapter 24 - Emily Short Post Nov 4

[Coming to this at the closure of the game, I note several things.

Practical points:

— Ziv Wities' riddle has no answer. He told me what he wanted the answer to be, and sounded like he might contribute that content himself, but perhaps ran out of time. So I should fill that in.

— Currently the endings that exist in the game are very chancy to get to. It's easy for the player to miss the turnoffs that lead to interesting things (like the waking of Happy, or hearing the riddle in the first place). It would be wise to provide more clues leading into these plot branches.

— There are also some hints about the missing king that are never followed up on.

— After the first few turns, Snow White is not very active.

Nuance points:
	
-- The current design doesn't do a very good job of taking into account the mood of the interaction: it's easy to leap from one tonality to a completely different one. 

Thematic points:

So far we have the possession by Lilith, vampirism, the evil endowment of the magic mirror, Snow White's contempt for men (see especially Chapter 5 - Rob Dubbin Oct 29), and her desire to find an apple as major components. 

A quick trip to Wikipedia suggests these dovetail together fairly well, in fact: Lilith is in some versions of legend supposed to have been Adam's first wife, before Eve, who is cast out because she wants to be his equal sexually and in other respects; she is thus a patroness of feminism (see the Lilith fair) and witchcraft (associated with Hecate at some points). She is also (!) in some versions supposed to live inside mirrors. What's more, Lilith is associated with the Lamia, a vampiric sort of creature in Roman mythology.

If anything, this is almost too much to work with. But I'd like to make the various strands as cohesive as possible.]

Section 1 - Answers to the Riddle 
		
Before subject-changing when the previous quip is no-5 or the previous quip is request a repetition of the riddle:
	say "[one of]'You don't care to guess the answer to my riddle, then?' [paragraph break]'Not now,' you reply.[or]'You are so shy of my riddle,' she says, a little petulantly[pathetic]. [paragraph break]You ignore the complaint.[or]'Come, I thought you were better at guessing games than this,' she says. 'Surely it should appeal to a man who spends his days tracking footsteps through the woods.' [paragraph break]'I have had enough of guessing with the Queen,' you say. 'So I changed the subject. What do you say?'[or]She makes a little face[set body-context to Snow White], but you ignore her.[stopping]". [* The subject-changing hook exists to note when the player is deliberately moving away from a conversation topic to some other thread. Most of the time there's no need to have the NPC comment on this, but it's occasionally useful.]

suggest blood is a performative quip. The comment is "'[if immediately]Do you mean... [otherwise]Is the answer to your riddle... [end if]blood?' What would she expect to find in the woods, if not some animal from which she could feed? ".
 Understand "guess" or "pick" or "choose" as suggest blood.
 It mentions blood.
 The response is "She laughs wickedly[angry]. 'Wrong! And your answer reveals more of yourself than you might like.'".
 It is negated by riddle-solved.
 It indirectly-follows no-5, request a repetition of the riddle.
 It quip-supplies Snow White.

An availability rule for suggest an animal:
	if the player knows riddle-solved:
		it is off-limits.
		
suggest an animal is a performative quip. The comment is "'[if immediately]Do you mean[otherwise]Is the answer to your riddle[end if] some kind of animal?'".
Understand "guess" or "pick" or "choose" as suggest an animal.
 It mentions creatures.
 The response is "'More vegetable,' she replies, narrowing her eyes[set body-context to Snow White][angry]. 'Hardly mineral at all.'".
 It is negated by riddle-solved.
 It indirectly-follows no-5, request a repetition of the riddle.
 It quip-supplies Snow White. 

suggest a soul is a performative quip. The comment is "'[if immediately]Do you mean[otherwise]Is the answer to your riddle[end if] a soul? Lilith didn't have a soul, but Adam did? And they can't be shared?'".
Understand "guess" or "pick" or "choose" as suggest a soul.
 It mentions snake.
 The response is "She [for eye-gest]looks at you keenly from the corner of her eye[or walk-gest]bounces on the balls of her feet[or breath-gest]makes a disappointed click with her tongue[end for]. 'And you suppose we will find someone's soul lying on the ground, like a discarded toy ball?'".
 It is negated by riddle-solved.
 It indirectly-follows no-5, request a repetition of the riddle.
 It quip-supplies Snow White. 

suggest a snake is a performative quip. The comment is "'[if immediately]Do you mean[otherwise]Is the answer to your riddle[end if] a snake?'".
Understand "guess" or "pick" or "choose" as suggest a snake.
 It mentions snake.
 The response is "'Have you ever seen a round red snake?' she asks. 'You're not really trying, are you[angry]?'".
 It is negated by riddle-solved.
 It indirectly-follows no-5, request a repetition of the riddle.
 It quip-supplies Snow White. 

suggest a heart is a performative quip. The comment is "'[if immediately]Do you mean[otherwise]Is the answer to your riddle[end if] the hart's heart? If so, we've found it.'".
Understand "guess" or "pick" or "choose" as suggest a heart.
 It mentions hart's heart.
 The response is "She wrinkles her nose. 'No! How distasteful.' Then, a little amused, she adds: '[unless the player knows snow-white-possessed]Even I know that hearts[otherwise]Hearts[end unless] are not round.'".
 It is negated by riddle-solved.
 It indirectly-follows no-5, request a repetition of the riddle.
 It quip-supplies Snow White. 

suggest an apple is a performative quip. The comment is "'[if immediately]Do you mean[otherwise]Is the answer to your riddle[end if] an apple? If so, you have the wrong sort of woods — the trees here are pine and aspen.'".
Understand "guess" or "pick" or "choose" as suggest an apple.
 It mentions apple.
 The response is "'You answer right[riddle-solved], and yet you don't understand.' Her expression, dimly seen in the lantern light, is almost frustrated, as though there is something she wishes to tell you, but cannot[pathetic].".
 It is negated by riddle-solved.
 It indirectly-follows no-5, request a repetition of the riddle.
 It quip-supplies Snow White.

Rule for quip-introducing how an apple would help her:
	say "[roman type][one of]But it's not clear[or]You still don't see[stopping] how an apple would help her.";
	list no other quips.
	
 how an apple would help her is a questioning quip. The comment is "'What good would an apple do you?'[if the player does not know snow-white-possessed] It seems more and more that you are humoring the mad.[end if]".
 It mentions apple.
 The response is "'It is the food of mortals[apples-for-men],' she says. (Wistfully, you might think[pathetic].) 'Those who eat, die; those who die, live beforehand.'".
 It assumes riddle-solved.
 It quip-supplies Snow White.
 It is listed.

Rule for quip-introducing give up:
	say "[roman type][one of]Tempting to refuse to guess[or]You could refuse to guess; maybe she would explain[stopping].";
	list no other quips.

 give up is a performative quip. The comment is "'I have no time for riddles.' You don't, either: the Queen's patience must be wearing thin, back at the castle. 'Tell me what you want me to know in plain language, or do not tell me.'".
 Understand "guess nothing" or "refuse to guess/answer" or "[maybe]" or "give in" or "concede" as give up.
 It mentions riddle.
 The response is "'I [italic type]can't[roman type],' she says, with what sounds like real anguish[pathetic]. 'These teeth are not my teeth, nor this tongue my tongue.'".
 It directly-follows no-5, request a repetition of the riddle.
 It quip-supplies Snow White.
 It is listed.

oops-sorry is an NPC-directed quip.
	It mentions roots.
	The response is "'Ow!' She half-falls against a tree[pathetic][angry][wants-delay]. When she rights herself, she gives you a look as though her clumsiness were your fault[list to walk properly][list  why she seems trying to delay the escape]."
	It quip-supplies Snow White.
	
Rule for quip-introducing to walk properly when the current quip is oops-sorry:
	say "[roman type][if the player knows vampirism-revealed or the player knows snow-white-possessed]It seems she does not want to cooperate. You could tell her to walk properly, but will she obey?[otherwise if Snow White is whiny]Your patience is more or less at an end: it's like dealing with an infant. Better tell her [to walk properly], and hope she is minded to obey, for once.[otherwise]Perhaps you could [to walk properly]; or perhaps you should [she seems trying to delay the escape].[end if]";
	list no other quips.

[Currently yes-3 has no ways out; it's a trapped restrictive quip. So we need to fix that.]

 no-18 is a negative quip. The printed name is "say no". The true-name is "no-18". The comment is "'No. If I cannot trust you, why should I press you for answers? They might well be lies.'".
 It mentions deal.
 The response is "'Most men I know would press on anyway, to prove their mastery.' Which is an odd sort of comment: there are few men in the court these days, and fewer still who would dare to cross the strange princess[pathetic][angry]. ".
 It directly-follows yes-3.
 It quip-supplies Snow White.

 yes-18 is an affirmative quip. The printed name is "say yes". The true-name is "yes-18". 
 Understand "is" as yes-18.  The comment is "'Some measure of trust — or, failing that, civility.' You have to trust someone soon, or you will go mad.".
 It mentions yourself.
 The response is "'Hm.' She gives this some thought, then laughs suddenly[friendly][scary]. 'I don't envy you, [vocative huntsman], having to choose between me and the Queen. At least she is not here, or it would truly be the devil and the deep blue sea for you.'".
 It directly-follows yes-3.
 It quip-supplies Snow White

Section 2 - Questions about objects

[We also want to make sure that the player can show or ask about any object in the game and get at least one response. This involves going through the World index and checking the various objects implemented there, then going back and making sure we have at least one quip apiece. ]

 why the chain chafes her is a questioning quip. 
 Understand "is" as why the chain chafes her. The comment is "'Why does the silver chain cut into your wrist so much? You have not been wearing it long.'".
 It mentions silver chain, Snow White's wrists.
 The response is "'No doubt because the Queen made it to be painful[pathetic].' She says this without defiance or self-pity.".
 It quip-supplies Snow White.
 It assumes silver-hurts.

 demo-lantern-2 is a demonstrative quip. It mentions the lantern. The true-name is "demo-lantern-2". The comment is "'Does the light bother you?' You try holding it up near her face[set body-context to Snow White].".
 The response is "She flinches away a little, but no more than you would yourself. 'As long as you do not burn me with it, no[pathetic].'".
 It quip-supplies Snow White.

 demo-sky-4 is a demonstrative quip. It mentions the sky. The true-name is "demo-sky-4". The comment is "'Can you make out any of the stars?'".
 The response is "She shakes her head. 'But I do not need them to know which way is north and which is south — and nor do you[friendly].' It's true; long practice in these woods has taught you where the moss grows and where the land rises.".
 It quip-supplies Snow White.

 whether her dress will be enough for her is a weak questioning quip. The comment is "'Will your dress be enough for you, where you are going? It is not what I would have chosen to work in.'".
 It mentions green dress.
 The response is "'I have other clothes there. But it hardly matters. What I wear is of little importance.'".
 It quip-supplies Snow White.

 what the embroidery on her dress signifies is a questioning quip. The comment is "'What does the embroidery on your dress mean?'".
 It mentions gold embroidery, green dress.
 The response is "[if the player knows snow-white-possessed]'It is a message,' she says. 'In memory of Adam[adam-named].'[otherwise]She looks down, startled. 'It is a love-message,' she says. 'To one I lost a long time ago. He belongs to another woman; but though he spoke her name aloud, his heart cried always for me[pathetic].' [paragraph break]Which is odd, considering she is hardly old enough for long-past love-affairs[conditionally queue am-I-beautiful].[end if]".
 It quip-supplies Snow White.
 It assumes written-embroidery.

 who the man was is a weak questioning quip. The comment is "'What man was that? Some prince who came to the court?'".
 It mentions lost man.
 The response is "'No.' She shakes her head as though recalling herself. 'This was before the court.'".
 It directly-follows what the embroidery on her dress signifies.
 It is negated by snow-white-possessed.
 It quip-supplies Snow White.
 It is listed.

 why her skin seems so fair is a weak questioning quip. 
 The printed name is "why her skin is so fair". The true-name is "why her skin seems so fair". 
 Understand "is" as why her skin seems so fair. The comment is "'How is that your skin is so fair? I have never seen another woman who looked so like porcelain.'".
 The response is "'[if the player knows snow-white-possessed]If you put a bright flame in a jar, the jar will look translucent[otherwise]I wash it every blue moon in the milk of a rooster[conditionally queue am-I-beautiful][end if].'".
 It mentions Snow White's skin.
 It quip-supplies Snow White.

what the symbols on the box indicate is a questioning quip.
	The comment is "'If the box is able to undo magic, what are the symbols for?'"
	It mentions symbols, heart-sized box.
	The response is "'They are symbols of interruption and uncreation[symbols-uncreate]. There are words for stilling life, and words for unraveling the spirit from the flesh[if the player recollects what the symbols mean again]. I spoke only a few of them when you asked, but even those were enough to freeze your blood in your veins. More would have killed you, and every creature that could hear[end if]. And yet under very rare circumstances they can be used to positive purpose[if the player knows Snow-white-possessed]. The Queen intended—' But here she stops and looks sly and finishes the sentence by a different road. '— to try to use something far beyond her own understanding[end if].'".
	It assumes sundering-method.
	It quip-supplies Snow White.



Section 3 - Lead in to Adam and Eve

am-I-beautiful is a q-transitional NPC-directed quip.	
 The response is "She cocks her head coquettishly. 'Do you find me beautiful?'".
	It quip-supplies Snow White.
	
Before discussing something which does not directly-follow am-I-beautiful when the previous quip is am-I-beautiful: [* We want to produce a comment *before* the player speaks his subject-changing line, so the rule for subject-changing isn't useful here; we just want a straight before.]
	say "You ignore the provocation. [run paragraph on]";

 no-17 is a negative quip. The printed name is "say no". The true-name is "no-17". The comment is "'Better for a man like me to admire what is closer to his own kind,' you say finally. She [italic type]is[roman type] beautiful, but there are as many perils in saying so as in denying it.".
 Understand "[maybe]" or "be evasive" or "avoid/evade her" or "avoid/evade answering" as no-17.
 It mentions Snow White's skin.
 The response is "Her eyes flash[set body-context to Snow White][angry]. 'Yes, keep to your own flesh. [eve-named]Eve from [adam-named]Adam's rib: much less frightening.'".
 It is negated by snow-white-possessed. [see no-38]
 It directly-follows am-I-beautiful.
 It quip-supplies Snow White. 

Does the player mean discussing why she mentions Adam:
	it is very likely. [* this quip will usually face off against "what she knows about Adam", and the latter is a more generic repeatable, so we want this first.]

 why she mentions Adam is a questioning quip. The comment is "'Why [if immediately]do[otherwise]did[end if] you mention Adam?'".
 It mentions Adam.
 The response is "'All men are like Adam.' She looks you over, then adds, '...though some less than others.'".
 It assumes adam-named.
 It is negated by misses-adam.
 It quip-supplies Snow White.

Does the player mean discussing why she mentions Eve:
	it is very likely. [* same reason as with the comment above..]

 why she mentions Eve is a questioning quip. The comment is "'Why [if immediately]do[otherwise]did[end if] you mention Eve?'".
 It mentions Eve.
 The response is "She turns restlessly. From the lantern, her shadow falls against the tree. For a moment you might almost fancy that the shadow had[winged-shadow] wings[scary]. [paragraph break]'I have always despised Eve,' she remarks. '[italic type]I[roman type] would have known what the snake was up to.'".
 It assumes eve-named.
 It quip-supplies Snow White.

why her shadow hath wings is a questioning quip. 
 The printed name is "why her shadow has wings". The true-name is "why her shadow hath wings". 
 Understand "has" as why her shadow hath wings. The comment is "'Your shadow has wings,' you observe[if immediately], as calmly as you are able[end if].".
 It mentions shadows, wings.
 The response is "'If you had eyes to see, you would need neither shadows nor reflections.' Nonetheless, her dress lies flat over her shoulder-blades, and you have never seen her fly.".
 It is negated by snow-white-possessed.
 It quip-supplies Snow White.
 It assumes winged-shadow.

 what she knows of the snake is a questioning quip. The comment is "'[if immediately]You have an[otherwise]You say you would have known what the snake was up to: do you have[end if] intimate knowledge of tempters and demons?' ".
 It mentions snake, demons.
 The response is "'More simply, I am not such a fool.' [for breath-gest]Her jaw tightens. [or eye-gest]She glares at you as though you were someone else, someone who disappointed and angered her in the past.[or walk-gest]She a few long, unlady-like strides as though her angry energy could be paced away. [end for] 'What a different race we could have made, [adam-named]Adam and I. Not so subservient, not so ignorant. Not so desperate always to be told what to do. [eve-named]Eve's children needed laws written on a tablet, and kings appointed to them, and scriptures, because they did not [italic type]dare[roman type] to be wise on their own.'".
 It indirectly-follows why she mentions eve.
 It quip-supplies Snow White.

 yes-16 is an affirmative quip. The printed name is "say yes". The true-name is "yes-16". The comment is "'You must know that you are beautiful. No man would think otherwise.'".
 It mentions Snow White's skin.
 The response is "[if the player knows snow-white-possessed]'You might be surprised,' she says. 'Some men see no beauty in what they cannot control.'[otherwise]She blinks quickly and turns away.[end if]".
 It directly-follows am-I-beautiful.
 It quip-supplies Snow White.

 whether her teeth seem normal is a questioning quip. 
 The printed name is "whether her teeth are normal". The true-name is "whether her teeth seem normal". 
 Understand "are" as whether her teeth seem normal. The comment is "'Show me your teeth,' you say.".
 It mentions Snow White's teeth.
 The response is "[if the player knows snow-white-possessed]She opens her mouth so that you can see the sharp even white teeth. 'They are ordinary enough,' she says. 'As you see.'[otherwise]She turns on you and bares her teeth. They are sharp and white and even, but they are not inhuman. Not exactly[conditionally queue am-I-beautiful].[end if]".
 It quip-supplies Snow White.

 why her hair seems so shiny is a weak questioning quip. 
 The printed name is "why her hair is so shiny". The true-name is "why her hair seems so shiny". 
 Understand "is" as why her hair seems so shiny. The comment is "'Why is your hair so shiny?'".
 It mentions Snow White's hair.
 The response is "'[if the player knows snow-white-possessed]Luck[conditionally queue am-I-beautiful][otherwise]This? It is mostly the princess[']; my hair was longer once[end if].'".
 It quip-supplies Snow White.

An availability rule for whether she seems Lilith:
	if the turn count is less than 2:
		it is off-limits;
	if the player knows lilith-named:
		it is off-limits;
	if the player's command does not include "Lilith":
		it is off-limits.
		
A plausibility rule for whether she seems Lilith:
	it is implausible.

 whether she seems Lilith is a questioning quip. 
 The printed name is "whether she is Lilith". The true-name is "whether she seems Lilith". 
 Understand "is" or "if" or "snow white" as whether she seems Lilith. The comment is "'Lilith.' The name is old, old, a legend of a legend, centuries dead, and yet you know it. 'You are Lilith.'".
 It mentions Lilith.
 The response is "Her eyes[set body-context to Snow White] meet yours. 'Trapped inside this naive little virgin, for the moment. Yes. Sometimes she speaks; sometimes I speak for her. I suffer and she whines. I remember and she fears. She is a daughter of [eve-named]Eve, to be sure[snow-white-possessed].' [paragraph break]She backs herself to where she can lean against a tree trunk, as though very tired. 'I did not think you would know me, however much I might try to tell you,' she says. 'Men— usually do not choose to recognize me[scary][friendly].'".
 It is negated by snow-white-possessed.
 It quip-supplies Snow White. [* With this quip, we allow the possibility of the player recognizing the character before her name has actually been mentioned.]

why she hath told me is a weak questioning quip. 
 The printed name is "why she has told you". The true-name is "why she hath told me". 
 Understand "has" or "you" as why she hath told me. The comment is "'I am surprised to hear you confess it.'".
 It mentions Lilith.
 The response is "She shrugs. 'You knew my name. It makes a difference.'".
 It directly-follows whether she seems Lilith.
 It quip-supplies Snow White.
 It is listed.

Understand "lilith" as Snow White when the player knows Snow-White-possessed.

 what Lilith wants is a questioning quip. The comment is "'What do you— Lilith— want?'".
 Understand "she" or "wishes" or "desires" as what Lilith wants.
 It mentions Lilith.
 The response is "'I kill boy-children[murderous-lilith]: did you know? I suck out their lives and leave them to die. It is punishment for all the children of my own that Adam stole from me, when he cast me out of Eden, when he rejected me and sent me away[scary].'".
 It quip-supplies Snow White.
 It assumes Snow-White-possessed.
 It is listed.

Rule for quip-introducing what Lilith wants again:
	say "[roman type][one of]But it[or]It still[stopping] does not seem to you that you have heard what she wants.";
	list no other quips.

 what Lilith wants again is a questioning quip. The comment is "'[if immediately]But is that what you want[otherwise]Is vengeance your only desire[end if]?'".
 Understand "she" or "wishes" or "desires" as what Lilith wants again.
 It mentions adam, Lilith.
 The response is "She looks up at you and her eyes[set body-context to Snow White] are filled with grief beyond tears. 'I want [italic type]Adam[adam-named][misses-adam][roman type],' she says. 'And he wanted me[queue want-an-equal as postponed obligatory]. When he had [eve-named]Eve, that docile witless blonde, he saw the bargain he had made and he wanted to take it back, and at night it was [italic type]my[roman type] body he dreamed and my name he called. But it was too late. And I was cast out into the storm and the desert and I had no equal on earth.' ".
 It indirectly-follows what Lilith wants.
 It quip-supplies Snow White.
 It is listed.

Before discussing something when the current quip is what Lilith wants again:
	if the noun does not indirectly-follow what Lilith wants again and the noun does not mention Adam:
		say "Out of respect, you give her a moment to collect herself after this outburst[pathetic]. She walks a few steps away from you, out of the circle of the lamplight, to gather her thoughts. When she turns back, she seems calmer and you suppose it safe to speak again.[paragraph break]";

whether that was Eve's fault is a questioning quip.  The comment is "'Surely it was not Eve's fault that she was created [if immediately]under such circumstances[otherwise]as a lesser replacement for Lilith[end if].'".
 It mentions Eve.
 The response is "'Oh, yes, there are many inferior things that creep and crawl on the earth, and it is not [italic type]their fault[roman type] that they should be so. But that does not make them any less inferior in fact.'".
 It indirectly-follows what Lilith wants again.
 It quip-supplies Snow White

what happened to Adam is a questioning quip. The comment is "[if immediately]'Forgive me, [otherwise]You gather the courage to go back to a difficult subject. 'Forgive me — perhaps I should not return to this — [end if]but surely Adam is long dead.'".
 It mentions Adam.
 The response is "She bows her head and her dark hair hides her cheek like a curtain of silk. There is a strange, horrible sound, like a wounded animal or the wail of rising wind. It does not come from her mouth: rather, she summoned it out of the woods. But it is no less a scream of grief for all that[pathetic]. [paragraph break]'Yes, he is dead,' she says. 'For he was made mortal, but I was not. That was his punishment and also mine. God —' She says the name in syllables you have not heard before but you know whose name it is— 'God is very efficient in his methods. [paragraph break]'But to what purpose did he make [italic type]me[roman type]? I have been a plague to men for generations, and for generations a giver of dangerous gifts. That is how the Queen knew me, and how she used me[queen-invoked-lilith]: merely a demon, a power for her witchcraft, to make her more beautiful than anyone else.'".
 It indirectly-follows what Lilith wants again.
 It quip-supplies Snow White.

want-an-equal is an NPC-directed quip. 
	The response is "'You ask what I want,' she says slowly. 'I want what Adam should have been, if he had chosen differently. I want a man who is my match, who is clever and private and wise; one who likes the wasteland and the night-time and the open sea rather than the daylight crowd of [eve-named]Eve's feckless brood. With such a man, I would be mortal, live my days, and die, not cursed but reconciled[seeks-equal].'

You yourself prefer the wasteland and the open places."
	It quip-supplies Snow White.
	
 whether you could be such a man is a questioning quip. 
 The printed name is "whether you could be such a man". The true-name is "whether you could be such a man". 
 Understand "I" or "prefer" or "and" or "the" or "wasteland" or "can" or "sea" or "open places/sea" as whether you could be such a man. The comment is "'[if immediately]I prefer the wasteland and the night-time[otherwise]What about me?' you say. 'Could I be what you seek? I prefer the wasteland[end if].'

Speaking this is like the moment a bit of metal, coming too close to a lodestone, snaps to its side[retract-anger]. 

She is no less terrifying, and yet you know her as clearly as you have ever known anyone. You also know yourself. You have no desires that you would need to hide from her; no secrets that she could not know; there is nothing in the beauty of the forest or in the joy of the bloody hunt that would shame you in her eyes. All the restlessness of your life, your failure to take a wife, your discomfort in company, your awkward otherness — all that is translated from disorder into function, as she sees it.".
 It mentions yourself and Lilith.
 The response is "She frowns, and her eyes search yours. 'You are no one. An ordinary man.'".
 It is restrictive.
 It indirectly-follows want-an-equal.
 It quip-supplies Snow White.

 that you once was King is an informative quip. 
 The printed name is "that you once were King". The true-name is "that you once was King". 
 Understand "is" or "were" or "I" or "was" as that you once was King. 
 Understand "I" as that you once was King.  The comment is "'I was once the King,' you tell her. 'I performed a blood-sundering in order to reconcile my kingdom with the dwarfs. The Queen's magic is nothing to that which I performed, but mine was an act of expiation.'"
The response is "Her mouth opens. You have surprised Lilith.

'I didn't see that,' she says. 'You didn't know until—' She frowns, and looks at the corpse. 'Yes, I see. And now instead of reversing the sundering, you are willing to come away with me? Snow White is your daughter after all, then — you are willing to leave her to her fate, and the Queen that was your wife, and your kingdom, being destroyed by the Queen's malicious magics, and simply go?'"
It assumes you-were-king.
It quip-supplies Snow White.
It directly-follows whether you could be such a man.
It is restrictive.

Instead of saying no when the current quip is that you once was king:
	say "You cannot; you have come too far to change your mind."

Instead of saying yes when the current quip is that you once was king:
	say "'Yes. There is nothing more I can do for the kingdom; I have offered it all I have. As bad as it is now, I think it would get worse if I were to revoke the sundering — if I could even find a way to do it.'

She takes up the rest of the explanation. 'And besides this, though you still sometimes desire the Queen, you do not feel as a husband feels towards her; and though you protect Snow White, you do not feel that you are her father; and you do not want to feel those things.'
	
You do not meet her glance, but you and she both know that she can see into your mind at times. 

'Your weakness before was that you were too gentle to your own people, and did not punish them sufficiently when they showed cruelty to the dwarrows, and the injustice led to war, and war to death — but a war that you were winning, nonetheless. You did not act to save your own; you acted to save the dwarrows when you saw what you had done to them.' She has crept closer to you as she spoke, her gentle voice laying open old wounds, and now she tucks her hand under your elbow and stands beside you in what is almost an embrace. 'I too was cut away from what was mine. I know.'";
	pause the game;
	say "As you look, Snow White becomes two women: one the princess, your daughter-that-was, who does not recognize you. The pink returns to her cheeks and she looks, again, like an ordinary girl.

The other is stranger, older, an archaic face, framed in a tangle of nut-brown hair, and as Snow White was ice, this woman is a creation of fire. She reaches out to you and takes your hand, and your fingers are shocked by the warmth.

'Go,' says Lilith to Snow White. 'Find your place among the dwarrow. In time — if you can remember — tell them what has happened. Fight the Queen together, and reclaim your kingdom.'

Snow White looks dazed — just a little girl now. 'I can't,' she says, in her smallest voice.

Lilith reaches out and presses her finger to the girl's forehead, and when she takes it away there is a fiery brand. 'You [italic type]will[roman type],' Lilith says. 'For the sake of this man, who was your father, and now can do nothing more for his kingdom. You will do the rest, so that his sacrifice will not have been in vain.'

Then Snow White straightens her back and takes the lantern — a fair bargain, you suppose, since you have Lilith instead — and she walks away into the forest to the north, never turning back her head. You feel the ember of something that might have been pride. But she is cut off from you and will never again be your own daughter.";
	pause the game;
	say "'The Queen was not a bad woman, before,' you say, remembering.

Lilith's mouth quirks. 'But she is unquestionably of the stock of Eve.' She looks at the tip of her finger, which appears a little numb. 'I will be your equal, if you will be mine.'";
	record "Joining Lilith as her consort, knowing you had been King" as an ending;
	end the game saying "You and Lilith are free".

Rule for quip-introducing point out that only ordinary men now populate the earth:
	say "[roman type]But only ordinary men now populate the earth.";
	list no other quips. 
	
whether even the King would suffice is a weak questioning quip.  The comment is "'You, Lilith, would find even the King ordinary.'".
 It mentions King.
 The response is "'Now, perhaps. I think he might once have been something more than he is[queue point out that only ordinary men now populate the earth as immediate optional].'".
 It is restrictive.
 It is negated by you-were-king.
 It directly-follows whether you could be such a man.
 It quip-supplies Snow White

 point out that only ordinary men now populate the earth is a q-transitional performative quip. The comment is "'There are only ordinary men left.'".
 It mentions yourself, Adam.
 The response is "She walks closer to you and she says a word. Whereupon you do not see the beautiful child princess, nor again a winged demon, but a woman with nut-brown hair and keen eyes[set body-context to Snow White]. Whether she is beautiful or not, you could not say. Her expression does not invite you to judge her in that way[scary][friendly]. 'It is a long time, a very very long time, since any man offered me what you offer, knowing what I am. But it is likely that you would disappoint me and that I would kill you, soon enough. Under such conditions, would you dare try?'".
 The uncertainty-refusal is "[one of]'This is no time for shrugs and sheepishness.'[or]'Yes or no. Have the courage to choose one.'[or]'How Evelike. Make up your mind.'[at random][conditionally queue no-27]".
 It directly-follows whether you could be such a man.
 It quip-supplies Snow White.
 It is restrictive.
 It is listed.

 no-27 is a negative quip. The printed name is "say no". The true-name is "no-27".  The comment is "'I confess you have made it seem less enticing. The being-killed portion, in particular.'".
 It mentions yourself.
 The response is "'I expected nothing else,' she admits. 'It may be that the only way for me is to forget myself and become like one of Eve's daughters after all. How that outcome would please Him.' She half-shrugs. 'Well, then, [vocative huntsman], it is once again up to you to choose where we go from here.'".
 It directly-follows point out that only ordinary men now populate the earth.
 It quip-supplies Snow White.
	
 yes-17 is an affirmative quip. The printed name is "say yes". The true-name is "yes-17". The comment is "'Perhaps Adam also feared you,' you say. How strange that Lilith should appear to you vulnerable and dear. 'He acted in that fear. I choose not to. If you would have accepted his choice of you, then accept mine: this is what I want.'".
 It mentions Adam, Lilith.
 The response is "She considers you for a long time, as though she is rummaging in your thoughts and testing them for adequacy. 'Clever and wise,' she requested[retract-anger]; can you live up to that? A demon with many thousand years['] experience might be seeking more knowledge and wisdom than you can provide. [paragraph break]'Untie me,' she says finally. 'Let me let the princess['] body go.'".
The nag is "[one of]'Well? Take off the chain.'[angry][or]She turns to give you better access so that you can remove the chain.[angry][or]Sensing your hesitation, she remarks, 'It is too late now to change your mind. But I won't hurt you more than the Queen would.'[angry][or]You begin to hate yourself for your cowardice.[angry][or][angry][fail freedom][stopping]".
 It directly-follows whether you could be such a man, point out that only ordinary men now populate the earth.
 It quip-supplies Snow White.
 It is restrictive.

To say retract-anger:
	if anger is greater than 0:
		change anger to 0.

To say fail freedom:
	say "When you still do not untie Lilith, she sighs. 'So not unlike Adam after all, it seems,' she says: and you know that the moment is lost. There will never be an agreement between you; you will always be alien to one another in kind.

'Come to the haven,' she says at last. 'I will see that the dwarrows give you food and then set you on your way. You're not likely to survive if you return to the Queen. And I find I can spare you that much pity.'";
	record "Promising Lilith more than you could give" as an ending;
	end the game saying "You have failed to be other than ordinary after all."

Instead of saying no when the current quip is yes-17:
	say "You have come too far on this path to refuse her."
	
Instead of saying yes when the current quip is yes-17:
	try freeing Snow White.
	
Instead of freeing Snow White when the current quip is yes-17 or the current quip is point out that only ordinary men now populate the earth:
	say "With trembling fingers you unwind the silver chain.
	
She brings her hands around and chafes her wrists. 'There is a pain between my shoulder blades as well,' she remarks. 'That is a cruel way to bind a person for long— but I suppose it will be Snow White's discomfort alone.'

Then she blurs, and steps beside herself, so that you are faced with two women. One is the princess (still beautiful but less fey, with roses in her cheeks and lips such as any girl might have). The other is Lilith of the nut brown hair, and her shadow-wings envelop her so that she is not precisely naked.

Lilith turns (gracefully, as a flame is lithe) to the princess. 'You will have to make the rest of the journey alone,' she says, neither kindly nor unkindly.

The princess nods. She looks numb with shock.

Then Lilith holds out her hand to you. 'Come with me,' she says. 'I have a cavern in the dunes on the far side of the world, and there we will rest and speak and consider; and after sunset the air will be so warm that we can lie outside unclothed. Then I will tell you all the names of the stars in that part of the sky, and how they used to look when creation was new.'";
	if the player knows hart-pact:
		say "So you leave behind the hart's corpse, and the promise you made to that spirit, and the promise you made to Snow White, and the promise you made to the Queen: all those promises were made in ignorance.";
	record "Joining LIlith as her consort" as an ending;
	end the game saying "You are Lilith's consort now."

Instead of kissing Snow White when the player knows seeks-equal:
	say "[one of]You make half a move to do it, and know it's the [wrong-to-kiss]wrong thing[or]No, you must not[stopping]. An [italic type]equal[roman type], she said. It is doubtful — the longest of long shots — that this demon as old as the dawn of Eden would accept you as a consort; but if she would, you must not begin while she is tied and trapped and unable to resist."
	
Instead of hugging Snow White when the player knows seeks-equal:
	if the player recollects whether you could be such a man:
		say "You are past the point for physical gestures." instead;
	if the player knows wrong-to-kiss:
		say "You compromise on this, then, if you ought not kiss her. ";
	if the player carries something:
		now everything carried by the player is in the location;
		say "Setting down all you carry, you";
	otherwise:
		say "You";
	say " wrap your arms around her. Her body is just as small and delicately made as ever, and she nestles against you[friendly]. ";
	try discussing whether you could be such a man.
	
	
Section 4 - Snow White's Reactions to Other Actions
	
[Next: some reactions to various events that happen in front of Snow White.]

watching-box-opened is an NPC-directed quip.
	The response is "She angles her head so that she can watch you without seeming to. It isn't as stealthy as she might have hoped[pathetic]."
	It quip-supplies Snow White.

watching-inventory is an NPC-directed quip.
	The response is "'Lose anything on the way?' Her expression is unpleasantly amused."
	It quip-supplies Snow White.
	
 no-26 is a weak negative quip. The printed name is "say no". The true-name is "no-26".  The comment is "'No.' What good would it do to fence with her?".
 It mentions yourself.
 The response is "'Good. We would not want to have to go back.'".
 It directly-follows watching-inventory.
 It quip-supplies Snow White.

Instead of saying yes when the current quip is watching-inventory:
	say "But you do have all your possessions, and surely there is nothing to be gained by telling her such an easily-overturned lie. Better save your strength for more complicated acts of deception, should any such be required."

no-rape is an NPC-directed quip.
	The response is "'I didn't read you for the sort to rape a girl half your age, in the middle of the woods,' she says."
	It quip-supplies Snow White.
	
pleased-watching is an NPC-directed quip.
	It mentions the heart-sized box, symbols.
	The response is "Snow White watches the way a child watches fresh cakes [scary]being taken from the oven." [* This comment is triggered when you put the heart into the box. This is part of an ongoing effort to make Snow White more responsive to what the player does, not just to conversation itself; and it makes sense to focus especially on the core set of actions that the player is likely to have to follow given the prompting of the game (namely, cutting out the heart, storing it, and preparing to move again).]
	It quip-supplies Snow White.
	
whether she seems hungry is a questioning quip. 
 The printed name is "whether she is hungry". The true-name is "whether she seems hungry". 
 Understand "is" or "food" or "hunger" or "cake" or "cakes" or "delicious" or "watching" as whether she seems hungry.  The comment is "'Hungry?' you ask pointedly.".
 It mentions Snow White, hart's heart, heart-sized box.
 The response is "[if the player knows snow-white-possessed]'Snow White is, to be sure. [otherwise]'[end if]Aren't you?' she replies, matter-of-fact. And truth to tell, you are: the deer's flesh is fresh enough that it would make a good stew — if you were at home — if your home were intact[cabin-destroyed].".
 It directly-follows pleased-watching.
 It quip-supplies Snow White.

 yes-19 is an affirmative quip. The printed name is "say yes". The true-name is "yes-19".  The comment is "'I would not refuse a good rich stew, with plenty of meat and plenty of vegetables.' Your mind quickly sketches in the rest of this appealing image: mug of ale, crust of bread, warm fire, skin to throw over your lap...".
 The response is "She shrugs — a companionable, resigned shrug, as though to ask[friendly][pathetic], What can you do?".
 It directly-follows whether she seems hungry.
 It quip-supplies Snow White.

 no-19 is a negative quip. The printed name is "say no". The true-name is "no-19".  The comment is "'No.' A lie, but it never helps to dwell on hunger or cold or anything else that you can't remedy at the moment.".
 It mentions yourself.
 The response is "'I feel the same.'".
 It directly-follows whether she seems hungry.
 It quip-supplies Snow White.

giving-up-question is an NPC-directed quip. [* This is a response queued if you don't answer the question about killing the queen for enough turns]
	The response is "'I might have known you would not answer.'"
	It quip-supplies Snow White.

yes-20 is an affirmative quip. The printed name is "say yes". The true-name is "yes-20".  The comment is "'I can hardly say that I plan to kill my own sovereign,' you point out. 'Be reasonable.'".
 It mentions yourself, magic mirror.
 The response is "'The magic mirror doesn't see everything,' she replies scornfully[angry]. 'In fact, its knowledge is really very limited.'".
 It directly-follows giving-up-question.
 It quip-supplies Snow White.

apparently-you-would is an NPC-directed quip. [* This is the response queued if you don't say anything for a while in response to her "you wouldn't let me stay silent" challenge.]
	It mentions yourself.
	The response is "'Then again,' she says, bemused, 'perhaps you have more of a taste for silence than I had credited you with.'"
	It quip-supplies Snow White. 


 that you plan to rescue the king is an informative quip. 
 The printed name is "that you still plan to rescue the king". The true-name is "that you plan to rescue the king".
 Understand "is" or "I" or "we" or "still" as that you plan to rescue the king.  The comment is "'Treachery or not, it is time that the King returned home,' you say. 'Even if he is blood-sundered there may be a way to restore him.'".
 It mentions King.
 The response is "'I admire your valiant spirit,' she says[friendly]. 'But I think you will fail.'".
 It indirectly-follows it was not treachery.
 It assumes blood-sundering, snow-white-knows-sundering.
 It quip-supplies Snow White.
 It is listed.

 that you still plan to rescue the king is an informative quip. 
 Understand "is" or "I" or "we" as that you still plan to rescue the king.  The comment is "'Treachery or not, it is time that the King returned home,' you say[if the player knows you-were-king]. Assuming, of course, that you can accomplish your own return.[otherwise if the player knows blood-sundering]. Not that that will be an easy matter if you also need to rescue him from thinking he is someone else.[otherwise]. 'We mean to bring him back.'[end if]".
 It mentions King.
 The response is "'I wish you luck in that[friendly],' she says — though it is obvious that she does not expect you to have much.".
 It is negated by snow-white-knows-sundering. [* this an the alternate version for if the player does not know]
 It indirectly-follows it was not treachery.
 It quip-supplies Snow White.
 It is listed. 

Instead of smiling when the current quip is that you still plan to rescue the king or the current quip is that you plan to rescue the king:
	say "You smile without humor, as though to say: we will see."

thought-not is an NPC-directed quip. [* Queued to take us out of the question about whether she's allowed preferences].
	The response is "'As I thought,' she says, taking your silence for some kind of surrender."
	It quip-supplies Snow White.

weird-laugh is a q-transitional NPC-directed quip. [* Queued if you don't answer about why Lilith should be vampiric]
	The response is "She laughs mirthlessly, and under her breath she says something to herself."
	It quip-supplies Snow White.
	
do-not-try is an NPC-directed quip. [* Queued if you cut some of her hair off]
	The response is "'Do not try that again,' she says."
	It quip-supplies Snow White.
	
Instead of saying sorry when apologize is flagged-ready:
	try discussing apologize.

apologize is a performative quip. The comment is "'I did not mean to offend,' you say. 'I don't know what came over me.'".
 Understand "to Snow White" as apologize.
 It mentions Snow White's hair.
 The response is "'I do.' Her expression is cold[angry]. 'Do not do it again. I am not your possession.'".
 It directly-follows do-not-try.
 It quip-supplies Snow White

no-burning-again is an NPC-directed quip.
	The response is "'I could put your lantern out[if the player knows magic-revealed] with a word[otherwise] if I wanted[end if],' she says. 'You may fear the dark, but I have no reason to.'"
	It quip-supplies Snow White.
	

After examining something which is part of the corpse when the player does not recollect are-you-ogling and at least four examined things are part of the corpse:
	casually queue nice-corpse.
	
nice-corpse is an NPC-directed quip.
	The response is "She sighs impatiently. 'I am sure it is a very nice dead animal and excites a great deal of professional interest, but perhaps you could finish your examination another day?'".
	It quip-supplies Snow White.
	

After examining something which is part of Snow White when the player does not recollect are-you-ogling and at least four examined things are part of Snow White:
	casually queue are-you-ogling.
	
are-you-ogling is an NPC-directed quip.
	The response is "'Did you bring me out here so you could leer at me in private?'[conditionally queue am-I-beautiful]".
	It is restrictive.
	It quip-supplies Snow White.
	
 no-22 is a negative quip. The printed name is "say no". The true-name is "no-22".  The comment is "'No.'".
 It mentions yourself.
 The response is "She [for eye-gest]looks at you just as piercingly as you have looked at her[or walk-gest]stands deliberately too close[or breath-gest]exhales slowly[end for]. Then she repeats: '...no.'".
 It directly-follows are-you-ogling.
 It is negated by snow-white-possessed.
 It quip-supplies Snow White.

 yes-24 is an affirmative quip. The printed name is "say yes". The true-name is "yes-24".  The comment is "'Actually, yes, that was my purpose.' You say it to mock but it comes out with an unwelcome undercurrent of sincerity and [italic type]what is going wrong with you[roman type]?".
 It mentions yourself.
 The response is "'I've seen you,' she says cryptically[queue maybe-2]. 'All along.'".
 It directly-follows are-you-ogling.
 It is negated by snow-white-possessed.
 It quip-supplies Snow White.

 maybe-2 is a performative quip. The comment is "'Maybe.'". The printed name is "maybe".
 Understand "[maybe]" as maybe-2.
 It mentions Snow White.
 The response is "That cold flesh is not entirely appealing. But if she were to thaw? And speak to you all the thoughts that she only hints at? And be whatever she is, under the snowy mantle? Then...

She finishes your thought. 'You might still dislike me.'".
 It directly-follows are-you-ogling.
 It is negated by snow-white-possessed.
 It quip-supplies Snow White.

no-39 is a negative quip. The printed name is "say no". The true-name is "no-39".  The comment is "'No. The princess is only a child. Beautiful, but very young.'".
 It mentions yourself.
 The response is "She considers you archly. 'She has not been acting young of late,' she says. 'I am old. You may have forgotten how young she really is.' [paragraph break]This comes uncomfortably close to truth, but there are many half-formed thoughts you have had of late that have needed ignoring.".
 It directly-follows are-you-ogling.
 It assumes snow-white-possessed.
 It quip-supplies Snow White.

 yes-35 is an affirmative quip. The printed name is "say yes". The true-name is "yes-35".  The comment is "'Actually, yes, that was my purpose.' You say it to mock but it comes out with an unwelcome undercurrent of sincerity.".
 It mentions yourself.
 The response is "'You're confused,' she says. 'I have confused you.' The thought seems to please her. 'You are drawn by my personality, though it is in the body of a young girl.'".
 It directly-follows are-you-ogling.
 It assumes snow-white-possessed.
 It quip-supplies Snow White.

 maybe-3 is a weak performative quip. The comment is "'Maybe.' Even thinking in these directions is the way to madness.". The printed name is "maybe".
 Understand "[maybe]" as maybe-3.
 It mentions Snow White.
 The response is "'Ah.'".
 It directly-follows are-you-ogling.
 It assumes snow-white-possessed.
 It quip-supplies Snow White.



 no-28 is a weak negative quip. The printed name is "say no". The true-name is "no-28".  The comment is "'If it would save my own life, I could.' Though of course if you disposed of the Queen, there is always the chance that her guards — or her magic — would take vengeance for her.".
 It mentions yourself.
 The response is "'I am not sure I believe it.'".
 It directly-follows yes-2.
 It quip-supplies Snow White


smirk is a q-transitional NPC-directed quip.
	The response is "She smirks. 'Save it for the barmaids.'"
	It quip-supplies Snow White.

something-in-your-eye is an NPC-directed quip.
	The response is "'Something in your eye?' she asks."
	It quip-supplies Snow White.

 no-29 is a weak negative quip. The printed name is "say no". The true-name is "no-29".  The comment is "'No.'".
 It mentions your eyes.
 The response is "'Fortunate. Though at least you are free to rub your eyes should you wish to do so.'".
 It directly-follows something-in-your-eye.
 It quip-supplies Snow White.


funny-memory is an NPC-directed quip.
	The response is "She observes you dispassionately. '[if scariness is less than 0]I am glad you are able to find something amusing in our situation. Or do [end if]I assume we are remembering a ribald anecdote we heard at the local hostelry?'".
	It quip-supplies Snow White.

 yes-27 is a weak affirmative quip. The printed name is "say yes". The true-name is "yes-27".  The comment is "'As a matter of fact, yes. Fat Jacob has a story about a whore with a glass eye—'".
 It mentions ribald anecdote. 
The response is "'Yes, that story. It's older than he is, you know.'".
 It directly-follows funny-memory.
 It quip-supplies Snow White.

 no-31 is a weak negative quip. The printed name is "say no". The true-name is "no-31".  The comment is "'No, something much funnier.'".
 It mentions ribald anecdote.
 The response is "'Congratulations.'".
 It directly-follows funny-memory.
 It quip-supplies Snow White.

 no-32 is a weak negative quip. The printed name is "say no". The true-name is "no-32".  The comment is "'No, you must be thinking of a different story. This one is about what happened to him when he was a young man and went to a neighboring town to sell some ale for his uncle.'".
 It mentions ribald anecdote.
 The response is "'How sweet of you to believe him.'".
 It directly-follows yes-27.
 It quip-supplies Snow White

yes-28 is a weak affirmative quip. The printed name is "say yes". The true-name is "yes-28".  The comment is "'I did suspect the story might not have been original with him. I doubt even in his youth Fat Jacob had the stamina, for one thing.'".
 It mentions ribald anecdote.
 The response is "'Indeed[friendly].'".
 It directly-follows yes-27.
 It quip-supplies Snow White

could-be is a weak performative quip. The comment is "'That could be. It would not surprise me.'".
 Understand "[maybe]" as could be. The printed name is "say possibly". The true-name is "could-be".
 It mentions ribald anecdote.
 The response is "'He does no harm, I suppose,' she says, thawing a little[friendly]. 'Every village needs its storyteller, and there is a long tradition in telling stories you poached from someone else.'".
 It directly-follows yes-27.
 It quip-supplies Snow White

[And just in case she sees you using the box:]

really-you is an NPC-directed quip.
	The response is "She raises her eyebrows as you do this. 'You are trying it on yourself? There are not many such boxes in the world.'"
	
how-strange is an NPC-directed quip.
	The response is "'That box is not to be trifled with,' she remarks coolly. 'I hope you do not do anything that proves dangerous.'"

yes-33 is an affirmative quip. The printed name is "say yes". The true-name is "yes-33".  The comment is "'Yes, that is my hope as well.'".
 It mentions yourself.
 The response is "She steps carefully backward as though the magic might leap out of the box at her. 'The cautious live longest,' she remarks.".
 It directly-follows how-strange.
 It quip-supplies Snow White

Section 5 - Hart's Responses to Objects

demo-hart's heart-4 is a demonstrative quip. It mentions the hart's heart. The true-name is "demo-hart's heart-4". The comment is "You extend the heart for its former owner to view.".
 The response is "'What are you doing?' [the corpse] asks. 'Making fun of me? Put that thing away. You wouldn't like it if someone had your liver in his pocket, would you?'".
 It quip-supplies the corpse.

 demo-dagger-5 is a demonstrative quip. It mentions the dagger. The true-name is "demo-dagger-5". The comment is "'I have this,' you say, showing the dagger to the hart.".
 The response is "'I'm familiar enough with that, thanks,' [the corpse] responds. ".
 It quip-supplies the corpse.

 demo-trees-6 is a demonstrative quip. It mentions the trees. The true-name is "demo-trees-6". The comment is "'I suppose you're very familiar with this forest.'".
 The response is "'I thought so when I was alive,' [the corpse] replies. 'Being dead provides a new perspective.'".
 It quip-supplies the corpse.

 demo-lantern-7 is a demonstrative quip. It mentions the lantern. The true-name is "demo-lantern-7". The comment is "You hold up the lantern so that the hart can see it.".
 The response is "'Yes, very nice,' he says. '[if burn-box]I'm hoping you'll use that on your box there[otherwise]I'm sure it will prove handy.'".
 It quip-supplies the corpse.

 demo-undergrowth-8 is a demonstrative quip. It mentions the undergrowth. The true-name is "demo-undergrowth-8". The comment is "'Is there anything I should know about the undergrowth? Sometimes things move around in the dark.'".
 The response is "'Just small animals,' he says. 'These woods are inhabited by ordinary creatures, as well as the unusual ones.'".
 It quip-supplies the corpse.

 demo-sky-9 is a demonstrative quip. It mentions the sky. The true-name is "demo-sky-9". The comment is "'There's no moon or stars,' you point out, gesturing upwards.".
 The response is "'You'll have to rely on your own sources of light,' [the corpse] responds. 'It will be light in a few hours. There is nothing untoward about this darkness; it is ordinary.'".
 It quip-supplies the corpse.

 demo-roots-10 is a demonstrative quip. It mentions the roots. The true-name is "demo-roots-10". The comment is "'I never noticed how many roots and knots there were in the ground until Snow White made a point of tripping over every single one,' you gripe.".
 The response is "'She has a reason,' says [the corpse]. 'She usually has a reason for what she does.'".
 It quip-supplies the corpse

Section 6 - More Conversation with Happy

not-talking-now is an NPC-directed quip.
	It mentions Snow White.
	The response is "'She won't be contributing to our discussion,' says [the corpse]. 'But I think we can do without her input.'"

what he knows about me is a questioning quip. The comment is "'Do you have any words of wisdom about me? Since you know everything, I mean. Something that I should be aware of.'".
 It mentions yourself.
 The response is "'[if the player knows you-were-king]I think you already know more than is good for you[otherwise]I think you know exactly as much as you should be aware of[end if],' he says exactly.".
 It quip-supplies corpse.
 It assumes will-burn-box.
 It is listed.

what he knows of Lilith is a questioning quip. The comment is "'Do you know about a woman named Lilith?'".
 It mentions Lilith.
 The response is "[The corpse] looks at you keenly. 'That is the name of a demon,' he says. 'Those that are on first-name terms with her should be carefully avoided, and that is the best advice you could possibly have on the topic.'".
 It is negated by snow-white-possessed.
 It quip-supplies corpse.
 It assumes will-burn-box, lilith-named.

what Lilith does is a questioning quip. The comment is "'What does [if immediately]she[otherwise]Lilith[end if] do?'".
 It mentions demons, Lilith.
 The response is "'Whatever she wishes,' he says. 'Listen, there are things that you could know that would not be any favor to you. We made an agreement that you could ask for help.' He points at Snow White with his antlers. 'She's your immediate problem, friend.'".
 It indirectly-follows what he knows of Lilith.
 It quip-supplies corpse.

what to do about Snow White plus Lilith is a performative quip.
 The printed name is "what to do about Snow White and Lilith". The true-name is "what to do about Snow White plus Lilith". 
 Understand "and" as what to do about Snow White plus Lilith.  The comment is "'Snow White seems to be possessed,' you say. 'By a demoness.'".
 It mentions Lilith, Snow White, demons.
 The response is "'I know,' he says. 'I wish you didn't, though.' He trots in a neat circle. 'It will only complicate things, and really the situation is very simple. Snow White there? Not innocent. Not as you see her now. The possession has made her a vampire[vampirism-revealed]. She will kill to protect herself; she is dangerous. If you are clever, you'll do what I've told you to do — burning the box with the heart inside will set me free, and it will grant you protections, too.'".
 It quip-supplies corpse.
 It assumes  lilith-named, snow-white-possessed.

 why Grumpy seems such a character is a questioning quip.
 The printed name is "why Grumpy is such a character". The true-name is "why Grumpy seems such a character". 
 Understand "is" as why Grumpy seems such a character.  The comment is "'Grumpy doesn't sound like a very pleasant type.'".
 It mentions Grumpy.
 The response is "'He had a deprived childhood,' [the corpse] says. 'Always wound up with a hand-me-down pick.'".
 It indirectly-follows what the dirt was for.
 It quip-supplies corpse.

other-names is a privately-named questioning quip.  The comment is "'[if the previous quip is other-names][one of]Or[or]What about[or]How about[at random][otherwise][one of]Do you have any relations called[or]Or do you know a[stopping][end if] ['][guessed-nickname][']?'". The printed name is "whether he has a relation called [guessed-nickname]". The true-name is "other-names". [* we only want the player to trigger this if he types something like ASK ABOUT SNEEZY or ASK THE HART ABOUT THORIN — otherwise we won't have a nickname ready to swap in to the question and it will all go wrong.]
 It mentions dwarf-cousins.
 The response is "[one of]'Of course not,' he says. 'What kind of fool name is that?' And he mutters to himself for a little while about the stupidity of humans, and their ignorance about all other kinds of creature.[or][one of]'No.'[or](Bored): 'Absolutely not.'[or]'Nope.'[or]'Once more, No.'[or]'Is this game fun for you? Because the answer is still no.'[at random][stopping]".
 It is repeatable.
 It assumes happy-name-known.
 It quip-supplies corpse.

To say guessed-nickname:
	let N be indexed text;
	change N to "[player's command]";
	change N to "[N in title case]"; [* This adjusts the caps on the player's question even if it's typed in all lower-case or all-caps]
	let W be the number of words in N;
	say "[word number W in N]".
	
[And... Rob suggested that one other interesting outcome of the game would be to have the soul of Happy go off to inhabit the mirror, instead of its current bitter occupant. So that's going to need a bunch of additional conversation, first to introduce information about the mirror if the player hasn't already learned it from Snow White, and then to raise the possibility of replacing the soul.] 

whether the Queen seems dangerous is a questioning quip.
 The printed name is " ". The true-name is "whether the Queen seems dangerous". 
 Understand "is" as  whether the Queen seems dangerous.  The comment is "'Is the Queen a danger to me?'".
 It mentions Queen, witchcraft, magic mirror.
 The response is "'You know she is,' [the corpse] responds. 'She is a witch — and that magic mirror is dreadfully powerful. She made that, you know[queen-made-mirror] — placed the soul of one of her waiting women into it[servant-used], and now she goes on listening to the lady's insane bitter advice[bad-soul-in-mirror]. Things might have gone better if she'd got some good soul into the mirror instead... but it was to be expected, when all that she does, she does with the help of demons.'".
 It is negated by bad-soul-in-mirror.
 It quip-supplies corpse.
 It assumes will-burn-box.
 It is listed.

what would happen if the mirror contained a better soul is a questioning quip. The comment is "'What would happen if the mirror contained a good soul?'".
 It mentions magic mirror, souls.
 The response is "'Its advice would be better; its view of the world would be less warped. The waiting woman trapped there now... well, she's always thought of people as her enemies. Everything anyone does, she views through a distortion of jealousy and contempt. I doubt the Queen herself understands how much she's been changed by looking into that mirror so often.'".
 It indirectly-follows whether the Queen seems dangerous.
 It quip-supplies corpse.
 It assumes bad-soul-in-mirror, queen-made-mirror.
 It is listed.

whether a good soul could be put into the mirror is a questioning quip. The comment is "'Would it be possible to... repair the mirror?' you suggest. You only half understand these matters, but he might. 'Could we put a different soul into it?'".
Understand "can" as whether a good soul could be put into the mirror.
 It mentions magic mirror, souls.
 The response is "A keen light comes into his eyes[set body-context to the corpse]. 'For instance, mine?' he suggests.".
 The uncertainty-refusal is "[one of]'Come, man, I'm making you a sincere offer here,' he says. 'Give it a little thought.'[or]'No good.'[stopping]".
 It is restrictive.
 It indirectly-follows what would happen if the mirror contained a better soul.
 It quip-supplies corpse.
 It is listed.

 yes-5 is an affirmative quip. The printed name is "say yes". The true-name is "yes-5".  The comment is "'Certainly — why not? Your life was cut short, and this body does not suit you...'".
 It mentions corpse, magic mirror, souls.
 The response is "'It could be done,' he says. 'Best to do it without the Queen's knowledge, though.' He stamps his back leg, relishing the idea of tricking the Queen. 'Burn the box as we agreed, so that I am free of this hart, but then return to the castle with the ashes. The Queen will go before the mirror to consult about whether the ashes you brought are truly the ashes of Snow White's heart[take-ashes]. And then I believe I could perform the exchange... but it must be quickly done, before I fade from this world and into the next.'".
 It directly-follows whether a good soul could be put into the mirror.
 It quip-supplies corpse.

yes-25 is an affirmative quip. The printed name is "say yes". The true-name is "yes-25".  The comment is "'I will do it.' It could hardly make matters worse, after all.".
 The response is "His eyes[set body-context to the corpse] gleam in triumph. He bows his antlers.".
 It directly-follows yes-5.
 It quip-supplies corpse.

 no-23 is a negative quip. The printed name is "say no". The true-name is "no-23".  The comment is "'I...' You flounder. 'I wouldn't want to burden someone like you with the Queen's presence, day in and out.' Not to mention that it would be a strange matter to have your kingdom effectively ruled by a dwarf.". 
 It mentions magic mirror.
 The response is "He looks wounded. 'I do not offer for my own advantage! But very well — go and try to restore your kingdom on your own, in the best way you're able. I wish you well.'".
 It directly-follows whether a good soul could be put into the mirror.
 It quip-supplies corpse.


who exactly was Lilith again is a questioning quip. The printed name is "who Lilith was". The true-name is "who exactly was Lilith again". The comment is "'Do you know anything about Lilith?'".
 It mentions Lilith.
 The response is "'Adam's first wife[lilith-first-wife]?' He looks very wary. 'There are things from the world's beginning that it is best not to pry into. That is one such.'".
 It quip-supplies corpse.
 It assumes lilith-named.

whether he ever met Saint Eustace is a privately-named questioning quip.[* This is privately-named because we don't want this completely implausible quip to fire *unless* the player specifically mentions the saint in question] The comment is "'Say, did you meet Saint Eustace?'".
 Understand "st/saint" or "placidus" or "eustace/Eustachius/Eustathius" or "whether he ever met saint eustace" as whether he ever met Saint Eustace.
 The response is "'Ah, the Saint that had a vision of the Lord as glimpsed between the antlers of a stag.' He fixes you with an unsympathetic glare. 'An event that plainly occurred many centuries ago, as even you must know. So I am forced to conclude that you are toying with me.' [paragraph break]Yes, well, but what does he expect? None of the polite conversation topics you learned at the maternal knee seem precisely appropriate to the current circumstances.".
 It quip-supplies corpse. [* This is a total easter egg, courtesy of Sam Kabo Ashwell. Beta-testers will try all sorts of stuff. See http://en.wikipedia.org/wiki/Saint_Eustace_(legend) for an explanation and also a wacky picture of Saint Eustace mid-vision.]

[if the player types "st. eustace", this is read as the end of one command and the beginning of another. There is a canonical way to fix this using Punctuation Removal, but I am not going to use it right now because I am worried about the interactions with other "reading a command" rules. So instead I have juryrigged a crazy solution which may not work.]

Understand "eustace" or "Eustachius" or "Eustathius" as a mistake ("It is unlikely St. Eustace would help you now.").

how the juggling business went for him is a questioning quip.  The comment is "'[if immediately]Juggling, eh[otherwise]So you were a juggler, eh[end if]? How was that, a good living?'".
 It mentions corpse.
 The response is "He gives you the look best approximating sarcasm as rendered in cervid features. 'I had just worked out a rather inventive thing to do with the seventh flaming knife,' he says. 'But I doubt very much that you are actually interested in fair-business when you have other troubles to attend to.'".
 It indirectly-follows what his name seems.
 It quip-supplies corpse.

 no-33 is a negative quip. The printed name is "say no". The true-name is "no-33".  The comment is "'That is true. But since we'd recently been introduced, it seemed mannerly to take an interest.'".
 It mentions corpse.
 The response is "'I hope you don't mind if I don't reciprocate by asking your reminiscences of being a huntsman. I can recollect only too clearly my host's memories of being on the receiving end.'".
 It directly-follows how the juggling business went for him.
 It quip-supplies corpse.

 yes-29 is an affirmative quip. The printed name is "say yes". The true-name is "yes-29".  The comment is "'I am always interested in the ways of people unlike myself,' you say piously. This is a lie. But it is accurate to say that you have been trying to cultivate this interest: it is easy to fall into unhealthy patterns of thought about the dwarrows, even though the war is now over.".
 It mentions dwarrows.
 The response is "He coughs in debelief.".
 It directly-follows how the juggling business went for him.
 It quip-supplies corpse.

 waffle is a performative quip. The comment is "'Well, er — that is, it's true I have other things I ought to be doing, but I wouldn't want to seem impolite, and in any case I have thought recently that I ought to try to learn more about people who are unlike myself...'".
Understand "[maybe]" as waffle.
 It mentions dwarrows.
 The response is "He rolls his eyes. 'It's of no import now anyway,' he says. 'These hooves aren't made for juggling.'".
 It directly-follows how the juggling business went for him.
 It quip-supplies corpse.

 what exactly he hath done to time is a questioning quip. 
 The printed name is "what exactly he has done to time". The true-name is "what exactly he hath done to time". 
 Understand "has" as what exactly he hath done to time.  The comment is "'What exactly have you done to time?' you ask. 'It doesn't seem to be... going.'".
 It mentions night, witchcraft.
 The response is "'That,' he says, in a hollowly portentous tone of voice, 'is the sort of secret that can never be properly explained to someone who has not passed through the veil. Mortals do not understand eternity.'".
 It quip-supplies corpse.

why the lantern does not burn is a questioning quip. The comment is "'The lantern doesn't seem to be burning properly.'".
 It mentions night, lantern, flame.
 The response is "'No. It will resume — flickering, consuming fuel, being able to ignite other objects — once I release the grip of my magic and return you to the normal flow of time.'".
 It quip-supplies corpse

 what seems really at the safe haven is a questioning quip. 
 The printed name is "what is really at the safe haven". The true-name is "what seems really at the safe haven". 
 Understand "is" as what seems really at the safe haven.  The comment is "'What is at the safe haven, then?'".
 It mentions safe haven.
 The response is "'Seven dwarrow servants; food supplies, and other things that Snow White may need; enough to last through the winter, and (she hopes) to go unnoticed by the Queen. It is not a place that you would find comfortable or welcoming — but that is part of its purpose.'".
 It quip-supplies corpse.

what he means by other things is a questioning quip. The comment is "'[if immediately]What do you mean, [otherwise]What did you mean when you said the haven contained [end if][']other things[']?'".
 It mentions safe haven.
 The response is "[if the player knows vampirism-revealed]'Those arcane items that protect a vampire in sleep,' he says.[otherwise]He shakes his head. 'Certain items— but I would be telling the story backward if I explained now. The question really is, how much do you want to know about Snow White?'[end if]".
 It indirectly-follows what seems really at the safe haven.
 It quip-supplies corpse.
 It is listed.

what death seems like is a questioning quip. 
 The printed name is "what death is like". The true-name is "what death seems like". 
 Understand "is" as what death seems like.  The comment is "'What is death like?'".
 It mentions death.
 The response is "'Disappointing,' he says frankly. 'You build it up in your mind a long time in advance, and then it happens, and it is both strange and completely ordinary. From this side, believe me, it does not seem like the top experience of one's lifetime. Not even in the top ten. [paragraph break]'The state of knowledge that comes afterward, that's pretty good. But the death itself? You wouldn't make five pence charging admission to Death at the fair, is all I can say about that[casually queue hey-I-am-dead].'".
 It quip-supplies corpse.

 yes-30 is an affirmative quip. The printed name is "say yes". The true-name is "yes-30".  The comment is "'Oh, I'm sure that's true.'".
 It mentions death.
 The response is "'Well, it is true, and not just because of the obvious difficulties you'd have with Death's current reputation. Even if you could bring back some prior customers, maybe drum up some testimonials, they wouldn't have much to say for it.'".
 It directly-follows what death seems like.
 It quip-supplies corpse.

 no-34 is a negative quip. The printed name is "say no". The true-name is "no-34".  The comment is "'I can't believe it's so boring.'".
 It mentions death.
 The response is "He just shrugs. 'I wouldn't say boring exactly. Just not as spectacular as you're probably hoping. You've heard about beautiful lights, or choirs of angels, or perhaps crossing a river by boat...? Scratch all that. It's more on the order of a painful full-body sneeze. Brief, involuntary, and embarrassingly animal.'".
 It quip-supplies corpse.

hey-I-am-dead is an NPC-directed quip.
	The response is "'Of course, that doesn't stop you every few minutes afterwards thinking to yourself, [']hey, I'm dead!['] until the novelty wears off.'"
	

where vampires come from is a questioning quip. The comment is "'Where do vampires come from?'".
 It mentions vampirism.
 The response is "'Other vampires,' he says. 'And so on back to the earliest days of the world. They originated then in a kind of glitch — a break in the creative design, you might say, that demons were able to take advantage of.'".
 It quip-supplies corpse.

why demons did such a thing is a questioning quip. The comment is "'Why would demons have wanted to do such a thing?'".
 It mentions demons.
 The response is "'To have some project of their own in the world,' he says. 'Every other thing that walks or creeps or flies or swims, sooner or later, gets jealous of God. Able to make whatever He wants, whenever He thinks of it.'".
 It directly-follows where vampires come from.
 It quip-supplies corpse.
 It is listed.

 whether there seem any young princes in neighboring kingdoms is a questioning quip. 
 The printed name is "whether there are any young princes in neighboring kingdoms". The true-name is "whether there seem any young princes in neighboring kingdoms". 
 Understand "are" or "prince" as whether there seem any young princes in neighboring kingdoms.  The comment is "'Are there any eligible young princes in neighboring kingdoms?'".
 It mentions family.
 The response is "'None fool enough to marry Snow White as she currently is,' he says flatly. 'Her breath alone would keep any royal suitors at bay.'".
 It quip-supplies corpse.



banish-her-too is an NPC-directed quip.
	It mentions Snow White, Lilith.
	The response is "'As you know, Snow White's possessed,' [the corpse] remarks. 'So you must be careful of her — she has the teeth of a vampire[vampirism-revealed], but she is even older and more cunning than one of them, and if she gets to thinking you pose a threat to her, she'll rip your throat out or drive you mad, just as she thinks fit.

'If you keep your pact with me, though, and burn my heart in its box, the fumes will drive the demoness off, as well. And whatever you do, don't get to feeling sorry for her. That's how she works on people.'"
	It quip-supplies corpse.
	
 remark that his solution seems very convenient is a performative quip.
 The printed name is "remark that his solution is very convenient". The true-name is "remark that his solution seems very convenient". 
 Understand "is" as remark that his solution seems very convenient.  The comment is "'Very convenient that the thing I need to do in order to be rid of Lilith is the same thing you want me to do!'".
 It mentions Lilith.
 The response is "He tosses his head impatiently. 'It is neither a contrivance nor a strange coincidence,' he says. 'That box is made of wood of a certain tree of the Garden of Eden, and written over in the language of worldmaking, and it has the power to unmake magic that has twinned matter with the wrong spirit.' [paragraph break]Seeing your skepticism, he subsides. 'If I wished to lie to you, I might invent for your benefit some hocus spell, might I not? I am telling you the absolute truth, if you have sense enough to accept it.'".
 It directly-follows banish-her-too.
 It quip-supplies corpse.
 It is listed

how Snow White came to be possessed is a questioning quip. The comment is "'How did Snow White come to be possessed?' you ask. 'Where did the demon come from?'".
 It mentions Lilith, Snow White.
 The response is "'A side effect[magic-involves-demons] of dark magic,' he says. 'The Queen summoned up the demoness Lilith to make her beautiful[queen-invoked-lilith], and that demon had to go somewhere. Snow White was to hand.'".
 It quip-supplies corpse.
 It assumes snow-white-possessed.
 It is listed.

why the Queen so badly wishes to be beautiful is a questioning quip. The comment is "'Of all the foolish reasons to invoke demons,' you mutter. 'It's rank madness for the Queen to summon something up out of hell to do her face up!'".
 It mentions Queen, magic.
 The response is "An odd look crosses his face, almost pitying. 'Understand I'm no enthusiast of the Queen's,' he says. 'But it is not simple vanity that moves her on. She was left behind, you see, and she doesn't understand why, and she does not even remember properly what she lost. She is angry and afraid and lonely, and when she looks into the mirror the only witless advice it can offer her is that love and power come naturally to the beautiful. And so— this is the result.'".
 It quip-supplies corpse.
 It assumes queen-invoked-lilith.
 It is listed

why the King abandoned the Queen is a questioning quip. The comment is "'Why did [if the player knows you-were-king]I[otherwise]the King[end if] abandon her? If that is what happened, I mean.'".
 It mentions king.
 The response is "'A belated and somewhat misdirected sense of justice,' [the corpse] says, with surprising bitterness. [if the player knows happy-name-known]His nickname is evidently more of a suggestion than a guarantee. [end if]'I've never had much taste for dramatic sacrifices and grotesque punishments and so on. But [if the player knows you-were-king]you were[queue blood-sundering-clue as immediate obligatory][otherwise]the King was[end if] reared to think everything could be bought and paid for in blood — even peace. There's no sanity to be found in spiking yourself on your own regrets.'".
 It directly-follows  why the Queen so badly wishes to be beautiful.
 It quip-supplies corpse.
 It is negated by blood-sundering.
 It is listed.

that seems horrible is an informative quip.
 The printed name is "that is horrible". The true-name is "that seems horrible". 
 Understand "is" as  that seems horrible.  The comment is "'That's horrible!'".
 It mentions Queen.
 The response is "'Is it?' He's looking amused again, in that particularly annoying way of his. He seems keen to remind you of everything he knows that you don't. [paragraph break]'It doesn't show much maternal instinct, I'll grant you that. But the dwarrows are used to how menfolk treat anything smaller than themselves, so I suppose I am not so surprised as you.'".
 It indirectly-follows how Snow White came to be possessed .
 It quip-supplies corpse.


Section 7 - Interrogating Snow White about other topics

[A number of the items in this section are expansions that come of beta-testing, so the organization is even more disjointed than usual.]

 what seems on her lips is a questioning quip. 
 The printed name is "what is on her lips". The true-name is "what seems on her lips". 
 Understand "is" as what seems on her lips.  The comment is "'What is that on your lips? Blood?'".
 It mentions Snow White's lips.
 The response is "She makes a convincing grimace of distaste[pathetic]. 'Maybe from the hart... ew. I'd try dabbing my lips with a kerchief, but I'm underequipped for polite behavior just at the moment.'".
 It is negated by snow-white-possessed.
 It directly-follows what she was doing alongside the hart.
 It quip-supplies Snow White.

Instead of rubbing Snow White's lips when the player recollects what she was doing alongside hart:
	if the player knows vampirism-revealed:
		say "It wouldn't be wise to get that close.";
	otherwise if the player knows Snow-White-possessed:
		say "You'll leave bathing the demon-possessed princess to someone a little more foolhardy.";
	otherwise: 
		say "[one of][if the current quip is pleading-to-be-untied]'Possibly I'd better do this myself,' you reply. [end if]You wipe at Snow White's lips to clean them[possible-blood-drinking]. A few dark ruddy brown smudges come away, but it's hard to tell whether that is blood or dirt or some sort of cosmetic[or]You've done all you can do without the benefit of water[stopping]."
		
Instead of asking Snow White to try consuming the corpse's neck:
	try asking Snow White to try drinking the corpse's  veins. [ To capture such instructions as >SNOW WHITE, BITE NECK.]
	
Instead of asking Snow White to try consuming the corpse's  veins:
	try asking Snow White to try drinking the corpse's  veins.
	
Instead of asking Snow White to try drinking the corpse's veins when the player knows hart-no-longer-undead and to drink the hart's blood is flagged-ready:
	try discussing to drink the hart's blood.
	
Does the player mean asking Snow White to try drinking the corpse's veins:
	it is very likely.

 to drink the hart's blood is an informative quip.  The comment is "'Would you drink the hart's blood, please?'". The printed name is "her to drink the hart's blood".
 It mentions corpse's veins, corpse, vampirism.
 The response is "'What is this, some foul ritual of yours? Absolutely not[if the player knows vampirism-revealed][first-vampire-accusation]. And if you're trying to suggest I have a taste for such things, you might be interested to know that vampires vastly prefer human blood, and use that of animals only in desperation or in order to send a message[end if].' So much for your dreams of reviving the thing again.".
 It quip-supplies Snow White.
 It assumes hart-no-longer-undead.
 It is negated by snow-white-possessed.

 whether she knows any princes is a questioning quip.  The comment is "'Do you know any princes from other kingdoms?'".
 It mentions lost man.
 The response is "'Not well,' she says. 'Other kingdoms send us only heralds, not the princes themselves. I believe some of our neighbors have heard rumors of the Queen, and do not care to visit in her present condition.'".
 It quip-supplies Snow White.
 It is listed. 

reveal what it was like hunting the hart is a weak performative quip. 
Understand "like to hunt" as reveal what it was like hunting the hart.
The comment is "'There is always a pleasure in hunting an animal such as this.' You nod at the corpse beside you. 'It is a worthy adversary, though one also feels some compassion for the dead.'".
 It mentions corpse.
 The response is "'A truly worthy adversary would be one who was as likely to kill you as you to kill him.'".
 It quip-supplies Snow White.

maybe-again is a performative quip. The true-name is "maybe-again". The printed name is "equivocate". 
	Understand "[maybe]" as maybe-again.
	The comment is "'I don't know. My belief or disbelief hardly matters, though, does it?'".
 It mentions vampirism.
 The response is "'No.'".
 It directly-follows if she believes in vampires.
 It quip-supplies Snow White

 who seems the fairest of them all is a weak questioning quip.
 The printed name is "who is the fairest of them all". The true-name is "who seems the fairest of them all". 
 Understand "is" as who seems the fairest of them all.  The comment is "'Tell me, [vocative Snow White], who is the fairest of them all?'".
 It mentions magic mirror.
 The response is "She smirks. 'Very good — you have the Queen's voice exactly.'".
 It quip-supplies Snow White.

admit that you don't ken is a weak performative quip. 
 The printed name is "that you don't know". The true-name is "admit that you don't ken". 
 Understand "[maybe]" or "I" or "know" as admit that you don't ken.  The comment is "'I have not lived long enough to observe.'".
 It mentions vampirism.
 The response is "'[friendly]Wisely said.'".
 It directly-follows how vampires came to be.
 It quip-supplies Snow White.

 yes-26 is a weak affirmative quip. The printed name is "say yes". The true-name is "yes-26".  The comment is "'You are... possibly correct,' you admit.".
 Understand "[maybe]" as yes-26.
 It mentions Queen, yourself.
 The response is "'You have a weakness for her. I have observed it many times. It is excessive: not even her other servants feel it to the same degree. You should curb it if you can.'".
 It directly-follows yes-2.
 It quip-supplies Snow White.

whether the servants seem dwarrows is a questioning quip. 
 The printed name is "whether the servants are dwarrows". The true-name is "whether the servants seem dwarrows". 
 Understand "are" or "if" as whether the servants seem dwarrows.  The comment is "'[if immediately]These [']small['] servants being[otherwise]Your [']small['] servants are[end if] dwarrows, I suppose?'".
 It mentions dwarrows, servants.
 The response is "'You needn't ask in that tone of voice,' she replies, and your cheeks flood with heat. 'A dwarf is as good as a man — better, sometimes[servant-dwarrows]. And they owe nothing to the Queen.'".
 It is negated by servant-dwarrows.
 It indirectly-follows what seems at the safe haven.
 It quip-supplies Snow White

maybe is a performative quip. 
Understand "[maybe]" as maybe.
The comment is "'Maybe. I wouldn't expect royalty to be visible on the surface,' you say.".
 It mentions daughter.
 The response is "'A fair observation. There are lords even among the dwarrows, after all.' She considers you again, then adds, 'Nonetheless I am both someone's daughter, and no one's daughter at all.'".
 It directly-follows if she seems the king's daughter.
 It quip-supplies Snow White

 whether she likes the dwarrows is a weak questioning quip.  The comment is "'Do you know the dwarrows well?'".
 It mentions dwarrows.
 The response is "'They have no loyalty to the Queen, or to men[if the player knows snow-white-possessed]; they are one of God's side projects[end if]. Therefore I find them [comforting].' ".
 It quip-supplies Snow White.

 yes-21 is an affirmative quip. The printed name is "say yes". The true-name is "yes-21".  The comment is "'Of course,' you say firmly. 'I was wondering about their precise nature.'".
 It mentions queen, demons.
 The response is "'Ah[queue what demons the Queen invoked as immediate obligatory].'".
 It directly-follows what powers the Queen dabbles alongside.
 It quip-supplies Snow White.

 no-20 is a weak negative quip. The printed name is "say no". The true-name is "no-20".  The comment is "'Not all of us have studied these matters.'".
 It mentions witchcraft.
 The response is "'Ah yes. I must remember that[queue what studies as immediate obligatory] outside the castle these are not matters for constant conversation.'".
 It directly-follows what powers the Queen dabbles alongside.
 It quip-supplies Snow White.

 why the dwarrows became friendly is a weak questioning quip.  The comment is "'Why did [if immediately]they[otherwise]the dwarrows[end if] turn friendly?'".
 Understand "have become" or "turned" or "are" as why the dwarrows became friendly.
 It mentions dwarrows.
 The response is "'No one knows. You would have to ask one of them.'".
 It indirectly-follows what she knows about dwarrows.
 It quip-supplies Snow White.
 It is listed.

[And in case the player wants to go on talking...]

 how the competition was hindered is a questioning quip.  The comment is "'[if immediately]Hindered? How?[otherwise]How did Lilith 'hinder the Queen's competition'?' you ask[end if].".
 Understand "hindering" as how the competition was hindered.
 It mentions Lilith, witchcraft.
 The response is "[if the player knows snow-white-possessed]'The usual methods. Did you know' — she gives you a wicked sidelong glance — 'Helen of Troy came down with the pox two days after she got on her ship? It's true. She looked like a used pincushion by the time it was over. Menelaus didn't know her again[queue further-musings-on-beauty as immediate optional].'[otherwise]'Death, disease, accidental disfigurement. Those sorts of thing.'[end if]".
 It indirectly-follows whether Lilith made the Queen beautiful.
 It quip-supplies Snow White.

 whether she will be serious is a weak questioning quip.  The comment is "'Would you be serious?'".
 It mentions Snow White.
 The response is "'I am. Completely.'".
 It directly-follows how the competition was hindered.
 It assumes snow-white-possessed.
 It quip-supplies Snow White.
 It is listed.

further-musings-on-beauty is an NPC-directed quip.
	The response is "'Death is also very bad for the complexion,' she observes thoughtfully."
	It quip-supplies Snow White.


 why she avoids daylight is a weak questioning quip.  The comment is "'The people have noticed that you avoid the daylight.'".
 It mentions night.
 The response is "[if the player knows snow-white-possessed]'I can stand the sun. I merely dislike the company of men. Night is quieter and more suitable.'[otherwise]'The sun is bad for my skin.'[end if]".
 It quip-supplies Snow White.

 whether that seems a vampire trait is a weak questioning quip. 
 The printed name is "whether that is a vampire trait". The true-name is "whether that seems a vampire trait". 
 Understand "is" as whether that seems a vampire trait.  The comment is "'That is a vampire trait, is it not? [if the player knows vampirism-revealed][first-vampire-accusation][end if]A hatred of sunlight, a tendency to stay awake at night?'".
 It mentions vampirism.
 The response is "'Not only vampires are fair.'".
 It directly-follows why she avoids daylight.
 It is negated by snow-white-possessed.
 It quip-supplies Snow White.
 It is listed.


 what she knows about Grumpy is a questioning quip.  The comment is "'Tell me, are you acquainted with a dwarf called Grumpy?'".
 It mentions Grumpy.
 The response is "'That was the nickname the other dwarrows gave him,' she says. 'He preferred to be called Brassbiter. We were acquainted; he did some work for me[servant-dwarrows].'".
 It quip-supplies Snow White.
 It assumes  Grumpy-named.
 It is listed.

 what chores Brassbiter performed is a questioning quip.  The comment is "'And what sorts of things did Brassbiter do on your behalf?'".
 It mentions grumpy.
 The response is "'Just some preparations of the safe haven. I need many things in order to be able to live there without going outside, and to keep the Queen from guessing where I live. Some of the arrangements are straightforward, others arcane. Brassbiter has an unusually strong belief in the value of secrecy and privately-laid plans. I found him the most trustworthy of all his kind.'".
 It quip-supplies Snow White.
 It indirectly-follows what she knows about Grumpy.
 It is listed.

other-names-Snowy is a privately-named questioning quip.  The comment is "'[if the previous quip is other-names][one of]Or[or]What about[or]How about[at random][otherwise][one of]Do you know a dwarf called[or]Or do you know a[stopping][end if] ['][guessed-nickname][']?'". The printed name is "whether she knows a dwarf called [guessed-nickname]". The true-name is "other-names-Snowy".
 It mentions dwarf-cousins.
 The response is "'[one of]I have never known a dwarf to have such a foolish name[or]No[or]Credit them with more dignity[at random].'".
 It is repeatable.
 It assumes happy-name-known.
 It quip-supplies Snow White. 

how to find the King is a questioning quip.  The comment is "'How can I find the King, if he doesn't even know who he is himself?'".
 Understand "he" or "where" or "is" as how to find the King.
 It mentions king.
 The response is "She shakes her head. 'There are methods of divination — of consultation with the dead, for instance — that might reveal who he is. But among the ways open to the average mortal? I know no certain method.'".
 It quip-supplies Snow White.
 It is negated by you-were-king.
 It assumes blood-sundering, Snow-white-knows-sundering.
 It is listed. 

who the King seems is a weak questioning quip. 
 The printed name is "who the King is". The true-name is "who the King seems". 
 Understand "is" as who the King seems.  The comment is "'Who is the King, then?'".
 It mentions king.
 The response is "'I don't know, obviously.'".
 It quip-supplies Snow White.
 It is negated by you-were-king.
 It assumes blood-sundering, snow-white-knows-sundering.

 what she knows of Lamia is a questioning quip.  The comment is "'Tell me about Lamia — or should that be the Lamia?'". [* And here's another one that's thanks to beta-testers showing off. All the same, I think we can weave it in.]
 The response is "'It is the name people gave to a memory,' she says. 'There was no Lamia, no Queen of Libya who took to murdering children; certainly no half-serpent lady who went by that name. But neither is the story wholly false.'".
 It quip-supplies Snow White


why it takes a silver chain to involve her is a questioning quip. 
 The printed name is "why it takes a silver chain to contain her". The true-name is "why it takes a silver chain to involve her". 
 Understand "contain" as why it takes a silver chain to involve her.  The comment is "'Tell me, [vocative Snow White]: why does it take a silver chain to contain you[if the player knows vampirism-revealed]? [first-vampire-accusation]Is it because you're a vampire[end if]?'".
 It mentions silver chain.
 The response is "'[if the player does not know Snow-white-possessed]That is the Queen's lunacy, not mine.[otherwise]Come, surely you don't expect me to explain such things to you.[end if]'".
 It quip-supplies Snow White. 

 why the Queen suffers this delusion is a questioning quip.  The comment is "'Why does the Queen suffer this delusion, do you suppose?'".
 It mentions Queen.
 The response is "'The Queen's sanity has been crumbling away for a long time. The kingdom worries her; the King is absent; and then there is the matter of her vanity. Who knows where her delusions come from?'".
 It quip-supplies Snow White.
 It is negated by snow-white-possessed.
 It directly-follows why it takes a silver chain to involve her.
 It is listed.

 no-35 is a negative quip. The printed name is "say no". The true-name is "no-35".  The comment is "'Perhaps not.'".
 It mentions yourself.
 The response is "She [for eye-gest]looks at you coyly[or walk-gest]jerks one shoulder up as though dislodging a gadfly landed there[or breath-gest]mutters to herself under her breath[end for]. 'Silver is the metal of mortal innocence,' she says. 'It has value, but is not showy, not worth [italic type]too much[roman type]. It is not beyond a workman's means to earn. And it requires maintenance, must be cleaned and polished, and in time diminishes into tarnish and is lost. Its modesty, its very mediocrity has the power to bind and compel eternal spirits.'".
 It directly-follows why it takes a silver chain to involve her.
 It quip-supplies Snow White
 It assumes snow-white-possessed.

 yes-31 is an affirmative quip. The printed name is "say yes". The true-name is "yes-31".  The comment is "'I don't know what you might say: no reason not to ask, at least.'".
 It mentions yourself.
 The response is "'This must be very hard on you.'".
 It directly-follows why it takes a silver chain to involve her.
 It quip-supplies Snow White.
 It assumes snow-white-possessed.

 yes-32 is an affirmative quip. The printed name is "say yes". The true-name is "yes-32".  The comment is "'I've had easier evenings, it's true.'".
 It mentions yourself.
 The response is "'I doubt that even the Queen is enjoying a comfortable respite this night.' She sighs. 'It is strange, is it not, how closely we are bound to our enemies? She will not be able to sleep for wondering whether I am dead yet. To each of us the matter is consuming; and meanwhile many good neighbors, who would in human feeling pull either of us from a millpond if we were drowning, sleep soundly. And she and I are consumed by our angers and fears about one another, and so we study each other with the intensity of lovers.' [paragraph break]Her attention returns to the present. 'And then there is you, bystander in some of what has happened. But beside us three, who in the wide world marks the history of our private war?'".
 It directly-follows yes-31.
 It quip-supplies Snow White.

that Snow White shows marks of vampirism is an informative quip.  The comment is "'Come, you haven't hidden the marks of vampirism well[if the player knows vampirism-revealed][first-vampire-accusation][end if].'".
 It mentions vampirism, Snow White.
 The response is "'Most vampires are vampires without having any choice in the matter,' she says[scary], without denying it[vampirism-revealed]. 'Demons merely dabble, from time to time.'".
 It directly-follows whether Lilith likes drinking blood.
 It quip-supplies Snow White.
 It assumes snow-white-possessed.
 It is listed.

why Lilith would want to drink blood is a performative quip. The comment is "'Why would a demon wish to drink blood? Does it bring some advantage? Surely demons are immortal without the need for such things.'".
 It mentions Lilith, demons, vampirism.
 The response is "'There is strength in it,' she says[scary]. 'But sometimes one's host has no taste for it. The princess seems meek, but she can be very stubborn when something is not to her liking. We often —' (she says with some relish) '— have disagreements about what we should do.[paragraph break]'Of course, I win.'".
 It quip-supplies Snow White.
 It assumes snow-white-possessed.
 It indirectly-follows that Snow White shows marks of vampirism.

whether she hath hurt the princess is a performative quip.
 The printed name is "whether she has hurt the princess". The true-name is "whether she hath hurt the princess". 
 Understand "has" or "snow" or "white" or "lilith" as whether she hath hurt the princess.  The comment is "'Have you hurt the princess?'".
 It mentions Snow White.
 The response is "'If I were to leave her now, she would bear no physical scars.' ".
 It indirectly-follows that Snow White shows marks of vampirism.
 It assumes snow-white-possessed.
 It quip-supplies Snow White.
 It is listed.

Availability rule for if the queen still seems a witch:
	if the player recollects if the queen seems a witch:
		it is off-limits.

if the queen still seems a witch is a questioning quip.
 The printed name is "if the Queen is a witch". The true-name is "if the queen still seems a witch". 
 Understand "has" or "whether" as if the queen seems a witch. 
 Understand "is" as if the queen still seems a witch. The comment is "'[if the player knows queen-made-mirror]Do I take it that the Queen is a witch[otherwise]Is the Queen a witch[end if]?' you ask.".
 It mentions queen, witchcraft.
 The response is "'It is witchcraft to summon such demons as myself,' she says. 'So yes — though many other things popularly believed of witches are not true in her case.'".
 It quip-supplies Snow White.
 It is negated by demon-possession.
 It assumes snow-white-possessed. [paired with an alternate version]
 It is listed.

 no-36 is a negative quip. The printed name is "say no". The true-name is "no-36". 
 It mentions Queen, fear.
 The comment is "'I doubt it.' She is looking at you steadily; it is difficult to lie to the demon.".
 The response is "'There. It is best to know what you are capable of,' she says.".
 It quip-supplies Snow White.
 It assumes snow-white-possessed. [ This is the alternaet version of no-4]
 It directly-follows how to deal alongside the queen and  if you could kill the queen.

 no-37 is a negative quip. The printed name is "say no". The true-name is "no-37". 
 The comment is "'Perhaps you have some reason not to,' you say. 'Maybe I should take my present existence as a sign of your future intentions. But I am human, and we are nervous sorts.'".
 The response is "'As I have often observed,' she says.".
 It assumes snow-white-possessed.
 It quip-supplies Snow White.
 It directly-follows no-1. 

yes-34 is an affirmative quip. 
 The printed name is "say yes". The true-name is "yes-34". 
 The comment is "'Of course you could kill me.'".
 The response is "She sighs lightly. 'If I wanted to.'".
 It assumes snow-white-possessed.
 It quip-supplies Snow White.
 It directly-follows no-1. 

 no-38 is a negative quip. The printed name is "say no". The true-name is "no-38". The comment is "'I wouldn't dare presume.'".
 Understand "[maybe]" or "be evasive" or "avoid/evade her" or "avoid/evade answering" as no-38.
 It mentions Snow White's skin.
 The response is "She considers you. 'Very good, then.'".
 It assumes snow-white-possessed. 
 It directly-follows am-I-beautiful.
 It quip-supplies Snow White. 


Section 8 - Recognizing Lilith 

 whether she seems lilith really is a questioning quip.
 The printed name is "whether she is [if the player knows lilith-named]Lilith[otherwise]the victim[end if]". The true-name is "whether she seems lilith". 
 Understand "is" or "if" or "victim" or "the victim" as whether she seems lilith really.  The comment is "'Were you the victim?' If she is — owned — by [if the player knows lilith-named]Lilith[otherwise]some demon[end if], what will become of you?".
 It mentions demons, Lilith, Snow White.
 The response is "She meets your eye without speaking, but you know that this is the point to which she has been leading you, the thing she wanted you to understand without her having to say it in so many words[snow-white-possessed].

[if the player knows lilith-named]'I am Lilith — in part.'[otherwise]'Lilith[lilith-named] is the demon,' she says. 'The first wife of [adam-named]Adam[lilith-first-wife], before the milkmaid [eve-named]Eve came to the garden.'[end if]".
 It quip-supplies Snow White.
 It assumes demon-possession.
 It is negated by possible-blood-drinking.
 It is listed. 

 who Lilith was before is a questioning quip. The printed name is "who Lilith was". The true-name is "who Lilith was before".  The comment is "'Who [if the player knows Snow-White-possessed]were you, [otherwise]was [end if]Lilith?'".
 It mentions Lilith.
 The response is "'Lilith,' she repeats, sing-song. 'The first wife of Adam[adam-named][lilith-first-wife], before the milkmaid [eve-named]Eve came to the garden. She was formed from fire as Adam was formed from earth: she was his [italic type]equal[roman type], and said so, and so he cast her out.'".
 It assumes lilith-named.
 It is negated by lilith-first-wife.
 It quip-supplies Snow White.
 It is listed.

whether pact endures is a questioning quip.
	The comment is "'You still wish to keep our pact?' you ask. 'Now that I know what you truly are, I mean.'"
	It mentions deal.
	The response is "'I will not betray you to the Queen, if you do not try to harm me,' she says. 'There is honor even among demons.'"
	It quip-supplies Snow White.
	It assumes snow-white-possessed.
	It is listed.
	
 request another repetition of the riddle is a performative quip.
 It mentions snow white, riddle, apple.
 The comment is "'What was that riddle you asked me before? You mentioned yourself in it, and now I want to hear it again.'".
 The response is "She [for breath-gest]laughs under her breath[or walk-gest]shrugs[or eye-gest]holds your gaze for a moment, then looks away[end for]. 'I might as well simply tell you,' she says. 'I am looking for an apple[riddle-solved].'".
 It quip-supplies Snow White. 
 It assumes riddle-given, snow-white-possessed.
 It is negated by riddle-solved.
 It is listed.

Rule for quip-introducing whether she can read your thoughts really:
	say "[roman type]There's an interesting question: whether demons can read thoughts.";
	list no other quips.

whether she can read your thoughts really is a questioning quip. The comment is "'Can demons read minds?'".
 Understand "my" or "Snow White" or "could" as whether she can read your thoughts.
 It mentions mind reading, snow white, demons, lilith.
 The response is "'Your mind is not a book of words,' she says. 'I can't look at it and see every idea that has passed through. But sometimes — when you concentrate — or when your soul deliberates over a course of action — it is hard to mistake what you must be considering. [paragraph break]'Whether that is a supernatural gift, I do not know. I would not wonder if it is an ability shared by any very old being with a long experience of behavior.'".
 It directly-follows she-sees-hesitation, no-21, maybe-2. [* There is other thought-reading elsewhere.]
 It assumes snow-white-possessed. [An alternate version of this exists.]
 It quip-supplies Snow White.
 It is listed.


 whether Snow White wishes to be rid of Lilith is a questioning quip.  The comment is "'Do you wish to be rid of Lilith, [vocative Snow White]?'".
 Understand "she" as whether Snow White wishes to be rid of Lilith.
 It mentions lilith.
 The response is "'The princess is sick of me, and frightened,' Snow White says, in what must be Lilith's voice. 'Nonetheless this is where I make my home.'".
 It quip-supplies Snow White.
 It assumes snow-white-possessed.
 It is listed.

 whether Lilith wishes to remain in Snow White is a weak questioning quip.  The comment is "'Lilith, do you want to remain the princess? Or are you trapped there? She seems like a dull vessel for an old soul.' It is a dicy game you are playing now.".
 Understand "she" as whether Lilith wishes to remain in Snow White.
 It mentions lilith.
 The response is "'I don't want to remain, but neither do I want simply to return to the cold and the dark and the wilderness alone[when weird]. It is a long time since anyone has asked[friendly][end when].'".
 It quip-supplies Snow White.
 It assumes snow-white-possessed.
 It is listed

that you seem a decent man is an informative quip. It is weak.
 The printed name is "that you are a decent man". The true-name is "that you seem a decent man". 
 Understand "I am" or "are" as that you seem a decent man.  The comment is "'I take no pleasure in doing this,' you say[if anger is greater than 0], as though this would calm her[otherwise if scariness is greater than 1], trying to keep any note of pleading out of your voice[end if]. 'I am a decent man.'".
 It mentions yourself.
 The response is "'[when furious]How [comforting][or menacing]That is what all the Queen's servants tell themselves[or aggressive]I cannot express my relief[or whiny]Yet somehow I don't feel much pity for you[at other times]I know[friendly][end when].'".
 It quip-supplies Snow White.


Instead of Snow White discussing that you seem a decent man when the previous quip is you-promised-me: [* In this one context, she takes it as an attempt at reassurance.]
	now Snow White recollects that you seem a decent man;
	now that you seem a decent man is strong;
	clear irritated-promise from queue;
	say "'Very well,' she says, softening again[friendly]. She seems to take this as a promise to honor your agreement, though in fact you have said no such thing. But it's always best to make no firm commitments. You've learned that much, in the last few years in the company of the Queen.";

Instead of Snow White discussing that you seem a decent man when the player knows seeks-equal:
	now Snow White recollects that you seem a decent man;
	say "'Most of Eve's sons were decent; all but Cain made their sacrifices and loved their mother. I am looking for something other than such meager moral adequacy.'"

 what she means now is a questioning quip.  The comment is "'Is this a riddle game?'".
 It mentions riddle, lilith.
 The response is "She looks pleased. 'Say it is,' she replies. 'Who is a woman, not born of a woman? There is your clue.'".
It is negated by snow-white-possessed.
 It directly-follows maybe.
 It quip-supplies Snow White.
 It is listed.

 guess Eve is a performative quip. The comment is "'There was Eve, I suppose,' you say. 'She wasn't born, but was made of Adam's rib.'".
 Understand "pick" or "choose" or "suggest" as guess eve.
 It mentions Eve.
 The response is "She sets her jaw angrily[angry]. 'Yes, there was [eve-named]Eve,' she says. 'But Eve came after another woman, who should have been [adam-named]Adam's true wife, if he had had the wit and daring to be her equal.'".
It is negated by snow-white-possessed.
 It directly-follows what she means now.
 It quip-supplies Snow White.

 shrug is a weak performative quip. The comment is "'I don't care to play guessing games, [vocative Snow White]: I am only telling you what must happen, if we are to come out of these woods alive.'".
Understand "I don't know" or "don't know" or "dunno" or "do not know" or "I" as shrug.
 It mentions yourself.
 The response is "'Very well[angry].'".
It is negated by snow-white-possessed.
 It directly-follows to walk properly.
 It quip-supplies Snow White.

that the Queen burned your cabin is a weak informative quip.  The comment is "'The Queen had my home burned: I believe it was intended as a warning. Graciously she has been allowing me to sleep in the stables until it can be rebuilt.'".
Understand "my" as that the Queen burned your cabin.
 It mentions Queen, yourself, cabin.
 The response is "[Snow White smiles]. 'Almost humane, by her standards. She must like you.'[if Snow White is likely to continue][run paragraph on][otherwise][line break][end if]".
 It quip-supplies Snow White.
 It assumes cabin-destroyed.
 It is listed.

 no-21 is a negative quip. The printed name is "say no". The true-name is "no-21".  The comment is "'It is my occupation to hunt animals; I do not seek out the woods in order to avoid other people.' It is a lie, but a lie you have practiced many times — for the benefit of your mother while she lived, and for the meddling matrons in the village when they dared to inquire into your solitary habits, and even to the Queen. But the truth is that people are exhausting, and their lies and riddles and hidden meanings wear out your patience.".
 It mentions yourself.
 The response is "'I see.' [scary]She looks as though she has read your mind and sees a bit more than you'd like her to.".
 It directly-follows solace-in-desert.
 It quip-supplies Snow White

 yes-23 is an affirmative quip. The printed name is "say yes". The true-name is "yes-23".  The comment is "'I do,' you admit. 'Or at least I do not find solace in being in the presence of the court.'".
 It mentions yourself.
 The response is "'What about the villagers? You have no wife, I understand.' And just as well, too, considering what the queen did to your cabin[cabin-destroyed]. A wife would have no place to live.".
 It directly-follows solace-in-desert.
 It quip-supplies Snow White.

 explain that young women of the village hath never appealed is a performative quip.
 The printed name is "explain that young women of the village have never appealed". The true-name is "explain that young women of the village hath never appealed". 
 Understand "have" as explain that young women of the village hath never appealed.  The comment is "'The young women of the village are adequately pleasant,' you say. You grimace as it comes out of your mouth. Your mother always told you not to condescend, even if you didn't find their company stimulating (a deficiency, she implied, which was entirely your own fault).".
 It mentions family.
 The response is "She laughs[friendly]. 'I would not marry on such tepid—' She breaks off. 'Not that the Queen would permit my marriage in any case. The last thing she would desire is for me to have a husband.'".
 It directly-follows yes-23.
 It quip-supplies Snow White.
 It is listed.

 why the Queen would not want her to marry is a questioning quip.  The comment is "'Why would [if immediately]she[otherwise]the Queen[end if] object to your marriage?'".
 It mentions family, Queen, Snow White.
 The response is "'Because then [if the player knows snow-white-possessed]the little princess[otherwise]I[end if] would have a family, a champion, allies... she wants to keep everyone who might be a threat to her at odds with one another. That is why she keeps her serving women at each others['] throats.'".
 It indirectly-follows explain that young women of the village hath never appealed.
 It quip-supplies Snow White.
 It is listed.

whether she seems immortal is a questioning quip.
 Understand "mortal" or "mortals" or "immortals" or "life" or "alive" as whether she seems immortal. 
 The printed name is "whether she is immortal". The true-name is "whether she seems immortal". 
 Understand "is" as whether she seems immortal.  The comment is "'[if immediately]And you are[otherwise]Are you[end if] immortal?'".
 It mentions souls, Snow White, death.
 The response is "'Partly.' She walks away from you, around a tree, out of sight, and then circles back into view. 'It grows tiring. Under the proper conditions, I would change my situation[queue leading-comment-about-queen].'".
 It indirectly-follows how an apple would help her.
 It quip-supplies Snow White.
 It is negated by vampirism-revealed, snow-white-possessed.

Instead of Snow White discussing leading-comment-about-queen when the player recollects if the queen seems a witch:
	say "'The witch did all.' [for eye-gest]She gives you one of her sly looks[or walk-gest]She walks away from you and back again[or breath-gest]She puts her tongue between her front teeth, thoughtfully, and then withdraws it again[end for][scary]."

leading-comment-about-queen is an NPC-directed quip.
	It mentions witchcraft.
	The response is "'Perhaps we should begin with the Queen[if the player recollects  whether the Queen hurt her] after all[end if]. Her magic is to blame for all our situations.'"
	It quip-supplies Snow White.
	
 what the apple would do is a questioning quip.  The comment is "'What would an apple do? Would it make you mortal?'".
 It mentions apple, souls, death.
 The response is "She [for eye-gest]looks away from you[or walk-gest]walks away so her back is toward you[or breath-gest]breathes deeply[end for]. 'I knew a prophetess once in Egypt, who told me that if I ever ate an apple, it would snare me — make me forget what I am, that I am anything other than one of the daughters of [eve-named]Eve. I asked her whether this was a punishment; she said that it might be a gift.'".
 It quip-supplies Snow White.
 It assumes riddle-solved, apples-for-men, Snow-White-possessed.
 It is listed.

 suggest finding an apple later is a performative quip. The comment is "'If that is what you want, I would not wonder if you could find one later; but I doubt there will be any apples on the forest floor. This is not an orchard, or a marketplace,' you point out.".
 It mentions apple.
 The response is "'I merely wanted to prepare myself[pathetic],' she says, sounding tired. 'If things become much worse, I might desire— I have lived a long time. My knowledge becomes oppressive, and the Queen is the reflection of all that is wrong with my existence. It would comfort me to have an escape.'".
 It indirectly-follows what the apple would do.
 It quip-supplies Snow White.
 It is listed.

 what will happen if you burn the box is a questioning quip. 
 The printed name is "what will happen if you burn the box". The true-name is "what will happen if you burn the box". 
 Understand "I" or "when" or "burning" or "fire" as what will happen if you burn the box.  The comment is "'What will happen if I burn this box?'".
 It mentions heart-sized box.
 The response is "She glances at [the heart-sized box] sideways. '[if the hart's heart is in the box]Nothing good,[otherwise]Nothing,[end if]' she says, 'except that you will have to go back empty-handed to the Queen.'".
 It quip-supplies Snow White.
 It assumes burn-box.
 It is listed. 

Availability rule for whether the King was murdered:
	if the player knows blood-sundering and the player knows you-were-king:
		it is off-limits.

whether the King was murdered is a questioning quip.  The comment is "'Do you think that the King was murdered? Is that it? Or used up in some magical rite?'".
 It mentions death, murder, King.
 The response is "'No,' she says. 'Or not simply, anyway. What I think happened to the King was something more than a simple sacrifice.'".
 It directly-follows why the king left.
 It quip-supplies Snow White.

Availability rule for whether the King was a werewolf:
	if the player knows blood-sundering and the player knows you-were-king:
		it is off-limits.

 whether the King was a werewolf is a weak questioning quip.  The comment is "'Do you mean that the King was... or became... a werewolf?' You have never seen such a creature, and you would expect to have noticed if one lurked in the woods — assuming that such things are real. ".
 Understand "werewolves" or "lycanthropy" as whether the king was a werewolf.
 It mentions King.
 The response is "'No. Something more unusual and much more frightening, in its way.'".
 It directly-follows why the king left.
 It quip-supplies Snow White.

Rule for printing the name of what exactly she surmises:
	say "what exactly she surmises[unless the current quip is why the king left] about the King[end if]".

 what exactly she surmises is a questioning quip.  The comment is "'[if immediately]And what do you surmise?[otherwise]When you said you surmised what had happened to the king — what did you think that was?[end if]' You are close to something here: you can sense it.".
Understand "guess" or "surmise" or "about" or "the" or "king" as what exactly she surmises.
 It mentions King.
 The response is "She makes a face[set body-context to Snow White]. [if the player knows snow-white-possessed]'The answer is partly hidden even from me.'[otherwise]'I have difficulty being sure.'[end if][paragraph break]'But what do you think happened?' you ask. 'Whether you're sure or not.' [paragraph break]She answers slowly. 'There is an old magic, blood-sundering[blood-sundering][snow-white-knows-sundering]. By it daughters are made not-daughters, and sons not-sons; husbands and wives no longer know one another. Family is negated. Family members become strangers. [paragraph break]'I don't think that the king died, or went into exile. I think he was made not to be the king any longer[king-not-king]. But I [italic type]can't remember[roman type][queue what-do-you-recall as postponed obligatory]. Nothing about that night save the red ring around the moon, and the drumming of the dwarrows that went silent by dawn.'".
 It indirectly-follows why the king left.
 It is negated by blood-sundering.
 It quip-supplies Snow White.
 It is listed.

that you seem the king is an informative quip. 
 The printed name is "that you are the king". The true-name is "that you seem the king". 
 Understand "I am" or "are" as that you seem the king.  The comment is "'I am the King[unless the player knows snow-white-knows-sundering]. I was blood-sundered[end if].'".
 It mentions King, yourself, magic.
 The response is "[one of]'You shouldn't know that, if it is true,' she says, perturbed. 'Though it would explain— but...' She frowns, thinking over this puzzle, and then gradually — as blood dissipates in clear water — the frown goes away and her face[set body-context to Snow White] becomes blank. Either she has forgotten what you told her again, or she has some reason for pretending to do so.[or]She looks both surprised and annoyed. 'I know!' she says, though at the same time it seems as though she didn't. 'It's very strange that you should be, though, because then — perhaps you are my father after all? But that does not make sense, because the Queen...' And she trails off, like a child falling asleep[pathetic].[or]'You keep saying that,' she says. 'But I don't think it can be right. You would have had to be married to the Queen.' But soon that train of thought is gone again.[or]She rubs her eyes[set body-context to Snow White]. 'I feel like this keeps happening,' she says [pathetic]petulantly.[stopping]".
 It quip-supplies Snow White.
 It is repeatable. [* We want to allow the player to wear himself out repeating this, if he wants — she is just not going to hang on to it.]
 It assumes blood-sundering, you-were-king.

 that the King was blood-sundered is an informative quip.  The comment is "'I believe that the King was blood-sundered,' you say. 'If you know what that means. It is a kind of magic where—'".
 It mentions magic, king.
 The response is "'I know,' she says, looking amused. [scary]'It is something I have often wondered myself. He would have had to be cast out into the village, I think... or perhaps he would have gone a little further away. Not too far, though, I think[snow-white-knows-sundering][if the player does not recollect what-do-you-recall][queue what-do-you-recall as postponed obligatory][end if].'".
 It quip-supplies Snow White.
 It assumes  blood-sundering.
 It is negated by snow-white-knows-sundering.

what-do-you-recall is a q-transitional NPC-directed quip.
	The response is "[if the previous quip is yes-22]Only a fragmentary curiosity remains with her[otherwise if what-do-you-recall is not quippishly-relevant]She gets a far-away, thoughtful look — as though realizing something that has been in the back of her mind for a little while[otherwise]A curious look comes into her eyes[set body-context to Snow White][end if]. 'What do you remember about the night the King left?'" [* 'not quippishly-relevant' means 'something that doesn't seem to go with the current strand of discussion'; in other words, this will be true if Snow White reverts to the topic after a long ]
	The nag is "[one of]'Try to think. This is important.'[or]'Tell me: the night the King left. Try to remember.'[or]'Well?'[stopping]".
	It is restrictive.
	It quip-supplies Snow White.
	
 make up some lie is a weak performative quip. The comment is "'I remember the moon, red as though it had been dipped in blood, and then turning almost black,' you say. 'And I heard people moving in the darkness during the night. They might have been human or they might have been dwarf.'".
Understand "invent" or "devise" or "improvise" or "false" or "falsehood" or "prevaricate" or "prevarication" as make up some lie.
 It mentions dwarrows.
 The response is "She snorts. 'You remember no such thing[angry]. You're a poor liar.'".
 It directly-follows what-do-you-recall.
 It quip-supplies Snow White.
 It is listed.
	
 that you remember very little is an informative quip. 
 The printed name is "that you remember very little". The true-name is "that you remember very little". 
 Understand "I" or "memory" or "recollection" or "recall" or "nothing" as that you remember very little.  The comment is "'Very little.' Which is the truth. You would think that a night with a red-ringed moon, and the dwarrows drumming in the distance, would have been memorable to you. But all that seems infinitely distant now. The departure of the King was the beginning of a different time for everyone: the world became stranger and colder. You can barely remember how it was before.".
 It mentions King.
 The response is "'Strange.' She is looking at you with great attention, as though there were something she wanted to read printed on your forehead. 'You really don't remember. And I don't remember. And the Queen doesn't remember -- I have heard her restless questions to the mirror often enough[conditionally queue what became of the King].'".
 It directly-follows what-do-you-recall.
 It quip-supplies Snow White.
 It is listed. 

whether you could hath performed the blood-sundering is a weak questioning quip.
 The printed name is "whether you could have performed the blood-sundering". The true-name is "whether you could hath performed the blood-sundering". 
 Understand "have" or "I" or "if" or "can" as whether you could hath performed the blood-sundering.  The comment is "'Could I have performed the blood-sundering?'".
 It mentions yourself, witchcraft.
 The response is "'Considering your ignorance to this point, I rather doubt it.'".
 It quip-supplies Snow White.
 It assumes  blood-sundering, snow-white-knows-sundering.

whether the King could hath performed the blood-sundering is a questioning quip.
 The printed name is "whether the King could have performed the blood-sundering". The true-name is "whether the King could hath performed the blood-sundering". 
 Understand "have" or "if" or "can" as whether the King could hath performed the blood-sundering.  The comment is "'Could the King have performed the blood-sundering?'".
 It mentions King, witchcraft.
 The response is "She looks startled. 'It is possible. But to what end? If he had wanted the power to accomplish some great victory in the war, he could just as easily have performed the sundering on someone else.'".
 It quip-supplies Snow White.
 It assumes  blood-sundering, snow-white-knows-sundering.

whether Snow White could hath performed the blood-sundering is a questioning quip.
 The printed name is "whether Snow White could have performed the blood-sundering". The true-name is "whether Snow White could hath performed the blood-sundering". 
 Understand "have" or "she" or "if" or "can" as whether Snow White could hath performed the blood-sundering.  The comment is "'Could you have performed a blood-sundering?'".
 It mentions Snow White, witchcraft.
 The response is "'[if the player knows snow-white-possessed]If you mean the young princess, no: she did not do it. We were not merged into one until... some time afterward. The king vanished first; then the queen ran mad; then there was the mirror and the magic and the demons.[otherwise]Me? At the age of six? I was not so precocious.[end if]'".
 It quip-supplies Snow White.
 It assumes  blood-sundering, snow-white-knows-sundering.
 

 whether the dwarrows could hath performed the blood-sundering is a questioning quip. 
 The printed name is "whether the dwarrows could have performed the blood-sundering". The true-name is "whether the dwarrows could hath performed the blood-sundering". 
 Understand "have" or "if" or "can" as whether the dwarrows could hath performed the blood-sundering.  The comment is "'Do you think the dwarrows performed the blood-sundering? Could they have done that?' This all seems quite unreal.".
 It mentions dwarrows, war, witchcraft.
 The response is "'Maybe,' she says. 'I would not have expected it, but it is imaginable.'".
 It quip-supplies Snow White.
 It assumes blood-sundering and Snow-White-knows-sundering.


 whether the Queen could hath performed the blood-sundering is a questioning quip.
 The printed name is "whether the Queen could have performed the blood-sundering". The true-name is "whether the Queen could hath performed the blood-sundering". 
 Understand "have" or "if" or "can" as whether the Queen could hath performed the blood-sundering.  The comment is "'Do you think the Queen did [if immediately]it[otherwise]the blood-sundering[end if]?' you ask.".
 It mentions witchcraft, Queen.
 The response is "She laughs, a fey unpleasant laugh[scary]. '[if the player knows Snow-White-possessed]I know every magic the Queen has performed from the beginning of her dabbling until quite recently, and she did not perform this one.[otherwise]I am certain that she did not.[end if]'".
 It quip-supplies Snow White.
 It assumes blood-sundering and Snow-White-knows-sundering.

 what became of the King is a questioning quip.  The comment is "'What would have become of the King after this blood-sundering, if he became not-himself?'".
 It mentions King.
 The response is "'I doubt he would have gone far,' she says. 'He might still live nearby — in the village, in the palace itself. Who knows? The Queen might not even know him for who he is[casually queue anyone-move-in].'".
 It is negated by you-were-king.
 It indirectly-follows what exactly she surmises.
 It quip-supplies Snow White.
 It is listed.

anyone-move-in is an NPC-directed quip.
	The response is "'Did anyone new come to the village around the time of the King's departure?' she asks."
	It is restrictive.
	It quip-supplies Snow White.
	
shrug again is a performative quip. The printed name is "shrug". The true-name is "shrug again".  The comment is "'I can't recall.' You were, after all, new to the village yourself at the time.".
 Understand "[maybe]" as shrug again.
 It mentions yourself.
 The response is "'Hm.' She frowns in concentration. 'There are other people in town who might remember, but we can hardly ask anyone else now.'".
 It directly-follows anyone-move-in.
 It quip-supplies Snow White.
	
no-24 is a negative quip. The printed name is "say no". The true-name is "no-24".  The comment is "'Not that I can remember.' Except yourself — but you'd always lived nearby.".
 It mentions yourself.
 The response is "'I see. Maybe he went somewhere else, then.' [for eye-gest]She makes a face[set body-context to Snow White] that looks curiously frustrated.[or walk-gest]She walks away and kicks a few times at a root that apparently offends her.[or breath-gest]She makes an annoying sequence of clicking noises with her tongue.[end for][pathetic]".
 It directly-follows anyone-move-in.
 It quip-supplies Snow White.

 whether the King could be restored is a questioning quip.  The comment is "'Would it be possible to undo this blood-sundering magic?'".
Understand "can" as whether the King could be restored.
 It mentions King, witchcraft.
 The response is "She considers. 'It would,' she says. 'But at the cost of losing much of what the blood-sundering achieved. It is a great source of power; whoever performed the magic gained something considerable by it[power-from-sundering].'".
 It assumes snow-white-knows-sundering.
 It quip-supplies Snow White.
 It is listed.

 yes-22 is an affirmative quip. The printed name is "say yes". The true-name is "yes-22".  The comment is "'Myself,' you say. 'But I had always lived nearby. The village is full of cousins of mine. Other than that, I believe not.'".
 It mentions yourself.
 The response is "'Hm.' Her eyes[set body-context to Snow White] meet yours and for just a moment you feel as though you are both thinking the same thing — something extraordinary [friendly]— and then she shakes her head and the idea, whatever it was, is gone again. ".
 It directly-follows anyone-move-in.
 It quip-supplies Snow White.

 how the blood-sundering could be done is a questioning quip.  The comment is "'How would a blood-sundering be done?' you ask. 'I am curious about the methods.'".
Understand "can" or "blood" or "sundering" as how the blood-sundering could be done.
 It mentions witchcraft.
 The response is "She [for eye-gest]looks at you sidelong and sharply[or breath-gest]hums under her breath[or walk-gest]gives a curious half-hop on the balls of her feet[end for][if the player does not know sundering-method]. 'That is not the first question an honest man would have asked,' she remarks[end if]. '[if the player does not know magic-involves-demons]Like all magic, such things must be done with the cooperation of a demon. [end if]One would invoke the desired power, make certain offerings, spell out the nature of the exchange. The victim would have to be present: perhaps bound, but it would give the demon still greater satisfaction if he remained of his own free will, being compelled by something even more terrible. Then—' She shrugs. '—there would be some violence to the victim's soul, such as a demon is never allowed to perform unless men themselves provide the opening. Tear out a man's tie to family and friends, and you half-destroy him; he becomes more violent, less constrained.'".
 It quip-supplies Snow White.
 It assumes power-from-sundering.

 how the blood-sundering could be undone is a questioning quip.  The comment is "'How would a blood-sundering be undone?' you ask.".
Understand "can" or "blood" or "sundering" as how the blood-sundering could be undone.
 It mentions witchcraft.
 The response is "She gestures with her chin at the heart-sized box. 'That,' she says. 'It has the power to undo many sorts of magics that work on the soul. You'd need to find the King and convince him to contribute a little blood to the cause[sundering-method]. Fill the box part-way full, and then light it on fire in front of him. Then — if you have the right person — the magic is undone and he is himself again.'".
 It quip-supplies Snow White.
 It assumes power-from-sundering.
 It is listed.

 point out that the war ended as the King vanished is a performative quip. The comment is "'The war ended when the King vanished,' you point out. 'Perhaps the dwarrows used the blood-sundering in order to make him unable to fight, in order to bring an end to the conflict. Perhaps they used the power they gained to attain something else — perhaps acceptance among humans.'".
 It mentions war, dwarrows.
 The response is "'That is possible,' she says. But you can tell that she does not think it very likely.".
 It indirectly-follows whether the dwarrows could hath performed the blood-sundering.
 It quip-supplies Snow White.
 It is listed.



Section 9 - Happy on the King

where the King went is a questioning quip. The comment is "[if the player knows blood-sundering]'Is it true that the King was blood-sundered? And, if so, where did he go?'[otherwise]'Where is the King?' You lean towards him eagerly. 'He vanished, but where did he go? Did the Queen kill him?'[end if]".
 It mentions King.
 The response is "His laughter is sharp. 'You are the King[you-were-king].'".
 It quip-supplies the corpse.
 It assumes will-burn-box.
 It is listed.
 It is restrictive.

request that he repeat himself is a performative quip. The comment is "'I'm sorry — what did you just say?'".
 Understand "what" or "repetition" or "Happy/corpse/hart" or "hart corpse" or "decomposing hart corpse" or "decomposing corpse" as request that he repeat himself.
 It mentions King, corpse.
 The response is "'This is fun,' he says. 'I could do this all day. I said that you [italic type]are[roman type] the King. Not that you'll find it easy to retain that information. Try to hold onto it, or it will slip out of your head again.'".
 It directly-follows where the King went.
 It quip-supplies corpse.
 It is listed.

what happened to me is a questioning quip. 
	Understand "you" as what happened to me. The printed name is "what happened to you". The true-name is "what happened to me". The comment is "'That's impossible — I don't remember being King. I grew up nearby... I have cousins in the village. The King on the other hand—'".
 It mentions King.
 The response is "'Probably has cousins in the village as well, considering the way his father and grandfather behaved while they were alive,' says [the corpse] dryly. 'But you wouldn't remember[queue blood-sundering-clue as immediate obligatory].'".
 It directly-follows request that he repeat himself.
 It quip-supplies corpse.
 It is listed.
 It is restrictive.

blood-sundering-clue is an NPC-directed quip.
	The response is "'[if the player knows blood-sundering]That's how blood-sundering works[otherwise]You've been blood-sundered. It is a magic that [blood-sundering]unmakes families, destroys the connections between people, and in so doing yields great power that can be used for other purposes[end if].'"

who performed the magic is a questioning quip.
	The comment is "[if immediately]'But — who would have done such a thing to me?[otherwise]'Who would have performed such magic on me?'[end if]".
	It mentions magic, King, yourself.
	The response is "'As King, you were not kind to the dwarrows,' he says, his voice harder than you have heard ever before. 'You provoked us. You gave plain preference to your own kind, allowing them to mistreat us, upsetting the balance that your father had — with great difficulty — established. There was suffering and war.'"
	 It quip-supplies corpse.
	It directly-follows blood-sundering-clue.
	It is listed.
	It is restrictive.

how the current peace came about is a questioning quip.
	The comment is "'I don't understand.' This makes less and less sense as you go. 'If I provoked the dwarrows — if I was such a bad King —' (and how could you have been the King at all?) '— then how is it that there is peace now?'"
	It mentions war.
	The response is "'When you saw what you had done, you felt— you regretted it.' He tilts his head at you. 'You decided to create a peace. But that is very difficult work, and it is not the usual use of magic.' He smirks, if an animal's face[set body-context to corpse] can be said to hold such an expression. 'Demons arrange magic, and as a rule they prefer to do more harm than good. So you were forced to sacrifice yourself, your family, and lose your throne, and dwindle into no one. In exchange for this, we have had this— ragged sort of calm[casually queue appreciate-the-gift]."
	 It quip-supplies corpse.
	It directly-follows who performed the magic.
	It is listed.
	
how blood-sundering might be undone now is a questioning quip. The comment is "'Tell me[if the player knows you-were-king], then[end if], how can blood-sundering be revoked? Undone? Made null?'".
 Understand "blood" or "sundering" as how blood-sundering might be undone now.
 It mentions magic.
 The response is "He looks suddenly shifty. 'No, I don't have to answer that one,' he says. 'Because if I told you, and you did it, it would revoke, undo, and make null our agreement, and if our agreement is null, I don't have to tell you anything. You're on your own with the terrifying magics. That's demon-territory anyway.'".
 It quip-supplies corpse.
 It assumes blood-sundering.

appreciate-the-gift is an NPC-directed quip.
	The response is "'Don't mistake me,' he adds. 'A poor peace is better than none. I appreciate the gift — though I doubt that anyone else does. It cannot be known what you did, you see. So the dwarrows still hate your name.'"


Section 10 - The New Snow White 

where the haven seems really is a questioning quip. 
 The printed name is "where the haven is". The true-name is "where the haven seems really". 
 Understand "is" as where the haven seems really.  The comment is "'Where is your safe haven?' you ask. 'You never told me.'".
 It mentions safe haven.
 The response is "'North,' she says numbly. 'We— I put it to the north, among the dwarrows. That might not have been the wisest choice, but she seemed to know how to control them. It was easy for her to make them do her bidding[haven-location].'".
 It quip-supplies new-Snow-White.
 It is negated by haven-location.
 It is listed.

 how Lilith manipulated the dwarrows is a questioning quip.  The comment is "'How did Lilith control the dwarrows? Did she cast a spell on them, put them under her power somehow?' How wide-spread was this creature's control, anyway?".
 It mentions dwarrows, Lilith.
 The response is "She shakes her head. 'There was no magic in it. She just could see what they wanted, and how to make them think that they were doing what they wanted when in fact they were doing what [italic type]she[roman type] wanted. I didn't like her. But that would be a useful ability to have.'".
 It indirectly-follows where the haven seems really.
 It quip-supplies new-Snow-White.
 It is listed. 

 where the King seems is a questioning quip. 
 The printed name is "where the King is". The true-name is "where the King seems". 
 Understand "is" as where the King seems.  The comment is "'Do you know where to find the King?' you ask.".
 It mentions king.
 The response is "She looks puzzled. 'He has been gone a long time. No one expects him to return. I do not think he is dead, but he is likely not coming back.'".
 It quip-supplies new-Snow-White.
 It is negated by you-were-king.
 It is listed.

 what the heart-sized box was for is a questioning quip.  The comment is "'You don't happen to know what the heart-sized box was originally for, do you?''".
 It mentions heart-sized box, witchcraft.
 The response is "*** she shouldn't ever reach this point ***".
 It quip-supplies new-Snow-White.
 It is listed.
 It is repeatable.

 why the exorcism succeeded is a questioning quip.  The comment is "'Why do you suppose that exorcism succeeded? I do not understand why a little smoke was enough to drive Lilith off.'".
 It mentions heart-sized box, demons, exorcism, Lilith, witchcraft.
 The response is "*** unreachable ***".
 It quip-supplies new-Snow-White.
 It is listed.
 It is repeatable.

 what the library contained is a questioning quip.  The comment is "'[one of]What[or]I ask again: what[stopping] books were in the Queen's library, and where did she get them?'".
 It mentions the library, witchcraft.
 The response is "*** she shouldn't ever reach this point ***".
 It quip-supplies new-Snow-White.
 It is listed.
 It is repeatable.
 It indirectly-follows what studies.

 whether she ever saw any other powers besides Lilith is a questioning quip.  The comment is "'[one of]Did you ever see any other powers besides Lilith, when you observed the Queen?[or]I asked you about powers the Queen summoned. I must know. Were there any besides Lilith?[stopping]'".
 It mentions powers, witchcraft, demons.
 The response is "*** she shouldn't reach ***".
 It quip-supplies new-snow-White.
 It is listed.
 It is repeatable.

 how did vampire teeth grow is a questioning quip.  The comment is "'[one of]Tell me, when you gained the teeth of a vampire, did they grow all at once to a new size[or]But surely you can at least remember what it felt like when your teeth changed shape[or]Why won't you tell me about your teeth[stopping]?''".
 It mentions vampirism, Snow White's teeth.
 The response is "*** shouldn't reach ***".
 It quip-supplies new-Snow-White.
 It is repeatable.

 what Lilith wanted is a questioning quip.  The comment is "'What did Lilith want?'".
 It mentions Lilith.
 The response is "'I am not certain,' Snow White replies. 'She was better at hiding her counsels from me than I from her. Sometimes I could not guess why she made us do the things she did. But sometimes I think she was jealous of me — and wanted to be something ordinary — and at other times she seemed very angry.'".
 It quip-supplies new-Snow-White.

 how the mirror works is a questioning quip.  The comment is "'What do you know about the magic mirror and its workings?'".
 It mentions magic mirror.
 The response is "*** she shouldn't ever reach this point ***".
 It quip-supplies new-Snow-White.

 whether she likes apples is a questioning quip.  The comment is "'Do you like apples?".
 It mentions apple.
 The response is "'Do you have one?' she asks eagerly. 'I am starving.' She looks quite disappointed when it turns out that you have no hidden food supplies.".
 It quip-supplies new-Snow-White. 
			
Definition: a quip is nerve-wracking if it mentions vampirism or it mentions witchcraft or it mentions magic mirror.

Before discussing something nerve-wracking when the current interlocutor is new-snow-white:
	say "[one of][or]'This is important,' you tell her. [or]You move around so that you are in front of her again, impossible to ignore. [or]'I need you to think!' you exclaim. Her weakness is exasperating — what feeble stock she is! [or][stopping][run paragraph on]"

Instead of new-Snow-White discussing something nerve-wracking:
	increase insanity count by 1;
	if insanity count is:
		-- 1:
			if the current quip mentions vampirism:
				say "'Please don't ask me,' she says. 'I remember-- too clearly.'";
			otherwise:
				say "She shakes her head. 'All knowledge of that sort belonged to Lilith, not to me,' she says. 'Even when she wanted us to read things, it is she who remembered them.'";
		-- 2:
			say "She shivers. 'Please -- I can't tell you. It is horrible to think about.'";
		-- 3:
			say "She looks glassy and pretends not to hear, but her eyes[set body-context to Snow White] are filling with tears.";
		-- 4:
			say "Instead of answering, she looks green, and runs over to retch into the undergrowth. It does not appear that she actually had anything to vomit up.";
		-- otherwise:
			say nervous breakdown. 
	
Insanity count is a number that varies.
	
To say nervous breakdown:
	say "This appears to be one question too many, because she flings herself on the forest floor, screaming and crying. What she says isn't very articulate, but you can pick out bits like 'leave me alone!' and 'go away' and 'I'm sorry I'm sorry.'

Later her shouts change. 'Come back,' she says sometimes; and 'I'll have the apple then' and even once '...blood, blood. Hungry.'[paragraph break]";
	wait for any key;
	say "The sun rises and brings no greater degree of sanity. On the contrary, she scoots away from the filtering patches of light and huddles at the base of one of the larger pines, still crying and occasionally screaming.[paragraph break]";
	wait for any key;
	say "You try leaving her alone for a while in case exhaustion brings her some release from the fit: sometimes those taken with a shaking disease come around again, given time.

Finally, around midday, she is worn out enough to sleep. But you can go neither forward nor back, cannot take a damaged princess to the haven nor return her to the castle; you will have to find some third way.";
	record "Exorcising Snow White, then driving her mad with questions" as an ending;
	end the game saying "Snow White is mad". [* From a story-telling point of view, we want to emphasize how seriously Snow White's experience has damaged her -- she must be suffering from major trauma, and it's reasonable for her to crack sooner or later. (Maybe actually it would take a little longer for the cracks to manifest themselves, but the game scale is compact.) From a game-play point of view, we want to narrow the player's options for finding things out so that he is forced to discover most of the cool answers while he is still dealing with Lilith. This is riskier but will also lead to other interesting outcomes.]


Chapter 25 - Repeatable Quips

[This section is for a handful of pieces of information that we want to allow the player to ask about repeatedly, with regenerated variations of the initial output.]

[First, we have some mood-dependent way of introducing used facts again:]

To say fact-reintroduction: 
	if anger is greater than 0 and a random chance of 1 in 3 succeeds: [* Some but not all of the time, she will make a snippy comment about being asked to repeat herself]
		say "[one of]She sighs. '[n-s][or]'If we must go over this again... [n-s][or]'You really do have a memory like a sieve, don't you? [n-s][or]'I see why the Queen never made you Chief Archivist. [n-s][or]She groans. '[n-s][or]'I imagine this is how the Queen's mirror feels — always the same questions... [n-s][or]'Try to remember this time. [or]'Again? [n-s][stopping]";
	otherwise if the mood of Snow White is warm: [* If she's friendly toward you, then she never says anything rude about having to repeat herself.]
		say "[one of]'Oh, you remember: [c-s][or]'Weren't we just talking about this? I'm not sure I remember much else, but let's see. [n-s][or]'Hm. [n-s][or]'I think I've said all I know: [c-s][or]'Hm.' She frowns. 'I think I told you [c-s][at random]";
	otherwise:
		say "[one of]'I've already told you: [c-s][or]'All right, to review,' she says. '[n-s][or]She represses a sigh. '[n-s][or]'I've said all I know: [c-s][or]'You've heard what I can tell you: [c-s][or]'Once again... [n-s][or]'It's like I said before. [n-s][at random]"; [* The n-s and c-s stuff is markup to indicate whether the new information should begin with a capital letter or not.]
		


New-sentence is a truth state that varies. To say new-sentence: now new-sentence is true. To say not new-sentence: now new-sentence is false.

To say n-s: say new-sentence; To say c-s: say not new-sentence.

Stuttering is a truth state that varies.

To say (chosen list - a list of text) stuttering out of order:
	sort chosen list in random order;
	now stuttering is true;
	say chosen list in order.
	
To say (chosen list - a list of text) out of order:
	sort chosen list in random order;
	say chosen list in order.
	
To say (chosen list - a list of text) stuttering in order:
	now stuttering is true;
	say chosen list in order.
	
To say (chosen list - a list of text) in order:
	let N be the number of entries in chosen list;
	let index be 0;
	repeat with item running through chosen list:
		increase index by 1;
		let repetition flag be true;
		let this-count be 0;
		while repetition flag is true:
			increase this-count by 1;
			if new-sentence is true:
				let sample be indexed text;
				let sample be "[item]";
				if character number 1 in sample is in lower case:
					let X be indexed text;
					let X be character number 1 in sample;
					let X be "[X in upper case]"; [* We do this rather than using sentence case because setting the whole sample to sentence case will flatten any capital I elements that we might want to keep.]
					replace character number 1 in sample with X;
				say "[sample]";
			otherwise:
				say "[item]";
			if this-count is 1 and stuttering is true and a random chance of 1 in 3 succeeds:
				let repetition flag be true;
			otherwise:
				let repetition flag be false;
			if this-count is greater than 1:
				change repetition flag to false; [* Stop fact-repetition if you've done any given item twice]
			if index is N and repetition flag is false:
				say ".[run paragraph on]";
			otherwise:
				if this-count is greater than 1 and a random chance of 1 in 2 succeeds:
					say ".[paragraph break]'[n-s]"; [* This is so that there's a fair chance of her breaking up into paragraphs passages that focus on the same things.]
				otherwise:
					say "[one of]. [n-s][or]; [c-s][or] and [c-s][or]. And [c-s][or]. Also, [c-s][at random]";
	now stuttering is false.

[Now: to produce responses after the first one, we put variations of each sentence into a list, and shuffle the list before replaying. The result will be randomized rephrasings of the same information, presented in varying orer.]
 
A quip can be dull or interesting. A quip is usually interesting. [* In fact we hope that these are interesting too, but they are relatively default information rather than plot-advancing stuff.]

Does the player mean discussing a dull quip:
	it is unlikely.

 who her real mother was is a dull questioning quip.  The comment is "'[one of]Who was [or]Do you know anything else about [or]Can you tell me again about [stopping]your real mother?'".
 It mentions family.
 The response is "[one of]'She was from the north. They have very fair skin there. She loved music, and embroidery, and quiet. She died when I was very young.'[or]'Not really.'[or][fact-reintroduction][Mom-comments out of order][if anger is less than 0][one of] I know it isn't much to know about one's own mother, but she didn't leave much evidence behind.[or][stopping][end if]'[line break][stopping]".
 It is repeatable.
 It quip-supplies Snow White.

Mom-comments is a list of text that varies. Mom-comments is {
	"[one of]she was from the north, where they have fair skin[or]she looked like me[or]she came from away in the north[or]she was from a petty noble family in the north[or]she had very fair skin, like mine, because she came from the north[at random]", 
	"[one of]she liked music[or]she liked embroidery[or]I have a few of her embroidery pieces[or]she left some instruments behind[or]she was good at needlework[or]her talents were for music and embroidery[or]she played a few instruments[at random]", 
	"[one of]she died when I was very young[or]I was young when she died[or]I don't remember her because she died before I was very old[at random]"
	}.
	
 who her real father was is a dull questioning quip.  The comment is "[if the player knows not-daughter]'[one of]Who was [or]Can you tell me again about [stopping]your real father?'[otherwise]'Tell me about your father.'[end if]".
 The printed name is "who her [if the player knows not-daughter]real [end if]father was".
 It mentions family.
 The response is "[if the player knows not-daughter][one of]'[n-s][or][fact-reintroduction][stopping][Dad-comments in order]'[line break][otherwise][not-daughter]'We weren't acquainted. I only know a few things about him.' At your surprised look, she adds, 'I'm no relative of the King, if that is what you thought.'[end if]".
 It is repeatable.
 It quip-supplies Snow White. 

Dad-comments is a list of text that varies. Dad-comments is {
	"[one of]I never knew him[or]even the rumors of his identity might be wrong[or]the servants who raised me would not speak of him[or]I used to know more, but I forgot it[at random]", 
	"[one of]he came from these parts[or]his family was good[or]his relatives were well-known around here[or]he was a native of this region[at random]",
	"[one of][one of]I know nothing about[or]I can't tell you about[or]don't ask for anything concerning[at random] his [one of]hobbies[or]hobbies, likes, or dislikes[or]hobbies, likes, dislikes, traits, personality, the nature of his character, not even what he looked like[stopping][or]he fought in the dwarf wars[at random]"
	}. 


 what she knows about the king is a dull questioning quip. 
 Understand "snow white knows" or "king" as what she knows about the king. 
 It mentions King.
 The comment is "'Tell me[one of][or] again[stopping], what do you know of the King[one of], whatever you remember hearing about him[or][stopping]?'".
 The response is "[one of]'[n-s][or][fact-reintroduction][stopping][king-comments stuttering in order]'[line break]".
 It quip-supplies Snow White.
 It assumes king-gone.
 It is repeatable. 

king-comments is a list of text that varies. 

king-comments is {
	"[one of]he is said to have been a good King, loyal to his people and to his wife[or]he was not very old when he left[or]the people admired him[or]he married for love and governed from affection[at random]",
	"[one of]he used to lead great hunts through these woods[or]at Christmas he held feasts for all the villagers around[or]he gave the poor from his own treasury[or]he had great difficulty settling disputes[or]his greatest flaw (they say) is that he was more merciful than just[or]those who loved him least were those who preferred strictness and cold discipline[or]he was not much like his [king-had-stern-father]stern father[at random]",
	"[one of]it was his fault that war with the dwarf lords began anew[or]he gave offense to the dwarrows by preferring his own people[or]he had great remorse over the war[or]he blamed himself[at random]"
	}.
	
Availability rule for what she knows about the king's father:
	if the player does not know king-had-stern-father:
		it is off-limits.
	
 what she knows about the king's father is a dull questioning quip. 
 Understand "snow white knows" as what she knows about the king's father. 
 The comment is "'Tell me[one of][or] again[stopping], what do you know of the King's father[one of], whatever you remember hearing about him[or][stopping]?'".
 The response is "[one of]'[n-s][or][fact-reintroduction][stopping][king-dad-comments in order]'[line break]".
 It quip-supplies Snow White.
 It is repeatable. 

king-dad-comments is a list of text that varies. 

king-dad-comments is {
	"[one of]he was not always popular among his own people[or]he was a grim old man[or]his justice was swift[or]he was a miser with his money and his heart[at random]",
	"[one of]he brought a kind of peace between men and dwarrows[or]he suppressed the warfare with the dwarrows[at random]",
	"[one of]everyone, men and dwarrows alike, lived under strictest laws while he ruled[or]he tolerated nothing, no levity or high spirits or childlike mistake, that might set the dwarrows against men[at random]"
	}.

what she knows about the war is a dull questioning quip. 
 Understand "snow white knows" as what she knows about the war. 
 It mentions war.
 The comment is "'Tell me[one of][or] again[stopping], what do you know of the war?'".
 The response is "[one of]'[n-s][or][fact-reintroduction][stopping][war-comments in order]'[line break]".
 It quip-supplies Snow White.
 It is repeatable. 

war-comments is a list of text that varies. 

war-comments is {
	"[one of]war between men and dwarrows has happened many times over[or]there have been many wars[or]some say men and dwarrows cannot live in peace naturally, because peace so seldom lasts[at random]",
	"[one of]there are others who say that men might easily wipe the dwarrows out completely, if they would only harden their hearts[or]in recent years the dwarrow have come close to ceasing to exist[at random]",
	"[one of]nonetheless, a dwarf can still do heavy damage to a man[or]all the same, when we were at war with them, they killed many of our own[or]it is a mistake to underestimate their strength or their will to live[at random]"
	}.


 who are the servants is a dull questioning quip. The printed name is "who the servants are". The true-name is "who are the servants". [* The adjusted name sounds better in phrases like "You could ask who are the servants."] 
 It mentions safe haven.
 The comment is "'Who are these servants? Will they protect you from the Queen?'".
 The response is "[one of]'They will keep me hidden, and take care of me[pathetic], but no one is safe from the Queen once she discovers you.'[or][fact-reintroduction][servant-comments in order]'[line break][stopping]".
 It quip-supplies Snow White.
 It indirectly-follows what seems at the safe haven.
 It is repeatable.
 It is listed.

Servant-comments is a list of text that varies. Servant-comments is {
	"[one of]the servants will hide me[or]my servants at the cottage will look after me[or]I have servants who will look after me[at random]", 
	"[one of]the Queen is a formidable adversary[or]it is hard to be sure you are safe from the Queen[or]the Queen does not rest easily once she decides someone is her enemy[or]when the Queen finds you, no one is safe[or]it is impossible to be completely safe from the Queen[at random]"
}.


 what she knows about dwarrows is a dull questioning quip. 
 Understand "snow white knows" or "dwarrows" as what she knows about dwarrows. 
 It mentions dwarrows.
 The comment is "'Tell me[one of][or] again[stopping], what do you know of the dwarrows?'".
 The response is "[one of]'[n-s][or][fact-reintroduction][stopping][dwarf-comments in order]'[line break]".
 It quip-supplies Snow White.
 It is repeatable.  

dwarf-comments is a list of text that varies. 

dwarf-comments is {
	"[one of]they are their own kind[or]they are proud[or]they were made differently from men and live apart from us[at random]", 
	"[one of]the Queen still fears them very much because of past wars[or]there was a time when they were a threat, it is said[or]in the past they were at war with us[or]there were long battles, in years past[at random]",
	"[one of]in recent years they have given no trouble[or]they have lately become friendly to the kingdom[or]now the danger has passed[or]the blame was given to both dwarrows and men, at different times[or]though they have not attacked recently there are still old peasant women who put up garlic and buckthorn to keep them away (as though that would)[or]though we are now at peace, there is still little mingling between the people and dwarrows[at random]"
	}.


 what studies is a questioning quip. 
 It mentions snow white, witchcraft, library.
 Understand "she" or "Snow White" as what studies.
 The comment is "'[if previous quip is why she sees the mirror's reflections as the queen does]'Studies'?[otherwise]You mentioned you've [']studied well.['][end if] [one of]Where [or]Tell me again, where [stopping]does one go to study such witchcraft and sorcery?' [if the player does not know snow-white-possessed][one of]You consider that had the girl been content to stay away from such knowledge, she might have led a safer and happier life.[or][stopping][end if]".
 The response is "[one of]'All that I know, I learned at my stepmother's knee — and then, when I fell out of her favor, in secret in her library[library-known]. If ever you find me unsettling, and begin to doubt the wisdom of our little pact, simply recall that the Queen is no better than I am — save that she has had more time to conceal herself.'[or][fact-reintroduction][learning-comments in order]'[line break][stopping]".
 It quip-supplies Snow White.
 It is repeatable.
 It indirectly-follows why she sees the mirror's reflections as the queen does.
 It is negated by snow-white-possessed.
 It is listed.

learning-comments is a list of text that varies. 
learning-comments is {
	"[one of]I learned at my stepmother's knee[or]my stepmother taught me[or]the Queen told me stories[or]the Queen educated me[at random]", 
	"[one of]later I studied in the Queen's library[or]her library served when she herself had stopped[or]afterward, I used her library[or]she has many books in her library that few can read[or]she should have kept me from her library as well[or]her books are numerous and very old[or]the time came when I could read books from her library that even the Queen could not understand[or]afterward... not even she can read all the books in her library[at random]"
	}.


what she knows about Eve is a dull questioning quip.  The comment is "'What do you know of Eve?[if the player does not know Snow-White-possessed] She died long before you were born. I doubt the stories tell her history fairly.[end if]'".
Understand "snow white knows" as what she knows about eve.
 It mentions Eve.
 The response is "[one of]'[n-s][or][fact-reintroduction][stopping][Eve-comments in order][one of][if the player knows snow-white-possessed] When she was in her last sickness, I stood by her pillow, and I spat in her face. And so she died.[end if][or][stopping]'[line break]".
 It quip-supplies Snow White.
 It is repeatable.
 It assumes  eve-named.

Eve-comments is a list of text that varies. Eve-comments is {
	"[one of]she had the disposition of a milkmaid[or]she smiled very prettily[or]she was blonde[or]her face was a perfect oval[or]her eyes were hazel (which is not really any color)[at random]",
	"[one of]she couldn't make up her mind whether Adam was like God, or like one of the animals to be petted[or]her favorite gesture was a nod[or]after sex she always told Adam it was very nice[or]her blank expression was the most dangerous[or]she was agreeable without being obedient[or]she liked to coo at Adam as though she were one of those doves[or]when she rebelled she cried afterward[or]she thought the snake was awfully clever for having doubts about God[at random]",
	"[one of]she made a terrible mother (too permissive and too strict by turns)[or]Cain loved her[or]Abel thought she was too protective[or]when the incident happened with Cain she blamed Adam[or]one of the angels had to teach her how to nurse the babies (even though she'd seen the animals do it often enough)[or]she refused to let Adam mention me aloud[at random]"
}

what she knows about Adam is a dull questioning quip.  The comment is "'[if the current quip is what Lilith Wants again]Tell me about him, then. Sometimes it eases the heart to remember.[otherwise if the player knows Snow-White-possessed]Tell me more about Adam. If there is anything you care to share, that is.[otherwise]What do you know of Adam?[end if]'".
Understand "snow white knows" as what she knows about adam.
 It mentions Adam.
 The response is "[one of]'[n-s][or][fact-reintroduction][stopping][Adam-comments in order]'[line break]".
 It quip-supplies Snow White.
 It is repeatable.
 It assumes  adam-named.

Adam-comments is a list of text that varies. Adam-comments is {
	"[one of]he was the first of God's really difficult projects[or]none of the animals were like him[or]the big gorillas and chimpanzees were jealous of him[or]he had all the signs of being the first of his kind[at random]",
	"[one of]he wanted to understand everything around[or]he wanted to build things too, like God[or]he made diagrams of himself in the sand at the bank of the river[or]he used to form little animals from clay[or]from time to time he would talk about the animals and plants he wanted to design[or]naming the animals was not enough for him[or]God found him funny at first[at random]",
	"[one of]when he was cast out of Eden he was very angry[or]sometimes he would go to the gate of Eden, afterward, and fence with the angel with the flaming sword[or]he never understood anything until he had lost it[or]when he was hurt or angry he demanded to be given rules to follow so that he could not be hurt the same way again[at random]"
}

 what she knows about God is a dull questioning quip.  The comment is "'[one of]What do you know about God?[or]But you must know something about God that I do not.[or]I'm still curious about anything you can tell me about God.[or]Anything else you can tell me about God?[stopping]'".
Understand "snow white knows" as what she knows about god.
 It mentions God.
The response is "[one of]'Fair point.'[or]'We are not really on speaking terms.'[or]'Look, why are you so bothered about Him? Do you think He's equally interested in you?' She stops, considering. 'Well, to be fair: He probably is a little. But if you think He's got your life solved already and is just waiting for you to puzzle out the right answers, you're wrong. In fact, He is probably pretty tired of people asking him for instructions.'[or]'Not really.'[stopping]".
 It is repeatable.
 It quip-supplies Snow White. 

what she knows about Eden is a dull questioning quip.  The comment is "'What [one of][or]else [stopping]do you remember of Eden?'".
Understand "snow white knows" as what she knows about Eden.
 It mentions Eden.
 The response is "[one of]'I don't miss it,' she says. 'Not any more, not since Adam was cast out. I never was in it for the animals and the self-growing food.'[or]'There's no point reminiscing,' she says. 'The old homestead is gone.'[or]'Well, if you want to know, there is one thing I miss. The stars were different there. Different pattern, different constellations. I liked them better than the ordinary ones we have now.'[or]She just shrugs and shakes her head as though irritated by this line of argument.[stopping]".
 It quip-supplies Snow White.
 It assumes snow-white-possessed.
 It is repeatable.  

what she knows about Cain is a dull questioning quip.  The comment is "'What do you know of Cain?'".
Understand "snow white knows" as what she knows about cain.
 It mentions cain.
 The response is "[one of]'[n-s][or][fact-reintroduction][stopping][Cain-comments in order]'[line break]".
 It quip-supplies Snow White.
 It assumes snow-white-possessed.
 It is repeatable. 

Cain-comments is a list of text that varies. Cain-comments is {
	"[one of]everyone knows his story[or]his story is written in the church windows for anyone to see[or]they tell his story every year[at random]",
	"[one of]he was jealous[or]he did not love his brother as much as he loved his own pride[or]he was troubled by jealousy[at random]",
	"[one of]he was undone[or]he deserves more sympathy than his brother[at random]"
}

what she knows about Abel is a dull questioning quip.  The comment is "'What [one of][or]else [stopping]do you know of Abel?'".
Understand "snow white knows" as what she knows about eve.
 It mentions Abel.
 The response is "[one of]She merely shudders. 'Ugh. Intolerable little man.'[or]'Do you know, he was the hairiest creature I have ever set eyes on? If I thought she had the imagination, I would credit Eve with getting him from a goat.'[or]'He was a dead bore, all pompous and pious and authoritarian. When Cain wandered— afterward— I felt nothing but gratitude.'[or]'Let's not talk about him any longer. I've gotten tired of the subject.'[or]'Really — would you care to talk endlessly about famous people you knew when you were six? That is how it seems to me.'[or]She ignores the question.[stopping]".
 It assumes snow-white-possessed.
 It quip-supplies Snow White.
 It is repeatable. 

Chapter 26 - Beta-stage additions

Section 1 - Mark Musante

how to kill the queen is a questioning quip.  The comment is "The Queen's magic may foresee my attempt to kill her.  If I were to make such an attempt, what would you suggest?".
It mentions queen, magic mirror, witchcraft, murder.
The response is "Snow White shakes her head, but her expression is hard to make out in the dim light.  Is it sadness?  Or regret?  'You are the huntsman, not I.  Your asking of the question betrays your inability.  Perhaps you could learn witchcraft, but perhaps that would lead you down the same path that the queen took.'".
It is negated by snow-white-possessed.
It directly-follows how to deal alongside the queen.
It quip-supplies Snow White.

Part 10 - Endings

[These are all actions that are currently prohibited, but that might at some point lead to endings of the game.]

Chapter 1 - Potentially Game-ending Interactions

Instead of kissing Snow White:
	say "[set body-context to Snow White]";
	say "[one of]She is the age of your own daughter. If you had one, that is.

That is not what stops you, though, if you are honest. What stops you is that engraving that the Queen showed to you. The one of the night creature with its fangs. Who would kiss such a mouth?[or]The idea fills you with unsettling distaste.[stopping]"

Instead of touching Snow White:
	say "Her skin — you've learned from having to drag her through the woods — is very cold. It is not a sensation you would seek out for its own sake."
	
Instead of touching something which is part of Snow White:
	try touching Snow White.

Instead of giving something to Snow White:
	say "Her wrists are fastened behind her back. What are you going to do, have her take [the noun] in her teeth? No."
	
Instead of cutting Snow White:
	try attacking Snow White.

Instead of attacking Snow White:
	say "It will be a nasty business if you do: you must slice off her head, and cut out her heart, and leave her body where the sun will shine[one of][scary][or]. You just cannot bring yourself to it unless you are sure of what she is[stopping]."
	
Instead of attacking Snow White when the player knows vampirism-revealed or the player knows Snow-White-possessed:
	say "There is no reason to delay, now that you know what you know. You wait until her attention is elsewhere and you stab her swift, to the heart.
	
Her scream is terrible. She struggles; she tries to bite you, with teeth suddenly sharper and longer than human teeth should be. But you are deft and practiced and you have taken the heart out of many a larger animal. When hers is cut entirely out, she dies and moves no more.

The next business is to cut off her head.";
	pause the game;
	say "[if the player knows snow-white-possessed]The demon's grip on her has slackened: she looks gentler now. [otherwise]In repose, she looks more gentle and more familiar. [end if]Kneeling beside her in the lamplight you recall how, when she was four years old, her mouth formed a tiny open triangle when she slept, not like a grown person's mouth at all; how diligently she learned to read, pointing at each word she didn't know until someone would come along and tell her what it meant; how her mother taught her to embroider with a tiny hoop and an outsized needle; how her head rested on her shoulder when she fell asleep and you chose to carry her up to bed — not letting any of the servants touch her. 

How much she looked like you, and like your mother, from her earliest days. How she used to carry mouthfuls of venison out from the table, and spit them into the bowl for the hounds to eat, and how scandalized her mother was by this unladylike trick.

How you loved her. How you loved her mother. How happy you were, before — before you weren't.

But what is done is done, and none of you are yourselves any longer. As though to mark the point, her lips fall a little open, revealing those cruel incisors, and then you are able to remove her head. Then you have no trouble dragging the trunk of her to an open patch of ground where the sun will shine and bleach her bones to innocence.";
	pause the game;
	say "The Queen, when she receives the news, knows that you are not lying. She weeps, and for a little while she even lets you hold her. But as the grief passes so also does the knowledge. The Queen dwindles back into a stepmother and a witch; you are only a huntsman, and Snow White is only a monster who has been defeated.";
	record "Killing the vampiric Snow White and returning to the Queen" as an ending;
	end the game saying "Soon this too will be a nightmare."
	
Understand "untie [something]" or "free [something]" or "set [something] free" as freeing. Freeing is an action applying to one thing.

Instead of freeing Snow White's wrists: 
	if Snow White's wrists are part of Snow White:
		try freeing Snow White;
	otherwise:
		try freeing new-Snow-White.
		
Instead of freeing the silver chain: 
	try freeing Snow White's wrists.
	
Instead of freeing Snow White when haven warning is true: [* The concern here is that we do not want the player to get into a cycle where he is trying to someplace, but the message there tells him he'll have to untie her, so he tries to untie her and gets told he has to make up his mind where to go. We *do* want to make sure that the player is unable to bring about one of these endings without realizing that it's a potentially-dangerous commitment -- no 'sudden death' -- but once he's decided what to do he should not be hampered in doing it.]
	if the current quip is yes-17 or the current quip is point out that only ordinary men now populate the earth:
		continue the action;
	if destiny warning is true:
		say "And then go to the palace, or to her safe haven?" instead;
	say "You've decided, then, to let her go and proceed to the haven alone? >>";
	if the player consents:
		remove the chain from play;
		follow the haven-ending rules;
	otherwise:
		say "On second thought, that might be dangerous thing to attempt."

Instead of freeing Snow White when destiny warning is true:
	if the current quip is yes-17 or the current quip is point out that only ordinary men now populate the earth:
		continue the action;
	if haven warning is true:
		say "And then go to the palace, or go to her safe haven?" instead;
	say "You've decided, then, to let her go and return to the palace alone? >>";
	if the player consents:
		remove the chain from play;
		follow the castle-ending rules;
	otherwise:
		say "Not now, anyway. Perhaps later it will be a good idea."

Instead of freeing Snow White:
	say "[one of][initial dog-memory][or]Perhaps — if you have made up your mind either to go to the haven or to go to the palace — you could free her in that moment. But as things stand it seems foolish to let her free and then continue to spend time in her company, prey to anything she might decide to do[stopping].";
	if the current quip is pleading-to-be-untied: [* She has just asked you to untie her]
		queue she-sees-hesitation as immediate obligatory.
		
To say initial dog-memory:
	if the player recollects whether she kens who killed your dogs:
		say "You consider it, and then you hesitate again. She may claim to have nothing to do with the death of your dogs, but what use is her word to you? And if she can do that, what else could she do? These were not lapdogs she (perhaps) drained.";
	otherwise:
		say "You consider it — and then you consider your exsanguinated hounds[if the player knows vampirism-revealed]. If she could do that, she could finish you with equal ease[otherwise].
	
The rumors might be false. You've always tried to be a rational man. The Queen's taste for witchcraft disturbs you very much.

Nonetheless, the dogs were dead, all three[end if]. [possible-blood-drinking]Bloodless".
		
Instead of freeing new-Snow-White:
	say "And then go to the palace, or to her safe haven?" instead.
		
Report freeing:
	say "It is not clear that that will accomplish anything of use."

	
Chapter 2 - Leaving
 
Understand "go to castle/palace/queen/home" or "return" or "go back/home" or "go back home" or "go back to castle/palace/queen/home"or "castle" or "palace" or "home" or "go to the castle/palace/queen/home" or "go back to the castle/palace/queen/home" as going home. Going home is an action applying to nothing.

[Instead of going home:
	try going south.]

Definition: a subject is haveny if it is the safe haven.

Understand "go to [any haveny subject]" or "[any haveny subject]" as going onward to. Going onward to is an action applying to one visible thing.[* We supply compass directions out of convention, but let's suppose that the player might try to express these obvious actions in a more natural way.]

Instead of going onward to:
	try going north.

Instead of going nowhere when the player does not know haven-location:
	say "She hasn't ever told you the exact direction to the safe haven: before you go on from here, you should get directions. And then, of course, you'll have to be sure whether you want to take her there[list where the safe haven seems]...";
		
Destiny warning is a truth state that varies. Destiny warning is false.

Instead of going a direction which is not north when the player knows haven-location:
	try going home.
	
Instead of going home:
	if stopped-time is happening:
		say "You take a few steps and it becomes difficult to go further. 'Sorry,' says [the current interlocutor]. 'You'll have to wait until time begins to pass again.'" instead;
	otherwise if destiny warning is false:
		if the chain is part of Snow White's wrists:
			say "If you go on from here, it should be to Snow White's haven[if the player knows haven-location] — the tiny house she has had fitted, and the servants she hired, and the sealed coffin-bed that she had made for herself (for protection, she said, from the vision of the mirror). And that lies to the north, she says[end if].

It would be dangerous to return to the castle while Snow White is still with you.";
		otherwise:
			say "You could go back now to the palace, leaving Snow White to go on alone. All the same you feel a little uncertain — will she be well if you do so? Will you? 

But you will have to make a choice sooner or later. Perhaps now is the right time after all.";
		now destiny warning is true;
	otherwise:
		say "If you are really going to leave, you will have to decide: do you set Snow White free? If not, she will have to come back with you, still chained, to face the Queen again. >>";
		if the player consents:
			remove the chain from play;
			say "You take off the chain, and it vanishes as soon as it is removed. No ordinary silver, that.";
		otherwise:
			say "You harden your heart.";
		follow the castle-ending rules.
		
The castle-ending rules are a rulebook. [The idea is that if the player goes back to the castle, we will check thorugh the conditions until we find one appropriate for ending the game]

A castle-ending rule when the current interlocutor is Snow White and the chain is part of Snow White's wrists:
	say "'We're going back to the castle,' you announce, making up your mind. 'You're coming with me.'

Snow White tries to run away in order to avoid being dragged back, but you catch her and carry her along. The Queen will just have to be faced, you tell yourself, though it is possible that the balance of your judgment has been upset by the long journey.

But the Queen, it seems, is very determined not to have her stepdaughter back again: as soon as the two of you emerge from the woods, and in the bleak light of dawn approach the castle, archers appear on the battlements. Their expressions are grim, but they shoot you without compunction. 

You drop Snow White and the chains break: she scampers away into the woods again with a fleetness that seems not quite human — if you didn't know better you might even think that she developed wings. But this strange sight is your last on earth, and you die without knowing for certain whether the Queen is able to recapture her, or whether the princess escapes after all.";
	record "Returning with Snow White in chains, and being shot by archers"  as an ending;
	end the game in death;
	rule succeeds.
	
A castle-ending rule when the current interlocutor is Snow White and the chain is not part of Snow White's wrists:
	if the player carries the heart-sized box and the heart-sized box contains the hart's heart:
		say "'I'm going back now,' you tell her. 'You can go on on your own; I'll take my chances lying about this matter to the Queen.' [if the player knows will-burn-box]This is to betray the promise that you made to the hart, but there are priorities in life, and in this case... you don't care to find out what would happen if you return without the box. [otherwise]
		
'I wish you luck,' she says. 'My life depends on your lie, as well as your own.'[end if]

You part ways. She is quickly gone to the north, her footsteps no longer audible. You make your way through a growing dawn back to the palace, where you present the Queen immediately with the heart in its box.

You expect her to take it before the mirror for verification, but she does not: not immediately. Instead she opens the box and looks at the heart-flesh inside, and commands you to leave her.";
		pause the game;
		say "After a day or two of waiting, you decide that the Queen has accepted the ruse. ";
		pause the game;
		say "For a time, everything is quiet. The mirror is heard less often in the halls. The Queen almost seems at peace. The servants begin to relax. The winter solstice feasts are celebrated as gaily as you can remember.

And then — after the frosts pass — there begin to be new rumors of trouble among the dwarrows. After their difficult pacification, they are growing restless again, angry, dangerous. You had very little part in the last war, but the news fills you with a nameless dread. Somehow this is her fault. The escaped princess is taking revenge, not only on her stepmother but on all the realm of men and dwarrows[if the player knows riddle-given and the player does not know riddle-solved]. You often wonder what it was she was trying to tell you, there in the woods[otherwise if the player knows riddle-solved]. You often wonder what secret she whispered to the hart — if that is what she did[otherwise]. But you cannot escape the feeling that whatever evil task she has come to, she did not wholly want it, or why would she have caused so much delay, there in the woods? She had some secret[end if].

The Queen sickens, and her famous beauty fades a little. She spends many hours closeted again with the mirror. Sometimes she even asks you to give her advice — though, of course, you have none of significance to offer. She seems to know that the princess must be alive, though she does not charge you with betrayal. The silence between you is full of secrets not spoken or even admitted in thought. Around you she wears her hair immodestly unbound, and does not cinch her robe, and though she knows she is torturing you a little, no one says anything about it. Even the servants do not find it strange.

And so for most of the spring she stands in her tower looking down in thought, as the orchards fill with apple blossom.";
		record "Returning to the Queen with the hart's heart"  as an ending;
		end the game saying "You survive — for the time being.";
	otherwise:
		say "'You go on on your own,' you say. 'I'm returning to the Queen.'
		
She looks skeptical. 'Without the box and a heart inside to show her? That seems foolish.'

'I'll make up a lie,' you tell her.

'You're not good at it. I [when furious]am just as happy for the Queen to take vengeance on you[or aggressive]don't care if you squander your life[or warm]would rather the Queen didn't kill you[at other times]cannot tell you what to do with your own life[end when], but I would most especially prefer for my own safety in hiding not to be spoiled by the Queen finding out you didn't kill me.'

'I'm going,' you tell her anyway.

'Very well.' Her eyes glitter at you in the dark. 'It is your own fool choice. Remember that you are the one who chose this.'";
		pause the game;
		say "So you part ways. She vanishes to the north, her footsteps quickly silenced by distance or cunning. You walk back toward the palace, eager to be out of the woods though they are normally a comfort to you. The lantern begins to burn low, but the grey of dawn is beginning, and you can see your way dimly.

You are at the edge of the forest, almost back to the palace and daylight, when something catches you around the neck. You are conscious of constriction and startling pain. And then of nothing at all.";
		record "Returning to the Queen empty-handed and being killed before reaching her"  as an ending;
		end the game in death;
	rule succeeds.
	
A castle-ending rule when the current interlocutor is new-Snow-White and the silver chain is part of Snow White's wrists:
	say "In a fit of fool stubbornness, you direct your footsteps back toward the castle; and she, poor girl, does not know enough to protest.
		
But the Queen, it seems, is very determined not to have her stepdaughter back again: as soon as the two of you emerge from the woods, and in the bleak light of dawn approach the castle, archers appear on the battlements; and their grim expressions are the last thing that you see alive.";
	record "Returning to the Queen with the exorcised Snow White, and being shot by archers" as an ending;
	end the game in death;
	rule succeeds.
	
A castle-ending rule when the current interlocutor is new-Snow-White and the player knows ashes-scattered and the silver chain is not part of Snow White's wrists:
	say "'Go on alone,' you say. 'You're free now; you should be able to find your way to the haven you prepared. I must return to the Queen, and as quickly as possible, before she begins to question my absence.'
	
'But —'

'Go!' You give her a little push in the small of her back. 'Remember whatever it was you did while Lilith possessed you, and use that knowledge to save yourself. And I will do likewise.'

You turn and go quickly, already wondering what you will say about the absent box. But you have fulfilled your promise to Happy, and set Snow White free of the creature that possessed her. ";
	pause the game;
	say "That is, in fact, all you can think on much, as you wander back through the woods: not your likely execution or the Queen's anger or anything that is ahead of you, but a sense that you made some kind of restitution and set right a tiny part of what ails the world. Does it matter at what cost to yourself?

Strange how familiar even that question seems: as though you had been your whole life walking through this pre-dawn towards your slaughter. The choice is behind you, the punishment ahead, and you are at peace because there is nothing more to do.

The walk is very long and very cold, but you come out of the edge of the woods at last. The castle before you is pale ivory in the dawning, and the birds are beginning to sing.";
	record "Returning to the Queen empty-handed after scattering the hart's ashes" as an ending;
	end the game saying "You may well die, but you accomplished some good at least";
	rule succeeds.
	
A castle-ending rule when the current interlocutor is new-Snow-White and the player knows take-ashes and the player carries the ashes and the silver chain is not part of Snow White's wrists:
	say "'You'd better go ahead,' you say. 'You're free now; you should be able to find your way to the haven you prepared. I must return to the Queen, and as quickly as possible: there is something more I must accomplish, for the good of the kingdom.'
	
'But —'

'I'll come back for you, if I can — if things become safe,' you say. You can only hope that Happy's prediction was correct and that he will be able to possess the mirror and use it to neutralize the Queen's evil influence.

'Now go!' You give her a little push in the small of her back. 'Remember whatever it was you did while Lilith possessed you, and use that knowledge to save yourself. And I will do likewise.'

You turn and begin walking quickly back in the direction of the palace; already rehearsing in your mind what you will say to account for the absence of the box.";
	pause the game;
	say "Your interview with the Queen is a difficult one — you are not a practiced liar. Even more terrible is the interlude afterward, when she has you put under guard for the time being, and takes the handful of ashes you brought back... and disappears with them into her own chamber. The shrieking conversation of the mirror is audible but incomprehensible from several rooms away.
		
And then, abruptly, it goes silent. When it begins again, it is in a different key: Happy's voice mimicking the voice of the errant serving woman. The change is so obvious to you even at a distance that you imagine the game must be up. Surely the Queen realizes?

But she does not emerge from her room for the rest of the day; nor for the next. It is only on the third day that she is seen in the halls again. She is pale, her voice unsteady, and yet it is like someone whose fever is broken. Her eyes do not look glassy.

She bathes only in cold water, and eats only fresh fruit. These changes are reported among the servants.";
	pause the game;
	if the player knows you-were-king:
		say "It is not until nearly three weeks have elapsed that she remembers you. She comes to your guarded room one afternoon, and asks you to tell her the true story of what happened in the forest. And you oblige — extraordinary as the story is.
		
'I feared it would not deceive you,' you say at the end.

The Queen lays down the core of the apple she was eating, and looks at you. 'I was not [italic type]deceived[roman type].' She closes her eyes and frowns. 'No: let's say, I chose to permit myself to be deceived. The whole world had gone rotten and sickly, as the mirror showed it to me. I could scarcely bear to look into it; I could not look away.'

Her eyes open and you see your wife. 'Nonetheless,' she says, 'I could go back to what I was. It needs only to blood the mirror once more. The task is not difficult.'

She puts her hand over yours. Her hair is loose around her face and she is sleepy-eyed, and you have been without her touch for a long, long time. You turn your hand in hers and lace your fingers together, yours and hers.

'I miss the King,' she says, and her voice is thickened by tears. 'Why don't you bring him back? I cannot stay sane this way. I will go back to being what I was. Please find him.' She squeezes your hand until your fingers pinch. 'Please bring him back to me.'

Indeed. But how can you? The sundering is not easy to undo, and sometimes already when you look into the mirror you only see a huntsman.";
		record "Returning to the Queen with Happy's ashes, knowing you had been the King" as an ending;
		end the game saying "Time is slipping away from you";
	otherwise:
		say "It is not until nearly three weeks have elapsed that she remembers you. She comes to your guarded room one afternoon, and asks you to tell her the true story of what happened in the forest. And you oblige — extraordinary as the story is.
		
'I feared it would not deceive you,' you say at the end.

The Queen lays down the core of the apple she was eating, and looks at you. 'I was not [italic type]deceived[roman type].' She closes her eyes and frowns. 'No: let's say, I chose to permit myself to be deceived. The whole world had gone rotten and sickly, as the mirror showed it to me. I could scarcely bear to look into it; I could not look away.'

Her eyes open and you see some glimmer of the old Queen behind them. 'Nonetheless,' she says, 'I could go back to what I was. It needs only to blood the mirror once more. The task is not difficult.'

She puts her hand over yours: the first physical contact you have ever had. Her hair is loose around her face and she is sleepy-eyed and you want what you should not want. She knows, of course.

'Don't mistake: this now is only an idyll of sanity. The other mood will come again. If you are loyal,' she says, 'you will fetch home the King; and quickly.'

But this — this proves to be very difficult, for he is nowhere to be found. And day by day her sanity ebbs.";
	record "Returning to the Queen with Happy's ashes" as an ending;
	end the game saying "Time is slipping away from you";
	
A last castle-ending rule: [* We want to default down to this when other special-case exceptions fail, because it's much less interesting than most of the other options]
	say "'Go on alone,' you say. 'You're free now; you should be able to find your way to the haven you prepared. I must return to the Queen, and as quickly as possible, before she begins to question my absence.'
	
'But —'

'Go!' You give her a little push in the small of her back. 'Remember whatever it was you did while Lilith possessed you, and use that knowledge to save yourself. And I will do likewise.'

You turn and begin walking quickly back in the direction of the palace. When you arrive, you have, of course, not thought of any sufficient explanation for the absence of the box. The Queen is very angry at its loss, and takes you to speak before the mirror: and for a moment you glimpse what the mirror is reflecting: a craven, gibbering, venal man without the courage to make any difficult sacrifice, whether for loyalty or for the good of all. A weakling, spineless, lustful, easily persuaded even by a child.

You expect her to execute you then. In her anger she makes some mention of gelding. But in the end she simply banishes you from her sight — an outcome that leaves you heartsick despite yourself. 

From the village you still watch the castle, especially the lit tower in which she spends many of her nights. She is preparing something against the little princess, but you hardly know what.";
	record "Returning to the Queen empty-handed after exorcising Snow White"  as an ending;
	end the game saying "You have failed everyone".


Haven warning is a truth state that varies. Haven warning is false.

Instead of going north when the player knows haven-location: 
	if stopped-time is happening:
		say "You take a few steps and it becomes difficult to go further. 'Sorry,' says [the current interlocutor]. 'You'll have to wait until time begins to pass again.'" instead;
	if the hart's heart is part of the corpse:
		say "You stopped here for the hart; you might as well take its heart, or it will have died to no purpose." instead;
	if the player does not recollect oops-sorry and the current interlocutor is Snow White and the player does not know snow-white-possessed:
		say "You nudge Snow White onward, but you don't get two steps before she makes a show of falling over the roots yet again[queue oops-sorry]." instead;
	if haven warning is false:
		say "You could go on, take Snow White to safety, and return alone. But afterward, you will have to lie to the Queen — perhaps for the rest of your life. It is not something to undertake unless you are certain. 
	
But you will have to make a choice sooner or later. Perhaps now is the right time after all.";
		now haven warning is true;
	otherwise:
		if the chain is part of Snow White's wrists:
			say "If you are really going to leave, you will have to decide: do you set Snow White free? If not, you'll be dragging her chained through the woods to the haven. >>";
			if the player consents:
				remove the chain from play;
				say "You take off the chain, and it vanishes as soon as it is removed. No ordinary silver, that.";
			otherwise:
				say "You harden your heart.";
		follow the haven-ending rules. 
		
The haven-ending rules are a rulebook.

A haven-ending rule when the current interlocutor is Snow White and the chain is part of Snow White's wrists:
	say "'We go on,' you say. 'I'm going with you to your safe haven.'

She pouts and fusses some about being forced through the woods in the current circumstances, but there is not much that she can do about it. Presently (because what else can she do?) she gives you more precise directions to finding the cottage. It is well into the territory of the dwarrows, away from the land that your people inhabit: the paths are narrower, the turns sharper.";
	pause the game;
	say "When you arrive, she shouts for the dwarrows, and they boil out of their little cottage and across the silty river to meet you. They are small, but there are many more of them: they quickly get her away from you and take off the chain that ties her wrists.

'Is he friend or foe?' they ask her. They are carrying axes and daggers and small brutal hammers.

She puts a hand on the shoulder of one of them. In the improving light you can see the trace of blood, real blood, on her mouth[if the player knows hart-thanked and the player does not know undead-hart] — what [italic type]did[roman type] she do to the hart[otherwise if the player knows undead-hart] — the hart's, no doubt, and why did you not question him more fully when you had the chance[otherwise]— why didn't you spend a little longer in the woods with her before deciding to bring her with you[end if]? 

She smiles nastily at you. 'He hasn't decided,' she says archly. 'Let him live for a few days, at least, while he considers it.'";
	record "Going with a chained Snow White to the haven" as an ending;
	end the game saying "Your position is rather precarious"	

A haven-ending rule when the current interlocutor is Snow White and the chain is not part of Snow White's wrists:
	say "'We go on,' you say. 'I'm going with you to your safe haven.'

'If you must,' she says — and freed of her bonds she walks very quickly and quietly, so that it is not easy for you to keep up. 

She knows the path unerringly, though the land soon becomes more broken and hilly, and the way gets narrower and passes over rock. A little later there are gorges, crossable only by rough wood bridges that are made for smaller feet. White water runs swiftly far beneath you. 

Descending along these gorges, you come to a point where the water widens; and there you find the cottage.";
	pause the game;
	say "And what a strange place it proves to be: a low rambling barrow of earth built into the gorge face, with reeds up almost to its walls, and a rough-wrought door. Three dwarrows come out to meet you in the chilly dawn light. You must ford the river once more to reach them (wishing you'd worn better boots, hoping they will think right to offer you breakfast).
	
She puts a hand on the shoulder of one of them. In the improving light you can see the trace of blood, real blood, on her mouth[if the player knows hart-thanked and the player does not know undead-hart] — what [italic type]did[roman type] she do to the hart[otherwise if the player knows undead-hart] — the hart's, no doubt, and why did you not question him more fully when you had the chance[otherwise] — why didn't you spend a little longer in the woods with her before deciding to bring her with you[end if]? 
	
Inside there are more dwarrows — you are losing count — and in the front room they show to Snow White the provisions they made for her coming. A box like a coffin, bedded with new dirt; a robe sized for a human girl but made in dwarf-fashion, with ornamented discs of wood and brass at the neckline; wine, obviously bought or stolen from humans; stores of winter food that look pitiful to your eyes, all withered fruit and old grains, and a freshly-killed rabbit that no one has thought to clean.

Eagerly, nervously, they watch as she inspects all these, and pronounces them good. You might as well not be in the room. They do [italic type]not[roman type] offer breakfast. They murmur to one another. At last Snow White thanks them all for their preparations and asks to be left here alone. Her voice is a little girl's, but it is terribly commanding.

You all shuffle out together, you and the dwarrows. From the corner of your eye as you go you see Snow White climbing into the dirt-box, with a look of relief after a trying night.";
	pause the game;
	say "Outside, one of the dwarrows speaks up:

'You are not staying here.'

You explain your position — how much you gave up to save Snow White — that you cannot return to the Queen. That things are difficult now with the King gone.

They look at one another and snicker and giggle. 'Perhaps you should fetch the King back,' suggests the largest, whose hat comes up to your navel.

'Yes, go find the King,' says a second, in on the joke. 'Bring him back.'

'I've always wanted to meet him again — he's got something belongs to me,' says a third, holding up the stump of an arm. Wounded in the war, most likely, and why didn't you think more carefully about [italic type]that[roman type]? 

In the end you manage to barter for a day's rest and a little food to take away with you, when you go— wherever you are going. To find the King, if you can.";
	record "Going with a freed Snow White to the haven" as an ending;
	end the game saying "You have a quest that is unlikely to succeed"	

A haven-ending rule when the current interlocutor is new-Snow-White and the chain is not part of Snow White's wrists:
	say "'I'm coming with you to your haven,' you say. 'I think that's safest.'
	
She nods, obviously relieved. 'I do know the way, still,' she says. 'But Lilith did most of the negotiation with the dwarrows, and I am a bit nervous of them.'

And it proves to be as she says. The way to the cottage takes you out of the part of the woods that you know, into a rougher land cut with gorges and littered with fallen trees — a place that seems gloomier and more blasted than your own territory. Here and there sloping tunnels descend into the earth, but you know better than to enter any of those.

When you reach your destination, she draws closer to you and holds your hand. It is dawn now, but a cloudy, unhappy dawn.";
	pause the game;
	say "Before you is a low, unwelcoming cottage built into the side of the gorge, which you can reach only by fording the river. Three dwarrows form a welcoming committee, which is to say that they stand outside the cottage and stare at you in silence as you struggle through the icy grey water.

'What a beautiful cottage,' Snow White lies, gritting her teeth and dragging sodden skirts through the floods. 'And a well-protected location.'

The dwarrows do not speak.

'Is there good fishing?' you ask one of them courteously, as you dump the liquid from your boots.

'No. Who are you?'

And you explain; and then Snow White tries to explain; but it is clear that your explanations are only making them more suspicious.";
	pause the game;
	say "Inside, you find that they have made preparations for Snow White — rather sordid ones. A coffin of dirt has a place of honor by the fire, and there is a fresh-killed rabbit, still bloody. 'This is — would have been — my nice cozy breakfast and comfortable bed,' Snow White says to you, under her breath.
	
'Everything as you asked,' says the nearest dwarf, stroking the broken neck of the rabbit with one hand as he looks eagerly up at her.

'Lovely,' she says, looking rather ill. 'But now I must speak with— my retainer.' She gestures at you.

The dwarrows look at one another, then bow, as one, and go out.";
	pause the game;
	say "You and she sink into different corners of the room and are silent for a little while, contemplating all the horror that is behind you and everything left to come. It is obvious you won't be able to stay here for long; you will have to go on, do something more. 
	
Finally Snow White opens the wine the dwarrows left, and pours some of it into a cup. After a sip, she makes a face and passes the cup to you. 'It's— rough,' she warns. 

'You've grown used to palace vintages,' you say. 'You're not used to what ordinary folk drink. Any wine at all's a treat; most of us make do on ale.' Warmed by this moral lesson, you take a swig.

'We could look for the King,' Snow White suggests, while you are still coughing and wiping the tears from your eyes. 'Lilith was very interested in him; I remember that. She thought something mysterious had happened to him. She was disappointed that I didn't know more myself. Perhaps if we were able to bring him back... I don't suppose the dwarf-spirit told you where to find him?'

[if the player does not know you-were-king]You shake your head simply[otherwise]You look away. There is no explaining this to her. You have made your decision, to leave yourself sundered; soon enough you will even forget that there was a decision to make, and be merely the huntsman again[end if].";
	record "Taking the exorcised Snow White to the haven"  as an ending;
	end the game saying "You are in exile"



Chapter 3 - Using the Box

 [  Using the box. ]

[ 

Burning:
	— the hart's heart
		undoes Happy's possession of the hart and sets him free
	— something of your own
		undoes *your* blood-sundering and restores you to full knowledge of the situation
]

After inserting the hart's heart into the heart-sized box:
	say "[one of]You tuck [if the prior named noun is the heart-sized box]in the heart[otherwise if the prior named noun is the heart]it into its fitted box[otherwise]the heart into its box, where it fits exactly[end if][if Snow White is the current interlocutor][queue pleased-watching as immediate optional][otherwise if the corpse is the current interlocutor]. He watches with a definite gleam of satisfaction[end if][or]You return the heart to the box, where it belongs[stopping]."
 
Igniting it with is an action applying to two things.

Understand "burn [something] with [something]" as igniting it with. 

Understand "set fire to [something]" as burning. Understand "set fire to [something] with [something]" as igniting it with.

Understand "light [something] on fire" as burning. Understand "light [something] on fire with [something]" as igniting it with.

Understand the command "ignite" as "burn".

Before igniting something with the lantern when the lantern is not carried:
  carry out the implicitly taking activity with the lantern;
  if the lantern is not carried then stop the action.

Check igniting something with something:
   if the second noun is not the lantern then 
     say "You can't burn anything with [the second noun]." instead;
  otherwise say "[The noun] won't burn." instead.

Check igniting the lantern with the lantern:
  say "The lantern is already lit." instead.

Instead of igniting Snow White with the lantern (this is the Snow White ignition rule):
	if the player recollects no-burning-again:
		say "As you approach with the lantern, Snow White speaks a strange word. The flame in the lantern leaps up in response — as though in joy — and then it vanishes entirely. [paragraph break]'Goodbye, [vocative huntsman],' she says. 'Explain to the Queen as well as you can!' And from the nimble footsteps, you guess that her earlier trouble walking was all performed for your benefit. Soon you cannot even hear her moving."; [* Supposedly Adam was made of earth but Lilith of flame. If wikipedia isn't lying to me, anyway.]
		record "Trying to burn Snow White, and being left in the dark"  as an ending;
		end the game saying "You are alone in the dark";
	otherwise:
		say "As Snow White shies away from the lantern, the glare she gives you seems almost enough to extinguish it[queue no-burning-again]."

Instead of igniting the dress with the lantern:
	try igniting Snow White with the lantern.
	
Instead of igniting the embroidery with the lantern:
	try igniting Snow White with the lantern.

Instead of igniting something that is part of Snow White with the lantern: 
  try igniting Snow White with the lantern.

Instead of igniting hair with the lantern:
	say "Have you ever smelled what happens when you do that? Disgusting."

Instead of burning the lantern:
  say "The lantern is already lit." instead.

Instead of igniting the hart's heart with the lantern:
  say "It would take a stronger fire to get the raw meat to burn unaided."

Instead of igniting the trees with the lantern:
  say "You don't want to start a forest fire; it would be certain to attract the Queen's attention, and you might perish in the flames."

Instead of igniting the undergrowth with the lantern:
	try igniting the trees with the lantern.
	
Instead of igniting the corpse with the lantern:
	say "You could roast the creature, but it would be better to clean it properly first, and you would need better-laid tinder to cook it. In the cold and dark, with the meager flame of the lantern, you won't be able to do more than singe its fur."
	
Instead of igniting something which is part of the corpse with the lantern:
	try igniting the corpse with the lantern instead.

Instead of burning something:
   say "(with the lantern)[command clarification break]";
   try igniting the noun with the lantern.

Instead of igniting the heart-sized box with the lantern:
	if the dripped blood is in the heart-sized box:
		end the sundering instead;
	if the hart's heart is not in the heart-sized box:
		say "You need to keep the box for the heart.";
	otherwise if Stopped-Time is happening:
		say "You seem unable to burn the box in this time between times.";  
	otherwise if the player knows snow-white-possessed or the player knows will-burn-box:
		delete all queued conversation for the current interlocutor;
		move ashes to the location;
		say "The box begins to smoulder, and then to burn. [if the heart-sized box is carried]You hastily drop it, before it singes your fingers. [end if] 

As the heart within catches fire, it emits clouds of foul-smelling fumes, causing you to choke. But their effect on Snow White is even more dramatic: she tries to back away, but seemingly loses control of her limbs, flailing around like one demented.

Suddenly, she lets out a loud scream that pierces the night like a screech of a thousand owls. A moment later a small cloud of black, oily smoke boils out of her mouth, and then dissipates into the fumes of the burning heart.
	
For a moment Snow White looks on the point of fainting, then she recovers herself, swaying on her feet and staring around her like someone just woken from a long sleep. She staggers towards you and then falls sobbing into your arms, a lost, frightened girl. Instinctively, you wrap your arms protectively around her. 'It's all right, now,' you assure her.";
		swap new-Snow-White for Snow White;
		start talking to new-Snow-White;
		queue what-next as immediate obligatory;
		queue shall-we-go-now as postponed obligatory; [* This is the quip that will end the game when you get around to it, but as long as you keep talking she'll keep answering instead]
		stop the action;
 	 otherwise:
		say "You were planning to keep the hart's heart to show the Queen, and right now you see no reason to change that plan."

[ This method of chasing off a demon is suggested by Tobit 8.1-4, where the innards of a fish are burnt to drive off the demon Asmodeus, and Numbers Rabbah 19.8, which suggests the employment of the smoke from roots. ]

To end the sundering:
	follow the sundering-ended collage rule;
	now the current graphics drawing rule is the sundering-ended collage rule;
	say "The box begins to smoulder, and then to burn. [if the heart-sized box is carried]You hastily drop it, before it singes your fingers. [end if] 

As the heart within catches fire, it emits clouds of foul-smelling fumes. You start to choke and cough, and your eyes are full of bleary tears, and you remember —

how the Queen begged you not to sunder yourself

Snow White sitting on her lap

sunlight in the castle towers

...and before that, spooling back, years of pleasure, peace, and even grace; fine feasts at the great table; the beasts you took from the forest, and the revelry; the dwarrows always at the edge of things. Doing well enough, you thought, and if people were sometimes a little cruel at their expense, well, humans are high-spirited — they meant no real harm...

Until, of course, the dwarrows['] patience snapped, and the war came on; a war which the dwarrows would not win, but which they would not give up fighting; a war harsh on humans but likely to mean the total destruction of the dwarrow kind, simply because their pride would not brook any more of being kicked because they were about foot-height, of being sold the worst wine and used as jokers in festivals, of having their daughters snatched away by human males with idiosyncratic tastes.

When you saw them dying, because you had not been able to control your stupid dear bumbling awful people, you made a certain choice.

And now you have unmade that choice.";
	pause the game;
	say "You wake. It is light. Snow White is bent over you — rose-cheeked, innocent, a girl properly again. Your daughter.

'Daddy.' She throws herself against your chest before you can even properly sit up, and sobs as though she were five years old, though she is nearly fifteen.

'There was a demoness,' she says. 'In my head. She's gone now. I'm so sorry. I killed your hounds, I think. We drank their blood. It was disgusting. Afterward I made us throw up. I'm sorry.'

You hold her tightly and try to think: but you, the King, sitting on the ground alone in the woods, have no more powers than you had as huntsman, and your problems are considerably more numerous. 

To prevent the outbreak of war again, and to keep the dwarrows safe, now that your sacrifice-magic is no longer holding everything in check. 

To retrieve the Queen from her magic — your wife, Snow White's true mother — and make her again as she was before the sundering. It is a relief, at least, to understand why you have lusted after and longed for her, and helplessly watched her. That was the ghost of love, dry and joyless, and, being already dead, impossible to kill. But now instead you are the husband of a mad witch, and it was your desertion that made her so, and sundered her from her family. Guilt piles on guilt and you can mend nothing without breaking something else.

The sun rises in the sky and the air grows warmer. You sit like that for a long time, you and your daughter, and think on what to do.";
	record "Freeing yourself from the blood-sundering"  as an ending;
	end the game saying "Now you are yourself again"

Instead of dropping the ashes when the player carries the ashes:
	if the player knows will-burn-box:
		say "You toss the ashes to the four ordinal directions[ashes-scattered] — northwest to southeast, southwest to northeast in a cross. It comes naturally to do it this way.

There is no immediate or visible change in your environment, but something has altered. You can feel the chill lessening in the air.";
	otherwise:
		say "For no reason other than a feeling of sentiment, you distribute the ashes to the four cardinal directions, north, east, south, west in turn. Nothing visible results, but visible results are not always the purpose of such gestures.";
	remove the ashes from play.
	

Chapter 4 - Cutting Things

Understand "cut [something] with [something preferably held]" as cutting it with. Cutting it with is an action applying to one thing and one carried thing.

Understand "cut [something] off/out with [something preferably held]" as cutting it with. Understand "cut off/out [something] with [something preferably held]" as cutting it with. 

Check cutting something with the player:
	say "So far, your wit hasn't proven that sharp." instead.
	
Check cutting something with Snow White:
	say "She isn't an implement you know how to control." instead.

Check cutting it with:
	if the second noun is not the dagger:
		say "[The second noun] won't provide much of a cutting edge." instead.
		
Carry out cutting it with:
	try cutting the noun instead. [Since there's only one cutting instrument in the game, there's no reason to make the action more elaborate.]
	
Understand "cut out/off [something]" or "cut [something] out/off" as cutting. Understand the command "stab" as "cut".

Instead of attacking the hart when the hart's heart is part of the corpse:
	try taking the hart's heart.

Instead of attacking the hart's heart when the hart's heart is part of the corpse:
	try taking the hart's heart.

Instead of taking the hart's heart when the hart's heart is part of the corpse:
	try cutting the hart's heart.
	
Instead of cutting the hart's heart when the hart's heart is not part of the corpse:
	say "It has been removed; you don't want to damage it further."
	
The block cutting rule is not listed in any rulebook.

Definition: a thing is solid if it is not fleshy.

Definition: a thing is fleshy:
	if it is part of a person:
		yes;
	if it is a person:
		yes;
	no.

Check an actor cutting something solid (this is the necessary-to-check slicing rule): [* In fact a lot of objects have their own specialized refusal for cutting, but this is an extra generic one]
	if the actor is the player:
		say "[The noun] [is-are]n't the sort of thing you can easily cut with your dagger." instead;
	otherwise:
		stop the action.

Check an actor cutting (this is the no daggerless cutting rule):
	if the actor does not carry the dagger:
		if the actor is the player:
			say "(first retrieving the dagger)[line break]";
			silently try taking the dagger;
		otherwise:
			try the actor taking the dagger;
	if the actor does not carry the dagger:
		stop the action.
	
Instead of cutting the corpse:
	if the hart's heart is part of the corpse:
		try cutting the hart's heart;
	otherwise if the corpse is the current interlocutor:
		say "Plainly mad.";
	otherwise:
		say "You've done enough. It is not convenient now to bring away his meat for stewing.".

Carry out cutting the hart's heart when the hart's heart is part of the corpse:
	now the player carries the hart's heart;
	delete purgeable conversation for the current interlocutor; [This should take over Snow White's attention from whatever else she was doing.]
	casually queue thank-the-hart.
	
Report cutting the hart's heart:
	change the prior named noun to the hart's heart; [* Fiddly; this is so that we can generate better prose later for multi-stage actions involving cutting the heart, opening the box, and putting the heart inside all in one action.]
	say "You slice the heart out of the dead animal. It is cold now, just like any ordinary sort of meat[if implicitly acting is true]. [run paragraph on][otherwise].[end if]" instead.
	
Rule for implicitly taking the hart's heart: 
	if the player carries the hart's heart:
		do nothing instead;
	try silently taking the hart's heart; 
	if the player carries the hart's heart:
		say "You slice the heart briskly out of the dead animal. [run paragraph on]"

Report cutting something:
	say "Nicked." instead. [* We actually don't want ever to see this message: everything should be special-cased either to refuse cutting or to have some customized response. But it is better to supply the message than to leave it out, just in case we miss something. ]

Chapter 5 - After Lilith is Banished

new-Snow-White is a privately-named woman. Understand "Snow" or "White" or "princess" or "girl" as new-Snow-White. The printed name is "Snow White".
	The description is "Her appearance is not very much different than it was before, and yet all the coldness has gone out of it."
	
Instead of kissing new-snow-white:
	say "She seems infinitely more vulnerable now. And childish. All the womanly knowledge has ebbed out of her and she is just a girl."
	
Instead of burning or cutting or attacking new-Snow-White:
	say "She is truly the princess now: it would be treason to harm her, whatever the Queen might have said."
	
Instead of burning or cutting or attacking or taking or waving or swinging something which is part of new-Snow-White:
	try cutting new-Snow-White.
	
Instead of tying new-Snow-White to something:
	say "She has endured enough bondage for tonight."
	
Instead of tying something to new-Snow-White:
	if the noun is new-Snow-White:
		say "The advantages of this are obscure." instead;
	try tying new-snow-white to the second noun.

Instead of rubbing or squeezing or touching new-Snow-White:
	say "You chafe her arms to warm her up[one of] a little. Her skin has regained a little of its natural warmth, but she is clearly still very chilly from being out in the woods in such insufficient dress. (You wonder, now, that you did not bring a cloak for her, and one for yourself as well. But you supposed at the time that you were going to a death, yours or hers.)[or] more.[or]. She giggles. 'That tickles,' she says. The old Snow White would never have allowed herself to giggle.[or].[stopping]".

Instead of rubbing or squeezing or touching something which is part of new-Snow-White:
	say "You are cautious of being too forward. All the same, it's an emergency. [run paragraph on]";
	try rubbing new-Snow-White.
	
Instead of tasting or consuming new-snow-white:
	say "Matters are not so dire as to require cannibalism. If you continue through the woods, you will reach her safe haven: surely it has food. Or you can be brave and go back to the palace, and make up some story to tell the Queen."
	
Instead of tasting or consuming something which is part of new-Snow-White:
	try consuming new-Snow-White.
	
Instead of tasting or consuming Snow White's hair when Snow White's hair is part of new-Snow-White:
	say "[going-crazy]".

Instead of waving or swinging new-Snow-White:
	say "This is hardly the terrain for country-dancing."
	
Instead of searching or looking under something which is part of new-Snow-White:
	say "She suffers the minor indignity meekly, as though you were the court physician. But you find no marks on her."
	
Instead of smelling something which is part of new-Snow-White:
	try smelling new-Snow-White.

Instead of smelling new-Snow-White:
	say "[one of]The charnel smell of earlier is gone, leaving only the pure crisp scent of apples[or]The apple scent is crisp and innocent[stopping]."
	
Instead of dropping new-snow-white:
	say "She is already standing (more cooperatively) on her own two feet."
	
Instead of searching new-Snow-White:
	say "There is now rather less to find than there used to be, you imagine."
	
Instead of looking under new-Snow-White:
	say "Her recent tribulations have left no mark on the ground beneath her."
	
Instead of taking new-Snow-White:
	say "She looks well enough to walk, at least for the time being, so there is no need to carry her. (And you are exhausted, so it is just as well.)"
	
Instead of pulling or pushing or turning new-Snow-White:
	say "She has likely had absolutely enough of being someone's puppet."
	
Instead of pulling or pushing or turning something which is part of new-Snow-White:
	try pulling new-Snow-White.
	
Instead of throwing something at new-Snow-White:
	try attacking new-Snow-White.
	
Instead of attacking or burning or cutting the green dress when the green dress is worn by new-snow-white:
	say "You're more of a mind to wrap her up warmly in cloaks and blankets (though, alas, you have none to hand at the moment)."
	
Instead of pulling or pushing or turning or looking under or searching or listening to or smelling the green dress when the green dress is worn by new-Snow-White:
	say "You wouldn't want to make her uncomfortable."

To swap (new person - a person) for (old person - a person):
	move new person to the location of old person;
	repeat with item running through things which are part of the new person:
		remove item from play; [* We get rid of new-snow-white's body parts and replace them with Snow White's]
	now everything which is carried by the old person is carried by the new person;
	now everything which is worn by the old person is worn by the new person;
	now everything which is part of the old person is part of the new person;
	remove the old person from play.

What-next is an NPC-directed quip.
	The response is "'Lilith is gone,' she says. 'I'm free.'"

shall-we-go-now is an NPC-directed quip.
	The response is "'Let us go,' she says. 'It is growing cold here.'"
	
[After someone discussing shall-we-go-now:
	end the game saying "You and Snow White are fugitives now"]
	
Chapter 6 - Tracking Endings

Include Recorded Endings by Emily Short. The File of Conclusions is called "alabconc". 

Part 11 - Testing - Not for release

[There are several things here that we need to test. First, the basics of all of our gameworld objects: do they behave correctly, are they adequately described, etc. Second, major conversation strands: do they all work?]

Chapter 1 - Standard Rules on Objects

Section 1 - For the Player 

[Include Object Response Tests by Juhana Leinonen.
[Now we go to the World index, cut the name of every in-play object in the game (that will exclude the abstract quip objects), and put them into test scripts:]

Test analysis with "test an1 / test an2 / test an3 / test an4 / test an5 / test an6 / test an7/ test an8 / test an9 / test an10". 

Test an1 with "analyze sky / analyze roots / analyze trees / analyze undergrowth / analyze aspen / analyze pine".

Test an2 with "analyze snow white / analyze Snow White's wrists / analyze chain / analyze Snow White's hair / analyze Snow White's skin / analyze Snow White's head / analyze Snow White's face / analyze Snow White's heart  / analyze Snow White's neck / analyze Snow White's  veins"

Test an3 with "analyze Snow White's ears / analyze Snow White's nose / analyze Snow White's chin / analyze Snow White's lips / analyze Snow White's teeth / analyze Snow White's eyes / analyze breath / analyze green dress / analyze gold embroidery".

Test an4 with "analyze corpse / analyze hart's heart / analyze antlers / analyze chest cavity".

Test an5 with "analyze me / analyze dagger / analyze lantern / analyze heart-sized box / analyze symbols".

Test an6 with "analyze ashes" holding the ashes.

Test an7 with "analyze my  wrists / analyze my  hair / analyze my  skin / analyze my  head / analyze my  face  / analyze my  neck / analyze my  veins"

Test an8 with "analyze my  ears / analyze my  nose / analyze my  chin / analyze my  lips / analyze my  teeth / analyze my  eyes / analyze my  breath".

Test an9 with "analyze corpse's hooves / analyze corpse's  hair / analyze corpse's  skin / analyze corpse's  head / analyze corpse's  face / analyze corpse's  heart  / analyze corpse's  neck / analyze corpse's  veins"

Test an10 with "analyze corpse's  ears / analyze corpse's  nose / analyze corpse's  chin / analyze corpse's  lips / analyze corpse's  teeth / analyze corpse's  eyes".

The test burning rule is not listed in any rulebook. ]

Section 2 - For the Snows White

[Include NPC Object Response Tests by Emily Short. [* This is a totally derivative add-on that just copies all the object response tests to rules that lets tem fire for actors]

Test NPC-analysis with "test NPC-an1 / test NPC-an2 / test NPC-an3 / test NPC-an4 / test NPC-an5 / test NPC-an6 / test NPC-an7 / test NPC-an8/ test NPC-an9 / test NPC-an10". [* Note that this same test can equally be used on new-snow-white, even though she will have a different ruleset for responding. So we don't have to write a whole new test pattern for her.]

Test NPC-an1 with "snow, analyze sky / snow, analyze roots / t injury / snow, analyze trees / snow, analyze undergrowth / snow, analyze aspen / snow, analyze pine".

Test NPC-an2 with "snow, analyze snow white / snow, analyze Snow White's wrists / snow, analyze chain / snow, analyze  Snow White's hair / snow, analyze  Snow White's skin / snow, analyze  Snow White's head / snow, analyze  Snow White's face / snow, analyze Snow White's heart  / snow, analyze neck / snow, analyze Snow White's veins"

Test NPC-an3 with "snow, analyze Snow White's ears / snow, analyze  Snow White's nose / snow, analyze  Snow White's chin / snow, analyze  Snow White's lips / snow, analyze  Snow White's teeth / snow, analyze  Snow White's eyes / snow, analyze breath / snow, analyze green dress / snow, analyze gold embroidery".

Test NPC-an4 with "snow, analyze corpse / snow, analyze hart's heart / snow, analyze antlers / snow, analyze chest cavity".

Test NPC-an5 with "snow, analyze me / snow, analyze dagger / snow, analyze lantern / snow, analyze heart-sized box / snow,  analyze symbols".

Test NPC-an6 with "snow, quit / snow, save / snow, restore / snow, version / snow, verify / snow, notify on / snow, notify off / snow, verbose / snow, brief / snow, superbrief / snow, restart / snow, transcription on / snow, transcription off".

Test NPC-an7 with "snow, analyze my  wrists / snow, analyze my  hair / snow, analyze my  skin / snow, analyze my  head / snow, analyze my  face / snow, analyze my  heart  / snow, analyze my  neck / snow, analyze my  veins"

Test NPC-an8 with "snow, analyze my  ears / snow, analyze my  nose / snow, analyze my  chin / snow, analyze my  lips / snow, analyze my  teeth / snow, analyze my  eyes / snow, analyze my  breath".

Test NPC-an9 with "snow, analyze corpse's  hooves / snow, analyze corpse's  hair / snow, analyze corpse's  skin / snow, analyze corpse's  head / snow, analyze corpse's  face / snow, analyze corpse's  heart  / snow, analyze corpse's  neck / snow, analyze corpse's  veins"

Test NPC-an10 with "snow, analyze corpse's  ears / snow, analyze corpse's  nose / snow, analyze corpse's  chin / snow, analyze corpse's  lips / snow, analyze corpse's  teeth / snow, analyze corpse's  eyes".


Section 3 - For the Hart

Test hart-analysis with "test hart-chat / test hart-agreeing / test hart-an1 / test hart-an2 / test hart-an3 / test hart-an4 / test hart-an5 / test hart-an6 / test hart-an7 / test hart-an8 / test hart-an9 / test hart-an0". 

Test hart-an1 with "hart, analyze sky / hart, analyze roots / hart, analyze trees / hart, analyze undergrowth / hart, analyze aspen / hart, analyze pine".

Test hart-an2 with "hart, analyze snow white / hart, analyze Snow White's wrists / hart, analyze chain / hart, analyze Snow White's hair / hart, analyze Snow White's skin / hart, analyze Snow White's head / hart, analyze Snow White's face / hart, analyze snow White's heart  / hart, analyze Snow White's neck / hart, analyze Snow White's  veins"

Test hart-an3 with "hart, analyze Snow White's ears / hart, analyze Snow White's nose / hart, analyze Snow White's chin / hart, analyze Snow White's lips / hart, analyze Snow White's teeth / hart, analyze Snow White's eyes / hart, analyze Snow White's breath / hart, analyze green dress / hart, analyze gold embroidery".

Test hart-an4 with "hart, analyze corpse / hart, analyze hart's heart / hart, analyze antlers / hart, analyze chest cavity".

Test hart-an5 with "hart, analyze me / hart, analyze dagger / hart, analyze lantern / hart, analyze heart-sized box / hart,  analyze symbols". 

Test hart-an6 with "hart, quit / hart, save / hart, restore / hart, version / hart, verify / hart, notify on / hart, notify off / hart, verbose / hart, brief / hart, superbrief / hart, restart".

Test hart-an7 with "hart, analyze my  wrists / hart, analyze my  hair / hart, analyze my  skin / hart, analyze my  head / hart, analyze my  face / hart, analyze my  heart  / hart, analyze my  neck / hart, analyze my  veins"

Test hart-an8 with "hart, analyze my  ears / hart, analyze my  nose / hart, analyze my  chin / hart, analyze my  lips / hart, analyze my  teeth / hart, analyze my  eyes / hart, analyze my  breath".

Test hart-an9 with "hart, analyze corpse's  hooves / hart, analyze corpse's  hair / hart, analyze corpse's  skin / hart, analyze corpse's  head / hart, analyze corpse's  face / hart, analyze corpse's  heart  / hart, analyze corpse's  neck / hart, analyze corpse's  veins"

Test hart-an0 with "hart, analyze corpse's  ears / hart, analyze corpse's  nose / hart, analyze corpse's  chin / hart, analyze corpse's  lips / hart, analyze corpse's  teeth / hart, analyze corpse's  eyes".]

Chapter 2 - Getting to Situations

Test hart-chat with "cut hart / ask Snow White about what she was doing / wake hart".

Test hart-agreeing with "ask why / no / z / smile / a how / a cosmic / yes / yes / yes".

Test hart-finishing with "a name / a snow white / a whether snow white is a vampire / yes / z / no / z".

Test pact-completion with "put heart in the box / burn the box".

Test removing-Lilith with "test hart-chat / test hart-agreeing / test hart-finishing / test pact-completion".

Test removing-Lilith-2 with "cut hart / put heart in box / burn box".

Test provoking-vampires with "test hart-chat / test hart-agreeing / test hart-finishing / a what she knows about vampires / a how vampires came to be".

Test prov-2 with "test hart-chat / test hart-agreeing / test hart-finishing / a how vampires came to be / say recently / a what she knows".

Test prov-3 with "test hart-chat / test hart-agreeing / test hart-finishing / ask about why she avoids the mirror / a what she sees / a stand there".

Test wounding with "x me / drop dagger / cut me / g / g / put blood in box / g / fill box with blood / pour blood in box / pour wound in box / clean wound / lick wound / lick blood / x me".

Test finding-Lilith with "t roots / say she is delaying / ask her about delay / no / a lilith / a adam / t apple / a apple / a mortals / a if queen is a witch / ask what powers / a demons / a queen is possessed / ask how / ask what bargain / ask what demons / a lilith / ask where lilith is now / ask whether lilith likes / t explain / yes / ask what lilith wants / ask what lilith wants / a adam".

Test finding-king with "test hart-chat / test hart-agreeing / a king / a repeat / ask what happened / a who performed / a current peace / z".

Chapter 3 - Testing Endings

 [We need to be sure that all endings can be reached as intended.]

Test end-1 with "z / a haven / no / no / a where / s / s / no". [You return with her in chains to the castle without having accomplished anything much.]

Test end-2 with "take heart / put heart in box / z / a haven / no / no / a where / s / s / yes". [You free her and return with the heart]

Test end-3 with "z / a haven / no / no / a where / s / s / yes". [You free her and return without the heart]

Test end-4 with "test hart-chat / test hart-agreeing / test hart-finishing / kill Snow White". [Find out that she is a vampire, then kill her.]

Test end-5 with "test provoking-vampires". [Technically prov-2 and prov-3 are also alternate versions of this ending.]

Test end-6 with "burn snow white / g". [Trying to burn her with the lantern.]

Test end-7 with "test removing-Lilith / a where haven is / s / s / no". [You remove Lilith then force her to return to the palace with you.]

Test end-8 with "test removing-Lilith / get ashes / scatter ashes / a where haven is / s / s / yes". [You remove Lilith and scatter the ashes, then leave without Snow White.]

Test end-9 with "test removing-Lilith / a where haven is / s / s / yes". [You remove Lilith, then leave without Snow White and without honoring your pact with the hart.]

Test end-10 with "test hart-chat / test hart-agreeing / a mirror / g / g / yes / yes / a snow white vampire / yes / ask what will happen / z / z / z / z / put heart in box / burn box / get ashes / s / a haven / s / s / yes". [You find out about how to put Happy in the mirror; remove Lilith, then collect the ashes and return to the Queen without Snow White]

Test end-11 with "z / a haven / no / no / a where / get heart / n / n / n / yes". [Free her and go with her before knowing much]

Test end-12 with "z / a haven / no / no / a where / get heart / n / n / n / no".  [Keep her tied and go with her before knowing much]

Test end-13 with "test removing-Lilith / a where haven is / n / n / yes". [Remove Lilith and then accompany Snow White away to the north.]

Test end-14 with "z / z / explain / test finding-Lilith / t me / yes / untie Snow White". [Encounter Lilith and agree to be her consort.]

Test end-15 with "z / z / explain / test finding-Lilith / t me / yes / z / z / z / z / z". [Encounter Lilith, agree to go with her, but then fail to untie her when she demands it.]

Test end-16 with "test finding-king / z / z / t roots / say she is delaying / ask her about delay / no / a lilith / a adam / t apple / a apple / a mortals / ask if the queen is a witch / ask what powers / ask what demons / a queen is possessed / a how / a bargain / a victim / ask what lilith wants / g / z / t me / t king / yes". [Get the truth from the hart, then find out who Lilith is, then offer yourself and tell her your history.]

Test end-17 with "test finding-king / t I am king / g / g / g / t king was blood-sundered / say I remember / ask whether the king could be restored / ask how the blood-sundering could be undone / cut me / g / put blood in box / burn box". [Get the truth from the hart, then burn the box.]

Test end-18 with "test removing-lilith / a what lilith wanted / a why exorcism succeeded / a powers besides lilith / a box was for / a teeth / g". [Remove Lilith and then pester Snow White until she goes insane.]


Chapter 4 - Testing Repeatables

[Because most repeatable quips assemble information into a complete paragraph via randomized rules, they need repeated testing to make sure that they are producing plausible output the majority of the time. We also rig a test that skips over any issues of conversational flow so that we don't first have to navigate to the right place in order to hammer on these. The way to run this test is immediately at the start-up of a game, without doing anything else.]

Understand "stress-test" as stress-testing.  Stress-testing is an action applying to nothing.

Carry out stress-testing:
	repeat with item running through repeatable quips:
		say "[italic type]Testing [item]: [roman type][line break]";
		repeat with N running from 1 to 10:
			say "[line break][N]: "; 
			say "[comment of item][paragraph break][response of item]";
		say paragraph break.
		
Chapter 5 - Testing Queues

[Especially during the opening portion of the game before the player is deep in conversation, when he is likely to be spending some of his time investigating the environment, we want her to show some liveliness by responding to some of his behavior: what he looks at, what he does, even his inventory-taking.]

Test queues with "i / open box / snow white, take off dress / cut hair / x hair / x eyes / x mouth / x face / x wrists / t safe haven too far / z / untie her / z / put heart in the box box / burn her with the lantern". 

Chapter 6 - Other Unusual Functionality

Test winking with "wink / wink at Snow White / wink at me / wink at my eye / wink at Snow White's eye / cut hart / wink / x hart / x glassy eye / wink / wink at Snow White".




Part 12 - Conversation Cache

[This information is stored in hard code so that it need not be recalculated at the start of play. If this section is missing, the game will build the source for a new cache. That source section can then be pasted in and recompiled for the benefit of future builds. See the optimization section above.]
  
Section 1 - Partial Cache - Not for release

The check for mis-defined items rule is not listed in any rulebook. The scan for unidentified subjects rule is not listed in any rulebook. 

Section 2 - Main Cache
 

The cache reporting rule is not listed in any rulebook. The caching thread position information rule is not listed in any rulebook. The indirect-following initializing rule is not listed in any rulebook.

generic-quip is  dead-ended and shallowly-buried and a quip.
rhafanido is  dead-ended and shallowly-buried and a quip.
what she seems is  dead-ended and shallowly-buried and a quip.
you-promised-me is   shallowly-buried and a quip.
irritated-promise is  dead-ended and shallowly-buried and a quip.
that the safe haven seems too far away to reach tonight is  dead-ended and  a quip.
what she means about the servants being small is  dead-ended and  a quip.
why she avoids the magic mirror is   shallowly-buried and a quip.
no-3 is upwardly-restrictive and dead-ended and  a quip.
thank-the-hart is   shallowly-buried and a quip.
hart-stirs is   shallowly-buried and a quip.
whether the hart moved is  dead-ended and  a quip.
what she was doing alongside the hart is upwardly-restrictive and   a quip.
how such a debt could be repaid is upwardly-restrictive and dead-ended and  a quip.
demo-dagger-1 is  dead-ended and shallowly-buried and a quip.
whether she kens who killed your dogs is  dead-ended and shallowly-buried and a quip.
what she did to anger the Queen is   shallowly-buried and a quip.
if she seems a witch is   shallowly-buried and a quip.
whether she drinks blood is   shallowly-buried and a quip.
if she seems a living being is upwardly-restrictive and dead-ended and  a quip.
whether she seems getting colder is  dead-ended and shallowly-buried and a quip.
whether she will keep the pact is   shallowly-buried and a quip.
why the Queen fears her is  dead-ended and shallowly-buried and a quip.
why you should avoid the mirror is upwardly-restrictive and   a quip.
what authority seems is upwardly-restrictive and dead-ended and  a quip.
what the symbols mean is   shallowly-buried and a quip.
what the symbols mean again is upwardly-restrictive and   a quip.
what just happened is upwardly-restrictive and dead-ended and  a quip.
how the mirror reflects you to the Queen is   shallowly-buried and a quip.
what happened to other huntsmen is upwardly-restrictive and   a quip.
what the obscene suggestion was is  dead-ended and  a quip.
how to deal alongside the queen is   shallowly-buried and a quip.
yes-2 is upwardly-restrictive and   a quip.
if you could kill the queen is upwardly-restrictive and   a quip.
no-4 is upwardly-restrictive and dead-ended and  a quip.
say xyzzy is  dead-ended and shallowly-buried and a quip.
why she sees the mirror's reflections as the Queen does is   shallowly-buried and a quip.
to walk properly is   shallowly-buried and a quip.
whether the Queen hurt her is upwardly-restrictive and dead-ended and  a quip.
whether she hath trouble walking is upwardly-restrictive and dead-ended and  a quip.
she seems delaying on purpose is upwardly-restrictive and dead-ended and  a quip.
why she seems trying to delay the escape is   shallowly-buried and a quip.
yes-3 is upwardly-restrictive and   a quip.
no-5 is upwardly-restrictive and   a quip.
request a repetition of the riddle is   shallowly-buried and a quip.
if the queen seems evil is upwardly-restrictive and dead-ended and  a quip.
does the Queen fear Snow White is  dead-ended and  a quip.
why she made a pact alongside you is  dead-ended and shallowly-buried and a quip.
if she met the king is   shallowly-buried and a quip.
yes-4 is upwardly-restrictive and dead-ended and  a quip.
no-6 is upwardly-restrictive and dead-ended and  a quip.
answer-is-no is  dead-ended and shallowly-buried and a quip.
what the Queen did to her is  dead-ended and shallowly-buried and a quip.
where the safe haven seems is  dead-ended and  a quip.
that the Queen will kill you is  dead-ended and shallowly-buried and a quip.
it was not treachery is upwardly-restrictive and   a quip.
if she kens her way around the woods is   shallowly-buried and a quip.
how she kens her way around the woods is upwardly-restrictive and   a quip.
why she needs privacy is upwardly-restrictive and   a quip.
what the Queen thinks of you is  dead-ended and shallowly-buried and a quip.
why so few people understand witches is upwardly-restrictive and dead-ended and  a quip.
what the symbols mean yet again is upwardly-restrictive and dead-ended and  a quip.
why she drinks red wine is   shallowly-buried and a quip.
yes-6 is upwardly-restrictive and dead-ended and  a quip.
no-7 is upwardly-restrictive and dead-ended and  a quip.
hart-thanks-you is   shallowly-buried and a quip.
why he thanks you is upwardly-restrictive and dead-ended and  a quip.
that he's welcome is upwardly-restrictive and dead-ended and  a quip.
annoyed-offer is   shallowly-buried and a quip.
yes-7 is upwardly-restrictive and   a quip.
yes-8 is upwardly-restrictive and dead-ended and  a quip.
maybe-need is upwardly-restrictive and   a quip.
no-8 is upwardly-restrictive and dead-ended and  a quip.
no-9 is upwardly-restrictive and dead-ended and  a quip.
what Snow White did is  dead-ended and shallowly-buried and a quip.
hart-smile is  dead-ended and shallowly-buried and a quip.
what the terms seem is   shallowly-buried and a quip.
how that would be possible is upwardly-restrictive and   a quip.
what exactly he intends by cosmic is upwardly-restrictive and   a quip.
yes-9 is upwardly-restrictive and   a quip.
no-10 is upwardly-restrictive and dead-ended and  a quip.
you're sorry to hear is upwardly-restrictive and dead-ended and  a quip.
what dwarrows do is  dead-ended and  a quip.
how its soul got there is  dead-ended and  a quip.
how its soul got there again is  dead-ended and shallowly-buried and a quip.
whether snow white seems a vampire is   shallowly-buried and a quip.
no-11 is upwardly-restrictive and dead-ended and  a quip.
yes-10 is upwardly-restrictive and   a quip.
why her existence bothered the Queen is  dead-ended and  a quip.
if she believes in vampires is   shallowly-buried and a quip.
yes-11 is upwardly-restrictive and dead-ended and  a quip.
no-12 is upwardly-restrictive and dead-ended and  a quip.
yes-12 is upwardly-restrictive and dead-ended and  a quip.
no-13 is upwardly-restrictive and dead-ended and  a quip.
whether the heart will fool the Queen is  dead-ended and shallowly-buried and a quip.
what the big wooden box was for is  dead-ended and  a quip.
ask-away is   shallowly-buried and a quip.
no-25 is upwardly-restrictive and dead-ended and  a quip.
why you must burn the box is   shallowly-buried and a quip.
yes-13 is upwardly-restrictive and dead-ended and  a quip.
no-14 is upwardly-restrictive and dead-ended and  a quip.
Hart-expires is  dead-ended and shallowly-buried and a quip.
whether the Queen herself made the magic mirror is   shallowly-buried and a quip.
suggest that you seem not likely to make any mirrors is upwardly-restrictive and dead-ended and  a quip.
that the Queen ought to guard the mirror is upwardly-restrictive and dead-ended and  a quip.
whether such magic can ever be used for good is  dead-ended and  a quip.
if the queen seems a witch is   shallowly-buried and a quip.
what bargain the Queen made is  dead-ended and  a quip.
if Snow White seems possessed is  dead-ended and shallowly-buried and a quip.
where Lilith seems now is  dead-ended and  a quip.
whether Lilith likes drinking blood is   shallowly-buried and a quip.
it would explain a few things is upwardly-restrictive and   a quip.
yes-14 is upwardly-restrictive and dead-ended and  a quip.
no-15 is upwardly-restrictive and dead-ended and  a quip.
whether the animals ever answer is upwardly-restrictive and dead-ended and  a quip.
solace-in-desert is   shallowly-buried and a quip.
that your reasons do not concern her is upwardly-restrictive and dead-ended and  a quip.
how she hired servants without the Queen knowing is  dead-ended and  a quip.
whether vampires made the mirror is  dead-ended and  a quip.
whether the Queen hath such books in her library is upwardly-restrictive and dead-ended and  a quip.
what became of the history books is  dead-ended and  a quip.
what she knows about vampires is  dead-ended and shallowly-buried and a quip.
how vampires came to be is   shallowly-buried and a quip.
vampires only recently came to be is upwardly-restrictive and dead-ended and  a quip.
vampires always existed is upwardly-restrictive and dead-ended and  a quip.
why she threw a rock at the mirror is   shallowly-buried and a quip.
go-ahead-and-cut is  dead-ended and shallowly-buried and a quip.
remark that she isn't very comforting is upwardly-restrictive and dead-ended and  a quip.
whether other huntsmen were sent to kill her is  dead-ended and  a quip.
stop-taunting is  dead-ended and shallowly-buried and a quip.
pleading-to-be-untied is   shallowly-buried and a quip.
maybe-later is upwardly-restrictive and dead-ended and  a quip.
no-1 is upwardly-restrictive and   a quip.
no-30 is upwardly-restrictive and dead-ended and  a quip.
maybe-so is upwardly-restrictive and dead-ended and  a quip.
no-2 is upwardly-restrictive and   a quip.
yes-1 is upwardly-restrictive and dead-ended and  a quip.
she-sees-hesitation is   shallowly-buried and a quip.
whether the Queen seems unworthy of the mirror is  dead-ended and  a quip.
who seems worthy of the mirror is  dead-ended and  a quip.
whether she can read your thoughts is upwardly-restrictive and dead-ended and  a quip.
no-21 is upwardly-restrictive and   a quip.
maybe is upwardly-restrictive and   a quip.
yes-15 is upwardly-restrictive and dead-ended and  a quip.
no-16 is upwardly-restrictive and dead-ended and  a quip.
what will happen if she kens you ken she's a vampire is upwardly-restrictive and   a quip.
tell the corpse to stop it is upwardly-restrictive and   a quip.
more-disturbance is  dead-ended and shallowly-buried and a quip.
seriously please stop telling me these things is upwardly-restrictive and dead-ended and  a quip.
if she needs privacy because she's a vampire is upwardly-restrictive and dead-ended and  a quip.
if she doesn't stand there because she's a vampire is upwardly-restrictive and dead-ended and  a quip.
suggest blood is  dead-ended and  a quip.
suggest an animal is  dead-ended and  a quip.
suggest a soul is  dead-ended and  a quip.
suggest a snake is  dead-ended and  a quip.
suggest a heart is  dead-ended and  a quip.
suggest an apple is  dead-ended and  a quip.
how an apple would help her is   shallowly-buried and a quip.
give up is upwardly-restrictive and dead-ended and  a quip.
oops-sorry is  dead-ended and shallowly-buried and a quip.
no-18 is upwardly-restrictive and dead-ended and  a quip.
yes-18 is upwardly-restrictive and dead-ended and  a quip.
why the chain chafes her is  dead-ended and shallowly-buried and a quip.
demo-lantern-2 is  dead-ended and shallowly-buried and a quip.
demo-sky-4 is  dead-ended and shallowly-buried and a quip.
whether her dress will be enough for her is  dead-ended and shallowly-buried and a quip.
what the embroidery on her dress signifies is   shallowly-buried and a quip.
who the man was is upwardly-restrictive and dead-ended and  a quip.
why her skin seems so fair is  dead-ended and shallowly-buried and a quip.
what the symbols on the box indicate is  dead-ended and shallowly-buried and a quip.
am-I-beautiful is   shallowly-buried and a quip.
no-17 is upwardly-restrictive and dead-ended and  a quip.
why she mentions Adam is  dead-ended and shallowly-buried and a quip.
why she mentions Eve is   shallowly-buried and a quip.
why her shadow hath wings is  dead-ended and shallowly-buried and a quip.
what she knows of the snake is  dead-ended and  a quip.
yes-16 is upwardly-restrictive and dead-ended and  a quip.
whether her teeth seem normal is  dead-ended and shallowly-buried and a quip.
why her hair seems so shiny is  dead-ended and shallowly-buried and a quip.
whether she seems Lilith is   shallowly-buried and a quip.
why she hath told me is upwardly-restrictive and dead-ended and  a quip.
what Lilith wants is   shallowly-buried and a quip.
whether that was Eve's fault is  dead-ended and  a quip.
what happened to Adam is  dead-ended and  a quip.
want-an-equal is   shallowly-buried and a quip.
that you once was King is upwardly-restrictive and dead-ended and  a quip.
whether even the King would suffice is upwardly-restrictive and dead-ended and  a quip.
point out that only ordinary men now populate the earth is upwardly-restrictive and   a quip.
no-27 is upwardly-restrictive and dead-ended and  a quip.
yes-17 is upwardly-restrictive and dead-ended and  a quip.
watching-box-opened is  dead-ended and shallowly-buried and a quip.
watching-inventory is   shallowly-buried and a quip.
no-26 is upwardly-restrictive and dead-ended and  a quip.
no-rape is  dead-ended and shallowly-buried and a quip.
pleased-watching is   shallowly-buried and a quip.
whether she seems hungry is upwardly-restrictive and   a quip.
yes-19 is upwardly-restrictive and dead-ended and  a quip.
no-19 is upwardly-restrictive and dead-ended and  a quip.
giving-up-question is   shallowly-buried and a quip.
yes-20 is upwardly-restrictive and dead-ended and  a quip.
apparently-you-would is  dead-ended and shallowly-buried and a quip.
that you plan to rescue the king is  dead-ended and  a quip.
that you still plan to rescue the king is  dead-ended and  a quip.
thought-not is  dead-ended and shallowly-buried and a quip.
weird-laugh is  dead-ended and shallowly-buried and a quip.
do-not-try is   shallowly-buried and a quip.
apologize is upwardly-restrictive and dead-ended and  a quip.
no-burning-again is  dead-ended and shallowly-buried and a quip.
nice-corpse is  dead-ended and shallowly-buried and a quip.
are-you-ogling is   shallowly-buried and a quip.
no-22 is upwardly-restrictive and dead-ended and  a quip.
yes-24 is upwardly-restrictive and dead-ended and  a quip.
no-39 is upwardly-restrictive and dead-ended and  a quip.
yes-35 is upwardly-restrictive and dead-ended and  a quip.
maybe is upwardly-restrictive and dead-ended and  a quip.
no-28 is upwardly-restrictive and dead-ended and  a quip.
smirk is  dead-ended and shallowly-buried and a quip.
something-in-your-eye is   shallowly-buried and a quip.
no-29 is upwardly-restrictive and dead-ended and  a quip.
funny-memory is   shallowly-buried and a quip.
yes-27 is upwardly-restrictive and   a quip.
no-31 is upwardly-restrictive and dead-ended and  a quip.
no-32 is upwardly-restrictive and dead-ended and  a quip.
yes-28 is upwardly-restrictive and dead-ended and  a quip.
could-be is upwardly-restrictive and dead-ended and  a quip.
really-you is  dead-ended and shallowly-buried and a quip.
how-strange is   shallowly-buried and a quip.
yes-33 is upwardly-restrictive and dead-ended and  a quip.
demo-hart's heart-4 is  dead-ended and shallowly-buried and a quip.
demo-dagger-5 is  dead-ended and shallowly-buried and a quip.
demo-trees-6 is  dead-ended and shallowly-buried and a quip.
demo-lantern-7 is  dead-ended and shallowly-buried and a quip.
demo-undergrowth-8 is  dead-ended and shallowly-buried and a quip.
demo-sky-9 is  dead-ended and shallowly-buried and a quip.
demo-roots-10 is  dead-ended and shallowly-buried and a quip.
not-talking-now is  dead-ended and shallowly-buried and a quip.
what he knows about me is  dead-ended and shallowly-buried and a quip.
what he knows of Lilith is   shallowly-buried and a quip.
what Lilith does is  dead-ended and  a quip.
what to do about Snow White plus Lilith is  dead-ended and shallowly-buried and a quip.
why Grumpy seems such a character is  dead-ended and  a quip.
other-names is  dead-ended and shallowly-buried and a quip.
whether the Queen seems dangerous is   shallowly-buried and a quip.
yes-5 is upwardly-restrictive and   a quip.
yes-25 is upwardly-restrictive and dead-ended and  a quip.
no-23 is upwardly-restrictive and dead-ended and  a quip.
who exactly was Lilith again is  dead-ended and shallowly-buried and a quip.
whether he ever met Saint Eustace is  dead-ended and shallowly-buried and a quip.
no-33 is upwardly-restrictive and dead-ended and  a quip.
yes-29 is upwardly-restrictive and dead-ended and  a quip.
waffle is upwardly-restrictive and dead-ended and  a quip.
what exactly he hath done to time is  dead-ended and shallowly-buried and a quip.
why the lantern does not burn is  dead-ended and shallowly-buried and a quip.
what seems really at the safe haven is   shallowly-buried and a quip.
what he means by other things is  dead-ended and  a quip.
what death seems like is   shallowly-buried and a quip.
yes-30 is upwardly-restrictive and dead-ended and  a quip.
no-34 is  dead-ended and shallowly-buried and a quip.
hey-I-am-dead is  dead-ended and shallowly-buried and a quip.
where vampires come from is   shallowly-buried and a quip.
why demons did such a thing is upwardly-restrictive and dead-ended and  a quip.
whether there seem any young princes in neighboring kingdoms is  dead-ended and shallowly-buried and a quip.
banish-her-too is   shallowly-buried and a quip.
remark that his solution seems very convenient is upwardly-restrictive and dead-ended and  a quip.
how Snow White came to be possessed is   shallowly-buried and a quip.
why the Queen so badly wishes to be beautiful is   shallowly-buried and a quip.
why the King abandoned the Queen is upwardly-restrictive and dead-ended and  a quip.
that seems horrible is  dead-ended and  a quip.
what seems on her lips is upwardly-restrictive and dead-ended and  a quip.
to drink the hart's blood is  dead-ended and shallowly-buried and a quip.
whether she knows any princes is  dead-ended and shallowly-buried and a quip.
reveal what it was like hunting the hart is  dead-ended and shallowly-buried and a quip.
maybe-again is upwardly-restrictive and dead-ended and  a quip.
who seems the fairest of them all is  dead-ended and shallowly-buried and a quip.
admit that you don't ken is upwardly-restrictive and dead-ended and  a quip.
yes-26 is upwardly-restrictive and dead-ended and  a quip.
whether the servants seem dwarrows is  dead-ended and  a quip.
maybe is upwardly-restrictive and   a quip.
whether she likes the dwarrows is  dead-ended and shallowly-buried and a quip.
yes-21 is upwardly-restrictive and dead-ended and  a quip.
no-20 is upwardly-restrictive and dead-ended and  a quip.
why the dwarrows became friendly is  dead-ended and  a quip.
what she knows about dwarrows is   shallowly-buried and a quip.
whether she will be serious is upwardly-restrictive and dead-ended and  a quip.
further-musings-on-beauty is  dead-ended and shallowly-buried and a quip.
why she avoids daylight is   shallowly-buried and a quip.
whether that seems a vampire trait is upwardly-restrictive and dead-ended and  a quip.
what she knows about Grumpy is   shallowly-buried and a quip.
what chores Brassbiter performed is  dead-ended and  a quip.
other-names-Snowy is  dead-ended and shallowly-buried and a quip.
how to find the King is  dead-ended and shallowly-buried and a quip.
who the King seems is  dead-ended and shallowly-buried and a quip.
what she knows of Lamia is  dead-ended and shallowly-buried and a quip.
why it takes a silver chain to involve her is   shallowly-buried and a quip.
why the Queen suffers this delusion is upwardly-restrictive and dead-ended and  a quip.
no-35 is upwardly-restrictive and dead-ended and  a quip.
yes-31 is upwardly-restrictive and   a quip.
yes-32 is upwardly-restrictive and dead-ended and  a quip.
that Snow White shows marks of vampirism is upwardly-restrictive and   a quip.
why Lilith would want to drink blood is  dead-ended and  a quip.
whether she hath hurt the princess is  dead-ended and  a quip.
if the queen still seems a witch is  dead-ended and shallowly-buried and a quip.
no-36 is upwardly-restrictive and dead-ended and  a quip.
no-37 is upwardly-restrictive and dead-ended and  a quip.
yes-34 is upwardly-restrictive and dead-ended and  a quip.
no-38 is upwardly-restrictive and dead-ended and  a quip.
whether she seems lilith is  dead-ended and shallowly-buried and a quip.
who Lilith was before is  dead-ended and shallowly-buried and a quip.
whether pact endures is  dead-ended and shallowly-buried and a quip.
request another repetition of the riddle is  dead-ended and shallowly-buried and a quip.
whether she can read your thoughts really is upwardly-restrictive and dead-ended and  a quip.
whether Snow White wishes to be rid of Lilith is  dead-ended and shallowly-buried and a quip.
whether Lilith wishes to remain in Snow White is  dead-ended and shallowly-buried and a quip.
that you seem a decent man is  dead-ended and shallowly-buried and a quip.
what she means now is upwardly-restrictive and   a quip.
guess Eve is upwardly-restrictive and dead-ended and  a quip.
shrug is upwardly-restrictive and dead-ended and  a quip.
that the Queen burned your cabin is  dead-ended and shallowly-buried and a quip.
yes-23 is upwardly-restrictive and   a quip.
explain that young women of the village hath never appealed is upwardly-restrictive and   a quip.
why the Queen would not want her to marry is  dead-ended and  a quip.
whether she seems immortal is  dead-ended and  a quip.
leading-comment-about-queen is  dead-ended and shallowly-buried and a quip.
what the apple would do is   shallowly-buried and a quip.
suggest finding an apple later is  dead-ended and  a quip.
what will happen if you burn the box is  dead-ended and shallowly-buried and a quip.
whether the King was murdered is upwardly-restrictive and dead-ended and  a quip.
whether the King was a werewolf is upwardly-restrictive and dead-ended and  a quip.
that you seem the king is  dead-ended and shallowly-buried and a quip.
that the King was blood-sundered is  dead-ended and shallowly-buried and a quip.
what-do-you-recall is   shallowly-buried and a quip.
make up some lie is upwardly-restrictive and dead-ended and  a quip.
that you remember very little is upwardly-restrictive and dead-ended and  a quip.
whether you could hath performed the blood-sundering is  dead-ended and shallowly-buried and a quip.
whether the King could hath performed the blood-sundering is  dead-ended and shallowly-buried and a quip.
whether Snow White could hath performed the blood-sundering is  dead-ended and shallowly-buried and a quip.
whether the dwarrows could hath performed the blood-sundering is   shallowly-buried and a quip.
whether the Queen could hath performed the blood-sundering is  dead-ended and shallowly-buried and a quip.
what became of the King is  dead-ended and  a quip.
anyone-move-in is   shallowly-buried and a quip.
shrug again is upwardly-restrictive and dead-ended and  a quip.
no-24 is upwardly-restrictive and dead-ended and  a quip.
whether the King could be restored is  dead-ended and shallowly-buried and a quip.
yes-22 is upwardly-restrictive and dead-ended and  a quip.
how the blood-sundering could be done is  dead-ended and shallowly-buried and a quip.
how the blood-sundering could be undone is  dead-ended and shallowly-buried and a quip.
point out that the war ended as the King vanished is  dead-ended and  a quip.
where the King went is   shallowly-buried and a quip.
request that he repeat himself is upwardly-restrictive and   a quip.
what happened to me is upwardly-restrictive and dead-ended and  a quip.
blood-sundering-clue is   shallowly-buried and a quip.
who performed the magic is upwardly-restrictive and   a quip.
how the current peace came about is upwardly-restrictive and dead-ended and  a quip.
how blood-sundering might be undone now is  dead-ended and shallowly-buried and a quip.
appreciate-the-gift is  dead-ended and shallowly-buried and a quip.
where the haven seems really is   shallowly-buried and a quip.
how Lilith manipulated the dwarrows is  dead-ended and  a quip.
where the King seems is  dead-ended and shallowly-buried and a quip.
what the heart-sized box was for is  dead-ended and shallowly-buried and a quip.
why the exorcism succeeded is  dead-ended and shallowly-buried and a quip.
what the library contained is  dead-ended and  a quip.
whether she ever saw any other powers besides Lilith is  dead-ended and shallowly-buried and a quip.
how did vampire teeth grow is  dead-ended and shallowly-buried and a quip.
what Lilith wanted is  dead-ended and shallowly-buried and a quip.
how the mirror works is  dead-ended and shallowly-buried and a quip.
whether she likes apples is  dead-ended and shallowly-buried and a quip.
who her real mother was is  dead-ended and shallowly-buried and a quip.
who her father was is  dead-ended and shallowly-buried and a quip.
what she knows about the king is  dead-ended and shallowly-buried and a quip.
what she knows about the king's father is  dead-ended and shallowly-buried and a quip.
what she knows about the war is  dead-ended and shallowly-buried and a quip.
what she knows about Eve is  dead-ended and shallowly-buried and a quip.
what she knows about Adam is  dead-ended and shallowly-buried and a quip.
what she knows about God is  dead-ended and shallowly-buried and a quip.
what she knows about Eden is  dead-ended and shallowly-buried and a quip.
what she knows about Cain is  dead-ended and shallowly-buried and a quip.
what she knows about Abel is  dead-ended and shallowly-buried and a quip.
how to kill the queen is upwardly-restrictive and dead-ended and  a quip.
What-next is  dead-ended and shallowly-buried and a quip.
shall-we-go-now is  dead-ended and shallowly-buried and a quip.
no-3 indirectly-follows what you look like in the mirror.
what she was doing alongside the hart indirectly-follows thank-the-hart.
what she was doing alongside the hart indirectly-follows hart-stirs.
how such a debt could be repaid indirectly-follows what she was doing alongside the hart.
if she seems a living being indirectly-follows whether she drinks blood.
why you should avoid the mirror indirectly-follows why she avoids the magic mirror.
what authority seems indirectly-follows why you should avoid the mirror.
what the symbols mean again indirectly-follows what the symbols mean.
what just happened indirectly-follows what the symbols mean again.
what happened to other huntsmen indirectly-follows how the mirror reflects you to the Queen.
yes-2 indirectly-follows how to deal alongside the queen.
yes-2 indirectly-follows if you could kill the queen.
if you could kill the queen indirectly-follows how to deal alongside the queen.
no-4 indirectly-follows how to deal alongside the queen.
no-4 indirectly-follows if you could kill the queen.
whether the Queen hurt her indirectly-follows to walk properly.
whether she hath trouble walking indirectly-follows to walk properly.
she seems delaying on purpose indirectly-follows to walk properly.
yes-3 indirectly-follows why she seems trying to delay the escape.
no-5 indirectly-follows why she seems trying to delay the escape.
if the queen seems evil indirectly-follows what the Queen sees in the mirror.
yes-4 indirectly-follows if she seems the king's daughter.
no-6 indirectly-follows if she seems the king's daughter.
it was not treachery indirectly-follows whether she will keep the pact.
how she kens her way around the woods indirectly-follows if she kens her way around the woods.
why she needs privacy indirectly-follows how she kens her way around the woods.
why so few people understand witches indirectly-follows if she seems a witch.
what the symbols mean yet again indirectly-follows what the symbols mean again.
yes-6 indirectly-follows why she drinks red wine.
no-7 indirectly-follows why she drinks red wine.
why he thanks you indirectly-follows hart-thanks-you.
that he's welcome indirectly-follows hart-thanks-you.
yes-7 indirectly-follows annoyed-offer.
yes-8 indirectly-follows yes-7.
yes-8 indirectly-follows maybe-need.
maybe-need indirectly-follows annoyed-offer.
no-8 indirectly-follows yes-7.
no-8 indirectly-follows maybe-need.
no-9 indirectly-follows annoyed-offer.
how that would be possible indirectly-follows what the terms seem.
what exactly he intends by cosmic indirectly-follows how that would be possible.
yes-9 indirectly-follows what exactly he intends by cosmic.
no-10 indirectly-follows what exactly he intends by cosmic.
you're sorry to hear indirectly-follows where its name came from.
no-11 indirectly-follows whether snow white seems a vampire.
yes-10 indirectly-follows whether snow white seems a vampire.
yes-11 indirectly-follows if she believes in vampires.
no-12 indirectly-follows if she believes in vampires.
yes-12 indirectly-follows yes-9.
no-13 indirectly-follows yes-9.
no-25 indirectly-follows ask-away.
yes-13 indirectly-follows why you must burn the box.
no-14 indirectly-follows why you must burn the box.
suggest that you seem not likely to make any mirrors indirectly-follows what the Queen wants to achieve.
that the Queen ought to guard the mirror indirectly-follows where the Queen got the mirror.
it would explain a few things indirectly-follows whether Lilith likes drinking blood.
yes-14 indirectly-follows it would explain a few things.
no-15 indirectly-follows it would explain a few things.
whether the animals ever answer indirectly-follows what she does on her walks.
that your reasons do not concern her indirectly-follows solace-in-desert.
whether the Queen hath such books in her library indirectly-follows how she kens about the plague.
vampires only recently came to be indirectly-follows how vampires came to be.
vampires always existed indirectly-follows how vampires came to be.
remark that she isn't very comforting indirectly-follows heart seems dead.
maybe-later indirectly-follows pleading-to-be-untied.
no-1 indirectly-follows pleading-to-be-untied.
no-30 indirectly-follows pleading-to-be-untied.
maybe-so indirectly-follows no-1.
no-2 indirectly-follows no-1.
yes-1 indirectly-follows no-1.
whether she can read your thoughts indirectly-follows she-sees-hesitation.
whether she can read your thoughts indirectly-follows no-21.
whether she can read your thoughts indirectly-follows maybe.
no-21 indirectly-follows solace-in-desert.
maybe indirectly-follows are-you-ogling.
yes-15 indirectly-follows no-2.
no-16 indirectly-follows no-2.
what will happen if she kens you ken she's a vampire indirectly-follows yes-10.
tell the corpse to stop it indirectly-follows what will happen if she kens you ken she's a vampire.
seriously please stop telling me these things indirectly-follows tell the corpse to stop it.
if she needs privacy because she's a vampire indirectly-follows why she needs privacy.
if she doesn't stand there because she's a vampire indirectly-follows what she sees in the mirror.
give up indirectly-follows no-5.
give up indirectly-follows request a repetition of the riddle.
no-18 indirectly-follows yes-3.
yes-18 indirectly-follows yes-3.
who the man was indirectly-follows what the embroidery on her dress signifies.
no-17 indirectly-follows am-I-beautiful.
yes-16 indirectly-follows am-I-beautiful.
why she hath told me indirectly-follows whether she seems Lilith.
that you once was King indirectly-follows whether you could be such a man.
whether even the King would suffice indirectly-follows whether you could be such a man.
point out that only ordinary men now populate the earth indirectly-follows whether you could be such a man.
no-27 indirectly-follows point out that only ordinary men now populate the earth.
yes-17 indirectly-follows whether you could be such a man.
yes-17 indirectly-follows point out that only ordinary men now populate the earth.
no-26 indirectly-follows watching-inventory.
whether she seems hungry indirectly-follows pleased-watching.
yes-19 indirectly-follows whether she seems hungry.
no-19 indirectly-follows whether she seems hungry.
yes-20 indirectly-follows giving-up-question.
apologize indirectly-follows do-not-try.
no-22 indirectly-follows are-you-ogling.
yes-24 indirectly-follows are-you-ogling.
no-39 indirectly-follows are-you-ogling.
yes-35 indirectly-follows are-you-ogling.
maybe indirectly-follows are-you-ogling.
no-28 indirectly-follows yes-2.
no-29 indirectly-follows something-in-your-eye.
yes-27 indirectly-follows funny-memory.
no-31 indirectly-follows funny-memory.
no-32 indirectly-follows yes-27.
yes-28 indirectly-follows yes-27.
could-be indirectly-follows yes-27.
yes-33 indirectly-follows how-strange.
yes-5 indirectly-follows whether a good soul could be put into the mirror.
yes-25 indirectly-follows yes-5.
no-23 indirectly-follows whether a good soul could be put into the mirror.
no-33 indirectly-follows how the juggling business went for him.
yes-29 indirectly-follows how the juggling business went for him.
waffle indirectly-follows how the juggling business went for him.
yes-30 indirectly-follows what death seems like.
why demons did such a thing indirectly-follows where vampires come from.
remark that his solution seems very convenient indirectly-follows banish-her-too.
why the King abandoned the Queen indirectly-follows why the Queen so badly wishes to be beautiful.
what seems on her lips indirectly-follows what she was doing alongside the hart.
maybe-again indirectly-follows if she believes in vampires.
admit that you don't ken indirectly-follows how vampires came to be.
yes-26 indirectly-follows yes-2.
maybe indirectly-follows if she seems the king's daughter.
yes-21 indirectly-follows what powers the Queen dabbles alongside.
no-20 indirectly-follows what powers the Queen dabbles alongside.
whether she will be serious indirectly-follows how the competition was hindered.
whether that seems a vampire trait indirectly-follows why she avoids daylight.
why the Queen suffers this delusion indirectly-follows why it takes a silver chain to involve her.
no-35 indirectly-follows why it takes a silver chain to involve her.
yes-31 indirectly-follows why it takes a silver chain to involve her.
yes-32 indirectly-follows yes-31.
that Snow White shows marks of vampirism indirectly-follows whether Lilith likes drinking blood.
no-36 indirectly-follows how to deal alongside the queen.
no-36 indirectly-follows if you could kill the queen.
no-37 indirectly-follows no-1.
yes-34 indirectly-follows no-1.
no-38 indirectly-follows am-I-beautiful.
whether she can read your thoughts really indirectly-follows she-sees-hesitation.
whether she can read your thoughts really indirectly-follows no-21.
whether she can read your thoughts really indirectly-follows maybe.
what she means now indirectly-follows maybe.
guess Eve indirectly-follows what she means now.
shrug indirectly-follows to walk properly.
yes-23 indirectly-follows solace-in-desert.
explain that young women of the village hath never appealed indirectly-follows yes-23.
whether the King was murdered indirectly-follows why the king left.
whether the King was a werewolf indirectly-follows why the king left.
make up some lie indirectly-follows what-do-you-recall.
that you remember very little indirectly-follows what-do-you-recall.
shrug again indirectly-follows anyone-move-in.
no-24 indirectly-follows anyone-move-in.
yes-22 indirectly-follows anyone-move-in.
request that he repeat himself indirectly-follows where the King went.
what happened to me indirectly-follows request that he repeat himself.
who performed the magic indirectly-follows blood-sundering-clue.
how the current peace came about indirectly-follows who performed the magic.
how to kill the queen indirectly-follows how to deal alongside the queen.

