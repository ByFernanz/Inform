"Color the Truth" by Brian Rushton.
The story genre is "Mystery". The story headline is "A variegated verity". The story description is "A conversational murder mystery." The story creation year is 2016. The release number is 2.

Use no scoring, the serial comma, and American dialect.

Include Basic Screen Effects by Emily Short.

Include Glulx Text Effects by Emily Short.

Release along with cover art ("Four eyes in red, green, blue, and yellow"). Release along with source text. Release along with an interpreter.

Part 1 - Preliminaries

The Phone Booth is a Room.

Instead of looking when the player is in the phone booth:
	say "[one of][or]You are in a phone booth, trying to look inconspicuous. You are holding the handset, the cord dangling between you and the base on the wall.[stopping]";

The cord is in Phone Booth. The cord is scenery. The description of the cord is "The cord dangles from the handset."

The glass wall is in the phone booth. The printed name of the glass wall is "phone booth". Understand "walls" or "phone booth" or "booth" as the glass wall. The glass wall is scenery. The description of the glass wall is "The phone booth wall is made of glass, but the fog outside makes it hard to see anyone."

The phone base is part of the glass wall. The phone base is scenery. The description of the phone base is "The base of the phone is grimy and sticky. The quarter slot is surrounded by a layer of grime, and the coin return flap is rusted." Understand "grimy" or "sticky" as the phone base.

The quarter slot is part of the phone base. The quarter slot is scenery. The description of the quarter slot is "The quarter slot is surrounded by a layer of grime." Understand "layer" or "grime" as quarter slot.

Instead of rubbing the quarter slot:
	say "You idly buff it with your hand, but nothing happens. The grime seems to have set in."
	
The coin return flap is part of the phone base. The coin return flap is scenery. The coin return flap is an openable container. The coin return flap is closed. The description of the coin return flap is "The coin return flap is empty."

The fog is in the phone booth. The fog is scenery. The description of the fog is "Grey fog swirls about outside the booth."

Instead of exiting when the player is in the phone booth:
	say "You need to finish this call, first."

[Studioing is an action out of world. Understand "studio" as studioing.

Carry out studioing:
	now the player is Maia;
	now the player is in MaiaControl.]

Section 1 - Information

Understand "who" or "what" or "when" or "where" or "why" or "how" or "who's" or "what's" or "when's" or "where's" or "why's" or "how's" as "[query]". 

Understand "[query] [text]" or "[query]" as a mistake ("[story title] understands commands, such as 'GO NORTH', but not questions. For more instructions, type COMMANDS."). 

Abouting is an action out of world. Understand "about" or "help" as abouting.

Carry out abouting:	
 say "Color the Truth is a parser game, which means that you type in short phrases that determine the main character's actions. For a list of standard commands, type COMMANDS and press enter.

[bold type]Speech[roman type]: In this game, you have a list of topics that you can talk to others about (found by typing TOPICS, or just T). You communicate with others by typing SAY [bracket]some topic[close bracket] when you are around another person, or by just typing the name of the topic. The text of a topic and the history of the suspects[apostrophe] responses can be found by examining the topic.

[bold type]Flashbacks[roman type]:  You can relive the experiences of witnesses by SAYing STATEMENT to them. All flashbacks have a natural ending, but, to exit a flashback early, you can type STOP.

[bold type]Deductions[roman type]: You will frequently need to catch people in contradictions. Therefore, topics can be linked to each other to create new topics by deduction. To link two topics, just type LINK [bracket]first topic[close bracket] TO [bracket]second topic[close bracket]. Linking is an essential part of this game, but only after the statements have been taken.
	
[bold type]Credits[roman type]: For credits, type CREDITS. For hints, type HINTS.";

Hinting is an action out of world. Understand "hints" or "hint" as hinting.

FirstHint is a number that varies. FirstHint is 0.

To say ShadowyHint:
	if hello is not carried by the player:
		say "[one of]All you need to do is wait.[or]Just type WAIT.[stopping]";
	otherwise 	if Hello is MeihRosa:
		say "[one of]You should say hello to Rosalita.[or]Type HELLO and press enter.[stopping]";
	otherwise if the number of RosaGiven quips carried by the player is one:
		say "[one of]You should try saying something else to Rosalita.[or]For instance, you can type SALES.[stopping]";
	otherwise if Funds is not carried by the player:
		say "[one of]You need to deduce a new topic from old ones. You should LINK one topic TO another topic that contradicts it.[or]Type LINK BROKE TO SALES or LINK BROKE TO CHARITY.[stopping]";
	otherwise:
		say "[one of]Tell Rosalita about your new deduction.[or]Type FUNDS.[stopping]";

To say CopHint:
	if the location of the player is ChiefOffice:
		if Chief is in HoldingCell:
			say "[one of]You just need to wait.[or]Type WAIT.[stopping]";
		otherwise if the manila folder is not carried by the player:
			say "[one of]You should take the folder.[or]Type TAKE FOLDER.[stopping]";
		otherwise:
			say "[one of]You can talk to the Chief, if you like, by typing the names of topics.[or]You can just leave.[or]Type SOUTH.[stopping]";
	otherwise if Volunteer is not carried by the player:
		if the location of the player is CopDannyOffice:
			say "[one of]You need to take Danny's statement, and let him finish telling you all of it.[or]Type STATEMENT.[stopping]";
		otherwise:
			let way be the best route from the location of the player to CopDannyOffice, using doors;
			say "[one of]You haven't finished all the statements, yet.[or]You need to take Danny's statement.[or]Go [way] to get closer to Danny.[stopping]";
	otherwise if Speechless is not carried by the player:
		if the location of the player is CopChuckOffice:
			say "[one of]You need to take Chuck's statement, and let him finish telling you all of it.[or]Type STATEMENT.[stopping]";
		otherwise:
			let way be the best route from the location of the player to CopChuckOffice, using doors;
			say "[one of]You haven't finished all the statements, yet.[or]You need to take Chuck's statement.[or]Go [way] to get closer to Chuck.[stopping]";
	otherwise if Stranger is not carried by the player:
		if the location of the player is CopBreak:
			say "[one of]You need to take Cindy's statement, and let her finish telling you all of it.[or]Type STATEMENT.[stopping]";
		otherwise:
			let way be the best route from the location of the player to CopBreak, using doors;
			say "[one of]You haven't finished all the statements, yet.[or]You need to take Cindy's statement.[or]Go [way] to get closer to Cindy.[stopping]";
	otherwise if Home is not carried by the player:
		if the location of the player is CopStudio:
			say "[one of]You need to take Maia's statement, and let her finish telling you all of it.[or]Type STATEMENT.[stopping]";
		otherwise:
			let way be the best route from the location of the player to CopStudio, using doors;
			say "[one of]You haven't finished all the statements, yet.[or]You need to take Maia's statement.[or]Go [way] to get closer to Maia.[stopping]";
	otherwise if Hidden is not carried by the player:
		if Buses is not carried by the player:
			say "[one of]You need to look for holes in the suspects['] statements.[or]For instance, Maia said she took the bus home. Is this true?[or]Cindy said the buses weren't running, which contradicts Maia's statement.[or]You need to LINK Maia's comment on buses TO Cindy's comment on buses.[or]Type LINK PARADE TO HOME.[stopping]";
		otherwise if the location of the player is not CopStudio:
			let way be the best route from the location of the player to CopStudio, using doors;
			say "[one of]You have more to discuss with Maia.[or]Go talk to Maia in the studio.[or]Go [way] to get closer to Maia.[stopping]";
		otherwise if Buses is MeihMaia:
			say "[one of]You should tell Maia about the contradiction you discovered between her and Cindy.[or]Try talking to Maia about buses.[or]Type BUSES.[stopping]";
		otherwise:
			say "[one of]You need to take Maia's statement again, and let her finish it completely.[or]Type STATEMENT.[stopping]";
	otherwise if Tryst is not carried by the player:
		if Conversation is not carried by the player:
			if Flowers is not carried by the player:
				say "[one of]Chuck said he didn't talk to Rosalita, but that's a bit odd.[or]You don't have anything yet that contradicts this.[or]It might help to ask people about what was bothering Rosalita, or to explore her office in a flashback.[or]Cindy's too distracted in her flashback; try Danny's.[or]During Danny's flashback, when you're in Rosalita's office, type LOOK AT NOTE.[or]Alternatively, you can type TROUBLED when talking to Cindy.[stopping]";
			otherwise:
				say "[one of]You need to look for holes in the suspects['] statements.[or]For instance, Chuck said he didn't talk to Rosalita. Is this true?[or]Chuck said he didn't talk to Rosalita, which contradicts the note on the flowers.[or]You need to LINK Chuck's comment about not talking with Rosalita TO the comment about the flowers.[or]Type LINK SPEECHLESS TO FLOWERS.[stopping]";
		otherwise if the location of the player is not CopChuckOffice:
			let way be the best route from the location of the player to CopChuckOffice, using doors;
			say "[one of]You have more to discuss with Chuck.[or]Go talk to Chuck in his office.[or]Go [way] to get closer to Chuck.[stopping]";
		otherwise if Conversation is MeihChuck:
			say "[one of]You should tell Chuck about the contradiction you discovered between him and the others.[or]Try talking to Chuck about his conversation.[or]Type CONVERSATION[stopping]";
		otherwise:
			say "[one of]You need to take Chuck's statement again, and let him finish it completely.[or]Type STATEMENT.[stopping]";
	otherwise if Fraud is not carried by the player:
		if Missing is not carried by the player:
			say "[one of]You need to look for holes in the suspects['] statements.[or]For instance, Danny said that all the records were in perfect order. Is this true?[or]Maia said some receipts were hidden behind the console, which contradicts Danny's statement.[or]You need to LINK Danny's comment on records TO Maia's comment on receipts.[or]Type LINK RECORDS TO HIDDEN.[stopping]";		
		otherwise if Absent is not carried by the player:
			say "[one of]You need to look for more holes in the suspects['] statements.[or]For instance, Danny said that Rosalita was volunteering at a shelter on Wednesday. Is this true?[or]Chuck says he spent Wednesday night with Rosalita, which contradicts Danny's statement.[or]You need to LINK Danny's comment on volunteering TO Chucks's story about Rosalita.[or]Type LINK VOLUNTEER TO TRYST.[stopping]";		
		otherwise if the location of the player is not CopDannyOffice:
			let way be the best route from the location of the player to CopDannyOffice, using doors;
			say "[one of]You've learned quite a bit about Rosalita and the studio; you need to talk to someone about it all.[or]Go talk to Danny in his office.[or]Go [way] to get closer to Danny.[stopping]";
		otherwise if Missing is MeihDanny:
			say "[one of]You should tell Danny about the contradiction you discovered between him and Maia.[or]Try talking to Danny about the missing records.[or]Type MISSING.[stopping]";
		otherwise if Absent is MeihDanny:
			say "[one of]You should tell Danny about the contradiction you discovered between him and Chuck.[or]Try talking to Danny about Rosalita not going to the shelter.[or]Type ABSENT.[stopping]";
		otherwise:
			say "[one of]You need to take Danny's statement again, and let him finish it completely.[or]Type STATEMENT.[stopping]";
	otherwise if Truth is not carried by the player:
		if Accomplice is not carried by the player:
			say "[one of]You need to look for holes in the suspects['] statements.[or]For instance, Cindy said she did all the accounting for Roses for Rehab. Is this true?[or]Cindy said she did accounting for Roses for Rehab, but Danny said that Rosalita was stealing from Roses for Rehab.[or]You need to LINK Danny's comment on fraud TO Cindy's comment on accounting.[or]Type LINK FRAUD TO ACCOUNTING.[stopping]";
		otherwise if the location of the player is not CopBreak:
			let way be the best route from the location of the player to CopBreak, using doors;
			say "[one of]You have more to discuss with Cindy.[or]Go talk to Cindy in the break room.[or]Go [way] to get closer to Cindy.[stopping]";
		otherwise if Accomplice is MeihCindy:
			say "[one of]You should tell Cindy about the contradiction you discovered between her and Danny.[or]Try talking to Cindy about her work with Rosalita.[or]Type ACCOMPLICE.[stopping]";
		otherwise:
			say "[one of]You need to take Cindy's statement again, and let her finish it completely.[or]Type STATEMENT.[stopping]";
	otherwise:
		if the location of the player is not CopChuckOffice:
			let way be the best route from the location of the player to CopChuckOffice, using doors;
			say "[one of]You need to discuss the truth with Chuck, or with the Chief.[or]You might as well try Chuck, in his office.[or]Go [way] to get closer to Chuck.[stopping]";
		otherwise:
			say "[one of]Confront Chuck with the truth.[or]Say TRUTH.[stopping]";

To say FraudHint:
	if FraudInvestigated is 0:
		if the location of the player is not DannyDannyOffice:
			let way be the best route from the location of the player to DannyDannyOffice, using doors;
			say "[one of]You need to look up the annual report in your office.[or]To get one step closer to your office, go [way].[stopping]";
		otherwise:
			say "[one of]You need to look up the annual report.[or]Type LOOK UP REPORT.[stopping]";
	otherwise:
		if OfficeRosalita is in FinishedCell:
			if Flowers is not carried by the player:
				if the location of the player is not DannyRosasOffice:
					let way be the best route from the location of the player to DannyRosasOffice, using doors;
					say "[one of]That vase of flowers and note in Rosalita's office seemed interesting.[or]You should go back and check out the note in her office.[or]To get one step closer to Rosalita's office, go [way].[stopping]";
				otherwise:
					say "[one of]You should check out that note; it looks interesting.[or]Type X NOTE.[stopping]";
			otherwise if the location of the player is DannyLobby:
				say "[one of]You can leave, now.[or]Just type SOUTH to leave.[stopping]";
			otherwise:
				let way be the best route from the location of the player to DannyLobby, using doors;
				say "[one of]You should go to the lobby and leave.[or]Go [way] to get one step closer to the lobby. When you reach the lobby, go south.[stopping]";
		otherwise if the location of the player is DannyRosasOffice:
			say "[one of]You need to talk to Rosalita.[or]Type TALK TO ROSALITA.[stopping]";
		otherwise:
			let way be the best route from the location of the player to DannyRosasOffice, using doors;
			say "[one of]You need to go to Rosalita's office at the north end of the building.[or]To get one step closer to Rosalita's office, go [way].[stopping]";

To say SadHint:
	if SearchCounter is less than 3:
		say "[one of]You need to pack up all your things.[or]You can take each object and put it in the box.[or]Type TAKE ALL, the PUT ALL IN BOX.[stopping]";
	otherwise:
		if the location of the player is NearbyDriveway:
			say "[one of]You need to go to Rosalita's house to the west.[or]Type GO WEST.[stopping]";
		otherwise if the location of the player is Driveway:
			say "[one of]You want to see if Rosalita will talk to you.[or]Try knocking on her door.[or]Actually, you can just GO NORTH.[stopping]";
		otherwise if the location of the player is ChuckChuckOffice:
			say "[one of]You want to follow Rosalita to try to talk to her.[or]Rosalita left to the east.[stopping]";
		otherwise:			
			say "[one of]You want to follow Rosalita to try to talk to her.[or]Rosalita left to the south.[or]Type GO SOUTH.[stopping]";

To say CleanHint:
	if burnt popcorn is in CindyToilet:
		if the location of the player is not CindyPrivateBathroom:
			let way be the best route from the location of the player to CindyPrivateBathroom, using doors;
			say "[one of]You need to go back and flush that popcorn![or]You need to go back to the bathroom.[or]Go [way] to get one step closer to the bathroom.[stopping]";
		otherwise:
			say "[one of]You need to flush the toilet![or]Type FLUSH.[stopping]";
	otherwise if the burnt popcorn is not in HoldingCell:
		if the burnt popcorn is carried by the player:
			if the location of the player is CindyPrivateBathroom:
				say "[one of]You need to get rid of the popcorn![or]Type THROW AWAY POPCORN.[stopping]";
			otherwise:
				let way be the best route from the location of the player to CindyPrivateBathroom, using doors;
				say "[one of]You need to find a place to throw the popcorn away![or]The bathroom has a trash can.[or]Go [way] to get one step closer to the bathroom.[stopping]";
		otherwise:
			if the burnt popcorn is in CindyMicrowave:
				if the location of the player is CindyBreak:
					if CindyMicrowave is closed:
						say "[one of]You need to deal with that smoking microwave.[or]You should figure out what's smoking.[or]Type OPEN MICROWAVE.[stopping]";
					otherwise:
						say "[one of]You should deal with that popcorn.[or]Type TAKE POPCORN.[stopping]";
				otherwise:
					let way be the best route from the location of the player to CindyBreak, using doors;
					say "[one of]You should go to the break room.[or]You should go [way] to get closer to the break room.[stopping]";
			otherwise:
				if the location of the burnt popcorn is the location of the player:
					say "[one of]You need to deal with that popcorn.[or]You should type TAKE POPCORN.[stopping]";
				otherwise:
					let way be the best route from the location of the player to the location of the burnt popcorn, using doors;
					say "[one of]You need to go back to the popcorn.[or]Go [way] to get closer to the popcorn.[stopping]";
	otherwise if FanTaken is 0:
		if CindyFan is in CindyCloset:
			if the location of the player is not CindyCloset:
				let way be the best route from the location of the player to CindyCloset, using doors;
				say "[one of]You need to blow away the smell somehow.[or]There's a fan in the closet.[or]Go [way] to get closer to the closet.[stopping]";
			otherwise:
				say "[one of]You need this fan to blow away the smell in the break room.[or]Type TAKE FAN.[stopping]";
		otherwise:
			if the location of Cindyfan is not CindyBreak:
				if Cindyfan is not carried by the player:
					if the location of Cindyfan is not the location of the player:
						let way be the best route from the location of the player to the location of Cindyfan, using doors;
						say "[one of]You need to put the fan in the break room.[or]You need to go get the fan again.[or]Go [way] to get closer to the fan.[stopping]";
					otherwise:
						say "[one of]You need to put the fan in the break room.[or]Type TAKE FAN.[stopping]";
				otherwise:
					let way be the best route from the location of the player to CindyBreak, using doors;
					say "[one of]You need to take the fan to the break room to stop the smell.[or]Just go [way] to get closer to the break room.[stopping]";
			otherwise:
				if Cindyfan is carried by the player:
					say "[one of]You need to use the fan to get rid of the smell.[or]You need to put it down and make sure it's turned on.[or]Type DROP FAN.[stopping]";
				otherwise:
					say "[one of]You just need to turn the fan on.[or]Type TURN ON FAN.[stopping]";
	otherwise if FoundRosalita is 0:
		let way be the best route from the location of the player to CindyRosasOffice, using doors;
		say "[one of]You can go clean out Rosalita's office now.[or]Go [way] to get closer to Rosalita's office.[stopping]";
	otherwise:
		if the location of the player is not CindyLobby:
			let way be the best route from the location of the player to CindyLobby, using doors;
			say "[one of]You need to call the police![or]There's only one phone in the building.[or]It's in the lobby.[or]Go [way] to get closer to the phone.[stopping]";
		otherwise:
			say "[one of]You can call the police now that you're by a phone.[or]Type CALL POLICE.[stopping]";

To say StudioHint:
	if MaiaRosalita is in MaiaBreak:
		say "[one of]You have something important to talk to Rosalita about.[or]Type TALK TO ROSALITA or TALK TO ROSA.[stopping]";
	otherwise if the player is in MaiaBreak:
		say "[one of]You need to get out of this room.[or]Go east to get out of here.[stopping]";
	otherwise if the player is on MaiaFloor:
		say "[one of]You just need to get up.[or]Type GET UP.[stopping]";
	otherwise if Buses is MeihMaia:
		say "[one of]You need to go home.[or]You should go south.[stopping]";
	otherwise if MaiaAlarm is 1:
		say "[one of]You need to leave![or]You can't go out the front, because someone might see you.[or]Go east.[stopping]";
	otherwise if the session tapes are carried by the player:
		if the location of the player is MaiaControl:
			say "[one of]You should see what fell out on the floor.[or]Just type LOOK AT STACK.[stopping]";
		otherwise:
			say "[one of]You should go back to the control room.[or]Go east.[stopping]";
	otherwise if LetsGo is 1:
		if the location of the player is MaiaControl:
			say "[one of]Your recording is done, you can take the tapes and go.[or]Just type GET TAPES.[stopping]";			
		otherwise:
			say "[one of]You should go back to the control room.[or]Go east.[stopping]";
	otherwise if DidSing is 1:
		if the location of the player is MaiaControl:
			say "[one of]You should check your recording to see how it turned out.[or]Type PRESS PLAY.[stopping]";
		otherwise:
			say "[one of]You should see how the recording turned out.[or]Go east to get back to the control room.[stopping]";
	otherwise if the record button is depressed:
		if the location of the player is MaiaStudio:
			say "[one of]You need to sing for your recording![or]Type SING.[stopping]";
		otherwise:
			say "[one of]You need to sing, but there's no microphone in the control room.[or]There are microphones in the studio.[or]Go west.[stopping]";
	otherwise if the location of the player is MaiaControl:
		say "[one of]You need to start recording, so you can sing.[or]Look at the console to see which button to use.[or]Type PUSH RECORD.[stopping]";
	otherwise if the location of the player is MaiaStudio:
		say "[one of]You need to record a song.[or]The controls for the recording equipment are in a different room.[or]Go east.[stopping]";		
	otherwise:
		let way be the best route from the location of the player to MaiaStudio, using doors;
		say "[one of]You need to sneak into the studio.[or]Go [way] to get to the studio.[stopping]" 

Carry out hinting:
	if FirstHint is 0:
		say "[if persona is the shadowy figure]After the prologue, [end if]Chief Schwarz will talk to you about every topic and give you advice. Talking with him is occasionally required to further gameplay.
	
		For a hint on what to do in your current situation, type HINT again. Each time you type HINT, you will receive more specific information.";
		now FirstHint is 1;
	otherwise:
		if persona is the Shadowy Figure:
			say ShadowyHint;
		otherwise if persona is Cop:
			say CopHint;
		otherwise if persona is Danny:
			say FraudHint;
		otherwise if persona is Chuck:
			say SadHint;
		otherwise if persona is Cindy:
			say CleanHint;
		otherwise if persona is Maia:
			say StudioHint;
	
[Walkthroughing is an action out of world. Understand "walkthrough" as walkthroughing.

Carry out walkthroughing:
	say "First, you should SAY STATEMENT to each of the four main suspects. You should also go to your office and LOOK UP each of the suspects.
	
	Each of the suspects has something to hide. By finding contradictions between their statements, you can get them to change their tune. You can LINK a statement TO another statement that contradicts it.
	
	Lawnmowering (just trying every option) will work, but it can become exhausting. The most important conversational topics are the ones picked up during statements. If you're stuck, you may just not have received all of the statements you need."]

Thoughting is an action out of world. Understand "SYSTEM" as thoughting.

Quotationing is an action out of world. Understand "QUOTES" as quotationing.

Carry out quotationing:
	say "All of these quotes come from Emily Short's article on conversation in the IF theory reader.
	
	'ASK/TELL conversation can also mean minimal characterization of the PC. With a menu system, the player sees the PC’s dialogue; with ASK/TELL, he may get only responses from the other character, like so:
[line break]
[line break]
	>ASK FRED ABOUT THE PORSCHE
[line break]
[line break]    
	'It belonged to my uncle,' Fred replies. 'Don’t tell me you want to borrow it too.'
[line break]
[line break]
There’s no indication here of what the player’s character might have said.'

'In other aspects of IF design, it’s possible to limit the number of things a player could reasonably do: there are only so many verbs, and only a specific set of objects available, and the scope of action is fairly well understood. An NPC who understood all topics of conversation, and all kinds of tonality and mood, could never be exhaustively programmed; there would always be another quirk unaccounted for.'

'One of the least person-like habits of the typical IF NPC is that he always answers the same questions in the exact same words, regardless of how many times the player has asked.'

'One of the artificial abilities we might like to give our NPCs, aside from the ability to wander around a map intelligently and carry out complex goals, is the ability to understand what they are told: to keep track of what items of knowledge they have so far, use them to change their plans and goals, and even draw logical inferences from what they’ve learned.'

'This may seem terribly unsubtle; indeed, it goes against an accepted
wisdom that one is trying to build a mechanism that doesn’t appear mechanical and that the ideal end result will be a conversation that feels real but in which the player is never conscious of how it works. That goal is an interesting one to pursue, but in my opinion (and there are other opinions out there), most interactive fiction is better served by a model that gives the player some clues about what is being modeled and how he can interact productively. If you show the player that the characters are drawing logical deductions from his statements (say), then he will realize that choosing what facts to reveal is an important part of the game and pursue that angle rather than others.'
"

Carry out thoughting:
	say "Conversation is one of the most difficult aspects of parser interactive fiction. There are 3 main conversational systems in use: ASK/TELL, where you use keywords to discuss topics; menu systems where you select from predetermined quotes; and hybrid topics, where you have a menu but can use keywords to alter it. 
	
	All of these have some difficulties; ASK/TELL requires players to guess topics, or, if they are listed, encourages lanwmowering (trying every option in a row). Also, you generally have no idea what your character is saying, only what the responses are. In menu systems, you have less flexibility, while hybrid systems can be complicated. See QUOTES to see several quotations from Emily Short's article on conversation methods in the IF theory reader.
	
That's why, for this game, I've attempted to come up with a system where repetition of topics is unnecessary (because you can just REMEMBER or EXAMINE the topics to recall the conversation); where there is the flexibility of the ASK/TELL system combined with the ability to see what you'll say (the 'preview' in the TOPICS list); and treating the topics like concrete objects, so that the player's field of action is clearly set out. Also, by having a large number of objects that can be combined in a variety of ways, it makes lawnmowering more difficult. I don't see this system as replacing all other conversational systems, because it has its own drawbacks, but I think that it works well in games where all characters care about similar topics and gameplay is relaxed and nonlinear.


"

Commandsing is an action out of world. Understand "COMMANDS" as commandsing.

Carry out commandsing:
	say "This game can be completed with just a few basic commands. The most important commands have abbreviations, and are listed below:
	
	Directional commands: NORTH (or N), SOUTH (or S), EAST (or E), WEST (or W). 
	
	LOOK (or L) reminds you of everything that is around you, including people and things.
	
	LOOK AT (or EXAMINE or X) [bracket]something[close bracket] gives you a closer look at something. Some clues in this game can only be found by examining objects or people.
	
	TAKE [bracket]something[close bracket] and DROP [bracket]something[close bracket] are the basic commands for moving around objects.
	
	INVENTORY (or I) lists your items, and TOPICS (or T) lists things you can say.
	
	SAY [bracket]something[close bracket] allows you to talk to others. You can only say things in your topics list.
	
	LINK [bracket]something[close bracket] TO [bracket]something[close bracket] lets you combine two topics into a new one.
	
	SAVE, RESTORE, RESTART, and QUIT let you manage your save games and the game itself.
	
	Other commands like EAT, CLEAN, LOOK UP or DIAL will be hinted at in the text.
	
	Finally, if this list is too long and has scrolled up screen, type COMMANDS1, COMMANDS2, and COMMANDS3 to see it in smaller portions."

Commandsoneing is an action out of world. Understand "COMMANDS1" as commandsoneing.

Carry out commandsoneing:
	say "This game can be completed with just a few basic commands. The most important commands have abbreviations, and are listed below:
	
	Directional commands: NORTH (or N), SOUTH (or S), EAST (or E), WEST (or W).
	
	LOOK (or L) reminds you of everything that is around you, including people and things.
"

Commandstwoing is an action out of world. Understand "COMMANDS2" as commandstwoing.

Carry out commandstwoing:
	say "LOOK AT (or EXAMINE or X) [bracket]something[close bracket] gives you a closer look at something. Some clues in this game can only be found by examining objects or people.
	
	TAKE [bracket]something[close bracket] and DROP [bracket]something[close bracket] are the basic commands for moving around objects.
	
	INVENTORY (or I) lists your items, and TOPICS (or T) lists things you can say.
	
	SAY [bracket]something[close bracket] allows you to talk to others. You can only say things in your topics list."
	
Commandsthreeing is an action out of world. Understand "COMMANDS3" as commandsthreeing.

Carry out commandsthreeing:
	say "LINK [bracket]something[close bracket] TO [bracket]something[close bracket] lets you combine two topics into a new one.
	
	SAVE, RESTORE, RESTART, and QUIT let you manage your save games and the game itself.
	
	Other commands like EAT, CLEAN, LOOK UP or DIAL will be hinted at in the text."

Crediting is an action out of world. Understand "credits" as crediting.

Carry out crediting:
	say "Color The Truth is written by Brian Rushton under the name Mathbrush, using Inform 7. Inform 7 and its helpful examples library were developed by Graham Nelson and Emily Short.
	
	The linking of topics is inspired by comments from Chandler Groover on my previous game, Halloween Dance. The idea of having conversational topics in your inventory showed up in The Baker of Shireton before this game or Halloween Dance, and was an inspiration. The radio station setting was inspired by the circus setting in Ballyhoo; I tried to find something that would invoke similar feelings 30 years later.
	
	I kept several classic mystery games in mind while writing this game, including Deadline, Ballyhoo, Make It Good, Toby's Nose, and An Act of Murder.
	
	Color the Truth was beta tested by Ade McTavish, Arno Von Bories, kettie, Alice Grove, XanMag, Teaspoon, Hanon Ondricek, Marco Innocenti, Sam Kabo Ashwell, Robin Johnson, Owlor, Felicity Banks, Sean Shore and Ben Kidwell. Ade McTavish in particular tested almost every version of the game and provided extremely useful feedback.
	
	Finally, none of this would be possible without the support of my wonderful wife Lindsey, who tested my game and gave me the encouragement to keep on trying.
	
	On copyrights and trademarks: This work mentions Infocom and several of its games by name in a favorable, incidental light. Infocom is a currently unregistered trademark of Omni Consumer Products LLC, and its titles are unregistered trademarks of Activision.
	
	The many book, movie, and song titles referenced are not trademarkable or copyrightable under United States law."

Section 2 - Available exits

[Taken from the example Ways Out]

When play begins: 
	now left hand status line is "Exits: [exit list]"; 
	now right hand status line is "[location]".

A room can be blocked or unblocked. A room is usually unblocked.

To say exit list: 
	repeat with way running through directions: 
		let place be the room way from the location of the player;
		if place is a room:
			if place is visited:
				say " [italic type][way][roman type]";
			otherwise if place is blocked:
				say " [second custom style][way][roman type]";
			otherwise:
				say " [first custom style][way][roman type]".

Rule for printing the name of a direction (called the way) while constructing the status line: 
	choose row with a heading of the way in the Table of Abbreviation; 
	say "[shortcut entry]".

Table of Abbreviation 
heading	shortcut
north	"N"
northeast	"NE"
northwest	"NW"
east	"E"
southeast	"SE"
south	"S"
southwest	"SW"
west	"W"
up	"U"
down	"D"
inside	"IN"
outside	"OUT"

Table of User Styles (continued)
	style name	color	
	special-style-1	"#00FF00"
	special-style-1	"#888888"

	
Chapter 4 - Conversation

Section 1 - Definitions

A thing can be physical or not physical. A thing is usually physical.

A quip is a kind of thing.  A quip is never physical. A quip has some text called the preview. The preview of a quip is usually "Insert preview here.". A quip has a list of text called history. A quip can be temp or perm. A quip is usually perm.

A quip can be DannyGiven or MeihDanny. A quip is usually MeihDanny. A quip can be CindyGiven or MeihCindy. A quip is usually MeihCindy.  A quip can be ChuckGiven or MeihChuck. A quip is usually MeihChuck.  A quip can be MaiaGiven or MeihMaia. A quip is usually MeihMaia. A quip can be ChiefGiven or MeihChief. A quip is usually MeihChief. A quip can be RosaGiven or MeihRosa. A quip is usually MeihRosa.

A quip has some text called ChuckResponse. The ChuckResponse of a quip is usually "Chuck doesn't seem interested in that.". 

A quip has some text called DannyResponse. The DannyResponse of a quip is usually "Danny doesn't seem interested in that.".

A quip has some text called CindyResponse. The CindyResponse of a quip is usually "Cindy doesn't seem interested in that.".

A quip has some text called MaiaResponse. The MaiaResponse of a quip is usually "Maia doesn't seem interested in that.".

A quip has some text called ChiefResponse. The ChiefResponse of a quip is usually "Your boss doesn't seem interested in that.".

A quip has some text called RosaResponse. The RosaResponse of a quip is usually "Rosalita doesn't seem interested in that."

A quip has some text called ChuckSummary. The ChuckSummary of a quip is usually "You tried talking about this with Chuck.".

A quip has some text called DannySummary. The DannySummary of a quip is usually "You tried talking about this with Danny.".

A quip has some text called CindySummary. The CindySummary of a quip is usually "You tried talking about this with Cindy.".

A quip has some text called MaiaSummary. The MaiaSummary of a quip is usually "You tried talking about this with Maia.".

A quip has some text called ChiefSummary. The ChiefSummary of a quip is usually "You tried talking about this with your boss.".


A quip has some text called RosaSummary. The RosaSummary of a quip is usually "You already discussed this with Rosa.".

Rule for deciding whether all includes quips: it does not.


Instead of inserting a quip into a container:
	say "That's not a physical object.";

Instead of putting a quip on a supporter:
	say "That's not a physical object.";

Understand "recall [something]" or "remember [something]" as examining.

Section 2 - Quip dynamics

After printing the name of a quip (called currentquip) when topicing:
	Say " - '[the preview of currentquip]'".

Instead of examining a quip (called currentquip):
	repeat with	 currenttext running through history of currentquip:
		say currenttext;	

Delivering is an action applying to one visible thing.

Carry out delivering something (called currentquip):
	if currentquip is carried by the player:
		rule succeeds;
	otherwise:
		if TempOn is 0:
			say "[bracket]New [if currentquip is temp]response[otherwise]topic[end if] - [currentquip]  [close bracket]";
		otherwise if currentquip is perm:
			say "[bracket]New topic - [currentquip][close bracket]";
		say "[line break]";
		add "'" to history of currentquip;
		add preview of currentquip to history of currentquip;
		add "'" to history of currentquip;
		[This code is to enable the inventory limit]
		now currentquip is in the location of the player;
		silently try taking currentquip;
		if currentquip is carried by the player:
			rule succeeds;
		otherwise:
			silently try taking currentquip;

Speeching it to is an action applying to two things. Understand "say [something] to [someone]" or "tell [something] to [someone]" or "t [something] to [someone]" or "a [something] to [someone]" as speeching it to.

Singlespeeching is an action applying to one thing.

Carry out singlespeeching:
	repeat with current running through people in the location of the player:
		if current is not the player:
			try speeching the noun to current;

Keywording is an action applying to one thing. Understand "[something]" as keywording.

Carry out keywording:
	if the noun is not a quip:
		say "You need to supply a verb.";
	otherwise:
		if the number of people in the location of the player is at least two:
			try singlespeeching the noun;
		otherwise:
			say "There is no one to talk to here.";

HelpfulText is some text that varies. HelpfulText is "[one of][line break][bracket]You can REMEMBER or RECALL a topic to remind yourself about previous conversations.[close bracket][line break][or][or][or][or][or][or][or][or][or][or][or][or][line break][bracket]You seem to be trying to go through every conversation topic with every person. Progress in this game is always made by linking two topics, and then using these new topics.[close bracket][or][stopping]"

A person can be a speaker or not a speaker. Cop is a speaker. Shadowy figure is a speaker.

Carry out Speeching it to:
	if persona is not a speaker:
		say "During flashbacks, conversation advances by using TALK TO [bracket]someone[close bracket].";
	otherwise if the noun is not a quip:
		say "You can only discuss your current thoughts as listed in your topics.";
	otherwise if the second noun is not a person:
		say "You can only do that to something animate.";
	otherwise:
		if the second noun is Chuck:
			if the noun is MeihChuck:
				say ChuckResponse of noun;
				say "[line break]"; 
				now the noun is ChuckGiven;
				add "[line break][line break]" to history of noun;
				add ChuckSummary to history of noun;
				now DontTalk is 1;
			otherwise:
				say "You've already discussed this with Chuck.[HelpfulText]";
		otherwise if the second noun is Danny:
			if the noun is MeihDanny:
				say DannyResponse of noun;
				say "[line break]"; 
				now the noun is DannyGiven;
				add "[line break][line break]" to history of noun;
				add DannySummary to history of noun;
				now DontTalk is 1;
			otherwise:
				say "You've already discussed this with Danny.[HelpfulText]";
		otherwise if the second noun is Maia:
			if the noun is MeihMaia:
				say MaiaResponse of noun;
				say "[line break]"; 
				now the noun is MaiaGiven;
				add "[line break][line break]" to history of noun;
				add MaiaSummary to history of noun;
				now DontTalk is 1;
			otherwise:
				say "You've already discussed this with Maia.[HelpfulText]";
		otherwise if the second noun is Cindy:
			if the noun is MeihCindy:
				say CindyResponse of noun;
				say "[line break]"; 
				now the noun is CindyGiven;
				add "[line break][line break]" to history of noun;
				add CindySummary to history of noun;
				now DontTalk is 1;
			otherwise:
				say "You've already discussed this with Cindy.[HelpfulText]";
		otherwise if the second noun is Chief:
			if the noun is MeihChief:
				say ChiefResponse of noun;
				say "[line break]"; 
				now the noun is ChiefGiven;
				add "[line break][line break]" to history of noun;
				add ChiefSummary to history of noun;
				now DontTalk is 1;
			otherwise:
				say "You've already discussed this with the Chief.[HelpfulText]";
		otherwise if the second noun is FakeChief:
			if the noun is MeihChief:
				say ChiefResponse of noun;
				say "[line break]"; 
				now the noun is ChiefGiven;
				add "[line break][line break]" to history of noun;
				add ChiefSummary to history of noun;
				now DontTalk is 1;
			otherwise:
				say "You've already discussed this with the Chief.[HelpfulText]";
		otherwise if the second noun is BoothRosa:
			if the noun is MeihRosa:
				say RosaResponse of noun;
				say "[line break]"; 
				now the noun is RosaGiven;
				add "[line break][line break]" to history of noun;
			  	add RosaSummary to history of noun;
				now DontTalk is 1;
			otherwise:
				say "You've already discussed this with Rosalita.[HelpfulText]";

[Keywording is an action applying to two things. Understand "[something] to [someone]" as keywording.

Carry out keywording:
	if the noun is a quip:
		try speeching the noun to the second noun;
	otherwise:
		say "In this game, typing the name of something is the same as saying it, but you have typed the name of something that cannot be said.";

After Speeching something to someone:
	repeat with currenttempquip running through all temp quips carried by the player:
		now currenttempquip is in HoldingCell;]

Helloing is an action applying to one thing. Understand "greet [someone]" as helloing.

SingleHelloing is an action applying to nothing. Understand "hello" as singlehelloing.

Carry out singlehelloing:
	if persona is the shadowy figure:
		try speeching Hello to BoothRosa;
	otherwise:
		say "In this game, speech is handled by using SAY [bracket]a topic[close bracket], where topics can be found by typing T.";

Carry out helloing:
	if persona is the shadowy figure:
		try speeching Hello to BoothRosa;
	otherwise if greeting is not carried by the player:
		say "In this game, speech is handled by using SAY [bracket]a topic[close bracket], where topics can be found by typing T.";
	otherwise:
		try speeching greeting to the noun.

QuerySmalling is an action applying to two things. Understand "ask [something] to [someone]" as querysmalling.

Carry out QuerySmalling:
	try speeching the noun to the second noun. 

Querying it to is an action applying to two things. Understand "ask [someone] about [something]" as querying it to.

Carry out querying:
	try speeching the second noun to the noun;

Talking to is an action applying to one thing. Understand "talk to [someone]" as talking to.

Carry out talking to:
	say "In this game, conversation works by 'saying' thoughts to other people. You choose from thoughts in your topics. For instance, to introduce yourself to someone, you can SAY GREETING."

HoldingCell is a room.

Report going a direction:
	repeat with currenttempquip running through all temp quips carried by the player:
		now currenttempquip is in HoldingCell;
		say "[line break][bracket]Lost response: [currenttempquip][close bracket]"	

Section 3 - Long term memory

[LongMemory is the player's holdall. LongMemory is not physical. Understand "long" or "memory" or  "long term memory" as LongMemory. The printed name of LongMemory is "long term memory". The description of LongMemory is "These are all of your thoughts on the case that aren't currently on your mind."

Rule for printing the name of LongMemory when topicing:
	say "[bold type]Long term memory[if LongMemory is open] (open)[otherwise] (closed)[end if][roman type]";

Rule for deciding whether all includes a container while dropping:
	it does not.

Check inserting something (called trialobject) into LongMemory:
	if trialobject is a quip:
		now trialobject is in LongMemory;
		rule succeeds;
	otherwise:
		say "That's too tangible."]

Check dropping a quip (called currentquip):
	say "You never forget." instead;

Chapter 5 - Switching Identities

Cop is a person. The description of cop is "You are Detective Hector King, a 30-year veteran of the force and nearing retirement. You are wearing a frayed ash-grey suit with a checkered tie and a silvery dress shirt. Your stiff black-and-grey hair is cropped short, and your badge is pinned to your suit pocket. "

The persona is a person that varies. Persona is shadowy figure. 

Check examining yourself:
	try examining persona instead;
	rule succeeds;

Skipping is an action applying to nothing. Understand "skip" as skipping.

Chapter 6 - Inventory

EmptyText is some text that varies. EmptyText is "[line break]   nothing[first time]

[bracket]You can see current conversational topics by typing T or TOPICS[close bracket][only]".

Instead of taking inventory:
	say "You are carrying:";
	repeat with currentitem running through physical things carried by the player:
		now EmptyText is "";
		say "[line break]   [currentitem]";
	say "[EmptyText]";
	now EmptyText is "[line break]   nothing";
	say "[line break]";
	
Topicing is an action out of world. Understand "t" or "topics" or "thoughts" as topicing.

Carry out topicing:
	if TempOn is 0:
		if the player is carrying a temp quip:
			say "[bold type]Current responses:[roman type]";
			repeat with currentquip running through all temp quips carried by the player:
				say "[line break]   ";
				say currentquip;
			say "[line break]";
	if the player is carrying a perm quip:
		say "[bold type]Current thoughts[if persona is not a speaker] [bracket]of Detective King[close bracket][end if]:[roman type]";
		repeat with currentquip running through all perm quips carried by the player:
			say "[line break]   ";
			say currentquip;
		say "[line break]";
	otherwise:
		say "[bold type]Current thoughts[roman type]
		
			You are currently not thinking of anything.";

TempOn is a number that varies. TempOn is 1.

TempingOn is an action out of world. Understand "r" as TempingOn.

Carry out TempingOn:
	now TempOn is one minus TempOn;
	if TempOn is 1:
		say "Available responses will be printed in full every turn.";
	otherwise:
		say "Available responses are now hidden. You can find the full list in your topics list.";

Every turn:
	if the player is carrying a temp quip:
		if TempOn is 1:
			say "[bold type]Current responses:[roman type]";
			repeat with currentquip running through all temp quips carried by the player:
				say "[line break]   ";
				say currentquip;
				say " - '";
				say preview of currentquip;
				say "'";

Chapter 7 - Personas

Section 1 - Basics

A person has a room called StartRoom. StartRoom is usually CopLobby.

Switcherooing is an action applying to one visible thing. Understand "interrogate [something]" as switcherooing.

TempCell is a room.

When play begins:
	now the long pea coat is worn by the player;
	now the low hat is worn by the player;
	now the dark glasses are worn by the player;
	now the scarf is worn by the player;
	now the handset is carried by the player.

The long pea coat is worn by the player. The description of the long pea coat is "This long coat masks your body."

The low hat is worn by the player. The description of the low hat is "The hat shades your face."

The dark glasses are worn by the player. The description of the dark glasses is "The glasses mask your eyes."

The scarf is worn by the player. The description of the scarf is "The scarf hides your neck and chin."

The handset is carried by the player. The description of the handset is "The telephone booth handset is black and worn." Understand "phone" or "receiver" as handset.

Tapping is an action applying to one thing. Understand "tap" as tapping.

Carry out tapping:
	if the noun is the handset:
		say "You tap the handset nervously.";
	otherwise:
		say "You don't feel like tapping anything.";

Does the player mean taking the handset: it is likely.

Instead of dropping the handset:
	say "You're still talking to Rosalita."

Instead of putting the handset on the phone base:
	try hanguping.

[FIXTHISHERE

To say ClueText:
	if persona is Chuck:
		if Speechless is carried by the player:
			say "[one of][line break]Chuck paints a sad picture, but is it true? Did he really not talk to Rosalita that day? [or][stopping]".
		otherwise if ]

Carry out switcherooing:
	if persona is cop:		
		repeat with currentthing running through physical things carried by the player:
			now currentthing is in TempCell;
	otherwise:
		repeat with currentthing running through physical things carried by the player:
			now currentthing is in HoldingCell;
		repeat with currentthing running through things in TempCell:
			now currentthing is carried by the player;
	if the noun is a person:
		say "[if the noun is not cop]You lean back as [noun] gives you a statement. You can picture it all in your head...[otherwise]You clear your mind, and return to yourself.[end if]
		
		***********************
		[bracket]Press any key to switch characters[close bracket]
		***********************
		
";
		wait for any key;
		repeat with current running through things worn by the player:
			now current is worn by persona;
		if the noun is cop:
			now the badge is worn by the player;
			now the checkered tie is worn by the player;
			now the frayed ash-grey suit suit is worn by the player;
			now the silvery dress shirt is worn by the player;
		otherwise if the noun is Danny:
			 now the thick red sweater is worn by the player;
			now the tieless dress shirt is worn by the player;
		otherwise if the noun is Chuck:
			now the designer jeans are worn by the player;
			now the blue t-shirt is worn by the player;
		otherwise if the noun is Cindy:
			now the dress suit is worn by the player;
			now the yellow blouse is worn by the player;
		otherwise if the noun is Maia:
			now the long green sundress is worn by the player;
			now the light short jacket is worn by the player;
			now the several bracelets are worn by the player;						
		now the persona is the noun;
		now the player is in StartRoom of the noun;
 	otherwise:
		say "You can only do that to something animate.";

Instead of taking off something worn by the player:
	say "You don't feel like taking that off right now.";

The badge is worn by the cop. The description of the badge is "A slightly tarnished brass shield that says [apostrophe]KING[apostrophe]." 

The checkered tie is worn by the cop. The description of the checkered tie is "A grey and white patterned tie that Jayla bought you."

The frayed ash-grey suit suit is worn by the cop. The description of the frayed ash-grey suit suit is "Your best gray suit is getting worn around the edges, but it's got a few more years in it."

The silvery dress shirt is worn by the cop. The description of the silvery dress shirt is "The pale silver of the shirt contrasts nicely with your warm brown skin."


The thick red sweater is worn by Danny. The description of the thick red sweater is "A comfortable and warm red sweater."

The tieless dress shirt is worn by Danny. The description of the tieless dress shirt is "A casual dress shirt, the top buttons open."


The designer jeans are worn by Chuck. The description of the designer jeans is "An expensive brand of jeans, recently purchased."

The blue t-shirt is worn by Chuck. The description of the blue t-shirt is "A navy t-shirt, long-sleeved."


The dress suit is worn by Cindy. The description of the dress suit is "A dark yellow skirt with matching jacket."

The yellow blouse is worn by Cindy. The description of the yellow blouse is "A pale yellow blouse, slightly worn."

The denim jacket is worn by cindy. The description of the denim jacket is "A beige denim jacket."

The yellow slacks are worn by Cindy. The description of the yellow slacks is "Some bright yellow slacks."

The long green sundress is worn by Maia. The description of the long green sundress is "An elegant aqua sundress."

The light short jacket is worn by Maia. The description of the light short jacket is "A darker green jacket that falls to the midriff."

The several bracelets are worn by Maia. The description of the several bracelets is "A collection of jade bracelets."

Ceasing is an action applying to nothing. Understand "stop" as ceasing.

CeaseCell is a room.

[this still buggy; stuff like popcorn]
Carry out ceasing:
	if persona is cop:
		say "You're not in a flashback.";
	otherwise if persona is the shadowy figure:
		say "You're not in a flashback.";
	otherwise:
		repeat with currentthing running through physical things carried by the player:
			now currentthing is in CeaseCell;
		say "'I'd like you to stop right there.'";
		try switcherooing cop;
		now SawMaia is 0.

Section 2 - Danny

[Danny Kostopolous; conservative talk show host]

To say CindyVisit:
	say "[one of]Someone taps on the door. You both look up to see Cindy.
	
	'Sorry,' she says in a low whisper. 'Mr. Kostopolous, I forgot to tell you that Mayor Cutter called last night to cancel his interview for Friday. He's going out of town for a convention.'
	
	He glowers. 'Yeah, sure he's going out of town.' He looks at you. 'This mess has been bad for business, detective. I hope you get it sorted out soon.'
	
	He says to Cindy, 'Call Alan and see if he can come Friday.' He glances at you, and goes on. 'After we're done here, of course.'[or]Danny rubs his eyes and yawns.[stopping]"

To say WindowTime:
	say "[one of]Unexpectedly, Danny stands up and walks to the window. He looks out at the storm.
	
	Without turning around, he says, 'Detective, none of this should have happened. Rosalita was a good woman.' HIs fists are clenched, but he slowly releases them, and he wipes his eyes before turning around.
	
	He sits at his desk again. 'I'm sorry, you can continue.'[or]Danny taps his foot impatiently. [stopping]"

To say DannyAction:
	if the current action is not speeching a MeihDanny quip to Danny:
		say "[one of]Danny clears his throat loudly.[or]Danny taps his fingers on his desk.[or]Danny scratches his ear.[or]Danny looks out the window at the rain.[or]Danny says, 'Did I tell you that I carved all the furniture in this office myself?'[or]Thunder booms in the distance.[or]Danny frowns. He says, 'I probably already told you this, but we need to be done here by ten. We can only broadcast reruns so long.'[or]Danny leans forward. 'So tell me, detective, you got any new leads?'[or]Danny reaches up to adjust his tie, before he realizes he isn't wearing one. His hand sinks slowly back.[or]Danny looks over the memorabilia on his shelves.[or]Danny rubs his knee, a grimace crossing his face.[or]Danny tries to pick something out of his teeth covertly.[or]Danny pauses; he asks, 'Did you hear that? It sounds like something in the hall?' He listens for a moment, then says, 'Nevermind.'[or][CindyVisit][or]Danny frowns in thought.[or]Danny looks you over with a critical eye.[or][WindowTime][or]Danny picks at a splinter on the desk.[or]Danny sighs and leans forward, running his hand through his hair.[or]You hear two people talking down the hall, one voice higher than the other. Danny doesn't seem to notice.[or]Danny reaches up to brush a hair from his face.[or]Danny takes off his glasses to wipe them with his sweater, then returns them to his face.[or]Danny leans back for a moment, his chair creaking loudly.[or]Danny stares into the corner, slightly shaking his head from side to side.[in random order]"

Every turn when the player is in CopDannyOffice:
	if DontTalk is 0:
		say "[one of]You pause by the doorway, and he glances at you. 'Please, come in,' he says, and you step into the room.[or][DannyAction][stopping]";
	now DontTalk is 0;

DannyName is some text that varies. DannyName is "[if Partner is carried by the player]Danny [otherwise]An older man who must be Danny Kostopolous [end if]"

[Danny is based on Uncle Athanasias]
Danny is a person. "[if DannyAnger is less than 3][DannyName]sits slightly slumped in his chair with his hands clasped on his lap.[otherwise if DannyAnger is less than 5]Danny is leaned forward in his chair with a furrowed brow.[otherwise if DannyAnger is less than 7]Danny is highly irritated. He is breathing deeply, trying to relax. [otherwise]Danny looks like a man possessed by some deep emotion.[end if]". Understand "Kostopolous" as danny. Understand "Kostopoulos" or "man" or "older" as Danny.

The description of Danny is "[if persona is Danny]You are Danny Kostopolous, a fit, attractive man in his prime. Your thick head of hair is carefully arranged and coifed. Your new glasses (more for style than for eyesight) give you a serious and thoughtful look. Your thick red sweater, pulled over a tieless dress shirt, lends you some warmth in the freezing office.[otherwise]Danny Kostopolous is wearing a thick red sweater, matching the confident colors of his office. Seen in person, the Voice of the Heartland is a short, slim man who radiates irritation. His hands constantly reach up to fuss with his thick gray-peppered ginger hair when pieces stray out of line. Fine wrinkles seam his face. His thick-rimmed glasses are perched on an thin nose and almost mask the dark circles under his eyes.[end if]"

Understand "hair" or "wrinkles" or "nose" or "eyes" or "dark circles" or "circles" or "circle" as Danny.

Section 3 - Chuck

[Chuck Lee; former news anchor, now on network tv]

To say MaiaVisit:
	say "[one of]Someone says, 'Hello, Chuck,' in a low voice. You turn to see Andromeda leaned against the door frame in an attempt at a sultry pose.
	
	He looks up for a moment, then looks away dismissingly. 'Oh, hello, Maia.'
	
	She says, 'I was wondering if you'd like to get some lunch when we're done here.'
	
	You stand up. 'Miss Morales, you're making this investigation more difficult than it needs to be. Please, return to your room.'
	
	She frowns, and walks away.[or]Chuck looks like he's having trouble staying awake.[stopping]".

To say ChuckSad:
	say "[one of]Without warning, Chuck balls his hands into fasts and leans his head on them, sobbing.
	
	You step to the doorway for a moment, giving him a moment of privacy. The hallway is empty, with small gleams of light coming from Danny's office and the lobby. The studio door admits no light.
	
	After a moment, the sobs subside, and you step back into Chuck's office.[or]Chuck says, 'Officer, are we almost done here?'[stopping]"

[Chuck is Hu]
To say ChuckAction:
	if the current action is not speeching a MeihDanny quip to Danny:
		say "[one of]A tear runs down Chuck's cheek.[or]A fly lands on Chuck's hair. He doesn't notice. It soon flies away.[or]Rain patters on the window.[or]You feel a cold chill seeping in from outside.[or]Thunder grumbles in the distance.[or]Chuck sighs deeply.[or]Chuck rubs his knee.[or]Chuck stretches his back and grimaces.[or]Chuck taps his foot restlessly.[or]Chuck draws in a rattling breath.[or]Chuck's bloodshot eyes meet yours for a moment before turning away.[or]Chuck pulls a handkerchief out from a hidden pocket and wipes his eyes, before returning it.[or]Chuck closes his eyes for a long moment.[or]You hear someone walk by in the hallway, but they're gone by the time you turn around.[or]You hear a toilet flush and a door close from down the hall.[or]If you strain to listen, you think you can hear singing from somewhere.[or][MaiaVisit][or][ChuckSad][in random order]"

Every turn when the player is in CopChuckOffice:
	if DontTalk is 0:
		say "[one of]His eyes follow you as you enter the room, but he doesn't say anything.[or][ChuckAction][stopping]";
	now DontTalk is 0;

Chuck is a person. "[if Ex is carried by the player]Chuck[otherwise]A striking man who must be Chuck Lee[end if] sits in the chair, trying not to look exhausted." The description of Chuck is "[if persona is Chuck]You are Chuck Lee, former anchor for KOLR. You look at yourself critically. Stocky body. Close-cut, thick black hair. Expressive eyes. (They're the only part of your face that can express anything, after the surgery). Today is your off day. You are wearing designer jeans and a blue t-shirt. You are deeply tired.[otherwise]Chuck Lee, famous news anchor, is ruggedly handsome, with a firm chin and stiff, short-cropped hair. His short but muscular frame is draped in a well-tailored baby blue suit. The smoothness of his face and lack of expression suggest surgical enhancements. His eyes are puffy from crying.[end if]"

Understand "suit" or "striking" or "young" or "man" or "baby" or "blue" or "well-tailored" or "enhancements" or "eyes" or "red" or "handkerchief" or "chin" or "hair" or "frame" or "face" or "eye" as Chuck.

Section 4 - Maia

[Maia Morales; sister, and aspiring singer]
To say MaiaRequest:
	say "[one of]Maia clears her throat. 'Detective, I have a question.'
	
	'Yes?' you say.
	
	'When all of this is done... could you give me Chuck's number?' she asks.
	
	'Miss Morales, I don't think-' She cuts you off. 'Now, I know what you're thinking. But he's got to be cut up about Rosalita, and, well, I might be able to help him feel better.'
	
	You say, 'Miss Morales, I think you'll have to try contacting him on your own.'
	
	She rolls her eyes and sighs.[or]Maia rubs her eyes and yawns.[stopping]"

[Maia is based on Palomeques]

To say DannyMad:
	say "[one of]Someone knocks at the door. You turn to see who it is.
	
	Danny Kostopolous is standing at the door. He points at Maia. 'Officer, this woman should not be in here. There's expensive equipment in here.'
	
	Maia looks at you. 'Please,' she says, 'I've been in here many times.' Danny's face gets red at this. She continues. 'I think I can handle it without breaking anything.'
	
	You turn to Danny. You say, 'I'll try and hurry, Mr. Kostopolous. But I'd like you to return to your room; you're delaying the investigation.'
	
	He grumbles, and begins to walk away, but he stops and walks to the control room door. He jiggles the handle, but it's locked. He gives Maia a smug grin and says, 'At least you're not getting in there.' Then he walks out.[or]Maia sighs deeply.[stopping]."

To say MaiaAction:
	if the current action is not speeching a MeihMaia quip to Maia:
		say "[one of]Maia twirls her hair around one finger.[or]Maia looks at the microphones longingly.[or]Maia looks at the window to the control room.[or]Maia leans back and stretches, her back popping.[or]Maia runs her finger over the table.[or]Maia leans forward and says, 'I still can't believe Chuck Lee is here,' smiling.[or]Maia sighs, and leans her head on her hand.[or]Maia looks you over, seemingly unsatisfied with what she discovers.[or]Maia twirls one of her bracelets.[or]Maia picks some lint off of her dress.[or]You hear a door close somewhere in the station. Maia leans to look out the door, then looks back at you and shrugs.[or]The floor creaks as you shift your weight.[or][MaiaRequest][or]Maia says 'Gosh, I'm hungry. I hope you're bringing lunch in.'[or]Maia sings a song under her breath.[or]She looks at you, and leans in. 'You know, I just thought of a new song, based on this. I mean, I had one earlier, but this one's better.'[or][DannyMad][or]Maia reaches a finger up and gently taps a microphone head.[or]Maia stares at the door, lost in thought.[or]Maia shakes her head, and mutters, 'Rosalita.'[or]Maia scratches at her leg.[or]Maia crosses her legs.[or]Maia waves her hand at her nose before letting out a high-pitched, loud sneeze.[or]Maia crosses her eyes slightly; you realize she's focusing on her hair. She pulls one out in a sharp tug; it's gray. She tucks it in a pocket.[or]Maia taps her nails.[or]You hear voices in the hallway; one of the suspects must be talking to another.[or]Maia yawns.[or]Maia slumps down in her chair.[in random order]"

Every turn when the player is in CopStudio:
	if DontTalk is 0:
		say "[one of]She gives you a once-over as you walk in the room, then sighs, disappointed.[or][MaiaAction][stopping]";
	now DontTalk is 0;
	
Maia is a person. "[if Sister is carried by the player]Maia Morales[otherwise]A green-haired woman who must be Maia Morales[end if] sits at the table, looking bored." The description of Maia is "[if persona is Maia]You are Andromeda, famous singer. At least, you're trying to be. Your hair is dyed dark green, and your eyebrows are plucked into elegant arches. Your arm is covered in spangly bracelets. And your heart has a song.[otherwise]Maia Morales, aka Andromeda, is a tallish woman with shocking neon green hair and thinly plucked eyebrows. She is wearing a long emerald sundress, and a light aqua jacket is thrown over her shoulders. She glances languidly about the room.[end if]";

The neon green hair is part of Maia. The description of the neon green hair is "Maia's hair is long and dyed a flamboyant shade of green."

The thinly plucked eyebrows are part of Maia. The description of the thinly plucked eyebrows is "Maia's eyebrows are plucked thin."

Understand "morales" or "andromeda" or "girl" or "woman" or "lady" as Maia.

Section 5 - Cindy

[Cindy Jalcek, young eastern european intern]

[To say CindyName:
	say "[if greeting is MeihCindy]Cindy[otherwise]A young woman[end if]"

To say FirstCindy:
	say "[first time][CindyName] sits at a table with her head in her hands.[only]"]

To say DannyVisit:
	say "[one of][DannyName] comes into the room. Cindy slumps down in her chair, not looking at him. He looks at you and smiles, ignoring her. He says, 'Hey, officer, just stopping in for a drink.'
	
	He puts a few coins in the vending machine, punches a button, and grabs a soda. He opens it and takes a sip, then looks at you.
	
	'Making any progress?'
	
	You shrug. You say, 'We'll have to see. Although it'd be easier for me if I knew where you were.'
	
	He sighs. 'I'll head back, don't worry.' He walks back to the lobby and down the hallway.[or]Cindy looks exhausted, like she might fall asleep at any time.[stopping]"

To say CindyQuery:
	say "[one of]Cindy stops and looks at you for a moment.
	
	'Mr. King?'
	
	'Yes, Ms. Jalcek?'
	
	'Do you think that a bad person, a criminal, can really change their life around? I mean, you've seen a lot of criminals. Have they ever changed?'
	
	'Well, it's not really pertinent to this investigation. But yes; I know an ex-con who gives music lessons;  he's one of the nicest people I know.'
	
	She nods, and smiles a bit.[or]Cindy yawns and stretches.[stopping]"

To say CindyAction:
	if the current action is not speeching a MeihCindy quip to Cindy:
		say "[one of]Cindy tries to rub a stain off the table with her thumb.[or]Cindy sighs and runs her fingers through her hair.[or]You catch Cindy looking you up and down with an unhappy expression.[or]Cindy sniffs and wrinkles her nose. She glances at the microwave.[or]Rain drums on the roof.[or]You hear a distant rumble of thunder.[or]You hear something like footsteps in the lobby, but you don't see anyone when you turn.[or]Cindy looks over your shoulder. You turn to look, but no one's there. You look back at Cindy, and she shrugs.[or]Cindy picks at her teeth.[or][DannyVisit][or][CindyQuery][or]Cindy sighs and leans back in her chair.[or]Cindy taps her leg.[or]Cindy hums a short song.[or]Cindy stares at your badge until she catches you looking.[or]Cindy notices some trash on the ground and picks it up.[or]Cindy pops her neck.[or]Cindy straightens her clothing.[or]Cindy's hand trembles slightly.[or]A flash of lightning is visible from the lobby to the east, followed by a rumble of thunder.[or]Cindy shudders. She says, 'I'm sorry, I just keep thinking of Rosalita's body...'[or]Cindy looks like she's going to be sick, but she recovers.[or]Cindy shivers, and rubs her arms.[or]Cindy runs her finger over the table, feeling the dents and stains.[or]Cindy looks over at the vending machine.[or]The fan clicks and sputters.[or]The lights flicker for a moment, and the fan slows. Then they pick up again.[in random order]"

Every turn when the player is in CopBreak:
	if DontTalk is 0:
		say "[one of]She looks up expectantly as you walk in.[or][CindyAction][stopping]";
	now DontTalk is 0;

[Cindy is based on Lorenz]
Cindy is a person. "[if Secretary is carried by the player]Cindy [otherwise]The secretary, whom you assume is Cindy Jalcek, [end if]is sitting at a table." The description of Cindy is "[if persona is Cindy]You are a young woman with washed-out eyes, a nose that's too big, and a tangle of frizzy blonde hair. You're wearing a dress suit with a yellow blouse.  Your long, delicate fingers are dry and cracked.[otherwise]Cindy Jalcek is a young woman with piercing eyes, an aquiline nose, and a burst of curly golden hair. She is wearing a beige jacket and dandelion yellow slacks.  Her long, delicate fingers are dry and cracked.[end if]".

Understand "young" or "woman" or "lady" or "girl" or "Jalcek" or "eye" or "eyes" or "nose" or "hair" as Cindy.

Section 6 - Chief

[Improve chief: The chief wants revenge for the murder of his wife years ago. He's consumed with each case, looking for parallels and trying to see if it's the same killer. He's thin, obsessed, but with a penchant for humor and trying to look on the bright side of things. (online http://io9.gizmodo.com/10-tips-and-tricks-for-creating-memorable-characters-1616544190 suggested writing a short backstory for characters)]

[Chief is based on Chico of Magnificent Seven. His name is Robert Schwartz.]
Chief is a person. "Chief Schwarz is seated at his desk.". The description of Chief is "Chief Schwarz is a man of contrasts, all light or all dark. He is a tall, pale man with deep-set eyes and a receding hairline. He's dressed up, probably for a media appearance, wearing a coal black suit with a charcoal tie. His knobby hands are restless, tapping his desk or rubbing his short-cropped hair." The printed name of Chief is "Schwarz". Understand "Schwarz" or "client" or "large" or "hands" or "black" or "eyes" or "hairline" or "suit" or "shiny" or "charcoal" or "shirt" or "red" or "tie" or "ensemble" or "hair" as Chief. 

To say HoldingUp:
	say "[one of]The Chief says, 'By the way, how have you been holding up?'
		
		You answer, 'It's been a rough year. I don't really want to talk about it now.'
		
		He nods. 'Yeah, I get that.'[or]The Chief starts to doze off a bit before waking up.[stopping]"

To say GalaText:
	say "[one of]The Chief asks you, 'Are you going to the Mayor's Gala next week?'
		
		You answer, 'Yeah, but only because I entered a painting in the auction.'
		
		He chuckles. 'I still can't see you as a painter.'
		
		You shrug.[or]The chief gives you a look as if to say, 'Are you really still here?'[stopping]";

DontTalk is a number that varies. DontTalk is 0;

To say ChiefAction:
	say "[one of]The Chief unlocks a drawer, pulls out a flask, and takes a sip before returning it and locking the drawer. He winks.[or]The Chief cracks his knuckles as he sits in thought.[or][HoldingUp][or]The Chief looks at the books on his shelves as he leans back in his chair.[or]Lightning flashes outside, and thunder rocks the precinct.[or][GalaText][or]The Chief straightens his tie.[or]The Chief drums his fingers on the table.[or]The building creaks as it settles.[or]Raindrops patter on the window.[in random order]";
		
Every turn when the player is in ChiefOffice:
	if Chief is in ChiefOffice:
		if DontTalk is 0:
			say "[one of]The Chief looks thoughtful, and taps his fingers on the desk.[or][ChiefAction][stopping]";
	now DontTalk is 0;

Chapter 8 - Bathroom stuff


Instead of examining the mirror:
	try examining the player;
	if persona is Danny:
		say "After completing your examination, you realize you haven't been looking too hot recently. It wasn't always that way; when you were younger, even Rosalita...";

The bathroom sink is a backdrop. Understand "tap" or "faucet" or "faucets" or "knobs" or "hot" or "cold" or "taps" or "hot" or "rusty" or "rust" or "crust" or "crusted" or "hard" or "deposits" or "cold" as the bathroom sink. The bathroom sink is scenery. The description of the bathroom sink is "A porcelain sink standing in the corner of the bathroom. The hot and cold knobs are rusty and crusted with hard water deposits." [The bathroom sink is in Men's Room and in Women's Room.]

Instead of turning the sink:
	try switching on the sink.

Instead of switching on the sink:
	say "You turn on the taps, both at the same time. You then turn off the cold, then alternate it with the hot. You then turn both off. It's an old habit from your childhood, and sometimes you use it to calm down."

Washing is an action applying to one thing. Understand "wash [something]" as washing.

The hands is a kind of thing. A hands is a part of every person. The description of a hands is "Fairly normal for hands, after all." Understand "hand" as hands.

Carry out washing:
	if the noun is hands:
		say "You don't need to wash your hands.";
	otherwise if the player is in the copprivatebathroom:
		say "You don't feel like getting anything wet.";
	otherwise if the player is in the cindyprivatebathroom:
		say "You don't feel like getting anything wet.";
	otherwise:
		say "You have nothing to wash with."

Drying is an action applying to one thing. Understand "dry [something]" as drying.

Carry out drying:
	if the noun is hands:
		if the player is in the copprivatebathroom:
			try taking the coppapertoweldispenser;
		otherwise:
			say "It doesn't seem like the right time for that.";
	otherwise:
		say "It doesn't seem like the right time for that.";

The small mirror is a backdrop. The small mirror is scenery. 

Peeing is an action applying to nothing. Understand "use toilet" or "use urinal" or "use stall" or "poo" or "poop" or "go poo" or "go pee" or "take a piss" or "take a pee" or "pee" or "piss" or "urinate" as peeing.

Carry out peeing:
	say "You don't have to go right now.";

ChuckThere is a number that varies. ChuckThere is 0.

Before switcherooing Cindy:
	now ChuckThere is 0;

After going west from CindyPrivateBathroom:
	try looking;
	if CindyTruth is 1:
		if the burnt popcorn is in HoldingCell:
			say "You hear the doors open, and you turn. You see Chuck, smiling.
		
		You frown. 'What are you doing back here?'
		
		His smile deepens. 'I have a 'business' meeting with Rosalita.'
		
		You smile as well. 'You did it!' You kiss him on the cheek. 'I can't believe you gave her those flowers after your last meeting. She wanted to throw them away, but Danny saw them already.'
		
		He nods, but his thoughts are elsewhere. 'In 15 minutes, we'll get our last installment.'
		
		'Does she know about me?' you ask.
		
		'I haven't told her yet; she's got some dirt on you too, you know. Best to keep her in the dark for now.'
		
		'Tell me how it goes,' you say. He smiles, and walks down the hallway.";
		now ChuckThere is 1;


Part 2 -Scenes

Chapter 1 - Setup

Greeting is a quip. The preview of greeting is "You need to introduce yourself to the suspects." Understand "hello" as Greeting.

Statement is a quip. The preview of statement is "You need to take the statements of the suspects" 

RoomsListing is an action out of world. Understand "roomslistx" as roomslisting.

Carry out roomslisting:
	repeat with current running through rooms:
		say "[bold type]";
		say current;
		say "[roman type]";
		say "[line break]";
		say "[description of current]";
		say "[line break]";

DescriptionsListing is an action out of world. Understand "thingslistx" as DescriptionsListing.

Carry out DescriptionsListing:
	repeat with current running through portable things:
		say "[bold type]";
		say current;
		say "[roman type]";
		say "[line break]";
		say "[description of current]";
		say "[line break]";

SceneryListing is an action out of world. Understand "scenelistx" as SceneryListing.

Carry out SceneryListing:
	repeat with current running through scenery:
		say "[bold type]";
		say current;
		say "[roman type]";
		say "[line break]";
		say "[description of current]";
		say "[line break]";

AllQuipping is an action out of world. Understand "allx" as AllQuipping.

Carry out AllQuipping:
	repeat with current running through quips:
		say "[current][line break][DannyResponse of Current][paragraph break]";
	repeat with current running through quips:
		say "[current][line break][ChuckResponse of Current][paragraph break]";
	repeat with current running through quips:
		say "[current][line break][CindyResponse of Current][paragraph break]";
	repeat with current running through quips:
		say "[current][line break][MaiaResponse of Current][paragraph break]";
	repeat with current running through quips:
		say "[current][line break][ChiefResponse of Current][paragraph break]";
	repeat with current running through quips:
		say "[current][line break][RosaResponse of Current][paragraph break]";

[DannyMowing is an action applying to nothing. Understand "a [someone]" as lawnmowering.

Does the player mean lawnmowering a person: it is likely.

Does the player mean lawnmowering a textfile: it is unlikely.

Carry out lawnmowering:
	if persona is the shadowy figure:
		say "You can't list available topics during the prelude.";
	otherwise if the noun is Danny:
		if a MeihDanny quip is carried by the player:
			say "[bold type]Current thoughts[if persona is not a speaker] [bracket]of Detective King[close bracket][end if]:[roman type]";
			repeat with currentquip running through all MeihDanny quips carried by the player:
				say "[line break]   ";
				say currentquip;
			say "[line break]";
	otherwise:
		say "You can only find available topics for one of the four suspects or the chief."]

Chapter 2 - Cop's offices

[CopParking is south from CopLobby. The printed name of CopParking is "Parking Lot". Understand "parking lot" as CopParking. The description of CopParking is "[first time]You park the car, grab the manila folder and step out. The cold rain obscures your view and splashes in puddles. You breathe deeply, a soft mist escaping your lips in the chill air.[only] 

At the north edge of the large parking lot, you see the studio. The grey brick building is short and narrow, crammed between a pawn shop and a Chinese restaurant. Its door lies to the [bold type]north[roman type], labelled [']KOLR['] in block black letters." 

Some Copcars are in CopParking. Some CopCars are scenery. The printed name of copcars is "cars". The description of copcars is "Few cars are in the lot this early; a battered Dodge Dakota, a newer Toyota Corolla with a few scrapes, and so on. They don't matter to you; your interest is in people." Understand "rows" or "toyota" or "corolla" or "scrapes" or "dodge" or "dakota" or "row" or "car" or "cars" as some copcars.

Some CopBuildings is scenery. Some CopBuildings are in CopParking. The description of copbuildings is "On one side of the parking lot is a Chinese restaurant you've never heard of; on the other is Wolfe Pawn, which you've heard too much about. You've been called there more than once to settle a dispute with the owner." Understand "brick" or "chinese" or "wolfe" or "pawn" or "check" or "cashing" or "wall" or "brick walls" or "buildings" as copbuildings.

CopStudios is scenery. CopStudios is in CopParking. Understand "KOLR" or "studio" or "lobby" or "door" or "grey" or "brick" or "studios" as CopStudios. The printed name of CopStudios is "KOLR studios". The description of CopStudios is "KOLR studios is small and nondescript, but you know that the people who run it have more influence than most in the county.".

The copcar is in CopParking. The copcar is scenery. The printed name of copcar is "your car". The description of copcar is "Your car is an older model, but reliable. The solid black frame is topped by the usual lights, and the department seal is blazoned on the side.". Understand "your car" or "seal" or "lights" or "car" or "cop" or "patrol" or "police" or "squad" as copcar.]

Section 1 - Lobby 

CopLobby is a room. The printed name of CopLobby is "Lobby". The description of CopLobby is "[first time]KOLR studios is smaller than you thought, decorated with a brown and tan theme with occasional splashes of color. [only]The neutral tones suggest a professional but boring atmosphere. A faded rainbow stencil on the wall invites you to KOL studios; the R has worn off. The sepia-hued front desk hardly fits in this cramped room, but the beige ceiling is quite high; it looks like it was once part of a larger room. The muddy carpet is scuffed and worn, curling up on the edges. One of the off-white fluorescent lights in the ceiling flickers intermittently[one of], and another buzzes at the edge of hearing.[or].  Another light buzzes just at the edge of hearing.[or]. Another light buzzes at the edge of hearing.[or]. Barely audible is the buzzing of another light.[or]. Another light buzzes at the edge of your hearing.[or]. There's a barely-audible buzz from another light.[at random] The smell of burnt popcorn wafts in from a break room to the [bold type]west[roman type]. The doorway to the [bold type]north[roman type] leads to the hallway. The parking lot is back to the south, and a bathroom door stands to the [bold type]east[roman type]. An ivory telephone sits on the desk."

The copfluorescentlights are scenery. The copfluorescentlights are in CopLobby. Understand "fluorescent" or "off-white" or "dingy" or "plastic" or "covers" or "lights" or "light" or "buzz" or "buzzing" or "flickering" as copfluorescentlights. The description of copfluorescentlights is "The fluorescent lights are screened by dingy plastic covers."

Instead of touching copfluorescentlights:
	say "It's too high!"

Instead of listening to the copfluorescentlights:
	try examining the copfluorescentlights;

Instead of going nowhere from CopLobby when the noun is south:
	say "You don't want to go out in that storm again." instead;

The copceiling is in CopLobby. The copceiling is scenery. Understand "beige" or "ceiling" or "high" as Copceiling. The printed name of copceiling is "ceiling". The description of copceiling is "The high popcorn ceiling is beige-tinted."

The CopStencil is in CopLobby. The CopStencil is scenery. Understand "faded" or "rainbow" or "r" or "stencil" or "rust" or "beige" or "washed-out" or "blue" or "green" as CopStencil. The description of CopStencil is "'Welcome to KOL', it says, painted in rust and washed-out blue and green. The R seems to have been painted in a yellow that's faded to the same beige as the walls."

The CopFrontDesk is in CopLobby. The CopFrontDesk is scenery. The printed name of CopFrontDesk is "front desk". Understand "front" or "sepia" or "hued" or "sepia-hued" or "desk" as CopFrontDesk. The description of the CopFrontDesk is "You eye the front desk. It's shoved into a corner of the room to keep a path open to the doorways. Its wood veneer bubbles up here and there, exposing the particle board. A telephone sits on the desk, and a chair is pushed up behind it.". CopFrontDesk is a supporter. CopFrontDesk is not enterable.

The CopPhone is on CopFrontDesk. The CopPhone is scenery. The printed name of CopPhone is "telephone". The description of CopPhone is "An ivory rotary telephone. All of the numbers have been rubbed off, but you can dial by memory.

[bracket]You can CALL CHIEF[close bracket] to discuss the case with him.". Understand "ivory" or "phone" or "tele" or "telephone" or "rotary" or "numbers" or "rubbed" as CopPhone.

Instead of taking the CopPhone:
	try dialing;

The CopFrontChair is in CopLobby. Understand "chair" as CopFrontChair.The CopFrontChair is scenery. The printed name of CopFrontChair is "chair". The description of CopFrontChair is "This low black chair is pushed up close to the desk." CopFrontChair is an enterable supporter.

Instead of taking the CopFrontChair:
	say "That's not really necessary."
	
Instead of pushing the CopFrontChair:
	say "You're not here to rearrange furniture."
	
Instead of pulling the CopFrontChair:
	say "You're not here to rearrange furniture."

CopHallDoor is a door.  CopHallDoor is scenery. The printed name of CopHallDoor is "hallway door". Understand "door" or "black" or "window" or "glass" or "reinforced" or "metal" or "chips" or "paint" or "rusty" or "rust" or "hallway" as CopHallDoor. CopHallDoor is north of CopLobby and south of CopHallway. CopHallDoor is closed and openable. The description of CopHallDoor is "A black metal door with a reinforced glass window. Chips of paint have flaked off the edges, revealing patches of rust." CopHallDoor is not lockable.

Instead of listening to CopLobby:
	say "You hear a buzzing noise in the background."

Instead of smelling CopLobby:
	say "You smell burnt popcorn from the west."

MuddyCarpet is a backdrop. MuddyCarpet is in CopLobby. MuddyCarpet is scenery. Understand "muddy" or "carpet" or "floor" as MuddyCarpet. The printed name of MuddyCarpet is "carpet". The description of MuddyCarpet is "The muddy carpet looks like it was tacked on poorly; it curls up at the edges." Understand "backing" or "curls" or "corners" as MuddyCarpet.

Instead of taking the MuddyCarpet:
	say "That's disgusting; you'd rather not touch that."

Some BeigeWalls are a backdrop. Some BeigeWalls are in CopLobby. BeigeWalls is scenery. Understand "beige" or "walls" or "wall" as BeigeWalls. The printed name of BeigeWalls is "walls". The description of BeigeWalls is "The walls are a patchy shade of beige."

Section 2 - Break Room

The printed name of CopChairs is "chairs". Some CopChairs are scenery. Some CopChairs are in CopBreak. Understand "scattered" as some CopChairs. The description of CopChairs is "The chairs are dark plastic with worn cloth seats." Understand "dark" or "chairs" or "plastic" or "worn" or "cloth" or "seat" or "seats" or "chair" as CopChairs. The CopChairs are a supporter. The CopChairs are enterable. 

[A CC camera is a backdrop. A CC camera is scenery. A CC camera is in DannyRoom. The description of CC camera is "The security cameras in this building  are small, unobtrusive black domes. The feeds can be accessed by the security guards, or, in this case, the chief." Understand "unobtrusive" or "small" or "black" or "domes" as CC camera.]

CopBreak is west of CopLobby. "The stench of burnt popcorn lingers in the air. A microwave with a thin tan layer of grease sits in the corner. A tawny brown vending machine sags in the corner with half of its buttons darkened or flickering. A small oat-colored fan is oscillating in the center, trying to clear out the smell. A few coffee-stained tables and several chairs are scattered around the room. The lobby is back to the [bold type]east[roman type]." Cindy is in CopBreak. The printed name of CopBreak is "Break Room".

Instead of smelling CopBreak:
	say "The stench is horrible."

Some BeigeWalls are in CopBreak. Some MuddyCarpet is in CopBreak. Some CopChairs are in CopBreak.

Some CopTables are in CopBreak. The printed name of CopTables is "tables". Some CopTables are scenery. Understand "table" or "tables" or "coffee" or "coffee-stained" or "figures" or "figure" or "abstract" or "surface" or "surfaces" or "ketchup" or "mustard" or "heavy" or "plastic" or "stains" as CopTables. The description of CopTables is "The circular tables are built from heavy plastic. Their surfaces are stained into abstract figures and designs by coffee, ketchup, and mustard." The coptables are a supporter.

A CopVendingMachine is in CopBreak. The printed name of CopVendingMachine is "vending machine". CopVendingMachine is scenery. The description of CopVendingMachine is "The vending machine leans in the corner. Half of the buttons are dark or flickering; all that works is a couple of rows of iced tea. Not your favorite." Understand "buttons" or "tawny" or "brown" or "button" or "vending" or "machine" or "dark" or "flickering" or "couple" or "rows" as CopVendingMachine. 

Instead of looking under CopVendingMachine for the first time:
	say "[one of]You look under the vending machine, and see a shim.[or]It looks the same as it ever did.[stopping]";

The shim is part of the CopVendingMachine. The description of the shim is "That seems to be keeping the vending machine from toppling over."

Before taking the shim:
	say "That seems dangerous; let's not do that." instead.

The oscillating fan is in CopBreak. The oscillating fan is scenery. The description of the oscillating fan is "The fan clicks and hums as it rotates about the room, trying to clear the air." Understand "clicks" or "oat-colored" or "oat" or "hums" as the oscillating fan.

Instead of taking the oscillating fan:
	say "Cindy says, 'Please don't move that!'".

Instead of switching off the oscillating fan:
	say "Cindy says, 'Please leave that on!'".

Instead of switching on the oscillating fan:
	say "It's already on.".

Instead of listening to CopBreak:
	try listening to the oscillating fan;

Instead of listening to the oscillating fan:
	say "You hear clicks and hums coming from the fan."

The CopMicrowave is on CopTables. The CopMicrowave is scenery. Understand "greasy" or "popcorn" or "tan" or "microwave" as CopMIcrowave. The printed name of CopMicrowave is "greasy microwave". The description of CopMicrowave is "This seems like a fairly pricey microwave; but its once-white microwave has been yellowed by accumulated layers of grease. It stinks of popcorn."  

Instead of switching on the CopMicrowave:
	say "You would not eat something cooked in this microwave."

Instead of switching off the CopMicrowave:
	say "It's already off."

Instead of opening the CopMicrowave:
	say "Cindy looks over at you and shakes her head. You draw back your hand.";

Instead of touching CopMicrowave:
	say "It feels greasy. You wipe you hands on your pants in disgust."

Section 3 - Hallway

CopHallway is a room. The printed name of CopHallway is "Hallway". The description of CopHallway is "You are standing in a dim hallway with muddy carpet and beige walls. A battered navy blue door leads to the [bold type]west[roman type]. A heavy seaweed-green door to the [bold type]east[roman type] is labelled 'Studio'. A red On-Air light is mounted next to the door. The hallway extends to the [bold type]north[roman type]. You can also go [bold type]south[roman type] to return to the lobby."

MuddyCarpet is in CopHallway. BeigeWalls are in CopHallway.

A CopBlueDoor is a closed openable door. CopBlueDoor is west of CopHallway and east of CopChuckOffice. The printed name of CopBlueDoor is "blue door". CopBlueDoor is scenery. The description of CopBlueDoor is "This door was painted a pleasant blue at some time, but deep gouges and scratches mar it. A name plate is on the door, but it is currently empty.". Understand "battered" or "pleasant" or "navy" or "blue" or "door" or "gouges" or "deep" or "scratches" or "window" as CopBlueDoor. 

The CopChuckPlate is part of the CopBlueDoor. The CopChuckPlate is scenery. The printed name of CopChuckPlate is "name plate". Understand "empty" or "name" or "plate" or "nameplate" as CopChuckPlate. The description of CopChuckPlate is "The name plate is empty."

The CopAirLight is scenery. The printed name of CopAirLight is "On-Air light". Understand "on" or "air" or "light" or "on-air" or "red" as CopAirLight. The CopAirLIght is in CopHallway. The description of CopAirLight is "This must be the light they use to show when the studio is in use. It's currently turned off.".

The CopStudioDoor is a door. The CopStudioDoor is scenery. The CopStudioDoor is east of CopHallway and west of CopStudio. CopStudioDoor is closed. The printed name of CopStudioDoor is "studio door". The description of CopStudioDoor is "A thick, heavy black door with 'studio' painted on it in white." Understand "door" or "seaweed" or "seaweed-green" or "green" as CopStudioDoor.

Section 4 - Chuck's room

CopChuckOffice is a room. "This comfortable office is mostly empty, but the owner's thoughtful personality is still evident in the azure carpet and the sky-blue walls. Lighter patches on the walls show where posters, picture frames and bulletin boards once hung. A navy blue door leads [bold type]east[roman type] to the hallway. A long desk has been shoved out of the way, and a single chair remains. The carpet is still dented from furniture legs." The printed name of CopChuckOffice is "Spare Office". Chuck is in CopChuckOffice.

The CopChuckChair is in CopChuckOffice. CopChuckChair is scenery. CopChuckChair is a supporter. CopChuckChair is enterable. Understand "single" or "chair" or "cushion" or "cushions" or "rigid" or "blue" or "dark" or "berry" or "wooden" or "furniture" as CopChuckChair. The description of CopChuckChair is "This a rigid wooden chair softened by dark berry blue cushions." The printed name of CopChuckChair is "chair".

Instead of entering the CopChuckChair:
	say "Chuck is already sitting in the chair."

The CopLongDesk is in CopChuckOffice. CopLongDesk is scenery. The printed name of CopLongDesk is "long desk". The description of CopLongDesk is "This desk has been recently washed and scrubbed. Its warm colors are offset by its bare surface." Understand "long" or "desk" or "mahogany" or "warm" or "colors" or "bare" or "surface"  or "furniture" as CopLongDesk. CopLongDesk is a supporter.

The CopChuckWalls are in CopChuckOffice. CopChuckWalls are scenery. The printed name of CopChuckWalls is "walls". The description of CopChuckWalls is "The sky blue walls have lighter patches where something must of once hung. A clock, a poster, bulletin boards, frames, an unidentifiable triangular object all must have left their mark." Understand "wall" or "walls" or "clock" or "bulletin" or "boards" or "board"  or "frames" or "triangular" or "lighter" or "sky" or "blue" or "sky-blue" or "patches" or "unidentifiable" or "uto" or "object" as CopChuckWalls.

The CopBlueCarpet is in CopChuckOffice. The printed name of CopBlueCarpet is "azure carpet". The CopBlueCarpet is scenery. The description of the CopBlueCarpet is "The blue carpet scrapes against the bottom of your shoes. Heavy furniture, now absent or moved aside, has left indentations in the carpet." Understand "blue" or "azure" or "rough" or "carpet" as CopBlueCarpet.

Section 5 - Hallway 2

CopHallway2 is north of CopHallway. The printed name of CopHallway2 is "End of Hallway". The description of CopHallway2 is "You move carefully through the dim and narrow hallway. It smells of cleaner mingled with faint perfume. It terminates to the [bold type]north[roman type] at a hazel wood door. Its smooth surface is polished and warm in the chill hallway. A nameplate next to the door reads 'Rosalita Morales'. To the [bold type]west[roman type] hangs an apple red door, with a nameplate saying 'Daniel Kostopolous'. To the [bold type]east[roman type] is a narrow metal closet door. "

MuddyCarpet is in CopHallway2. BeigeWalls are in CopHallway2.

The faint perfume is in CopHallway2. The faint perfume is scenery. The description of the faint perfume is "The smell lingers, but is hard to distinguish."

Instead of smelling the faint perfume:
	try examining the faint perfume.
	
Instead of smelling the CopHallway2:
	try examining the faint perfume.

The copdarkreddoor is a closed openable door. The copdarkreddoor is locked. The printed name of copdarkreddoor is "hazel wood door". The copdarkreddoor is scenery. The copdarkreddoor is north from CopHallway2 and south from CopRosalitasOffice. Understand "cherry" or "dark" or "hazel" or "door" or "wood" as the copdarkreddoor. The description of the copdarkreddoor is "The door to Rosalita's office is made of smooth hazel wood."

Before opening the copdarkreddoor:
	say "Rosalita's office has been sealed off, and you don't want to disturb it. Your specialty is interrogation, not forensics." instead;

Instead of touching copdarkreddoor:
	say "It's soft and pleasant to the touch."

The CopRosaPlate is part of the CopdarkredDoor. The CopRosaPlate is scenery. The printed name of CopRosaPlate is " nameplate for Rosalita". Understand "Rosalita" or "Rosa" or "name" or "plate" or "nameplate" as CopRosaPlate. The description of CopRosaPlate is "The name plate says 'Rosalita Morales'."

CopDannysdoor is a closed, openable door. CopDannysdoor is scenery. CopDannysdoor is east from CopDannyOffice and west from CopHallway2. The description of CopDannysdoor is "An apple red wooden door. It might be made of oak; it's definitely some kind of hardwood.". The printed name of CopDannysdoor is "plain door". Understand "plain" or "apple" or "red" or "wooden" or "Danny" or "danny's" or "oak" or "solid" or "door"  as CopDannysDoor.

The CopDannyPlate is part of the CopdannysDoor. The CopdannyPlate is scenery. The printed name of CopdannyPlate is "the name plate for Danny". Understand "Danny" or "name" or "plate" or "nameplate" as CopDannyPlate. The description of CopDannyPlate is "The name plate says 'Daniel Kostopolous'."

Copclosetdoor is a closed, openable door. CopClosetdoor is scenery. CopClosetdoor is west from CopCloset and east from CopHallway2. The description of CopClosetdoor is "A heavy, corn-yellow door squeezed into a tall, narrow frame. The word 'Janitor' is stencilled on it in white paint.". The printed name of CopClosetdoor is "closet door". Understand "heavy" or "corn" or "yellow" or "corn-yellow" or "closet" or "door" or "janitor" or "stencil" or "white" or "frame" or "tall" or "narrow" as CopClosetDoor. CopClosetDoor is locked.

Section 6 - Danny's room

Danny is in CopDannyOffice.

The lightning is a backdrop. The lightning is scenery. The lightning is in CopDannyOffice. The description of the lightning is "The lightning comes and go, flickering in the darkness."

Instead of touching the lightning:
	say "It's too far away."

Instead of rubbing the lightning:
	say "It's too far away."

Instead of tasting the lightning:
	say "It's too far away."

The printed name of CopDannyOffice is "Danny's Office". The description of CopDannyOffice is "This office is decorated with flashy shades of red. It's large enough to fit a massive cherry desk, rows of mahogany bookshelves, and a battered scarlet cabinet. The walls are empty and clean, although the paint is patchy in spots, reaching to a textured plaster ceiling. A wine red floor lamp in the corner has been turned off. The blinds are open to the dim light from outside, punctuated by flashes of lightning. The soft rose carpet is matted and ragged, but looks recently cleaned. "

CopDannyCarpet is in CopDannyOffice. CopDannyCarpet is scenery. The printed name of CopDannyCarpet is "soft rose carpet". Understand "soft" or "rose" or "red" or "carpet" or "matted" or "ragged" or "cleaned" or "recently" or "ragged" or "torn" as CopDannyCarpet. The description of CopDannyCarpet is "This carpet appears to have been luxurious, once. Its red color has been preserved, but heavy use and frequent cleanings have rendered it torn and ragged.".

CopDannyCeiling is in CopDannyOffice. CopDannyCeiling is scenery. The printed name of CopDannyCeiling is "plaster ceiling". Understand "plaster" or "textured" or "ceiling" as CopDannyCeiling. The description of CopDannyCeiling is "The plaster ceiling is textured in a style that was popular seventy years ago.";

CopDannyChair is in CopDannyOffice. CopDannyChair is scenery. The printed name of CopDannyChair is "office chair". Understand "office" or "chair" or "wooden" or "simple" as CopDannyChair. CopDannyChair is a supporter. The description of CopDannyChair is "A simple wooden office chair."

Instead of entering CopDannyChair:
	say "Danny probably wouldn't like that.".

BeigeWalls are in CopDannyOffice.

Some CopOfficeShelves are in CopDannyOffice. The printed name of CopOfficeShelves is "bookshelves". Understand "rows" or "mahogany" or "row" or "book" or "books" or "bookshelf" or "sturdy" or "shelf" or "shelves" or "bookshelves" as CopOfficeShelves. The CopOfficeShelves are scenery. The description of the CopOfficeShelves is "Sturdy mahogany shelves stretch around the room. They are stuffed with radio memorabilia, an old crystal radio set, stacks of tapes, rows of sleeved vinyl, histories of America, patriotic magazines, and other objects lost in the gloom." Understand "old" or "crystal"  or "set" or "tapes" or "vinyl" or "histories" or "patriotic" or "magazine" or "magazines" or "america" or "history" or "objects" as CopOfficeShelves. [expand this later]

CopRadioMemorabilia is scenery. CopRadioMemorabilia is in CopDannyOffice. The printed name of CopRadioMemorabilia is "radio memorabilia." Understand "radio" or "radium" or "radium-painted" or "KOLR" or "clock" or "posters" or "rolled-up" or "keychain" or "keychains" or "memorabilia" as CopRadioMemorabilia. The description of CopRadioMemorabilia is "A faintly glowing radium-painted KOLR clock, a few rolled-up posters, KOLR keychains, and so on."

Instead of taking the CopRadioMemorabilia:
	say "You can tell Danny values these things, and you leave them be."

Instead of reading the CopOfficeShelves:
		say "You feel uncomfortable searching through the shelves while Danny is watching.";

A CopDannysFile is in CopDannyOffice. CopDannysFile is scenery. The printed name of CopDannysFile is "scarlet filing cabinet". Understand "scarlet" or "filing" or "cabinet" or "filing cabinet" as CopDannysFile. The description of CopDannysFile is "This battered scarlet filing cabinet is not labeled in any way.". CopDannysFile is a container. CopDannysFile is lockable and locked.

CopDannysDesk is in CopDannyOffice. The printed name of CopDannysDesk is "cherry desk". CopDannysDesk is scenery. CopDannysDesk is a supporter. Understand "large" or "cherry" or "massive" or "carved" or "leg" or "splinter" or "legs" or "vintage" or "classic" or "desk" as CopDannysDesk. The description of CopDannysDesk is "Danny's desk is a vintage classic made from cherry wood; you wish you had a desk like this at home. It stands elegantly in the middle of the room, perched on its carved legs. It is currently cleaned off."

CopDannyLamp is scenery. CopDannyLamp is in CopDannyOffice. Understand "floor" or "lamp" or "wine" or "red" or "wine-red" as CopDannyLamp. The printed name of CopDannyLamp is "wine-red floor lamp". The description of CopDannylamp is "A wine-red metal floor lamp hides in the corner. It is turned off."

Instead of switching on CopDannyLamp:
	say "Danny looks over at you. 'I'd prefer to keep it off, if you don't mind.'"

CopDannysWindow is scenery. CopDannyswindow is in CopDannyOffice. The printed name of CopDannyswindow is "window". The description of CopDannyswindow is "It's grey and cold outside, and lightning flashes intermittently. The raindrops hit the glass and run down in streaks." Understand "window" or "blinds" or "blind" as CopDannysWindow.

Instead of opening CopDannysWindow:
	say "Danny might not like that."

Instead of closing CopDannysWindow:
	say "Danny might not like that."

Section 7 - Maia's Room/studio

The printed name of CopStudio is "Studio". The description of CopStudio is "The studio is decked out in luxurious shades of green. The hardwood floor in this large room steps up almost half a foot more than the carpet in the hallway to the [bold type]west[roman type], and is covered in a juniper-green carpet. The pistachio walls are noticeably thicker here, and partially covered in lime fabric-lined boards. A single table is placed in the center of the room with a few scattered chairs. Adjustable microphones hang from the ceiling, which is covered in pale mint acoustic panels. It's subtle, but you notice that none of the walls are at a right angle. The perfect stillness in the room is a relief after the incessant pitter-patter of rain and the buzzing of fluorescent lights. in the rest of the building. A bank of windows to the [bold type]east[roman type] reveals the intricate electronics of the control room, accessible by a slim door." Maia is in CopStudio.

The CopStudioWindows are in CopStudio. The CopStudioWindows are scenery. The printed name of CopStudioWindows is "bank of windows". Understand "bank" or "control" or "room" or "window" or "windows" or "intricate" or "electronics" or "electronic" or "soundproof" or "darkened" or "racks" or "tangles" or "tangle" or "wire" or "wires" or "audio" or "console" as CopStudioWindows.  The description of CopStudioWindows is "The bank of soundproof windows reveals a darkened control room. You recognize the audio console, but the racks with tangles of wires streaming out are unfamiliar."

Instead of taking the CopStudioWindows:
	say "That's not something you can take from here."

Instead of touching the CopStudioWindows:
	say "You'd best leave the window and the room alone."

The CopHardwoodFloor is in CopStudio. The CopHardwoodFloor is scenery. The description of  CopHardwoodFloor is "The hardwood floor is raised up several inches, and is covered in juniper-green carpet." Understand "hardwood" or "juniper" or "juniper-green" or "carpet" as CopHardwoodFloor.

The CopStudioWalls are scenery. The CopStudioWalls are in CopStudio. Understand "wall" or "walls" or "thicker" or "thick" as CopStudioWalls. THe printed name of the CopStudioWalls is "studio walls". The description of the CopStudioWalls is "The walls are thick and skewed, with fabric-covered boards softening their surface.".

Instead of touching the CopStudioWalls:
	say "The wall feels smooth, while the boards feel soft.".

Instead of touching the CopStudioBoards:
	say "The fabric is soft as you run your hand across it."

The CopStudioBoards are scenery. The CopStudioBoards are part of the CopStudioWalls. Understand "fabric" or "covered" or "fabric-covered" or "board" or "boards" as CopStudioBoards. The printed name of the CopStudioBoards is "fabric-covered boards". The description of the CopStudioBoards is "Soft fabric covers a few scattered boards, deadening the sound in the room."

Instead of looking under the CopHardwoodFloor:
	say "You're not a forensics expert, you're a detective."

Instead of listening to CopStudio:
	say "The perfect silence fills you with peace."
	
The CopStudioTable is in CopStudio. The CopStudioTable is scenery. The printed name of the CopStudioTable is "table". Understand "single" or "table" or "gouges" or "hardwood" or "indentations" or "gouges" or "scratches" or "surface" or "center" as CopStudioTable. The description of CopStudioTable is "This hardwood table stretches across much of the room, with chairs on both sides. The microphones droop from the ceiling to the table's center. You feel countless small indentations, gouges and scratches on the surface." The CopStudioTable is a supporter.

The CopStudioChairs are in CopStudio. The CopStudioChairs are scenery. The printed name of the CopStudioChairs is "chairs". The CopStudioChairs are plural-named. Understand "chair" or "chairs" as CopStudioChairs. The description of CopStudioChairs is "The chairs are heavy, musty fabric-covered swivel chairs, thirty years old at least." The CopStudioChairs are a supporter. The CopStudioChairs are enterable.

Report entering CopStudioChairs:
	say "You sit on a chair."

The CopStudioCeiling is scenery. The CopStudioCeiling is in CopStudio. The printed name of the CopStudioCeiling is "ceiling". Understand "acoustic" or "pale" or "mint" or "panels" or "panel" or "ceiling" as CopStudioCeiling. The description of the CopStudioCeiling is "The low ceiling is covered by pale mint acoustic panels."

The CopMicrophones are scenery.The CopMicrophones are part of the CopStudioCeiling. The printed name of the CopMicrophones is "microphones". Understand "microphone" or "microphones" as CopMicrophones. The description of CopMIcrophones is "Microphones droop from the ceiling to the table, patiently waiting for their next use."

Instead of talking to CopMicrophones:
	say "You get ready to practice a Cop Talk type radio show, but you are embarassed by Maia's presence.".

Using is an action applying to one thing. Understand "use" as using.

Carry out using:
	say "You should be more specific."
	
Instead of using CopMicrophones:
	say "You get ready to sing, but are embarassed by Maia's presence."

Understand "adjust [something]" as pushing.

Instead of using CopToilet:
	say "You'd really rather not.";
	
Instead of entering CopToilet:
	say "You'd really rather not.";

Instead of using CindyToilet:
	say "You'd really rather not.";

Instead of entering CindyToilet:
	say "You'd really rather not.";

Instead of pushing CopMIcrophones:
	say "You push the microphone. Its well-oiled mechanisms glide silently."

Instead of pulling CopMicrophones:
	say "You push the microphone. Its well-oiled mechanisms glide silently.".

The CopControlDoor is a door. The CopControlDoor is scenery. The CopControlDoor is east of CopStudio and west of HoldingCell. The CopControlDoor is locked. The printed name of the CopControlDoor is "control room door". Understand "control" or "room" or "door" or "slim" as CopControlDoor. The description of CopControlDoor is "This slim door leads to the control room, guarding the expensive equipment stored inside.".

Instead of opening the CopControlDoor:
	say "It is locked."


Section 8 - Bathroom

The Coptilefloors are scenery. Understand "old" or "tiles" or "dingy" or "tile" or "floors" or "floor" as Coptilefloors. The printed name of CopTileFloors is "tile floors". The description of the Coptilefloors is "The tiles used to some kind of brown or tan, but now it's hard to tell." [The tile floors are in Men's Room. The tile floors are in Women's Room. ]

[The stalls are a backdrop. The stalls are scenery. Understand "stall" as the stalls. The description of the stalls is "These stalls are different sizes from the remodeling the building went through after ADA. You have no desire to enter the stalls." The stalls are in Men's Room. The stalls are in Women's Room. ]

The printed name of Copbathroomwalls is "bathroom walls". The Copbathroomwalls are scenery. Understand "grey" or "bathroom" or "walls" or "wall" or "shades" as Copbathroomwalls. The description of the Copbathroomwalls is "The bathroom walls are painted in various shades of grey." [The bathroom walls are in Men's Room. The bathroom walls are in Women's Room.]

The Coppapertoweldispenser is scenery. Understand "combination" or "paper" or "towel" or "dispenser" as Coppapertoweldispenser. The description of the Coppapertoweldispenser is "A paper towel dispenser set over a trash can."[ The paper towel dispenser is in Men's Room and in Women's Room.] The Coppapertoweldispenser is in CopPrivateBathroom.

Before taking the Coppapertoweldispenser:
	say "The dispenser seems be empty." instead;

Instead of pushing the Coppapertoweldispenser:
	try taking the Coppapertoweldispenser;

The Coptrashcan is scenery. The coptrashcan is in CopPrivateBathroom. Understand "battered" or "combination" or "trash" or "can" as coptrashcan. The description of coptrashcan is "A rectangular metal trash can that is attached to the wall." The coptrashcan is a container.

Instead of searching coptrashcan:
	say "Fortunately, the can has recently been emptied.";

[The urinals are scenery. The urinals are in Men's Room. Understand "urinal" as urinals. The description of the urinals is "The urinals are made of scuffed-up chrome and yellowing porcelain." ]

[Instead of opening the stalls:
	say "You don't want to go in there.";

Instead of entering the stalls:
	say "You don't want to go in there.";]

[The women's room door is a closed openable door. It is scenery. It is southeast from OfficeHallway and northwest from Women's Room. ]

CopPrivatebathroom is a room. The printed name of CopPrivateBathroom is "Private Bathroom".  The description of copprivatebathroom is "The musty smell trapped in this room almost overwhelms you. Its low ceiling sports a single, dim bulb and a ventilation fan. The floor has faded from whatever its original color was into a dingy brown. A sink sits below a small mirror, and a nearby paper towel dispenser hangs over a battered trash can. The toilet itself squats in a corner."

Instead of smelling CopPrivateBathroom:
	say "The combination of must and sewer gas is overpowering."

The copbathceiling is scenery. The copbathceiling is in CopPrivateBathroom. The printed name of the copbathceiling is "ceiling". Understand "ceiling" or "low" or "bulb" or "fan" or "single" or "dim" or "ventilation" as copbathceiling. The description of copbathceiling is "The ceiling is lower in this room, with a single light bulb fixture and a ventilation fan."

Instead of switching on the copbathceiling:
	say "That's already on."

Instead of opening the copbathceiling:
	say "You're a detective, not an acrobat."

Instead of switching off the copbathceiling:
	say "Being in the dark, in this claustrophobic room, with this smell, is a delight you'd prefer to experience some other time.".

The coptilefloors are in copprivatebathroom. The copbathroomwalls are in copprivatebathroom. The small mirror is in copprivatebathroom. The bathroom sink is in CopPrivateBathroom.

The coptoilet is scenery. The coptoilet is in the copprivatebathroom.The printed name of CopToilet is "toilet". Understand "toilet" or "bowl" or "loo" as Coptoilet. The description of the coptoilet is "This off-white toilet seems backed up, with odd bits of black material floating on the top." 

Check entering coptoilet:
	say "You're not going to sit on that!" instead;

The black material is scenery. The black material is in coptoilet. Understand "odd" or "bit" or "bits" or "black" or "material" as the black material. The description of the black material is "You look closer, but it's hard to make out what it is. It could be ash, or it could be what you'd expect to be in a toilet...".

Instead of taking the black material:
	say "You not really a forensics expert; and you're not sticking your hand in a nasty old toilet."

CopPrivatedoor is a closed openable door. CopPrivatedoor is scenery. Understand "bathroom door" or "bathroom" or "door" as CopPrivateDoor. The printed name of CopPrivateDoor is "bathroom door".  CopPrivatedoor is east from CopLobby and west from CopPrivatebathroom. The description of CopPrivatedoor is "A door with a handicap symbol."

Understand "lock [something]" as locking keylessly. Locking keylessly is an action applying to one thing.

Check locking keylessly: 
	if the noun is not a door, say "[The noun] is not something you can lock." instead; 
	if the noun is locked, say "[The noun] is already locked." instead; 
	if the player is in the coplobby:
		if the noun is CopPrivateDoor:
 			say "You can't lock the door from this side without the key." instead.

Carry out locking keylessly: 
	say "You lock the [noun]";
	now the noun is locked.

Understand "unlock [something]" as unlocking keylessly. Unlocking keylessly is an action applying to one thing.

Check unlocking keylessly: 
	if the noun is not a door, say "[The noun] is not something you can lock." instead; 
	if the noun is unlocked, say "[The noun] is already unlocked." instead; 
	if the noun is the CopControlDoor, say "You don't have the key." instead;
	
Carry out unlocking keylessly: 
	say "You unlock the [noun]";
	now the noun is unlocked.

Chapter 5 - Intro

Section 1 - Phone Booth

The shadowy figure is a person. The description of the shadowy figure is "You've carefully disguised yourself so that no one from the radio station will recognize you. A long pea coat, a low hat, dark glasses, a scarf. You tap the handset nervously."

Instead of going nowhere from Phone Booth:
	say "You need to finish this call, first.";

Instead of listening to the phone booth:
	say "You hear Rosalita's rapid breathing."

When play begins:
	now persona is the shadowy figure;
	now the player is carrying some notes;
	say "You put a quarter in the slot and dial Rosalita's number. The phone rings a couple of times; there's nothing to do but wait.";
	say "[line break][bracket]If you are playing Color the Truth for the first time, you are strongly encouraged to type ABOUT.[close bracket][no line break]";

PhoneTime is a scene. Phonetime begins when the player is in the Phone Booth for the second turn.

When PhoneTime begins:
	say "The phone rings one more time before Rosalita picks up.
	
		She says, 'Hello?'"; 
	try delivering Hello;
	
Some notes are a thing. The description of some notes is "You glance over the notes:

[fixed letter spacing]Rosa. Income:
	Roses for Rehab: 500K?
	Cassette sales: 250k?[variable letter spacing]"
	
BoothRosa is a person. The description of BoothRosa is "Rosalita is at work, but you've dialed her private number." BoothRosa is scenery. BoothRosa is in the Phone Booth. The printed name of BoothRosa is "Rosalita". Understand "Rosalita" or "Rosa" as BoothRosa.

Instead of listening to BoothRosa:
	try examining BoothRosa;

Hello is a quip. The preview of Hello is "Hello, Rosalita." Understand "greeting" as hello.

The RosaResponse of Hello is "'Hello, Rosalita.' You speak quietly, glancing through the glass to see if anyone's watching.

'What do you want this time?' she asks.

'I think you know,' you say. 'Same as last time.'

'Good luck with that. It's all gone now. You just take, and you take, and you take, and now there's nothing left. I don't know what you want anymore.'"

RosaSummary of Hello is "Rosalita said that she had nothing left."

After speeching Hello to BoothRosa for the first time:
	try delivering Broke;
	say "She sounds sincere, but she has to be lying. You've been looking into this for over a year, seeing where the money goes. It took months before it all came together, and you're not about to let your work go to waste. You read your notes again:[fixed letter spacing]
	
Rosa. Income:

Roses for Rehab: 500K?

Cassette sales: 250k?[variable letter spacing][line break]";
	try delivering Charity;
	try delivering Sales;
	
A quip can be leading or not leading. A quip is usually not leading.

Broke is a leading quip. The preview of Broke is "Rosalita says she's broke."

RosaResponse of Broke is "'You honestly expect me to believe you're out of money?'

'I can't live like this anymore! People are going to get suspicious when I have to sell my house.'"

RosaSummary of Broke is "She said she's running out of money."

Charity is a leading quip. The preview of Charity is "Rosalita has stolen half a million from her charity."

RosaResponse of Charity is "'What about all the money you took from Roses for Rehab?'

'Quiet!' she hisses. In a low voice, she says, 'I told you not to talk about that.'"

RosaSummary of Charity is "She didn't want to talk about the charity."

Sales is a leading quip. The preview of Sales is "Rosalita has made thousands from cassette sales."

RosaResponse of Sales is "'You've been making plenty of money from your cassette sales.'

'Who have you been- never mind,' she says. 'I'm not going to tell you anything.'"

RosaSummary of sales is "She seemed concerned that you knew about this."

After speeching a leading quip to BoothRosa for the first time:
	say "You sigh in exasperation. You know Rosalita; she can evade your questions for hours. It's time to catch her in a lie.[line break]"

IntroOver is a number that varies. IntroOver is 0.

InBooth is a scene. InBooth begins when play begins. InBooth ends when IntroOver is 1.

When InBooth ends:
	wait for any key;
	now persona is cop;
	repeat with current running through everything carried by the player:
		now current is in HoldingCell;
	repeat with current running through everything worn by the player:
		now current is in HoldingCell;
	say "The walnut-stained radio plays in the background. [italic type]'Everyone loves, everyone loses, everyone rejoices. Join me for... an Evening with Rosa.'[roman type] 
	[line break][line break]An unfamiliar voice cuts in. You set down your charcoal.
	
	[italic type]'Tonight's episode will consist of highlights from the album [roman type]The Very Best of An Evening with Rosa (Part 3)[italic type], as a tribute to the celebrated radio personality Rosalita Morales, who was found dead this morning in her studio. The police have yet to-[roman type]'
	
	You shake your head, and switch off the radio. Jayla would have been devastated; she adored Rosalita Morales. You return your attention to your painting; it's not turning out the way you hoped. Your stomach sinks, knowing what's coming. And you only had one week left... 
	
	The phone rings. You sigh, and answer. 'Hello?'
	
	They answer. 'Hector, this Chief Schwarz. I need you to come in early tomorrow...'
	
[banner text]";
	now the player is in ChiefOffice;
	now persona is cop;
	now the badge is worn by the player;
	now the silvery dress shirt is worn by the player;
	now the frayed ash-grey suit suit is worn by the player;
	now the checkered tie is worn by the player;

Rule for printing the banner text when persona is the shadowy figure: do nothing.

[The description of YourOffice is "Chief Schwarz has assigned this room to you as a temporary office. A bare bookshelf left over from the previous tenants has been pushed into the corner, and a desk sits on the opposite side, held up by two cubicle walls. The old computer Chief Schwarz has made available for you sits on the desk."

Some BeigeWalls are in YourOffice. The MuddyCarpet is in YourOffice. A CC camera is in YourOffice.

A desk is in YourOffice. A bookshelf is in YourOffice. The bookshelf is scenery. A computer is on the desk. The computer is scenery.

The desk is scenery. Understand "simple" or "half" or "cubicle" or "half-cubicle" as desk. The description of the desk is "A half-cubicle with a simple desk. A computer has been provided for temporary use."

The description of bookshelf is "A large, empty bookshelf." Understand "large" or "empty" or "shelf" as bookshelf. ]


Chapter 6 - Chief's office

Section 1 - The office

[This office is based on hardboiled detective novels.]

ChiefOffice is a room.

The printed name of ChiefOffice is "Schwarz's Office". Chief is in HoldingCell. The description of ChiefOffice is "[first time]You glance about the office. [only]The Chief's office is full of shades of black; he's told you that it's a symbol of authority, and makes him more imposing. You find it depressing. 

The office is large, and the leaden morning light from the window does little to brighten it. Its many bookshelves are filled with sable leather-bound volumes; it smells like a library in here, a mix of old books and card stock. The new chief's broad oak desk is in the center of the room, spotted with ink stains.  A banker's lamp with a ebony shade stands on the desk, turned off. 

The only way out is through the coal-black door to the [bold type]south[roman type].".

The off-white walls are scenery. The off-white walls are in ChiefOffice. The description of the off-white walls is "Once alabaster white, these walls have darkened to a sort of eggshell from decades of tobacco smoke." Understand "eggshell" as the off-white walls.

The high ceiling is scenery. The high ceiling is in ChiefOffice. The description of the high ceiling is "The ceiling is lost in shadows."

Understand "card stock" or "old" as ChiefOffice.

Understand "pool" as the banker's lamp.

The puddles of shadow are scenery. The puddles of shadow are in ChiefOffice. The description of the puddles of shadow is "The light has pushed the shadows into dark puddles."

Instead of taking the puddles of shadow:
	say "You scoop the puddle of shadow into your hand at watch it drain away; at least you would if it wasn't a metaphor.";

The Chief's blinds are in ChiefOffice. Understand "window" or "windows" as blinds. The Chief's blinds are scenery.

Instead of examining the Chief's blinds:
	try examining the darkened sky.

Instead of searching the Chief's blinds:
	try examining the darkened sky.

Understand "raise [something]" as opening.

Instead of opening the blinds:
	say "The Chief likes them the way they are, and you respect that."
	
Instead of touching the blinds:
	say "You rest your hand on the blinds while you look outside".

Outlooking is an action applying to nothing. Understand "look outside" as outlooking.

Carry out outlooking:
	if the player is in ChiefOffice:
		try examining the darkened sky.

The darkened sky is in ChiefOffice. The darkened sky is scenery.Understand "storm" or "leaden" or "clouds" or "rain" as darkened sky. The description of the darkened sky is "The sky is dark and sunless, and rain pours down. You can only detect the clouds by the varying shades of black."

The Chief's bookshelves are scenery. The Chief's bookshelves are in ChiefOffice. Understand "book" or "books" or "volume" or "volumes" or "leatherbound" or "leather" as the chief's bookshelves. The description of the Chief's bookshelves is "The chief stocks his shelves with a mix of books: law journals, biographies of killers, murder mysteries."

The law journals are scenery. The law journals are in ChiefOffice. Understand "journal" as the law journals. The description of the law journals is "Journals detailing the results of numerous cases, some involving criminals caught by the chief."

Instead of taking the law journals:
	say "The Chief might let you read them, but not take them."

Instead of reading the law journals:
	say "You pull one out at random, but none of the cases look exciting."

The biographies of killers are in ChiefOffice. Understand "biography" as biographies of killers. The biographies of killers are scenery. The description of the biographies of killers is "The Chief gets a kick out of reading biographies of murderers; he says it gets him in their brain, lets him understand them better.";

Instead of taking the biographies of killers:
	say "The Chief might let you read them, but he would never let you take them."
	
Instead of reading the biographies of killers:
	say "You read through a couple of pages of [one of]My Friend Dahmer[or]The Stranger Beside Me[or]The Monster of Florence[or]Deviant[at random].";

The murder mysteries are in ChiefOffice. Understand "mystery" or "fifty" as murder mysteries. The murder mysteries are scenery. The description of the murder mysteries  is "The Chief has a wide variety of murder mysteries. There has to be fifty or more books here.";

Instead of taking the murder mysteries:
	say "The Chief might let you read them, but he would never let you take them."
	
Instead of reading the murder mysteries:
	say "You read through a couple of pages of [one of]And Then There Were None[or]The Murder of Roger Ackroyd[or]Dance Halls of the Dead[or]The Adventures of Sherlock Holmes[or]The Murders in the Rue Morgue[at random].";

Instead of touching the darkened sky:
	say "You press your fingers to the glass of the window and watch the sky."

Instead of tasting the darkened sky:
	say "You can't taste it, but you can smell the dirty smell of rain."
	
Instead of tasting the darkened sky:
	say "You can smell the rain as it mixes with the grime and mud."

Instead of smelling the ChiefOffice:
	say "You smell the odor of old books and cardboard, mingled with the dirty smell of rain."

Instead of listening to ChiefOffice:
	say "You hear the patter of rain and the distant rumble of thunder."

Report looking in ChiefOffice for the first time:
		say "The Chief should be here soon."

ChiefResponse of Greeting is "'How are you doing today, Chief?'

He smiles darkly. 'That,' he says, 'depends on you, Hector. I can deal with the reporters, the phone calls, the mayor's office breathing down my neck, but what I need is someone to go out and crack this case. This was a nasty job; we don't want someone like this loose in our city.'

'So that's why you called me in.'

'That's right. I know you can get the job done."

ChiefSummary of Greeting is "The Chief trusts you to solve this case."

ChiefResponse of Statement is "'Chief, do you know anything about the case that's not in the file? Did you meet Rosalita before?'

The chief is quiet for a moment. 'You know,' he says, 'Yes, I did meet her, a few years ago. Susan was a big fan, and dragged me to a book signing. Rosalita was confident, smart. Not easily intimidated.'

'Sounds like she left a good impression.'

'Maybe. She was hard to read.'";

ChiefSummary of Statement is "The Chief met Rosalita a few years ago."

The ChiefChair is scenery. The ChiefChair is in ChiefOffice. Understand "chair" as ChiefChair. The printed name of ChiefChair is "chair". The description of the ChiefChair is "A plain, sturdy chair that supports the chief as needed."

The ChiefPhone and the ChiefDesk are scenery.  ChiefPhone and ChiefDesk are in ChiefOffice. Understand "phone" or "telephone" or "ivory" or "ivory-colored" or "rotary" as ChiefPhone. Understand "desk" or "drawer" or "drawers" or "stain" or "stains" as ChiefDesk. The printed name of ChiefPhone is "phone". The printed name of ChiefDesk is "desk".

The banker's lamp is scenery. Understand "ebony" or "shade" as the banker's lamp. The banker's lamp is on ChiefDesk. The description of the banker's lamp is "A slender brass lamp with a horizontal green shade, used to focus light on the desk below.".

Instead of switching on the banker's lamp:
	say "The light turns on, creating a pool of light that pushes the shadows into inky black puddles in the corners of the room."
	
Instead of switching off the banker's lamp:
	say "Might as well leave it on, now.".

The description of ChiefDesk is "The broad oak desk stands firmly near the wall like a bastion of truth. Its imposing presence has intimidated scores of young policemen, but those days are long past for you. A phone and a banker's lamp sit on the desk. The desk has several locked drawers.".

The Chief's drawers are a part of ChiefDesk. The Chief's drawers are an openable container. The Chief's drawers are lockable and locked.

The description of the Chief's drawers is "The Chief keeps a variety of sensitive documents in these drawers, as well as, you suspect, a few flasks of gin.".

Understand "gin" or "flask" or "flasks" as Chief's drawers.

The description of ChiefPhone is "The chief has an ivory-colored rotary phone." 

ChiefEntrance is a scene. ChiefEntrance begins when persona is cop. ChiefEntrance ends when the time since ChiefEntrance began is two minutes.

When ChiefEntrance ends:
	now the Chief is in ChiefOffice;
	say "The door behind you opens, and Chief Schwarz slips in. 'Good, you're here,' he says as he grips your hand before sitting down. 'Listen, Hector. Rosalita Morales was found dead in the KOLR studio yesterday.'
	
	'Yeah, I heard.'
	
	He cracks his knuckles and says, 'The lab is working on the evidence, but so far we've got nothing. I've got our people talking to everyone she knew. There's a special assignment for you.'
	
	'She's left money to four people. A lot of money. I've asked them to come to the studio for questioning.'
	
	He reaches into a drawer and grabs a folder that he tosses on the desk. 'I've prepared a dossier on the suspects, along with the preliminary report on the death. Take it with you.'";
	try delivering Testament;
	try delivering Suspicion;
	say "On the desk is a worn manila folder.";
	now the worn manila folder is on ChiefDesk;
	
The Chief's door is a door. The description of the Chief's door is "The Chief's door stands immobile, its dark surface gleaming in the lamplight." The Chief's door is south of ChiefOffice and north of TempStreet.

The Chief's door is openable and lockable. The Chief's door is scenery. The Chief's door is closed.

The Chief's handle is a part of the Chief's door. The description of the Chief's handle is "You carefully examine the handle to the Chief's door, but discover nothing."

Instead of going nowhere from ChiefOffice:
	say "The exit lies to the [bold type]south[roman type]. Exits are listed in the status line."

Before opening the Chief's door:
	if Chief is in HoldingCell:
		say "You don't feel like waiting. You walk to the door and grasp the handle. 
		
		But you hesitate; better to hear what the Chief has to say before you go. You turn back." instead;
	otherwise if the worn manila folder is not carried by the player:
		say "It's a miserable, wet day outside, and you don't want to drive all the way back here for that folder. Might as well take it with you." instead;
	otherwise:
		say "You brace yourself as you leave the precinct. Piercing winds cut through your clothes and the rain drives at you as you get in your car. You turn the key and go.
		
		***********************
		[bracket]Press any key to travel to the studio[close bracket]
		***********************
		
		";
		wait for any key;
		say "After parking, you stop for a moment. The cold pewter-gray rain patters on your windshield, obscuring your view. You breathe deeply, a soft silver mist escaping your lips in the chill air. You sit for a moment, eyeing the studio. It's a sandy colored brick building, short and narrow, crammed between a pawn shop and a Chinese restaurant. You grab the manila folder, throw open your door and race inside.";
		now the player is in CopLobby instead.


The printed name of TempStreet is "On your way." The description of TempStreet is "INSERT TRAVEL DESCRIPTION HERE"

After taking the worn manila folder for the first time:
	say "Taken.
	
	The Chief smiles grimly. 'You can go now,' he says, pointing to the door to the [bold type]south[roman type].

	'Call me whenever you want to talk,' he says, pointing to his phone. He fixes you in place with his stare. 'This case is important to me. If you need anything, please call.'";
	try delivering Assistance;

Testament is a quip. The preview of Testament is "Rosalita left all of her possessions to four people in her will.". 

ChiefResponse of Testament is "'What all can you tell me about the will?'

'I had the agency attorney look over it. It looks legitimate; she signed the newest version about 2 years ago.'

'And you said there are only four people in the will?' you ask. 'What about the rest of her family?'

'We checked it out. Only had one sister, and her parents died when they were teens.'

'Murder?'

'Car crash. Looks like she kept to herself, too. Not many friends outside of work. So what does that leave us? Her sister, her coworker-turned-husband-turned-ex, her business partner, and her personal assistant.'

'Do you remember what she left them?'

'No, I don't. It's in the folder I left you, though; you can just look up each suspect.'".

ChiefSummary of Testament is "Information about each of the suspects is in your folder".

DannyResponse of Testament is "'Mr. Kostopolous, are you aware that Rosalita's will names you and three other people as inheritors?'

'Yes, I know, and please, call me Danny. Rosalita always said she'd leave her half of the station to me.' 

'You two were co-owners?'

'That's right,' he says, and shakes his head. 'But the others are a bunch of sycophants and parasites. Chuck certainly doesn't deserve anything from her, and that sister... who was the fourth person again?'

'Cindy Jalcek.'

'Hmph. Don't get me wrong, Cindy's not bad, but Rosalita should have just given her money to charity.'";

After speeching Testament to Danny:
	now DannyAnger is DannyAnger plus one;
	say "[AngerText]";

DannySummary of Testament is "Danny is unimpressed with the other heirs, but grateful for his share.";

ChuckResponse of Testament is "'Are you aware that Rosalita left her house to you in the will?'

'Yes, they told me about it when they had me identify the--her body, ' says Chuck. 

'So you weren't aware of it before?'

Chuck massages his temple with one hand. 'When we were still married, I knew her will left the house to me, but I thought she would have changed it by now.'

'Why do you think she left it to you?'

He says, 'It was our house together; she was always sentimental. I think she shouldn't have done it, but I'll preserve it in her honor.'"

ChuckSummary of Testament is "Chuck will preserve their former house in Rosalita's honor.".

MaiaResponse of Testament is "'Maia, are you aware that Rosalita left you a significant amount of money in her will?'

Maia's eyes gleam. 'I wasn't aware of the amount, but I know she always believed in staying close to family. Do you have an estimate of the amount?'

'I'm sorry, I don't really deal with that end of things. I do know that her house has been left to her former husband, while her shares in KOLR were left to Mr. Kostopolous. Also, she left some smaller items to Cindy Jalcek.'

'Oh. Well, you're right. This probably isn't the best time to talk about this all.' She seems to be lost in thought.";

MaiaSummary of Testament is "Maia was excited by Rosalita's legacy.";

CindyResponse of Testament is "'It seems Rosalita named four people in her will: you, her partner Danny Kostopoulos, her ex-husband Chuck Lee, and her sister.'

Cindy looks at you with surprise. 'Rosalita put me in her will?'

'Yes, she left you her car and a lump sum of $50,000.'

Cindy's eyes widen and she takes a rattling breath. 'Oh gosh, that's a lot of money.' 

'You weren't aware of this?'

'No, I wasn't.'";

CindySummary of Testament is "Cindy just laughed when told the terms of the will.";

Suspicion is a quip. The preview of Suspicion is "The Chief thinks that someone close to her was involved in her death."

ChiefResponse of Suspicion is "'Chief, what makes you so sure that one of these people was involved?'

'Listen, it's easy: Follow the money. These four are all much better off now that she's dead. It's not hard to see a connection there.'

'Right,' you say, 'but anyone could have done it when you come down to it; some stalker, or something.'

'You know, I'd almost prefer that. Find some psycho that's roaming the streets, clean up the city. And I'm working on that angle. But the fact is these four have a clear motive. You do your job, and I'll do mine.'
".

ChiefSummary of Suspicion is "The chief says to follow the money."

DannyResponse of Suspicion is "'We have reason to believe that someone at the station was involved in Rosalita's death.'

'You do? It's funny you say that; Rosalita seemed nervous the week before she died. But she never confided in me. She just seemed more on edge before her death.'

'Can you think of any reason that she wouldn't call the police?'

[if fraud is not carried by the player]Danny gives you a wry smile and leans forward, seemingly ready to speak, then thinks better of it. 'I can't.'

'Do you suspect anyone in particular?'

'Never trust a liar, is what I say on my show. If any of them lies to you, right to your face, that's the one who did it, in my opinion.'[otherwise]He says, 'Of course, she couldn't tell anyone she was embezzling money from company. Not the police, at any rate. I worry that someone was putting pressure on her.'

'Who?'

He shakes his head. 'I don't know.'[end if]";

DannySummary of Suspicion is "Danny suspects the other three.".

ChuckResponse of Suspicion is "'We have reason to believe that someone that knew Rosalita was involved in her death.'

Chuck frowns, annoyed. He says, 'Well, obviously they weren't. When I was brought in here, they told me that Cindy Jalcek caught the murderer in the act, and saw him running away from the station.'

'You don't think he might be connected to someone here at the station?' you ask.

He sits thoughtfully, then turns to you. 'You know what I think? I think one of her listeners became obsessed. When we were married, there were a couple of people who would call over and over again. We had to change our number. We got mail, long letters saying how much they loved her.' He gives a wry smile. 'I don't think someone at the station killed her.'"

ChuckSummary of Suspicion is "Chuck suspects an outsider killed Rosalita."

MaiaResponse of Suspicion is "'We believe that someone close to Rosalita was trying to kill her.'

She shakes her head. 'No. I heard it was a stranger, someone she didn't know.'

'Who told you that?'

'You know, stuff gets around. They say that her secretary saw the murderer.'

'Maia, who says she saw the murderer? Who have you been talking to?'

Maia flushes. 'I overheard the police talking about it when I identified the-Rosalita's body.'";

MaiaSummary of Suspicion is "Maia heard that the killer was a stranger to Rosalita."

Assistance is a quip. The preview of Assistance is "The chief is always willing to give you help. (This gives a basic tutorial)."

ChiefResponse of Assistance is "'I could use some help.'

'Listen, just do what you best. Talk to the suspects, make them feel at ease. Take their statements [bracket]by typing SAY STATEMENT[close bracket]. Just listen to them; if you're patient, they'll trip themselves up.  If two statements don't match up, tell them about it (by typing LINK one statement TO another to form a new thought). Keep them talking.'

He goes on. 'And listen, I want to be involved. Call me [bracket]by typing CALL CHIEF near a phone[close bracket] and tell me what's going on.'


'Sure thing, Chief,' you say."

ChiefSummary of Assistance is "'I could use some help.'

'Listen, just do what you best. Talk to the suspects, make them feel at ease. Take their statements [bracket]by typing SAY STATEMENT[close bracket]. Just listen to them; if you're patient, they'll trip themselves up.  If two statements don't match up, tell them about it (by typing LINK one statement TO another to form a new thought). Keep them talking.'

He goes on. 'And listen, I want to be involved. Call me [bracket]by typing CALL CHIEF near a phone[close bracket] and tell me what's going on.'


'Sure thing, Chief,' you say."

After looking in CopLobby for the first time:
	say "A young woman dressed in beige and dandelion yellow is perched on the desk. She hops down and approaches you hesitantly.
	
	She asks 'Are you... Detective King?'
	
	'Yes. And you are?' 
		
	She says, 'Oh, I'm just the secretary here. They asked me to come here for questioning, or something like that. A bunch of us, actually; we all took different rooms.' 
	
	You hear a rustling sound from the north hallway. You both turn to look. You catch a glimpse of neon green hair, and you hear footsteps down the hallway.
	
	The secretary looks back at you. 'That's Rosalita's sister. She's supposed to be waiting in the studio.' She rolls her eyes. 'She's a singer who calls herself Andromeda. Chuck's in his old office across the hall from her, and Danny's at the very end by...well, uh, by Rosalita's office. Anyway, I'll be in the break room.'
	
	She walks off to the west. You realize that you didn't catch her name. You'll need to get that later, along with her statement.";
	try delivering Greeting;
	try delivering Statement;

Section 2 - Documents


The worn manila folder is in HoldingCell.  

The description of the worn manila folder is "The Chief has provided you with a worn manila folder. There are five names and a single word written on the outside in black pen:[fixed letter spacing]

Daniel Kostopoulos

Chuck Lee

Cindy Jalcek

Maia Morales

Rosalita Morales

Timeline[variable letter spacing]

You can [bold type]look up[roman type] individual names to review your information, or the timeline to review the timing of events." 

Infoing is an action applying to one visible thing. Understand "look up [something]" or "search for [something]" or "lookup [something]" as infoing.

A textfile is a kind of thing.

Instead of opening a textfile:
	try infoing the noun.

Instead of opening the worn manila folder:
	try examining the worn manila folder;
	
Instead of closing the worn manila folder:
	say "You straighten the papers and make sure the folder is closed tight.";

DannyFile is a part of the worn manila folder. DannyFile is a textfile. DannyFile is scenery. The printed name of DannyFile is "Danny's File". Understand "Danny" or "Danny's" or "Daniel" or "Daniel Kostopoulos" or "Kostopoulos" or "file" or "kostopolous" as DannyFile.

ChuckFile is a part of the worn manila folder. ChuckFile is a textfile.  ChuckFile is scenery. The printed name of ChuckFile is "Chuck's File". Understand "Chuck" or "Lee" or "Chuck's" or "Hu" or "Chuck Lee" or "file" as ChuckFile.

CindyFile is a part of the worn manila folder. CindyFile is a textfile.  CindyFile is scenery. The printed name of CindyFile is "Cindy's File". Understand "Cindy" or "Cindy's" or "Jalcek" or "Cindy Jalcek" or "file" as CindyFile.

MaiaFile is a part of the worn manila folder. MaiaFile is a textfile.  MaiaFile is scenery. The printed name of MaiaFile is "Maia's File". Understand "Maia" or "Maia's" or "file" or "Maia Morales" as MaiaFile.

The timeline is a part of the worn manila folder. The timeline is a textfile. Understand "time" or "line" as timeline. The timeline is scenery.

RosalitaFile is a part of the worn manila folder. RosalitaFile is a textfile. RosalitaFile is scenery. The printed name of RosalitaFile is "Rosalita's File". Understand "Rosalita" or "Rosalita's" or "Rosa" or "Morales" or "Rosalita Morales" or "file" as RosalitaFile.

Partner is a quip. The preview of partner is "Daniel Kostopoulos was the business partner of Rosalita Morales.".

DannyResponse of Partner is "'Tell me more about your business relationship with Ms. Morales.'

Danny leans back and looks off at the corner for a second before looking back at you.

'Rosalita Morales and I built this station. Years ago...I provided the experience, I ran the business end of things, attracting clients. I already had spent a few years as a political guest expert on a few talk shows. Rosalita was the pretty face; she met with advertisers, potential employees, a charmer, you know? And her voice, oh man. There's a reason her show is so popular, you know. I was the brain, she was the face.'

He rubs his nose and goes on. 'Or, at least, that's how it was going to be. But she knew her stuff; she started doing our taxes, choosing our investments, selling cassetes. Before I knew it, she was running the day to day operations of KOLR, and I just kept to my own show.'";

DannySummary of Partner is "Danny said that Rosalita became the leading force in the partnership."

ChuckResponse of Partner is "'What do you know about Daniel Kostopoulos?'

Chuck says, 'Danny? I really respect him. 'The Voice of the Heartland', right?' He smiles briefly. 'And the voice of authority, here at KOLR. He likes to be involved with all the day to day operations at the station.'

'What is your personal relationship like?'

Chuck blinks slowly. 'Our personal relationship? Oh, let me think...Well, I don't think he likes me. I actually applied to work at the studio because of him; he was big back then. But then I met Rosalita. She was beautiful. But about Danny, we haven't really gotten along in the last few years. He's too touchy, too erratic; I irritate him.'"

ChuckSummary of Partner is "Chuck said that he respects Danny, but they don't get along.".

[STOP HERE NEXT TIME]
MaiaResponse of Partner is "'What do you know about Danny Kostopolous?'

'Danny?' She stretches her back and thinks. 'He's famous and all, but he's wound up too tight. He only cares about his show. He keeps an iron fist on the studio, never lets anyone rent it out.'

'He kept an iron fist? Even with Rosalita?'

'What? No,' says Maia. 'No one could keep Rosalita down. But she let him do what he wants around the station, if you know what I mean. He used to let people use the recording studio for a small fee on weekends, but that's gone and done.'

'It sounds like you two didn't get along.'

'I feel like he could be a lot more generous.'"

MaiaSummary of Partner is "Maia finds Danny to be stuffy and a tightwad."

CindyResponse of Partner is "'What do you know about Danny Kostopoulos?'

'Well, obviously I respect him as one of the owners, you know? And he is [apostrophe]The Voice of the Bread Basket[apostrophe] or whatever he calls himself.' 

You say, 'The Voice of the Heartland.'

'I know, I know. But he's so irritable all the time, he's got a real mean streak. Sometimes I'll pretend to be on the phone just to avoid talking to him.'"

CindySummary of Partner is "Cindy tries to avoid Danny."

ChiefResponse of Partner is "'What do you know about Danny Kostopoulos?'

'I know Danny pretty well. Not personally, you know; but he's a big name in politics. Schmoozing with the mayor, speaking at fundraisers, trying to pick the next big candidate. I don't like him, but I respect him.'"

ChiefSummary of Partner is "The chief said that Danny is involved in local politics."

Ex is a quip. The preview of Ex is "Chuck Lee is the former husband of Rosalita Morales."

ChuckResponse of Ex is "'Tell me about your relationship with Rosalita,' you say.

Chuck's eyes water up. He asks, 'You want me to tell you what our marriage was like?'

'I mean, what sort of interaction have you had with her recently?' you say.

Chuck stares at the floor as he answers. 'Well, I'll admit, things were tense at times. Anyone here at the station could tell you that. Can you imagine working next to your ex for half a decade? That's one reason I wanted to leave.'

'But she kept you in her will,' you say.

Chuck closes his eyes momentarily and sighs. 'Rosalita... had trouble letting go. She always wanted to go out for drinks, or to talk to each other. But it just wouldn't have worked out.'";

ChuckSummary of Ex is "Chuck said that he split up with Rosalita six years ago, but she still had feelings for him.";

CindyResponse of Ex is "'What do you know about Chuck Lee?'

She seems startled. 'What? What did you say?'

'I said, what's your relationship like with Chuck?'

She sighs in relief. 'Oh, our relationship. Well, you know, he was never one of the managers or owners at KOLR, so I didn't have to deal with him as much. He's a nice guy, handsome and all, but he and Rosalita...' She bites her lip. 'I mean, they broke up, you know? It was kind of awkward after that.'"

[After speeching Ex to Cindy:
	try delivering Ignorance.]

CindySummary of Ex is "Cindy said she didn't interact with Chuck very much.".

MaiaResponse of Ex is "'What do you know about Chuck Lee?'

'Chuck is just great.'

'So you like him.'

'I was so proud when Rosalita married him, I mean he's just really handsome, especially since his surgery. WESL is lucky to have him.'

'WESL the TV station?' you ask.

'Right. I know he and Rosalita broke up, but I've been trying to keep in contact with him. He's been pretty busy, though.'"

MaiaSummary of Ex is "Maia thinks Chuck is great.".

DannyResponse of Ex is "'What do you know about Chuck Lee?'

Danny glowers. 'Chuck Lee. The most trusted man in the county. What a joke. He was always after Rosalita's money, when he first dated her, and her connections.'

Before you can respond, he says 'That's how he got his big breaks, you know; he'd get interviews by telling people he was Rosalita Morales['] husband. Then he got too big for her, I guess. Left her, but kept a hefty chunk of money in the divorce settlement. She still gives him anything he wants. At least, he used to."

After speeching Ex to Danny for the first time:
	now DannyAnger is DannyAnger plus one;
	say "[AngerText]";

DannySummary of Ex is "Danny is not impressed with Chuck, and thinks of him as a moocher.".

ChiefResponse of Ex is "'What do you know about Chuck Lee?'

'Same as you; I know he did news on KOLR before switching to TV. My mother adores him, though; after his first report, she said he was the [apostrophe]handsomest man on TV[apostrophe]. She called me four times yesterday, asking when we were going to release him so he could go back on air.'

'Do you know anything else about him?'

'No, he's not as flashy as Danny Kostopolous. Seems like he's kept to himself, more.'"

ChiefSummary of Ex is "The chief said that Chuck keeps to himself."

Secretary is a quip. The preview of Secretary is "Cindy Jalcek was Rosalita's Secretary."

CindyResponse of Secretary is "'Tell me more about your work with Rosalita.'

She nods.

'Rosalita was the one who hired me on, three years ago. Apparently the previous secretaries couldn't handle the job,' she says, smugly. 'I've made a real impact here, and Rosalita liked it. I could clean the studio, help her with errands, and run the front desk without breaking a sweat.' A tear wells up in one eye. 'I really miss her.'

'You were close?'

'Yes, she liked to confide in me. She knew she could trust me.'

'Did she tell you anything unusual around the time of her death?'

She says, 'Nothing that I haven't told the police.'"

CindySummary of Secretary is "Rosalita hired Cindy three years ago, and Cindy said they had a good relationship.";

MaiaResponse of Secretary is "'What do you know about Cindy Jalcek?'

Maia frowns. 'Cindy's the secretary, right? I didn't know her last name. She's a suspicious sort of woman.'

'Does she have a criminal record?'

'Oh, not that kind of suspicious! She's always watching me when I go there, following me around. I wish she would cool off a bit. She seems good at what she does, though; I like the stencil she put up in the lobby.'".

MaiaSummary of Secretary is "Maia thinks that Cindy is too nosy."

DannyResponse of Secretary is "'What do you know about Cindy Jalcek?'

He rolls his eyes. 'Cindy. She's good intentioned but she just... She wears holes in everything with her scrubbing, she hides the toilet paper, she'll take really long lunch breaks at the Chinese restaurant.'

'She seems to have been on good terms with Ms. Morales.'

'Yeah, I would have fired her long ago, but Rosalita liked her. And at least she did good with the phones. Good service. Drove up sales. But still, I wish she had more sense. '"

DannySummary of Secretary is "Danny is annoyed by Cindy, but appreciates her."

ChuckResponse of Secretary is "'What do you know about Cindy Jalcek?' you ask.

Chuck yawns broadly. 'Cindy? Cindy is pretty reliable. We went through half a dozen interns before her, but she managed to last.'

'Did she fit in well at the studio?'

Chuck says, 'Oh, hmmm, well, I don't think Danny really likes her. Rosalita loved her. They use to take lunch together, and Cindy was always chatting in Rosalita's office. When I found out that Rosalita had written her into her will, though, it made me worried about Cindy's sincerity.'[if accomplice is ChuckGiven]

'What about her involvement in the fraud?' you ask.

Chuck says, 'What? Oh, yes, that. Terrible, that she was involved in that.' But his mind seems elsewhere.[end if]"

ChuckSummary of Secretary is "Chuck likes Cindy, but worries she was trying to get money from Rosalita."

ChiefResponse of Secretary is "'What do you know about Cindy Jalcek?'

'Nothing, personally. No connections; I figured Danny would try to get one of his buddies[apostrophe] kids in there as a favor, but she's a nobody, politically.'

'Did Rosalita hire her, then?'

'Yeah, and apparently they were close. You'll have to figure out anything else yourself.'";

ChiefSummary of Secretary is "The chief said that Cindy and Rosalita were close."

Sister is a quip. The preview of Sister is "Maia Morales was Rosalita's sister.".

MaiaResponse of Sister is "'What was your relationship like with your sister?'

She leans back. 'With Rosalita? We were close, you know? I spent a lot of time at her house. We used to hang out, go shopping all the time. But she's been a lot more on edge this last year.'

'On edge?' you ask.

 'She wasn't happy with my career change and my new look,' she says, pointing to her hair. 'Me becoming [']Andromeda['] disappointed her. She said it wasn't a real job. I said she was stuffy and boring, only cared about money. She would get really upset when I talked about it.'

She shrugs. 'I don't know how things got this way.'"

MaiaSummary of Sister is "Maia said that her relationship with Rosalita was deteriorating."

ChuckResponse of Sister is "'How well do you know Maia Morales?'

'Who, [']Andromeda[']?' He slowly shakes his head. 'I know her pretty well. She was always over at our place when we were married.' 

'Is that the house where Rosalita resided at the time of her death?' you ask.

Chuck massages his neck for a moment, stretching, then answers. 'Yes. After Rosalita and I broke up, I didn't see her very much. As soon as I got the job at WESL, though, Maia started calling me, talking about our friendship, our past. I think she's just looking for an angle.'"

ChuckSummary of Sister is "Chuck says that Maia is trying to worm her way into his friendship."

DannyResponse of Sister is "'What do you know about Andromeda?'

Danny rolls his eyes. 'She's even got the police saying her stage name. Listen, she's just Maia Morales, Rosalita's sister and the scum of the earth.'

'You don't like her.'

He snorts. 'Like her? She's been riding off Rosalita's back for years. Always asking for gas money, or borrowing her clothes and never returning them. This Andromeda thing is her latest racket; I think she's been sneaking into the studio to record, and I am [bold type]not[roman type] pleased with that.'"

After speeching Sister to Danny for the first time:
	now DannyAnger is DannyAnger plus one;
	say "[AngerText]";

DannySummary of Sister is "Danny dislikes Maia, and said she using the studio to record songs."

CindyResponse of Sister is "'What do you know about Maia Morales?'

Cindy opens her mouth, then pauses. 'I... I don't like her very much. Not much at all.'

'Why don't you like her very much?

'I mean, she's a mooch! My job is to keep people like her out. She's always hitting up Rosalita for money, or trying to sneak into our studio.' 

You ask, 'Why was she sneaking into the studio?' 

'To record. She thinks she's a rock star because she dyed her hair and calls herself Andromeda.' She shakes her head. 'I just don't like her.'".

CindySummary of Sister is "Cindy dislikes Maia for being a mooch."

ChiefResponse of Sister is "'What do you know about Maia Morales?'

'I thought I didn't know anything, until someone told me she was Andromeda.'

'So you heard her music?'

'Yeah,' he says, 'she sang at a political party Danny organized. She wasn't half bad, so I bought her singles.'

'I didn't know she had any talent.'

'She's good, don't get me wrong. But she was really fussy, demanding, like some bigshot. She hasn't gotten much work since then.'"

ChiefSummary of Sister is "The chief said that Maia was talented but demanding."

Death is a quip. The preview of Death is "Rosalita was shot on February 29th in her office."

DannyResponse of Death is "'Our records show that Rosalita was shot two days ago in her office.'

Danny looks incredulous. 'Well, isn't that fantastic. Congratulations on your wonderful record-keeping software. What's wrong with you? I got a phone call two days ago, telling me that my best friend and business partner was dead. That's how I knew.'

'You're being sarcastic.'

Danny rolls his eyes. 'You cops. No heart. No heart at all.'";

After speeching Death to Danny for the first time:
	now DannyAnger is DannyAnger plus 3;
	say "[AngerText]";

DannySummary of Death is "Danny was unhappy at being reminded of the circumstances of Rosalita's death."

MaiaResponse of Death is "'Our records show that Rosalita was shot to death on February 29th.'

Maia shivers. 'Yes.'

After a moments pause, she continues. 'I can't believe that someone could do that to her. Rosalita didn't deserve to die, especially not like that.'

'No one really deserves to be murdered.'

'I know; It was just so shocking, so unexpected.'";

MaiaSummary of Death is "Maia was shocked by the death."

ChuckResponse of Death is "You say, 'Our records show that Rosalita died two days ago, February 29th, from a gunshot wound to the head.'

Chuck thinks for a moment. A couple of tears roll down his cheek, one at a time. He nods. 'That's right.' He wipes his cheek. 'I identified the body.'

'Do you know any more details about the death?' you ask.

 'No. But I know this: the murderer was a savage. He deserves to die,' Chuck says, clenching his fist. He stares you in the eye. 'If you find out who did it, they should die.'

'I'll pass that along,' you say.".

ChuckSummary of Death is "Chuck cried when reminded of Rosalita's death".

CindyResponse of Death is "'Cindy, our records show that Rosalita Morales died two days ago, shot to death in her office.'

Cindy sits silently.

'Miss Jalcek, do you have any comment?'

'Oh! No, no comment. Just...it was so terrible. Do we really have to talk about this?'

'Yes, it's part of the investigation.'

'I was there when she was shot, but I didn't see it. I just heard it in the hallway, and saw the killer running away.'";

CindySummary of Death is "Cindy confirmed that she heard the shot and saw the killer."

ChiefResponse of Death is "'Do you know anything about Rosalita's death, outside of the files?'

The Chief says, 'No, I wouldn't play around with that. You know what I know.'

'But is there anything else that should be in the report? Gunshots are pretty loud; did anyone hear it?'

'Cindy is the only person who's claimed to have heard the gunshot; no other witnesses have come forward.'";

ChiefSummary of Death is "The chief said that Cindy was the only known witness.".
[
Forced is a quip. The preview of Forced is "The police report says that the windows were forced open by the intruder."

CindyResponse of Forced is "'The police say the windows were forced open.'

Cindy nods absentmindedly.

'The windows in Rosalita's office.'

Cindy nods.

'Cindy?'

'Yes?'

'Did you notice if the windows were open or not?'

'I mean sure, whatever.'"

CindySummary of Forced is "Cindy didn't pay attention when you talked about this."

MaiaResponse of Forced is "'The police say the windows were forced open.'

Maia shakes her head, and says 'I just don't get why someone would break into her office, shoot her, and run off.'

'People get shot every day. Could have just been a routine burglary gone wrong.'

'What was taken?'

'I'm not sure. Maybe he ran off before he could grab anything.'

'It doesn't add up,' says Maia. 'She was acting strange for a while before her death.'"

MaiaSummary of Forced is "Maia has doubts about the intruder story."

[LAST ONE CHANGED]

ChuckResponse of Forced is "'The police say the windows were forced open.'

'Which windows?'

'In Rosalita's office.'

'So they think the intruder came in through there?'

'That's the logical conclusion,' you say.

'That supports my theory that some rabid fan murdered Rosalita.'";

ChuckSummary of Forced is "Chuck asked for more information about the windows.";

DannyResponse of Forced is "'It seems the windows were forced open in Rosalita's office.'

'[if DannyAnger is at least 4]Figures[otherwise]Make sense[end if]. I always warned her about office security; here she was, a nationally known talk show host. And she refused to get an alarm system! This frankly a [if DannyAnger is at least 4]crappy[otherwise]mediocre[end if] neighborhood. [if DannyAnger is at least 4]None of this would have happened if she would have stopped acting like a fairtyale princess and tried to grow up for once![otherwise]I just wish she hadn't been so trusting, you know?[end if]'"

DannySummary of Forced is "Danny was upset with Rosalita's carelessness".

After speeching Forced to Danny:
	if DannyAnger is at least 4:
		now DannyAnger is DannyAnger plus two;
		say "[AngerText]";

ChiefResponse of Forced is "'The report says that the windows in Rosalita's office were forced open.'

'Suggests someone came in from the outside, right?'

'I suppose,' you say. 'But none of these suspects would have needed to force the windows.'

'See if anyone else noticed the windows,' says the Chief.";

ChiefSummary of Forced is "The chief suggested checking everyone's stories to see if they had more information on the windows."]

Instead of examining DannyFile:
	try infoing DannyFile;

Instead of examining MaiaFile:
	try infoing MaiaFile;

Instead of examining ChuckFile:
	try infoing ChuckFile;

Instead of examining CindyFile:
	try infoing CindyFile;

Instead of examining RosalitaFile:
	try infoing RosalitaFile;

Instead of examining the timeline:
	try infoing the timeline.

Carry out infoing:
	say "[fixed letter spacing]";
	if the noun is DannyFile:
		say "Name: Daniel Kostopoulos.

Age: 63

Occupation: Host of the conservative talk show, 'The Danny Kostopolous Show'.

Summary: Daniel Kostopolous co-founded KOLR with Rosalita Morales over 20 years ago. His talk show and her 'An Evening with Rosa' have consistently brought in the highest ratings and ad sponsorships for KOLR, outside of nationally syndicated programs. Daniel always goes by the name 'Danny'.  Daniel lives alone, with no former marriages or children.

In her will, Rosalita left him all of her share of KOLR, making him the sole owner.";	
		say "[variable letter spacing]";
		try delivering Partner;
	otherwise if the noun is ChuckFile:
		say "Name: Chuck Lee.

Age: 45

Occupation: TV News Anchor for WESL.

Connection with Morales: Chuck Lee is Rosalita's ex-husband, and former coworker. Chuck was the news anchor for KOLR local news. He recently left KOLR for television after being headhunted by WESL. 

Chuck and Rosalita were married 16 years ago, with no children resulting from the marriage. They divorced six years ago amicably, and for unspecified reasons. 

Chuck resides at 75 Radhaus Lane #701. 

In her will, Rosalita left Chuck the house that they had lived in together.";
		say "[variable letter spacing]";
		try delivering Ex;
	otherwise if the noun is CindyFile:
		say "Name: Cindy Jalcek.

Age: 19

Occupation: Receptionist for KOLR and personal assistant for Rosalita.

Connection with Morales: Cindy Jalcek has worked for KOLR as a receptionist for 3 years. Due to the small size of the organization, her duties also include cleaning, supply shopping, and working as Rosalita's personal assistant. Rosalita hired her permanently after a successful internship, and she became Rosalita's confidant. In the will, Rosalita left Cindy her car and a lump sum of $50,000.";
		say "[variable letter spacing]";
		try delivering Secretary;
	otherwise if the noun is MaiaFile:
		say "Name: Maia Morales (stage name Andromeda).

Age: 39

Occupation: Vocal artist.

Connection with Morales: Maia Morales, aka Andromeda, is Rosalita's only sibling. Both of their parents are deceased, making her Rosalita's closest living relative. 

Maia is an aspiring singer. Under the stage name Andromeda, she has released two singles and has fronted several big-name bands in the local area. Her two singles were recorded in KOLR studios. 

Rosalita left Maia the remainder of her estate, after the other bequests.";
		say "[variable letter spacing]";
		try delivering Sister;
	otherwise if the noun is RosalitaFile:
		say "Name: Rosalita Morales

Age: 42

Date of death: February 29.

Cause of Death: Gunshot wound in the head.

Summary: Officers were called to KOLR studios at 9:25am by her assistant, Ms. Jalcek, who discovered the body in the office when she came into work. 

The investigation is ongoing, but initial results suggest a break-in: the office windows were forced open, and Ms. Jalcek and Maia Morales report seeing an intruder flee the scene.

Background: Rosalita Morales was the host of the popular romance talk show, 'An Evening With Rosa'. She was the co-founder and co-owner of KOLR, together with Daniel Kostopoulos. She was previously married to celebrity news anchor Chuck Lee, but divorced 6 years ago, and now lives alone. Her closest associates were her assistant Cindy Jalcek and her sister Maia Morales, aka Andromeda.";
		say "[variable letter spacing]";
		try delivering Death;
	otherwise if the noun is the timeline:
		say "Timeline of Events:
		 
		Wednesday, February 27-Chuck Lee cleans out his office and leaves KOLR at approximately 5:00pm. He claims to have seen Rosalita, but left without talking.
		
		Thursday, February 28-Daniel Kostopolous discussed the annual report with Rosalita at around 3:00pm. He did not see her again after this.
		
		Friday, February 29-Maia Morales visited Rosalita at work some time before 9:00am, then left. Cindy Jalcek was cleaning the studios, and discovered a stranger in a yellow poncho standing over Rosalita's body. She called emergency dispatch at 9:05am.";
	otherwise if the noun is the annual report:
		say "[variable letter spacing]";
		say "[if DannyBroke is 0]You look over the annual report. You and Rosalita need to do your yearly audit of the budget. It's been a tough year, but you've done fairly well for yourselves. The charity drive had a record year, too. All accounts have been audited and all expenditures and donations have been accounted for, and the receipts carefully filed away.[otherwise]Paging through the report, your suspicions are confirmed. The amount KOLR donated to Roses for Rehab last year is less than you raised personally at your gala. There must be far more money than is being reported here. You could look at the bank reconciliation, but you already have enough information here.[end if]";
	otherwise if the noun is the charity file:
		say "Name: Roses for Rehab
		
		Roses for Rehab is a charity established by Rosalita Morales and KOLR studios to fund rehabilitation programs for drug addicts and alcoholics. Although the program was wildly successful at first, the income level has dropped significantly in recent years.";
	otherwise if the noun is the bank reconciliation:
		say "[variable letter spacing]Yes, the bank reconciliation confirms your worries. Each month for the last year or so, several checks were marked as 'pending' at the end of the month. But they never showed up, even half a year later. You need to talk to Rosalita.";
 	otherwise:
		say "[variable letter spacing]You can only look up a file.";
	say "[variable letter spacing]";

Instead of examining the bank reconciliation:
	try infoing the bank reconciliation.

[The bank reconciliation for a company's checking account begins with the company noting the balance per the bank statement and then making some notations about that balance. For example, the balance on the bank statement is probably not the amount that appears in the company's records. In all likelihood the checks written by the company in the days immediately before the date of the bank statement will not have cleared (been deducted from) the checking account. These are called outstanding checks. Another possibility is that the company received money on the closing date of the bank statement and properly recorded the amount in its records. However, the money was deposited into the bank too late in the day and will appear on the next bank statement. This known as a deposit in transit. Let's begin the bank reconciliation by assigning some amounts to the items just mentioned:]

Does the player mean infoing a textfile:
	it is likely. 

Chapter 7 - Danny's Story

[Danny's story is based on Anchorhead, the records scene]

Section 1 - Setup

DannyAnger is a number that varies. DannyAnger is 0.

DannyResponse of Greeting is "'Daniel Kostopolous?'

He rises to grip your hand firmly. 'I usually go by Danny,' he answers in a husky voice. He looks you up and down. 'I've seen you before, down at city hall. You've been on the force for a while, haven't you?'

'Excellent memory, Mr. Kostopolous. I'm Detective King; I've been assigned to investigate the death of Rosalita Morales. I apologize for disrupting your broadcasting.'

Danny says, 'Not at all; we're broadcasting from the telephone line, right now. All of our local shows air in the evening.' He looks at his watch. 'Hopefully this won't take too long.'

You say, 'Of course. I would like to ask you some questions about what happened that night.'

'Listen, call me Danny. Real shame what happened. She... I don't even like to think about it. It shouldn't have happened. Not to Rosa.' He sits down.";

After speeching Greeting to Danny:
	try delivering Partner.


Check speeching a quip to Danny:
	if the noun is Greeting:
		continue the action;
	otherwise if Greeting is MeihDanny:
		say "The man interrupts you. 'I don't believe we've met.'";
		try speeching Greeting to Danny;
		continue the action;

				
DannySummary of Greeting is "You have met Danny Kostopoulos."

Instead of speeching Statement to Danny:
	if greeting is MeihDanny:
		say "The man interrupts you. 'I don't believe we've met.'";
		try speeching Greeting to Danny;
	if Statement is MeihDanny:
 		say "'When was the last time that you saw Rosalita Morales?'

'All right, sure, I'll tell you. It was the day before she died. Here's what happened.'";
		now Statement is DannyGiven;
		add "[line break][line break]Danny has given you his statement already." to history of Statement;
	otherwise:
		say "'Tell me your story again.'

'Of course, officer.'";
	now StartRoom of cop is CopDannyOffice;
	try switcherooing Danny instead;

[Finished is a quip. The preview of Finished is "Rosalita told Danny she and Chuck were done for good."

DannyResponse of Finished is "'Did Rosalita say anything else about her relationship with Chuck?'

Danny shakes his head. 'No, she's not the kind to share details.'

'What did she seem like when she told you?'

'She sort of laughed and shook her head, you know? Just sort of dimissing the idea altogether.'"

DannySummary of Finished is "Danny seemed confident she wasn't back with Chuck."

ChuckResponse of Finished is "'Danny said that Rosalita and you were through.'

'Did he say that Rosalita told him that?'

'Yes,' you say.[if Tryst is carried by the player]

'He's wrong. Rosalita wanted to keep it quiet; Danny can get jealous sometimes.'[otherwise]

'I can't really speak for Rosalita, if that's how she feels. Although she never really liked discussing our love life with Danny, so it's hard to say if that's true.' He looks thoughtful."

ChuckSummary of Finished is "Chuck said that Rosalita may have lied to Danny."

MaiaResponse of Finished is "'According to Danny, Rosalita said she and Chuck were through for good.'

Maia smiles, but stops when she sees your look. 'Ms. Morales,' you ask, 'can you confirm or deny their relationship?'

'Well, that's the sort of thing that you never can tell, right? That's the way it is with ex's. One wants to get back, the other doesnt, a couple of years later it all turns back around.'"

MaiaSummary of Finished is "Maia says that they were always on again, off again.";

CindyResponse of Finished is "'Danny said that Rosalita and Chuck weren't ever getting back together.'

'Was that his idea, or did one of them tell him?' asks Cindy.

'He says Rosalita told him that.'

'That's surprising; you can tell that they still cared about each other."

CindySummary of Finished is "Cindy found it surprising."

ChiefResponse of Finished is "'Danny said that Rosalita and Chuck were done with each other.'

The chief says, 'The tabloids would hardly believe it. They've been writing about those two for months now.'

'Writing about what?'

'The two have been seen together, coming out of private places, talking. It's a big deal.'"

ChiefSummary of Finished is "The Chief said the tabloids are reporting the two have been meeting up."]

Section 2 - Lobby


DannyLobby is a room. The printed name of DannyLobby is "Lobby". The description of DannyLobby is "The lobby is the first thing visitors see when they visit KOLR, so you try to keep it looking good. As you cast about a critical eye, you notice that the stencil on the wall is wearing off. It'll need repainting in about a month. The carpet is clean, but it's looking worn out. The front desk looks pretty good, so you can keep that for a while. The buzzing of one of the lights resonates in your skull in a painful way; that's got to get fixed quick. The offices are back to the [bold type]north[roman type], and the parking lot is [bold type]south[roman type]. You can also reach the bathroom or the break room from here, but you prefer to spend as little time in those two rooms as possible."

Instead of going nowhere from DannyLobby when the noun is west:
	say "You would not eat the food in that room if you were dying."

Instead of going nowhere from DannyLobby when the noun is east:
	say "You never use that bathroom unless you absolutely have to. The smell is awful; probably mold behing the drywall."

The Dannyfluorescentlights are scenery. The Dannyfluorescentlights are in DannyLobby. Understand "fluorescent" or "dingy" or "plastic" or "covers" or "lights" or "light" or "buzz" or "buzzing" or "flickering" as Dannyfluorescentlights. The description of Dannyfluorescentlights is "That buzzing light has got to get changed. All of the lights are tightly covered by plastic, making it difficult for you to change yourself."

Instead of listening to the Dannyfluorescentlights:
	try examining the dannyfluorescentlights;

Instead of touching Dannyfluorescentlights:
	say "It's too high!"

The DannyStencil is in DannyLobby. The DannyStencil is scenery. Understand "faded" or "r" or "stencil" or "rust" or "beige" or "washed-out" or "blue" or "green" as DannyStencil. The description of DannyStencil is "The R is fading. You'll hunt up some high school art kid to repaint it."

The DannyFrontDesk is in DannyLobby. The DannyFrontDesk is scenery. The printed name of DannyFrontDesk is "front desk". Understand "front" or "desk" as DannyFrontDesk. The description of the DannyFrontDesk is "The front desk has some wear and tear, but you're not buying a new one any time soon. A telephone sits on the desk, and a chair is pushed up behind it. You see the receptionist on a call.". DannyFrontDesk is a supporter. DannyFrontDesk is not enterable.

The DannyPhone is on DannyFrontDesk. The DannyPhone is scenery. The printed name of DannyPhone is "telephone". The description of DannyPhone is "The receptionist is currently using the phone.". Understand "ivory" or "telephone" or "rotary" or "numbers" or "rubbed" as DannyPhone.

The DannyFrontChair is in DannyLobby. The DannyFrontChair is scenery. The printed name of DannyFrontChair is "chair". The description of DannyFrontChair is "This low black chair is pushed up close to the desk."

Instead of taking the DannyFrontChair:
	say "That's not really necessary."
	
Instead of pushing the DannyFrontChair:
	say "You're not here to rearrange furniture."
	
Instead of pulling the DannyFrontChair:
	say "You're not here to rearrange furniture."

DannyCindy is a person. DannyCindy is in DannyLobby. "Cindy is here, answering phone calls." The printed name of DannyCindy is "Cindy". Understand "Cindy" or "secretary" or "Jalcek" or "receptionist" as DannyCindy. The description of DannyCindy is "Cindy has the receiver cradled against her neck, and has one hand twirling the telephone cord."

Every turn when the player is in DannyLobby:
	say "[one of]'Thank you for calling KOLR Studios, this is Cindy.'[or]'Don't worry, the fundraiser is still going on. You can buy a box of dark-chocolate candybars or a year's supply of frozen pizza.'[or]'Yes, cassettes are available for individual episodes or for boxed sets. Of course I recommend the boxed set!'[or]'Yeah, Janet told me about it, it's called a Synpiece and you wear it like a contact lens. It's so weird.'[or]'And I told Michael, sure you can take a job there, I mean, you're a Verlac, but a fishing town is so depressing.'[or]'But I'm not sure the studio needs the Encyclopedia Imperii Subterranii Magni. I'm sure it's a lovely place....'[or]'Did you say that you lit your pants on fire? Sir, I think you have the wrong number.'[or]'Well if gnocchi reminds you of her, I would stop going to the pasta aisle. Of course I know it's not pasta!'[or]'And then I swear he spit at him, some kind of greenish goo. And he had a snakeskin thing on him. I don't know either.'[or]'And my uncle's all 'I just have to grab my street map from the attic,' and three days later we just give up and leave without him. Still don't know what happened there.'[or]'Oh, I completely recommend it, it's about these sentient dolphin-humans that sing to heal people. I know, right?'[or]'Yeah, and he just drive right into the guy's office, started filling out paperwork. They caught him trying to give a report to his boss.'[or]'No, there's no way I'm going to Burning Man again. Sand got in all my clothes and then it got all over my house.'[or]'Yeah, it was some kind of Artificial Intelligence exhibit. But she was so rude! Wouldn't even turn around.'[or]'I mean, she was just so rude, you know? She just took his favorite toy and crawled away.'[or]'Yeah, he just left the picnic on his own. No one knew where he went, and then he comes back the next day, wearing a blanket and covered in mud, grinning like a clown.'[or]'I get dreams like that too. Once, I was a rock star, but the whole crowd was birds, and my band was birds, but I wasn't birds, and-are you still listening?'[or]'And each color was a [italic type]mood[roman type]. And ultraviolet and infrared were colors...it was the strangest dream![or]'Yeah, that's why they called him Bonehead, he dropped the ball...'[or]'Uh huh, space whales, floating in front of the station. It was last week's episode.'[or]'And the statue was really a guy wearing a wolf mask...yes, salted caramel...I see...'[then at random]";

Instead of talking to DannyCindy:
	say "Cindy points to the phone and mouths the word 'sorry!'";

DannyHallDoor is a door.  DannyHallDoor is scenery. The printed name of DannyHallDoor is "hallway door". Understand "door" or "black" or "window" or "glass" or "reinforced" or "metal" or "chips" or "paint" or "rusty" or "rust" or "hallway" as DannyHallDoor. DannyHallDoor is north of DannyLobby and south of DannyHallway. DannyHallDoor is closed and openable. The description of DannyHallDoor is "You note with displeasure that the paint is chipping on the door." DannyHallDoor is not lockable.

Instead of listening to DannyLobby:
	say "That light is still buzzing, and the receptionist is yakking away about something, probably another complaint about racy ads on Rosa's show."

Instead of listening to Dannyphone:
	try listening to DannyCindy.
	
Instead of listening to DannyCindy:
	say "You'd rather not; her high-pitched voice gives you a headache."

Instead of smelling DannyLobby:
	say "You get a whiff of the mold in the bathroom. It makes you nauseous."

DannyMuddyCarpet is a backdrop. DannyMuddyCarpet is in DannyLobby. DannyMuddyCarpet is scenery. Understand "muddy" or "carpet" or "floor" as DannyMuddyCarpet. The printed name of DannyMuddyCarpet is "carpet". The description of DannyMuddyCarpet is "The carpet is holding up, but it's rolling back in places. You resolve to never use that company again.." Understand "backing" or "curls" or "corners" as DannyMuddyCarpet.

Instead of taking the DannyMuddyCarpet:
	say "That's disgusting; you'd rather not touch that."

Some DannyBeigeWalls are a backdrop. Some DannyBeigeWalls are in DannyLobby. DannyBeigeWalls is scenery. Understand "beige" or "walls" or "wall" as DannyBeigeWalls. The printed name of DannyBeigeWalls is "walls". The description of DannyBeigeWalls is "The walls are holding up fairly well; probably need paint in a year."

Check going nowhere from DannyLobby when the noun is south:
	if OfficeRosalita is in DannyRosasOffice:
		say "It's not time to go yet." instead;
	otherwise:	
		say "'But she was gone, Detective. And that's what happened.'";
		try switcherooing cop instead;

Section 3 - Hallways

DannyHallway is a room. The printed name of DannyHallway is "Hallway". The description of DannyHallway is "You're just outside the studio door, by Chuck's old office. Still stings each time you walk by. The On-Air light is turned off. Your office and Rosa's are to the [bold type]north[roman type]. You can also go [bold type]south[roman type] to return to the lobby."

DannyMuddyCarpet is in DannyHallway. DannyBeigeWalls are in DannyHallway.

A DannyBlueDoor is a closed openable door. DannyBlueDoor is west of DannyHallway and east of DannyChuckOffice. The printed name of DannyBlueDoor is "blue door". DannyBlueDoor is scenery. The description of DannyBlueDoor is "Chuck's door is banged up and scratched from the movers. You shake your head. Another company you're never hiring again. His name plate is empty.". Understand "battered" or "pleasant" or "blue" or "door" or "gouges" or "deep" or "scratches" or "window" as DannyBlueDoor. 

Instead of going west from DannyHallway:
	say "You don't want to see Chuck's empty office."

The DannyChuckPlate is part of the DannyBlueDoor. The DannyChuckPlate is scenery. The printed name of DannyChuckPlate is "name plate". Understand "empty" or "name" or "plate" or "nameplate" as DannyChuckPlate. The description of DannyChuckPlate is "The name plate is empty."

The DannyAirLight is scenery. The printed name of DannyAirLight is "On-Air light". Understand "on" or "air" or "light" or "on-air" or "red" as DannyAirLight. The DannyAirLIght is in DannyHallway. The description of DannyAirLight is "The light is off, because your show is over.".

The DannyStudioDoor is a door. Understand "door" or "Green" or "studio door" as dannystudiodoor. The DannyStudioDoor is scenery. The DannyStudioDoor is east of DannyHallway and west of DannyStudio. DannyStudioDoor is closed. The printed name of DannyStudioDoor is "studio door". The description of DannyStudioDoor is "This door is especially thick to keep the sound in the studio. It was quite expensive."

DannyHallway2 is north of DannyHallway. The printed name of DannyHallway2 is "End of Hallway". The description of DannyHallway2 is "This hallway always smells like Rosalita; she loves Chanel No. 5. Her office lies to the [bold type]north[roman type]. Her plain hazel wood door is not nearly as impressive as your bright red door to the [bold type]west[roman type]. The closet to the east is the receptionist's."

DannyMuddyCarpet is in DannyHallway2. DannyBeigeWalls are in DannyHallway2.

The Dannydarkreddoor is a closed openable door. The printed name of Dannydarkreddoor is "hazel door". The Dannydarkreddoor is scenery. The Dannydarkreddoor is north from DannyHallway2 and south from DannyRosasOffice. Understand "cherry" or "dark" or "brown" or "door" or "wood"  or "cherry wood" as the Dannydarkreddoor. The description of the Dannydarkreddoor is "The door to Rosalita's office is made of smooth hazel wood."

Instead of touching Dannydarkreddoor:
	say "It's soft and pleasant to the touch."

The DannyRosaPlate is part of the DannydarkredDoor. The DannyRosaPlate is scenery. The printed name of DannyRosaPlate is "Rosalita's name plate". Understand "Rosalita" or "Rosa" or "name" or "plate" or "nameplate" as DannyRosaPlate. The description of DannyRosaPlate is "The name plate says 'Rosalita Morales'."

DannyDannysdoor is a closed, openable door. DannyDannysdoor is scenery. DannyDannysdoor is east from DannyDannyOffice and west from DannyHallway2. The description of DannyDannysdoor is "You crafted this oak door by yourself. It took a week, but you're happy with the results, and with the nice red stain.". The printed name of DannyDannysdoor is "plain door". Understand "plain" or "wooden" or "oak" or "solid" or "door" or "red" as DannyDannysDoor.

The DannyDannyPlate is part of the DannydannysDoor. The DannydannyPlate is scenery. The printed name of DannydannyPlate is "Danny's name plate". Understand "Danny" or "name" or "plate" or "nameplate" as DannyDannyPlate. The description of DannyDannyPlate is "The name plate says 'Daniel Kostopolous'."

Dannyclosetdoor is a closed, openable door. DannyClosetdoor is scenery. DannyClosetdoor is west from DannyCloset and east from DannyHallway2. The description of DannyClosetdoor is "Cindy keeps all of her stuff in this closet; at least she doesn't leave it everywhere like the last receptionist.". The printed name of DannyClosetdoor is "closet door". Understand "heavy" or "yellow" or "closet" or "door" or "janitor" or "stencil" or "white" or "frame" or "tall" or "narrow" as DannyClosetDoor. DannyClosetDoor is locked. DannyCloset is blocked.

Section 6 - Danny's Office

The printed name of DannyDannyOffice is "Your Office". The description of DannyDannyOffice is "Your spacious office is neat and tidy; the receptionist must have cleaned it up. Bookshelves line two walls, stuffed with overflow from Chuck's office. Your heavy desk sits in the middle of the room with a black swivel chair pushed close to it. The large window that you pushed hard for stretches across one wall, and your soft carpet cushions your feet."

The DannyDannyWindow is in DannyDannyOffice. DannyDannyWindow is scenery. The printed name of DannyDannyWindow is "window". Understand "window" or "large" as DannyDannyWindow. The description of DannyDannyWindow is "You begged, threatened, and cajoled Rosalita into getting this window.";

DannyDannyCarpet is in DannyDannyOffice. DannyDannyCarpet is scenery. The printed name of DannyDannyCarpet is "soft red carpet". Understand "soft" or "red" or "carpet" or "matted" or "ragged" or "cleaned" or "recently" or "ragged" or "torn" as DannyDannyCarpet. The description of DannyDannyCarpet is "This carpet is top notch; you spent quite a bit of money to get it installed. It may be wearing out a bit, but you wouldn't change it for the world.".

DannyDannyCeiling is in DannyDannyOffice. DannyDannyCeiling is scenery. The printed name of DannyDannyCeiling is "plaster ceiling". Understand "plaster" or "textured" or "ceiling" as DannyDannyCeiling. The description of DannyDannyCeiling is "The plaster ceiling is textured the same way that your mother's house is.";

DannyDannyChair is in DannyDannyOffice. DannyDannyChair is scenery. The printed name of DannyDannyChair is "office chair". Understand "office" or "chair" or "wooden" or "simple" as DannyDannyChair. DannyDannyChair is a supporter. DannyDannyChair is enterable. The description of DannyDannyChair is "You carved this chair out a tree you cut down in your own backyard."

DannyBeigeWalls are in DannyDannyOffice.

Some DannyOfficeShelves are in DannyDannyOffice. The printed name of DannyOfficeShelves is "bookshelves". Understand "rows" or "row" or "book" or "books" or "bookshelf" or "shelf" or "shelves" or "sturdy" or "bookshelves" as DannyOfficeShelves. The DannyOfficeShelves are scenery. The description of the DannyOfficeShelves is "Your shelves are full of Chuck's leftover radio kitsch. There's hardly room for your stuff: an old crystal radio set, stacks of tapes, rows of sleeved vinyl, histories of America, patriotic magazines, and other objects lost in the gloom." Understand "old" or "crystal" or "radio" or "set" or "tapes" or "vinyl" or "histories" or "patriotic" or "magazine" or "magazines" or "america" or "history" or "overflow" or "objects" as DannyOfficeShelves. [expand this later]

DannyRadioMemorabilia is scenery. DannyRadioMemorabilia is in DannyDannyOffice. The printed name of DannyRadioMemorabilia is "kitsch." Understand "radio" or "kitsch" or "radium" or "radium-painted" or "KOLR" or "clock" or "posters" or "rolled-up" or "keychain" or "keychains" or "memorabilia" as DannyRadioMemorabilia. The description of DannyRadioMemorabilia is "A faintly glowing radium-painted KOLR clock, a few rolled-up posters, KOLR keychains, and so on."

Instead of taking the DannyRadioMemorabilia:
	say "You'll get the receptionist to sort through it later."

Instead of reading the DannyOfficeShelves:
		say "You flip through some of your books.";

A DannyDannysFile is in DannyDannyOffice.  "The cabinet with your documents is here.[if FraudInvestigated is 0] All you really need is the report, though.[end if]". The printed name of DannyDannysFile is "filing cabinet". Understand "filing" or "cabinet" or "filing cabinet" as DannyDannysFile. The description of DannyDannysFile is "You keep all of the financial records in here. You can [bold type]look up[roman type] any files by name.[if FraudInvestigated is 0] You have many files, but the annual report is your main concern.[otherwise] Now that you've reviewed the annual report, it's time to talk to Rosalita.[end if]". 

Instead of taking the DannyDannysFile:
	say "You don't need to take the whole thing, just the report."

Before opening the DannyDannysFile:
	try examining the DannyDannysFile instead;

FraudInvestigated is a number that varies. FraudInvestigated is 0.

A thing called the charity file is in DannyDannyOffice. The charity file is scenery. Understand "roses" or "rehab" as charity file.

Instead of taking the charity file:
	try infoing the charity file;

A thing called the bank reconciliation is in DannyDannyOffice. The bank reconciliation is scenery. Understand "roses" or "rehab" as bank reconciliation.

Instead of taking the bank reconciliation:
	try infoing the bank reconciliation;

A thing called the annual report is in DannyDannyOffice. The annual report is scenery.

Instead of taking the annual report:
	try infoing the annual report.

After infoing the annual report:
	if FraudInvestigated is 0:
		now FraudInvestigated is 1;
		if DannyBroke is 0:
			say "With the information in mind, you feel more comfortable talking to Rosalita.";
		otherwise:
			say "How is this possible? You have to talk to Rosalita.";
	try delivering Records;

Instead of examining the annual report:
	try infoing the annual report.

Records is a quip. The preview of Records is "All receipts and files for KOLR were accounted for last year."

DannyResponse of Records is "'Mr. Kostopoulos, can you tell me about the record-keeping system you used for the studio?'[if hidden is not carried by the player]

He says, 'Yes, the idea was that we kept all records on paper. All original receipts were filed away, as well copies of all expenditures, which were recorded in a log which was also filed away. It's a good system.'

'Is it though? What would you do if you found errors, something incorrect?'

He shakes his head. 'Not going to happen.'[otherwise]

'Does it matter? Obviously it didn't work. We were supposed to keep absolutely everything in the files.'"

DannySummary of Records is "Danny said that everything was supposed to be filed away."

ChuckResponse of Records is "You ask, 'Mr. Lee, were you involved in the accounting for the studio?'

He pauses before answering. 'The accounting...well, the accounting...I'm sorry, I've just been thinking about Rosalita so much it's hard to think of anything else. What was the question again? Was I involved in it? No, Not at all, officer. I was never involved in the business side of KOLR.'

'Do you know anything about the filing system?' you ask.

He concentrates, furrowing his brow. 'The filing system...I'm sorry, I'm just not much of an accountant. Danny talked about his system a lot. He was always paranoid that he'd lose a file and get audited, or something. Maybe you should talk to him.'"

ChuckSummary of Records is "Chuck said he didn't know much about the accounting."

CindyResponse of Records is "[if Fraud is MeihCindy]'Ms. Jalcek, were you involved in the accounting for KOLR?'

Cindy stares at you silently. She answers, ''Rosalita liked to take care of those details by herself.'[otherwise]'Were you involved in keeping the records for Roses for KOLR?'

'Yes, of course, we did all of it together, me and Rosalita.[end if]'"

CindySummary of Records is "Cindy said that Rosalita was the primary record keeper for the studio."

MaiaResponse of Records is "'Your sister had a strict filing system set up. Did you ever work on it?'

Maia laughs. 'Rosalita wouldn't let me get anywhere near the files. She thought I'd try to take a piece of the pie.'

'Did you?'

She frowns. 'Of course not.'"

MaiaSummary of Records is "Maia said she wasn't involved with the records."

ChiefResponse of Records is "'Chief, I've been investigating their record system. It seems they try to keep a tight ship.'

'That sounds about right, Hector. I've seen a couple of big businesses around here come into the DA's office for questioning just for a few missing documents around tax time.'"

ChiefSummary of Records is "The Chief thinks that big businesses generally have accurate records."

DannyDannysDesk is in DannyDannyOffice. The printed name of DannyDannysDesk is "your desk". DannyDannysDesk is scenery. DannyDannysDesk is a supporter. Understand "large" or "massive" or "carved" or "leg" or "legs" or "vintage" or "classic" or "desk" as DannyDannysDesk. The description of DannyDannysDesk is "Your desk is a classic; it cost you two months salary to convince the previous owner to give it up."

DannyDannyLamp is scenery. DannyDannyLamp is in DannyDannyOffice. Understand "floor" or "lamp" as DannyDannyLamp. The printed name of DannyDannyLamp is "floor lamp". The description of DannyDannylamp is "Your metal floor lamp hides in the corner. It is turned off."

Instead of switching on DannyDannyLamp:
	say "You like it off."

DannyDannysWindow is scenery. DannyDannyswindow is in DannyDannyOffice. The printed name of DannyDannyswindow is "window". The description of DannyDannyswindow is "The pale February light peeks in through the windows."

Understand the command "read" as something new. 

Understand "read [something]" as reading. Reading is an action applying to one thing

Carry out reading:
	try examining the noun.
	
Section 7 - Rosalita's Office

The printed name of DannyRosasOffice is "Rosalita's Office".

The description of DannyRosasOffice is "This huge office sprawls across the north side of the building. You offered it to her when you moved here. A tall desk chair upholstered with crimson leather dominates the room behind an art deco desk. Bookshelves crawl up the walls, illuminated by giant windows with burgundy drapes.[if talkinghelpcounter is 0]
	
	[bracket]Use TALK TO [bracket]someone[close bracket] to talk during flashbacks.[close bracket][end if]".

Some OfficeShelves are in DannyRosasOffice. Some OfficeShelves are a backdrop. OfficeShelves are scenery. The printed name of OfficeShelves is "bookshelves". Understand "bookshelves" or "bookshelf" or "books" or "shelf" or "shelves" or "illuminated" as OfficeShelves. The description of OfficeShelves is "Bookshelves line the walls, filled with the sort of books that look good but serve little purpose."

Some drapes are in DannyRosasOffice. The drapes are scenery. The description of the drapes is "The light through the drapes is weak and gray, and the sound of raindrops striking glass comes through.". Understand "windows" or "window" or "burgundy" or "giant" as drapes.

A ArtDecoDesk is in DannyRosasOffice. A TallDeskChair is in DannyRosasOffice. ArtDecoDesk is scenery. Understand "desk" or 
"art" or "deco" as ArtDecoDesk. The printed name of ArtDecoDesk is "desk".  The printed name of TallDeskChair is "crimson chair". TallDeskChair is scenery. Understand "tall" or "imposing" or "chair" or "red" or "crimson" as TallDeskChair. The description of TallDeskChair is "A tall, imposing crimson chair that Rosalita bought two years ago. "

The TallDeskChair is a supporter. The TallDeskChair is enterable.

Instead of entering the TallDeskChair:
	say "You don't want to sit in Rosalita's chair.".

The description of ArtDecoDesk is "The desk is polished and gleaming. Rosalita has carefully locked away all of her files, but a blue vase of flowers lies on the desk with a note attached."

The vase is on the ArtDecoDesk. The description of vase is "A slender blue vase with a potpourri of colors, filled with a bouquet of flowers. A note is attached." Understand "slender" or "blue" or "potpourri" or "colors" as the vase.

Instead of taking the vase:
 say "You don't want Rosalita chewing you out for snooping around. Look, but don't touch."

The printed name of the vase is "[one of]vase of flowers with a note[or]vase of flowers with a note that draws your attention[stopping]".

Instead of touching the vase:
 try taking the vase.

Understand "flowers" as the vase.

The note is on the vase. The note is scenery. The description of the note is "A small note is attached to the flowers. It says:

'[italic type]Thanks for dinner. Let's do it again some time.

-Chuck'[roman type]";

Instead of taking the note:
 try taking the vase.

After examining the note for the first time:
 try delivering Flowers;

OfficeRosalita is a person. Understand "Rosalita" or "Rosa" as OfficeRosalita. The printed name of OfficeRosalita is "Rosalita".

[Danny sees Rosalita as Josefina Mota]

OfficeRosalita is in HoldingCell. "Rosalita is wearing a muted pink dress with a black suit jacket. A large carnation is pinned to her lapel. Her brown hair dips low over one eye and is pulled into a low ponytail behind her. The creases under her eyes and at the corners of her mouth are deeper than they were a year ago.". The description of OfficeRosalita is "Rosalita is dressed in a pink and black outfit. She has bags under her eyes that foundation can't conceal, and worry lines mark her brow." Understand "pink" or "black" or "carnation" or "ponytail" or "jacket" or "lapel" or "worry" or "lines" or "foundation" or "brow" or "bags" or "bag" or "line" or "crease" or "corner" or "creases" or "corners" as OfficeRosalita.

Troubled is a quip. The preview of Troubled is "Rosalita was troubled by something when Danny spoke to her last."

DannyResponse of Troubled is "'Why do you think Rosalita was upset when you spoke with her last?'

'I'm not sure. She's been under a lot of pressure recently, you know. Advertising revenue is down, Chuck is leaving, we're all getting older. My guess is she was broken up about that loser Chuck.'

'You said revenue is down?'

Danny says, 'Yeah, it's TV, right? TV is more popular these days, you get to see the cereal or the soap flakes or the new toy. Drives down the demand for radio ads.'"

DannySummary of Troubled is "Danny guessed that she was troubled over Chuck leaving."

ChuckResponse of Troubled is "'Chuck, Danny says that Rosalita seemed unusually troubled the day before she died.'

Chuck sighs in exasperation. 'Danny loves to gossip, doesn't he? Well, I don't mean to be arrogant, but she was probably still upset that I was leaving. I mean, she clearly still had feelings for me. And she always hated WESL.'

You say, 'And WESL is the television station you've joined?'

'What? Yes, Rosalita hated their style, called it [']sensationalist[']. But a job's a job. I strongly suspect that that was the cause of Rosalita's tension.'"

ChuckSummary of Troubled is "Chuck believes that Rosalita was upset because of him."

CindyResponse of Troubled is "'Danny said that Rosalita was very upset the day before she died. She wouldn't tell him what it was about.'

'Yeah, she's been-I mean she had been preoccupied at work recently.'

'Do you know why?'

'I'm not sure. It could be just about anything. Chuck leaving, the two of them getting back together, or-'

[if tryst is MeihCindy]'Excuse me, did you say that her and Chuck were getting back together?'[otherwise]'Them getting back together?'[end if][if tryst is CindyGiven]

'Yeah, you said Chuck admitted to it, remember?'[otherwise if flowers are MeihCindy]

She blushes. 'Oh, gosh, I wasn't supposed to....listen, I saw some flowers, okay? On her desk. When I was cleaning, the day before she died. They had a note from Chuck. I don't remember what it said, but basically, you know, it seems like they hooked up.'[otherwise]

'Yes, like I said earlier, I saw the flowers and the note, and I could tell that Rosalita was nervous.'[end if]"

CindySummary of Troubled is "Cindy thinks it's because Chuck and Rosalita got back together."

Quips can be DannySaid or CindySaid. Flowers are DannySaid.

After speeching Troubled to Cindy:
	if Flowers is not carried by the player:
		now Flowers is CindySaid;
		try delivering Flowers;

MaiaResponse of Troubled is "'Danny says that Rosalita seemed especially troubled the day before she died.'

She nods. 'Yeah, she really did seem to have something on her mind. That morning she died, she was really rude to me.' 

Your expression gives her pause. 

She says, 'Listen, I know she's dead, but I'm just trying to tell you what happened. She was more upset than usual. And it wasn't just before she died, she was troubled for a long time.' 

'Do you know what was troubling her?'

'Something financial, I think. I don't know for sure, though.'"

MaiaSummary of Troubled is "Maia said that Rosalita had been troubled for some time about finances.".

ChiefResponse of Troubled is "'Danny said Rosalita seemed troubled recently.'

'Troubled how?' asked the chief.

'He said she was upset and distracted, and wanted to talk to Danny about something.'

'Did anyone else notice that her behavior seemed odd? I was told that her sister talked to her the day she died; what did she have to say?'

'I'll ask her about it.'

'Good.'";

ChiefSummary of Troubled is "The chief suggested talking to Maia more."


Volunteer is a quip. The preview of volunteer is "Rosalita told Danny she was at the shelter Wednesday night."

DannyResponse of Volunteer is "'Did Rosalita give you any more information when she said she visited the shelter?'

[if absent is MeihDanny]'No, but she's told me she spends a lot of time there. She likes to disburse some of the cash, help some of the families. She says she loves it there.'[otherwise]'No, but she she always used to tell me she was there. Now I don't know, after what you told me about her and Chuck. Something's just not right there.'"

DannySummary of Volunteer is "Rosalita always told Danny about her visits to the shelter."

ChuckResponse of Volunteer is "You say, 'Danny says Rosalita was volunteering at the shelter last week.'

Chuck yawns and stretches his arms before answering. 'I'm sorry,' he says. 'I didn't sleep well last night. Anwyay, [if absent is ChuckGiven]that's obviously not true; you should tell him I was with her that night. Figure out why he's lying to you. She's a good person, you know? But she wasn't doing this.'[otherwise]Rosalita really did seem to care about her charity. She was a good woman.' He stares at the floor. 'I wish she was still alive. I can't stop thinking about it.'[end if]"

ChuckSummary of Volunteer is "Chuck said that Rosalita was a good person."

CindyResponse of Volunteer is "'Rosalita told Danny she went to volunteer at the shelter last Wednesday night.'

[if Absent is MeihCindy]Cindy nodded. 'She didn't tell me what she was up to, but she loves going to the shelters. [otherwise]Cindy frowned. 'But you said that wasn't true... I mean, it's not like she hates the shelters, though. [end if]She likes to connect with the people in rehab, tell them her own rehab story.'

'Rosalita went to rehab?'

'Yeah, years ago, when she was a kid. Alcoholism. Makes for a great motivational story, made me feel motivated, even. It's part of the reason I signed up for the secretary job.'"

CindySummary of Volunteer is "'Cindy said that Rosalita went through rehab years ago.'";

MaiaResponse of Volunteer is "'I heard that Rosalita volunteered at a shelter last Wednesday.'

Maia rolls her eyes. 'Rosalita doesn't care about the shelters.'

You look at her, and ask, 'What do you mean?'

She leans forward. 'She just likes the attention. She made up some phony story about being in rehab as a kid, and people just lap it up. It makes me sick.'

You say, 'That's interesting.'"

MaiaSummary of Volunteer is "Maia said that Rosalita doesn't care about the shelter."

ChiefResponse of Volunteer is "'According to Danny, Rosalita was volunteering at a shelter on Wednesday night.'

[if Absent is MeihChief]'She's known as real philanthropist around the community. My wife attended one of her seminars; she's a great speaker.'[otherwise]'Yeah, well he should have talked with Chuck first, right?' He chuckles humorlessly. 'I've heard a lot of lies before to cover up a nighttime visit, but not volunteering at rehab. That's interesting though, you know? She's known as real philanthropist around the community. My wife attended one of her seminars; she's a great speaker. Didn't know she'd lie about it, though.'"

ChiefSummary of Volunteer is "The Chief said that Rosalita was known as a philanthropist."

Flowers is a quip. The preview of Flowers is "Chuck sent Rosalita flowers the day after they talked."

DannyResponse of Flowers is "[if flowers are DannySaid]'Tell me again about the flowers.'[otherwise]'Cindy Jalcek says that Chuck and Rosalita got back together; she saw a note from him on some flowers.[end if]

'[if flowers are CindySaid]Sure, I know what she's talking about. [end if]I came in on Rosalita, red-eyed, sad, and of course I look over and see flowers with a note from good old Chuck.' Danny shakes his head. 'It kills me to know that the day before she died she spent cut up about that loser.'

'Did he often send flowers to Rosalita?'

'When they were married, sure, it was his signature smooth move. But they haven't been together for a long time.'";

DannySummary of Flowers is "Danny confirmed his account.";

ChuckResponse of Flowers is "'Chuck, [if Flowers are DannySaid]Danny[otherwise]Cindy[end if] saw the flowers that you sent Rosalita last Thursday. She had them on her desk.'

Chuck narrows his eyes. 'Yes, I sent Rosalita flowers. What's your point?'

You say, 'I'm just asking some basic questions, Mr. Lee, there's no need to get upset.'

He taps his fingers on his leg, and says 'Let's talk about something more useful.'";

ChuckSummary of Flowers is "Chuck admitted giving flowers to Rosalita.";

MaiaResponse of Flowers is "'Chuck gave flowers to Rosalita on Thursday, thanking her for letting him stay Wednesday night.'

She looks put out. 'I wasn't aware that they were still seeing each other.' Her face saddens. She says, 'It must be especially hard for him, then, her death.'

You are silent.

'Maybe I should talk to him...'"

MaiaSummary of Flowers is "Maia didn't know that Chuck and Rosalita had been getting back together."

CindyResponse of Flowers is "[if flowers is CindyGiven]So you said that Rosalita had flowers from Chuck on her desk?[otherwise]'Danny said that Chuck had flowers delivered to Rosalita last Thursday.'[end if]

She says, 'Yes... Listen, Rosalita didn't want me talking about her relationships with anybody. I know it's important for the investigation and all, but I just don't want to talk about Rosalita and Chuck. Honestly, those two were back and forth with each other all the time. Getting together, breaking up, gifts, arguments, it just never stopped.'

'But you think they were most recently together?'

'That's a weird way of phrasing it, but yes, that's what I think.'"

CindySummary of Flowers is "Cindy said that Rosalita and Chuck had an on again/off again relationship."

ChiefResponse of Flowers is "'Chuck had flowers delivered to Rosalita's office the day before her death.'

'That's odd; I was under the impression that they were arguing because of Chuck's departure. Maybe he was trying to make things up to her.'

'The note implied that they had spent the night together.'

'Well, that changes things. Have you asked Chuck about this?'"

ChiefSummary of Flowers is "The chief suggested talking to Chuck."

OfficeCounter is a number that varies. OfficeCounter is 0.

OfficeTime is a recurring scene. OfficeTime begins when OfficeCounter is 1. OfficeTime ends when the current action is talking to OfficeRosalita.

Instead of talking to OfficeRosalita:
	say "";

Report going north from DannyHallway2:
	now OfficeCounter is OfficeCounter plus one.

When OfficeTime begins:
	now OfficeCounter is OfficeCounter plus one;

DannyBroke is a number that varies. DannyBroke is 0;

ChanceFlag is a number that varies. ChanceFlag is 0.

Before switcherooing Danny:
	now MaiaRosaCounter is 0;
	now OfficeRosalita is in DannyRosasOffice;
	now ChanceFlag is 0;
	now FraudInvestigated is 0;
	now the dannydarkreddoor is closed;
	[now Danny's door is closed;]
	now OfficeCounter is 0;

LastChance is a recurring scene. LastChance begins when ChanceFlag is 1. LastChance ends when the time since LastChance began is 2 minutes.

Before doing anything during LastChance:
	if persona is Danny:
		say "You pause." instead;

Check going south during OfficeTime:
	if persona is Danny:
		say "You pause.";
		try talking to OfficeRosalita instead;

Check going south during LastChance:
	if persona is Danny:
		say "You pause.";
		try talking to OfficeRosalita instead;

When OfficeTime ends:
	if persona is Danny:
		say "[if DannyBroke is 1]Rosalita speaks first. 'Danny, I'm glad you're here. Have you had a chance to review the annual report?'

'Just finished looking over it.'

She stands and rubs her eyes. They look red. You ask, 'Rosalita, are you okay?' 

'It's nothing. It's just that it's getting harder each year to get advertisers, you know? Everyone's moving over to television.'

'You've got your cassete sales.'

She smiles wryly, but then the smile fades.

'Rosalita, listen,' you continue, 'If there's something going on, you can tell me.'

She shakes her head. 'I'm fine.' She walks to the door, and hesitates.[otherwise]Rosalita speaks first. 'Danny, I'm glad you're here. Have you had a chance to review the annual report?'

'Just finished looking over it.'

She stands and rubs her eyes. They look red. 'Rosalita, are you okay?' 

'It's nothing. It's just that it's getting harder each year to get advertisers, you know? Everyone's moving over to television.'

'You've got your cassette sales.'

She smiles wryly, but then the smile fades. 'Also, I'm exhausted. I had dinner with a friend yesterday, then spent the evening at the shelter on Broad Street. It was exhausting.'

'But you always say the charity work makes you happier, like you're a better person.' She shrugs

'Rosalita, listen,' you continue, 'if there's something going on, you can tell me.'

She shakes her head. 'I'm fine.' She walks to the door, and hesitates before going out. 'I'll file the report tomorrow,' she says, and she walks out of the room and down the hallway.[end if]";
		now the dannydarkreddoor is open;
		if DannyBroke is 0:
			try delivering Troubled;
			try delivering volunteer;
			now OfficeRosalita is in FinishedCell;
		otherwise:
			now ChanceFlag is 1;

When LastChance begins:
	now ChanceFlag is 0;

When LastChance ends:
	if persona is Danny:
		say "'Rosalita, I've checked the numbers. I know about Roses for Rehab.'
	
	She presses her lips together, and closes her eyes as tears run down her cheek. 'Oh Danny, I'm so glad you know. It's been torture.'
	
	'I can't say I really feel sorry for you, Rosalita. That money--people really thought they were doing something good with it, like it meant something. And you just took it.'
	
	'Danny, I know, I know, I know. Listen, I'm going to write them a check, later in the week. I'm going to give the money back... I can't sleep, I can hardly eat, it's been torture. '
	
	'Rosalita...'

	 I-I just-' She stands still for a moment, then gives you a hurried hug before rushing out the door and down the hallway.";
		now OfficeRosalita is in FinishedCell;
		try delivering Fraud;

FinishedCell is a room.

[Weather is a quip. The preview of Weather is "It was raining outside when Danny saw Rosalita.".

After examining drapes:
	try delivering Weather.]


DannyRosasOffice is a room. 

Check opening the dannydarkreddoor:
	if FraudInvestigated is 0:
		say "You should really check out that report before talking to Rosalita." instead;

Before going south from DannyRosasOffice:
	if OfficeRosalita is in DannyRosasOffice:
		say "You move to go, but you pause." instead;


Section 8 - Studio

After switcherooing Danny:
	say "'I'm Danny Kostopolous, the Voice of the Heartland. Join me tomorrow, when I'll present my top ten government agencies that need to be eliminated. Thanks for listening.'
	 
	You pause until your theme song finishes playing. The technician from the control room gives you a thumbs up. You switch off your microphone. 
	
	You stand up. [if DannyBroke is 0]You have a meeting with Rosalita to go over the annual report. You need to look over it first, though.[otherwise]You sigh. If your suspicions are true, you're going to have to confront Rosalita. But you need to be absolutely sure; the annual report is in your filing cabinet, where she can't get it.[end if]";	

The printed name of DannyStudio is "Studio". The description of DannyStudio is "This studio is the jewel of the station; a green-colored jewel, like an emerald, you suppose. The walls, floor, and ceiling are thickened and hollowed, covered with acoustic panels. The microphones hanging from the ceiling are vintage, from the golden era of broadcasting, when they made microphones right. You constructed the table in the center out of soft wood. The chairs, though, are just left over from the last tenants, years ago. The soundproof door to the [bold type]west[roman type] leads to the hallway. The control room is to the east, but the technician is still reviewing the show before broadcast."

The startroom of Danny is DannyStudio.

The DannyStudioWindows are in DannyStudio. The DannyStudioWindows are scenery. The printed name of DannyStudioWindows is "bank of windows". Understand "bank" or "control" or "room" or "window" or "windows" or "intricate" or "electronics" or "electronic" or "soundproof" or "darkened" or "racks" or "tangles" or "tangle" or "wire" or "wires" or "audio" or "console" as DannyStudioWindows.  The description of DannyStudioWindows is "The bank of soundproof windows cost a pretty penny. You see the sound tech inside, reviewing the recording."

The sound tech is a person. The sound tech is scenery. The sound tech is in DannyStudio. The description of the sound tech is "The tech waves at you from inside the booth.".

Instead of waving when the player is in DannyStudio:
	say "The tech waves back."

The DannyHardwoodFloor is in DannyStudio. The DannyHardwoodFloor is scenery. The description of  DannyHardwoodFloor is "The hardwood floor is raised up several inches to boost the acoustics, and is covered in green carpet. You supervised the construction yourself."

The DannyStudioWalls are scenery. The DannyStudioWalls are in DannyStudio. Understand "wall" or "walls" or "thicker" or "thick" as DannyStudioWalls. THe printed name of the DannyStudioWalls is "studio walls". The description of the DannyStudioWalls is "None of the walls are parallel; it he  with the echoes. The wall's acoustics are softened with green fabric-covered boards.".

Instead of touching the DannyStudioWalls:
	say "The wall feels smooth, while the boards feel soft.".

Instead of touching the DannyStudioBoards:
	say "The fabric is soft as you run your hand across it."

The DannyStudioBoards are scenery. The DannyStudioBoards are part of the DannyStudioWalls. Understand "fabric" or "covered" or "fabric-covered" or "board" or "boards" as DannyStudioBoards. The printed name of the DannyStudioBoards is "fabric-covered boards". The description of the DannyStudioBoards is "Soft green fabric covers a few scattered boards, deadening the sound in the room."

Instead of looking under the DannyHardwoodFloor:
	say "You know very well what it's like under there."

Instead of listening to DannyStudio:
	say "The perfect silence fills you with peace."
	
The DannyStudioTable is in DannyStudio. The DannyStudioTable is scenery. The printed name of the DannyStudioTable is "table". Understand "single" or "table" or "gouges" or "hardwood" or "indentations" or "gouges" or "scratches" or "surface" or "center" as DannyStudioTable. The description of DannyStudioTable is "This hardwood table stretches across much of the room, and took you over a week to build. It has chairs on both sides. The microphones droop from the ceiling to the table's center. You run your hand over the well-loved surface." The DannyStudioTable is a supporter.

The DannyStudioChairs are in DannyStudio. The DannyStudioChairs are scenery. The printed name of the DannyStudioChairs is "chairs". The DannyStudioChairs are plural-named. Understand "chair" or "chairs" as DannyStudioChairs. The description of DannyStudioChairs is "You bought these chairs from the last owners of this building. They've lasted a long time." The DannyStudioChairs are a supporter. The DannyStudioChairs are enterable.

Report entering DannyStudioChairs:
	say "You sit on a chair."

Instead of pushing the DannyStudioChairs:
	say "You don't need to push the chairs around."

Instead of turning the DannyStudioChairs:
	say "You swivel the chairs back and forth; they're pleasingly silent."

The DannyStudioCeiling is scenery. The DannyStudioCeiling is in DannyStudio. The printed name of the DannyStudioCeiling is "ceiling". Understand "acoustic" or "panels" or "panel" or "ceiling" as DannyStudioCeiling. The description of the DannyStudioCeiling is "The low ceiling is covered by green acoustic panels."

The DannyMicrophones are scenery.The DannyMicrophones are part of the DannyStudioCeiling. The printed name of the DannyMicrophones is "microphones". Understand "microphone" or "microphones" as DannyMicrophones. The description of DannyMIcrophones is "Microphones droop from the ceiling to the table, patiently waiting for their next use."

Instead of talking to DannyMicrophones:
	say "Your show is over."

Instead of using DannyMicrophones:
	say "Your show is over."

Instead of pushing DannyMIcrophones:
	say "You push the microphone. Its well-oiled mechanisms glide silently."

Instead of pulling DannyMicrophones:
	say "You push the microphone. Its well-oiled mechanisms glide silently.".

Instead of switching on the DannyMicrophones:
		say "Your show is over.";

The DannyControlDoor is scenery. The DannyControlDoor is in DannyStudio. The printed name of the DannyControlDoor is "control room door". Understand "control" or "room" or "door" or "slim" as DannyControlDoor. The description of DannyControlDoor is "This slim door leads to the control room, guarding the expensive equipment stored inside.".

Instead of opening the DannyControlDoor:
	say "It is locked."

Section 9 - Danny's Breaking scene

DannyRequired is a number that varies. DannyRequired is 0.

DannyBreaking is a scene. DannyBreaking begins when DannyRequired is at least 2.

[After speeching Fraud to Cindy:
	now CindyFear is CindyFear plus two;]

Before speeching Absent to Danny for the first time:
	now DannyRequired is DannyRequired plus one

Before speeching Missing to Danny for the first time:
	now DannyRequired is DannyRequired plus one

[Understand "hey" as heying. Heying is an action applying to nothing. 

 Carry out heying:
	now DannyBroke is 1;]

When DannyBreaking begins:
	now DannyBroke is 1;
	say "Danny furrows his brow and looks at the floor. He chews on his lip, then speaks. 'I wasn't being completely honest about my conversation with Rosalita. There's more that I need to tell you.'
	
	'Sounds like we need to go over your statement again.'";

Fraud is a quip. The preview of Fraud is "Rosalita Morales was defrauding her charity before she died."

DannyResponse of Fraud is "'Tell me again about Rosalita's charity.'

'Listen, I'll try to keep it simple for you. Roses For Rehab has fundraisers every year. It's a big deal around here, everyone gets in on it. But I checked the records, and the numbers just weren't adding up. I confronted Rosalita about it, and she confessed to stealing it, then ran off. That's the last I ever saw her.'

'Does anyone else know about this?'

'I don't know; Cindy did everything with Rosalita, maybe she would know.'

'I'm not sure she'll talk to me. She might deny working with Rosalita.'

'Did she tell you anything about it before?' he asks.";

DannySummary of Fraud is "Danny confirmed that Rosalita committed fraud."

ChuckResponse of Fraud is "'Are you aware that Rosalita committed fraud against Roses for Rehab?' you say.

[if Accomplice is MeihChuck]Chuck stands up. 'Excuse me?' He walks about nervously as you answer.

'Multiple sources have confirmed that she was underreporting the incoming donations and keeping the difference.'

Chuck looks like he's going to be sick. You go on. 'Chuck, I need to know if you were involved in this.'

He shakes his head vigorously. 'No, no, I was not involved in this at all.'

'Do you know who might be?'

'Maybe Cindy, Danny. People close to her, business-wise.'

'No one is going to admit to committing fraud,' you say.

He shakes his head and sits back down. 'If you know they worked with Rosalita, they can't deny it.'[otherwise]'I mean, you already told me about Cindy and Rosalita. I still can't believe it, and it's horrible, but it's the truth.'

'Can you give me any other information?' you say.

He doesn't answer, but slowly shakes his head."

ChuckSummary of Fraud is "Chuck denied any involvement in Rosalita's fraud."

CindyResponse of Fraud is "'[if accomplice is MeihCindy]I've been informed that Rosalita was stealing money from Roses for Rehab.'

Cindy goes as pale as a sheet. 'Oh, no...'

'Do you know anything about this?'

'No, no, no... I wasn't a part of this. I wasn't involved.'

Something tickles the back of your brain... something Cindy said earlier...
[otherwise]'Cindy, tell me everything you know about Rosalita's fraud.'

'There's just too much, Mr. King-I mean Detective King. I can show everything to the police, though; statements, receipts, checks, accounts. I'm sorry, it's just all a mess.'

She begins to sob, and continues for some time. You remain silent.";

CindySummary of Fraud is "Cindy described her involvement in the charity."

MaiaResponse of Fraud is "'Ms. Morales, we have discovered that your sister was, in fact, stealing money from Roses for Rehab.'

Maia sits quietly, the silence stretching out.

'Of course...'

'What do you mean? Do you know something about this?

'No...no, I don't think I do. Well...'

She looks thoughtful.

'Rosa was a lot more troubled this last year, like she had a burden on her shoulders. She bought brand new equipment for the recording studio, but wouldn't let me use it anymore. It all adds up,' she says.

She looks at you. 'Have you told the others about this? This changes everything.'

'I'm working on it; I need to figure out who was involved with her, though, and put two and two together.'

She nods, but her face is hard to read.";

[After speeching Fraud to Maia:
	if Confession is MaiaGiven:
		try delivering Blackmail.]

MaiaSummary of Fraud is "Maia says that she believes Rosalita defrauded the charity."

ChiefResponse of Fraud is "'Did you know that Rosalita Morales was stealing money from Roses for Rehab?'

The chief is silent.

'Chief?'

'Of course...they've been asking the county for more funding recently. I was on the board that denied them...we'd all seen the drives, and we figured they were just burning through the money.'

He pauses. 'This changes everything. See what the others know; this may be the key we needed.'

'I suspect that one of the other suspects knew about the fraud. I have a couple of leads, things they told me earlier.'

'That's good; put the facts together, and you can confront them.'"

ChiefSummary of Fraud is "The chief suggested asking all the others about the fraud."

AngerText is in HoldingCell. The printed name of AngerText is "[if DannyAnger is less than 3]Danny seems annoyed by this topic.[otherwise if DannyAnger is less than 5]Danny is clearly agitated, and this conversation is making him worse. Although he is trying to calm down.[otherwise if DannyAnger is less than 7]Danny is having trouble focusing, clenching and unclenching his hands. He stands up for a second, and sits down again.[end if]";

Chapter 8 - Chuck's Story

[Chuck's story is based on Aisle]

Section 1 - Basics and setup

ChuckResponse of greeting is "You extend your hand, and he pauses for a moment before shaking it. You say, 'Thanks for coming into the studio today, Mr. Lee. I'm Detective Hector King, and I've been assigned to investigate the death of Rosalita Morales.'

He nods absent-mindedly, then regains his focus and looks you in the eye. 'You can call me Chuck,' he says in a hoarse voice. He flashes his famous TV grin, but it doesn't reach his eyes.".

ChuckSummary of Greeting is "You have met Chuck Lee."

After speeching greeting to Chuck:
	try delivering Ex;

Check speeching a quip to Chuck:
	if the noun is Greeting:
		continue the action;
	otherwise if Greeting is MeihChuck:
		say "The man interrupts you. 'Do I know you?'";
		try speeching Greeting to Chuck;
		continue the action;

Instead of speeching Statement to Chuck:
	if greeting is MeihChuck:
		say "The man interrupts you. 'Do I know you?'";
		try speeching Greeting to Chuck;
	if Statement is MeihChuck:
 		say "'Tell me what you know about the death of Rosalita Morales.'

'All right. Here's what happened.'";
		add "[line break][line break]Chuck has already given you his statement." to history of Statement;
	otherwise:
		say "'Tell me your story again.'

'Fine.'";
	now StartRoom of cop is CopChuckOffice;
	try switcherooing Chuck instead;

Section 2 - Office packing

SearchCounter is a number that varies. SearchCounter is 0.

UpPacking is an action applying to one thing. Understand "pack [something]" or "pack up [something]" or "pack away [something]" as Uppacking.

Carry out Uppacking:
	if persona is not Chuck:
		say "There is nothing to pack.";
	otherwise:
		if the location of the player is not ChuckChuckOffice:
			say "There is nothing to pack out here.";
		otherwise:
			try inserting the noun into the packing box;
			
GoTime is a number that varies. GoTime is 0.

PackingTime is a recurring scene. PackingTime begins when GoTime is 1. PackingTime ends when SearchCounter is 3. 

When PackingTime ends:
	now GoTime is 0;
	say "Footsteps tread down the hallway, and a scent wanders into the room: Chanel No. 5, Rosalita's perfume. She must have stayed late; [if ChuckLIedOne is 0]you didn't expect her to be here tonight.[otherwise]you hoped she would be here tonight.[end if][line break]You step towards the door, then stop. The footsteps move on, and the smell fades.[line break]"

Report switcherooing Chuck:
	say "It's time to clean up. You have to get all of your personal effects out of this room before you can go home.";
	now ChuckBulletin is not retrieved;
	now RosaPicture is in HoldingCell;
	now ChuckBluedoor is closed;
	now ChuckBulletin is in ChuckChuckOffice;
	now KOLR Poster is in ChuckChuckOffice;
	now certificate is in ChuckChuckOffice;
	now DrivewayCounter is 0;
	now SearchCounter is 0;
	now GoTime is 1;

Report switcherooing cop:
	now ChuckBulletin is in ChuckChuckOffice;
	now KOLR Poster is in ChuckChuckOffice;
	now certificate is in ChuckChuckOffice;

ChuckChuckOffice is a room. The description of ChuckChuckOffice is "Your office is depressingly empty. The movers have packed away and taken out most of the furniture; your desk remains, shoved against the wall, and a single chair. Danny pressured you into giving him most of your KOLR memorabilia, leaving you almost nothing. The hallway and the long drive home are waiting for you to the [bold type]east[roman type][if searchcounter is less than 3], once you're finished here[end if]. Your rugged blue carpet is still dented from furniture legs. At least you left a mark on something." 

The packing box is in ChuckChuckOffice. "A packing box lies in the middle of the room." The packing box is a container. The packing box is open. The description of the packing box is "A large packing box for you to pack away your things."

Instead of taking the packing box:
	say "The movers are going to take it tomorrow.";

A thing can be chuckful or not chuckful. A thing is usually not chuckful.

After taking a chuckful thing:
	say "You pick up [the noun] to look at it more closely.";
	say "[line break][description of the noun][line break]";
	
Instead of examining a chuckful thing:
	if the noun is not in packing box:
		if the noun is not carried by the player:
			try taking the noun;
		otherwise:
			say "[description of the noun][line break]";
	otherwise:
		say "[description of the noun][line break]";
		
A ChuckBulletin is in ChuckChuckOffice. The ChuckBulletin is chuckful. The printed name of ChuckBulletin is "bulletin board". Understand "bulletin" or "board" as ChuckBulletin. The description of ChuckBulletin is "[if chuckliedone is 0]This bulletin board is covered with fragments of your time at KOLR: letters from listeners, photos of the studio, your first paycheck. They must have been important to you, once.[otherwise]You scan the board impatiently. It's covered with letters from listeners, photos of the studio, your first paycheck; later you'll take it all off and start a new one for WESL.[end if]". Understand "letter" or "letters" or "listener" or "listeners" or "photos" or "pictures" or "paycheck" or "leftovers" as ChuckBulletin.

Instead of reading ChuckBulletin:
	say "Your mind is occupied with someone else right now."

A KOLR poster is in ChuckChuckOffice. The KOLR poster is chuckful. The description of the KOLR poster is "[if chuckliedone is 0]You didn't let Danny bully you out of this one. This was the first poster printed after you joined the team. It proclaims 'Tune in to KOLR 99.8!' in large, day-glo letters at the top; the bottom has pop-art style portraits of you, Danny, and Rosa.[otherwise]You know that Rosa loved this poster; maybe she'll ask to see it in a week or two, and it'll break the ice between you. It proclaims 'Tune in to KOLR 99.8!' in large, day-glo letters at the top; the bottom has pop-art style portraits of you, Danny, and Rosa.[end if]".

After inserting KOLR Poster into packing box:
	say "You roll up the poster and set it in the box.";
	now searchcounter is searchcounter plus one;

Does the player mean examining the KOLR poster: it is likely.
	
Does the player mean taking the KOLR poster: it is likely.

Instead of taking something (called removee) when the removee is in the packing box:
	say "You need to pack, not unpack."

The certificate is in ChuckChuckOffice. The certificate is chuckful. The description of the certificate is "[if chuckliedone is 0]You wonder yet again if you should even bother to keep this. A certificate of merit for 15 successful years of service at KOLR. It was signed by Danny and Rosalita as the station co-owners, and by the mayor. Danny always liked involving local politicians; it stoked their egos.[otherwise]You look over the certificate and smile. A certificate of merit for 15 successful years of service at KOLR, signed by the mayor; this should impress your new colleagues at WESL.[end if]"

ChuckBulletin can be retrieved or not retrieved. ChuckBulletin is not retrieved.

The RosaPicture is in HoldingCell. The RosaPicture is chuckful. The printed name of RosaPicture is "picture". Understand "rosalita" or "rosa" or "picture" or "photo" as RosaPicture. The description of RosaPicture is "This a photo of the 'grand opening' of your renovated live room. You can pick out Danny in the corner; Rosalita's sister Maia is in the foreground, before she went by Andromeda. You and Rosalita sit together, your arm around her.[if chuckliedone is 0] Your eyes begin to tear up, looking at it.[otherwise] You smile.[end if]"

Instead of inserting the RosaPicture into the packing box:
	say "You go to pack it, but you hesitate. Better to keep this one with you."

Instead of taking ChuckBulletin:	
	say "You pick up [the noun] to look at it more closely.";
	say "[line break][the description of ChuckBulletin]";
	now the ChuckBulletin is carried by the player;
	if ChuckBulletin is not retrieved:
		say "[paragraph break]As you lift the bulletin board off the wall, a picture flutters to the ground.";
		now RosaPicture is in ChuckChuckOffice;
		now ChuckBulletin is retrieved;

Report inserting something into packing box:
	now searchcounter is searchcounter plus one;

After taking something when the noun is inside packing box:	now searchcounter is searchcounter minus one;

["This comfortable office was in use until recently, but whoever left it cleaned it out. Patches on the walls show where posters, picture frames and bulletin boards once hung. A door leads [bold type]east [roman type] to the hallway. A long desk has been shoved out of the way, and a single chair remains. The rough blue carpet is still dented from furniture legs." ]

The printed name of ChuckChuckOffice is "Your Office". StartRoom of Chuck is ChuckChuckOffice.

The ChuckChuckChair is in ChuckChuckOffice. ChuckChuckChair is scenery. ChuckChuckChair is a supporter. ChuckChuckChair is enterable. Understand "single" or "chair" or "cushion" or "cushions" or "rigid" or "blue" or "wooden" or "furniture" or "carved" or "uncomfortable" as ChuckChuckChair. The description of ChuckChuckChair is "You'll be glad to abandon this old chair; Danny carved it for you, and you could never tell him how uncomfortable it was." The printed name of ChuckChuckChair is "chair".

The ChuckLongDesk is in ChuckChuckOffice. ChuckLongDesk is scenery. The printed name of ChuckLongDesk is "long desk". The description of ChuckLongDesk is "This mahogany desk has been recently washed and scrubbed. Its warm colors are offset by its bare surface." Understand "long" or "desk" or "mahogany" or "warm" or "colors" or "bare" or "surface"  or "furniture" as ChuckLongDesk. ChuckLongDesk is a supporter.

The ChuckChuckWalls are in ChuckChuckOffice. ChuckChuckWalls are scenery. The printed name of ChuckChuckWalls is "walls". The description of ChuckChuckWalls is "The beige walls have lighter patches where something must of once hung. A clock, a poster, bulletin boards, frames, an unidentifiable triangular object all must have left their mark." Understand "wall" or "walls" or "clock" or "bulletin" or "boards" or "board" or "poster" or "frames" or "triangular" or "lighter" or "patches" or "unidentifiable" or "uto" or "object" as ChuckChuckWalls.

The ChuckBlueCarpet is in ChuckChuckOffice. The printed name of ChuckBlueCarpet is "blue carpet". The ChuckBlueCarpet is scenery. The description of the ChuckBlueCarpet is "The blue carpet scrapes against the bottom of your shoes. Heavy furniture, now absent or moved aside, has left indentations in the carpet." Understand "blue" or "rough" or "carpet" as ChuckBlueCarpet.

Section 3 - Hallway

ChuckHallway is a room. The printed name of ChuckHallway is "Hallway". The description of ChuckHallway is "You are standing across from the studio door. You look around at the narrow hallway, the dingy carpet, the patchy walls; nothing like the WESL offices you toured. The On-Air light is off, and the studio seems deserted. Danny and Rosa's offices are north, but the faint scent of Rosalita's perfume draws you [bold type]south[roman type] to the lobby."



ChuckMuddyCarpet is in ChuckHallway. ChuckBeigeWalls are in ChuckHallway.

A ChuckBlueDoor is a closed openable door. ChuckBlueDoor is west of ChuckHallway and east of ChuckChuckOffice. The printed name of ChuckBlueDoor is "office door". ChuckBlueDoor is scenery. The description of ChuckBlueDoor is "You feel somewhat embarrassed; you gouged your door earlier, helping the movers take the cabinets out. You see that your name plate has been removed.". Understand "battered" or "gouged" or "blue" or "door" or "gouges" or "deep" or "scratches" or "window" as ChuckBlueDoor. 

The ChuckChuckPlate is part of the ChuckBlueDoor. The ChuckChuckPlate is scenery. The printed name of ChuckChuckPlate is "name plate". Understand "empty" or "name" or "plate" or "nameplate" as ChuckChuckPlate. The description of ChuckChuckPlate is "Your name plate is empty."

The ChuckAirLight is scenery. The printed name of ChuckAirLight is "On-Air light". Understand "on" or "air" or "light" or "on-air" or "red" as ChuckAirLight. The ChuckAirLIght is in ChuckHallway. The description of ChuckAirLight is "The On-Air light is off.".

The ChuckStudioDoor is a door. The ChuckStudioDoor is scenery. The ChuckStudioDoor is east of ChuckHallway and west of ChuckStudio. ChuckStudioDoor is closed. The ChuckStudioDoor is locked. The printed name of ChuckStudioDoor is "studio door". The description of ChuckStudioDoor is "A thick, heavy green door with 'Studio' painted on it in white." Understand "studio" or "thick" or "heavy" or "green" or "door" as ChuckStudioDoor.

ChuckHallway2 is north from ChuckHallway. ChuckHallway2 is blocked.

Instead of going north from ChuckHallway:
	say "Everyone's gone for the day, including Rosalita; maybe you should see if you can catch her."

Check going to ChuckHallway:
	if SearchCounter is less than 3:
		say "You have to finish packing." instead;


Instead of smelling ChuckLobby:
	say "You smell Rosalita's perfume, leading you south."

Section 4 - Lobby  and parking lot

ChuckLobby is a room. The printed name of ChuckLobby is "Lobby". The description of ChuckLobby is "You look around the lobby for the last time. The stencil on the wall has almost faded off. The furniture Rosalita bought for this room is getting worn out. The muddy carpet is scuffed and worn, curling up on the edges. The old break room is to the west, and the bathroom to the east, but those are places you'd rather forget. The doorway to the north leads to the hallway, but you never plan on going back there again. The parking lot, and Rosalita, are to the [bold type]south[roman type]."

ChuckPrivatedoor is a closed openable door. ChuckPrivatedoor is scenery. Understand "bathroom door" or "bathroom" or "door" as ChuckPrivateDoor. The printed name of ChuckPrivateDoor is "bathroom door".  ChuckPrivatedoor is east from ChuckLobby and west from ChuckPrivatebathroom. The description of ChuckPrivatedoor is "A door with a handicap symbol." ChuckPrivateBathroom is blocked.

ChuckBreak is west from ChuckLobby. ChuckBreak is blocked.

The Chuckfluorescentlights are scenery. The Chuckfluorescentlights are in ChuckLobby. Understand "fluorescent" or "dingy" or "plastic" or "covers" or "lights" or "light" or "buzz" or "buzzing" or "flickering" as Chuckfluorescentlights. The description of Chuckfluorescentlights is "The fluorescent lights are screened by dingy plastic covers."

Instead of listening to the Chuckfluorescentlights:
	try examining the Chuckfluorescentlights;

Instead of touching Chuckfluorescentlights:
	say "It's too high!"

Instead of going north from ChuckLobby:
	say "You don't want to miss Rosalita." instead;

The ChuckStencil is in ChuckLobby. The ChuckStencil is scenery. Understand "faded" or "r" or "stencil" or "rust" or "beige" or "washed-out" or "blue" or "green" as ChuckStencil. The description of ChuckStencil is "'Welcome to KOL', it says, painted in rust and washed-out blue and green. The R is old and worn out, just like the station."

The ChuckFrontDesk is in ChuckLobby. The ChuckFrontDesk is scenery. The printed name of ChuckFrontDesk is "front desk". Understand "front" or "desk" as ChuckFrontDesk. The description of the ChuckFrontDesk is "You feel sorry for Cindy, having to put up with this desk, this lobby, this station. She's enthusiastic now, but another three years behind this particle board palace and she'll be just like you.". ChuckFrontDesk is a supporter. ChuckFrontDesk is not enterable.

The ChuckPhone is on ChuckFrontDesk. The ChuckPhone is scenery. The printed name of ChuckPhone is "telephone". The description of ChuckPhone is "The secretary's phone is here, but she isn't.". Understand "ivory" or "telephone" or "rotary" or "numbers" or "rubbed" as ChuckPhone.

The ChuckFrontChair is in ChuckLobby. The ChuckFrontChair is scenery. The printed name of ChuckFrontChair is "chair". The description of ChuckFrontChair is "This low black chair is pushed up close to the desk."

Instead of taking the ChuckFrontChair:
	say "That's not really necessary."
	
Instead of pushing the ChuckFrontChair:
	say "You're not here to rearrange furniture."
	
Instead of pulling the ChuckFrontChair:
	say "You're not here to rearrange furniture."

ChuckHallDoor is a door.  ChuckHallDoor is scenery. The printed name of ChuckHallDoor is "hallway door". Understand "door" or "black" or "window" or "glass" or "reinforced" or "metal" or "chips" or "paint" or "rusty" or "rust" or "hallway" as ChuckHallDoor. ChuckHallDoor is north of ChuckLobby and south of ChuckHallway. ChuckHallDoor is closed and openable. The description of ChuckHallDoor is "A black metal door with a reinforced glass window. Chips of paint have flaked off the edges, revealing patches of rust." ChuckHallDoor is not lockable.

Instead of listening to ChuckLobby:
	say "You hear a buzzing noise in the background."

Instead of smelling ChuckLobby:
	say "You smell Rosalita's perfume."

ChuckMuddyCarpet is a backdrop. ChuckMuddyCarpet is in ChuckLobby. ChuckMuddyCarpet is scenery. Understand "muddy" or "dingy" or "carpet" or "floor" as ChuckMuddyCarpet. The printed name of ChuckMuddyCarpet is "carpet". The description of ChuckMuddyCarpet is "You'll be glad to be rid of this carpet. It always looked like someone had raised a litter of puppies on it." Understand "backing" or "curls" or "corners" as ChuckMuddyCarpet.

Instead of taking the ChuckMuddyCarpet:
	say "That's disgusting; you'd rather not touch that."

Some ChuckBeigeWalls are a backdrop. Some ChuckBeigeWalls are in ChuckLobby. ChuckBeigeWalls is scenery. Understand "beige" or "walls" or "wall" as ChuckBeigeWalls. The printed name of ChuckBeigeWalls is "walls". The description of ChuckBeigeWalls is "The walls are a patchy shade of beige. You've enjoyed watching them slowly deteriorate over time."

Instead of going west from ChuckLobby:
	say "You've already cleared out your stuff from the break room."

Instead of going east from ChuckLobby:
	say "You don't have to go, and even if you did, that bathroom is absolutely filthy."

ChuckLiedOne is a number that varies. ChuckLiedOne is usually 0.

[Chuck sees Rosalita as Sofia Vergara]

ParkingTime is a recurring scene. ParkingTime begins when the player is in Parking Lot. ParkingTime ends when persona is cop.

When ParkingTime begins:
	say "As you walk out, you try to see Rosalita. You spot her getting into her car, at the south end. She is wearing a blue, short-sleeved dress and her long hair is parted in the middle. Her thick eyebrows are shaped in gentle curves and her large hoop earrings dangle as she walks. She looks the same as she did 20 years ago when you first met.

You walk towards her, but she gets in her car and leaves without seeing you.";
	try delivering speechless;

Parking Lot is south from ChuckLobby. The description of Parking lot is "The parking lot is mostly empty, like usual for a Wednesday. Brick walls hem you in on either side. You've never enjoyed the claustrophobic feeling you get out here. Your blue car is parked near the back, to the [bold type]south[roman type]. KOLR studios squats behind you to the [bold type]north[roman type]."

ChuckStudios is scenery. ChuckStudios is in Parking Lot. Understand "KOLR" or "studio" or "studios" as ChuckStudios. The printed name of ChuckStudios is "KOLR studios". The description of ChuckStudios is "KOLR studios, your home for years. A short, ugly building, and one you'll be glad to erase from your memory.".

Some cars are in Parking Lot. Cars are scenery. The description of cars is "A variety of cars dot the parking lot." Understand "rows" as some cars.

Some buildings are scenery. Some buildings are in Parking Lot. The description of buildings is "The brick walls of the buildings to either side hem you in." Understand "brick" or "wall" or "brick walls" as buildings.

Departing is an action applying to nothing. Understand "enter car" or "drive" as departing.

Carry out departing:
	if the player is in Parking Lot:
		try going south;
	otherwise if the player is in NearbyDriveway:
		say "You don't feel like leaving.";
	otherwise:
		say "There's nowhere to go."

GroceryCar is in Parking Lot. GroceryCar is scenery. Understand "blue" or "plain" or "car" or "my car" as GroceryCar. The printed name of GroceryCar is "your car". GroceryCar is scenery. The description of GroceryCar is "Your car is off to the south. It's an older model, painted a cerulean shade. It's free of dents and scrapes, but you've had to replace two tires and the windshield, and it's starting to break down."

Speechless is a quip. The preview of speechless is "Chuck claims he didn't have a chance to talk with Rosalita on Wednesday afternoon."

ChuckResponse of Speechless is "'So, Chuck, you didn't speak to Rosalita at all last Wednesday?'[if Chuckliedone is 0]

He rolls his eyes. 'Yes, that's what I told you. I didn't want to see her and she didn't want to see me.'

'But you knew she was in the building,' you ask.

'Yes.'[otherwise]

He gestures with his hands. 'No, like I said, I really did see her; I'm sorry I didn't just say that from the beginning.[end if]";

ChuckSummary of Speechless is "Chuck confirmed that he didn't see Rosalita."

DannyResponse of Speechless is "'Chuck claims that he saw Rosalita last Wednesday but did not talk to her.'

[if flowers is MeihDanny]'Well, it's not like Chuck to keep his mouth shut. But I'm glad he didn't bother her.'

'Bother her?'

'He's been really melodramatic recently, ever since he ditched us. I think he felt guilty for stabbing us in the back; I hope he does feel guilty.'[otherwise]'Is that so? How does he explain the fancy vase of flowers and the note he sent her? Why is he lying to you? I think you need to lean on him and lean on him hard.'[end if]";

After speeching Speechless to Danny for the first time:
	if flowers is DannyGiven:
		now DannyAnger is DannyAnger plus one;
		say "[angertext]";

DannySummary of Speechless is "Danny was not impressed with Chuck";

MaiaResponse of Speechless is "'Apparently Chuck [if tryst is carried by the player]said[otherwise]says[end if] he saw Rosalita last Wednesday, but didn't talk to her.[if tryst is carried by the player]Although now he's changed his story.[end if]'

'I don't really know anything about that. I wasn't there that day, and I haven't been able to talk to Chuck these last few days.'

'Have you been trying?'

She nods. 'Chuck is an old friend of the family; at least he's my old friend. And I though he might need some companionship. But, he's been ignoring all of my messages.'"

MaiaSummary of Speechless is "Maia is interested in Chuck."

CindyResponse of Speechless is "'Chuck claims that he saw Rosalita last Wednesday but didn't talk to her.'

[if flowers is CindySaid]'Well, those flowers he sent say something different, don't they?'[otherwise if Flowers is CindyGiven]'Well, those flowers he sent say something different, don't they?'[otherwise]'Makes sense; he was pretty torn up about leaving, and so was Rosalita.'[end if]

'Were you there that day?'

She says, 'I wasn't there when Chuck cleaned out his stuff; I think I was on my lunch break.'
";

CindySummary of Speechless is "Cindy said she wasn't there when Chuck left."

ChiefResponse of Speechless is "'Chuck claims he didn't talk to Rosalita when he cleaned up the office, but he knew she was in the building.'

'So Chuck's story is that he and Rosalita just happened to miss each other?'

'Not quite.  He implied that they were avoiding each other.'

'Does that match up with the other statements?' asks the Chief.

'I'll check it out.'"

ChiefSummary of Speechless is "The chief suggested looking into Chuck's claim through the use of the other statements."

Instead of going north from parking lot:
	say "You don't need to go back in." instead;

Check going nowhere from parking lot:
	if the noun is south:
		if ChuckLiedOne is 0:
			say "'And that's what happened.'";
			try switcherooing cop instead;
		otherwise:
			say "'You were right; I lied earlier. I followed Rosalita that day.'";
			now the player is in NearbyDriveway instead;
	otherwise:
		say "The buildings are impenetrable." instead;


Section 5 - Visiting Rosalita's house

DrivewayCounter is a number that varies. DrivewayCounter is 0.

NearbyDriveway is a room. The printed name of NearbyDriveway is "Nearby Driveway". MaiaHouse is in NearbyDriveway. The printed name of MaiaHouse is "[if persona is Chuck]someone's house[otherwise]your house[end if]". Understand "house" as MaiaHouse. MaiaHouse is scenery. The description of NearbyDriveway is "You've parked your car here in front of a house on Rosalita's street. The driveway stretches out from the house here. Rosalita's house, your old house, lies to the west, and a door to a stranger's house is to the [bold type]north[roman type]. Trees dot the yard.[if DrivewayCounter is 0]

You see Rosalita off to the [bold type]west[roman type], gathering her purse and her sunglasses. She hasn't seen you yet. You watch as she walks up the steps and into the house, closing the door.[end if]"

Every turn when the player is in NearbyDriveway:
	now DrivewayCounter is DrivewayCounter plus one;

Check going nowhere from NearbyDriveway:
	if the noun is north:
		say "You don't want to enter houses of people you don't know." instead;
	otherwise:
		say "You could wander around the neighborhood, but you want to see Rosalita, and she's off to the west." instead;

Some BeautifulTrees are scenery. Some BeautifulTrees are in NearbyDriveway. The description of some BeautifulTrees is "These trees are large, and cover the lawn. They're some kind of lighter tree, like aspen or maple. You're not sure; trees aren't your thing." The printed name of BeautifulTrees is "beautiful trees".

The forest green door is scenery. The description of the forest green door is "A beautiful forest-green door." The forest green door is in NearbyDriveway.

Driveway is west from NearbyDriveway.

The description of Driveway is "The street stretches behind you, and Rosalita's house looms before you. The steps up to the front door are freshly cleaned, and bright blue drapes frame the windows. Rosalita's car is parked in front of you.

Her front door is to the [bold type]north[roman type].".

Check going nowhere from Driveway:
	if the noun is north:
		try knocking FrontDoor instead;
	otherwise if the noun is inside:
		try knocking FrontDoor instead;
	otherwise:
		say "You could wander around the neighborhood, but you want to see Rosalita, and she's inside, to the north." instead;

ChuckCar is in NearbyDriveway. ChuckCar is scenery. The printed name of ChuckCar is "your car". The description of ChuckCar is "Your car is a cerulean shade of an older model. It's free of dents and scrapes, but you've had to replace two tires and the windshield, and it's starting to break down."

Windshield is in NearbyDriveway. Windshield is scenery. Tires are in NearbyDriveway. Tires are scenery. The description of windshield is "Your windshield has recently been replaced.". The description of tires is "Two of your tires are newer than the others."

Rosalita's car is in the Driveway. Rosalita's car is scenery. The description of Rosalita's car is "A gleaming baby blue sedan with a small amount of clutter inside."

CarClutter is in the Driveway. CarClutter is scenery. Understand "small" or "paper" or "scrap" or "gatorade" or "bottle" or "mostly-empty" or "empty" or "manila" or "envelope" or "cup" or "holder" or "center" or "small piece" or "script" or "small piece of paper" or "clutter" or "small amount" or "amount" as CarClutter. The printed name of CarClutter is "clutter". The description of CarClutter is "Peering through the window, you see a few recording tapes. A mostly-empty sports drink bottle is in the center cup holder, and a script for her next show is on the seat. You recognize it by the blue highlighter she uses to mark it up."

RosalitaStreet is in Driveway. ChuckHouse is in Driveway. RosalitaStreet is scenery. ChuckHouse is scenery. Understand "street" as RosalitaStreet. Understand "house" as ChuckHouse.

The description of RosalitaStreet is "The street stretches away in both directions. The atmosphere is peaceful and mild.".

The description of ChuckHouse is "Rosalita's house brings back memories. You still think about when you moved out."

FrontDoor is in Driveway. FrontDoor is scenery. Understand "door" or "front" or "knocker" or "doorbell" or "front door" as FrontDoor. The printed name of FrontDoor is "front door".

The description of FrontDoor is "A brass-handled door with cut-out windows. A doorbell is at the side. You're nervous, but you could knock, or ring the doorbell."

Does the player mean knocking FrontDoor:
		it is likely.

ChuckSteps are in Driveway. ChuckSteps are scenery. Understand "steps" or "stairs" or "cleaned" or "fresh" or "freshly" as ChuckSteps.

The description of ChuckSteps is "Freshly cleaned steps lead up to the front door."

ChuckDrapes are in driveway. ChuckDrapes are scenery. Understand "bright" or "blue" or "drapes" as ChuckDrapes.

The description of ChuckDrapes is "Bright blue drapes mask everything inside the windows."

ChuckWindows are in driveway. ChuckWindows are scenery. Understand "windows" or "window" as ChuckWindows.

The description of ChuckWindows is "The windows are masked by bright blue drapes." 

Tryst is a quip. The preview of Tryst is "Chuck spent all night with Rosalita last Wednesday.".

DannyResponse of Tryst is "'Apparently Chuck saw Rosalita and spent the night with her last week.'

Danny goes pale, but tries to hide it. 'That scum! How dare he break Rosalita's heart [italic type]again[roman type] by leaving, and then come back and use her-- why would she just--' He shakes his head.[if Hidden is MeihDanny]'The things I could tell you about her... but I wouldn't want to hurt the station's reputation.'[end if]";

DannySummary of Tryst is "Danny seemed quite upset by this news."
		
ChuckResponse of Tryst is "You ask, 'So you spent the whole night with Rosalita last week?'

'Last week? Oh, yes, yes I did,' he says.

'And you felt like hiding this fact?'

He slowly shrugs. 'I'm sorry officer, I know it looks bad. But listen, would I have killed her if we were just getting back together?'

'I don't know, Chuck. I hope for your sake that that's all you lied about.' He shrugs again."

ChuckSummary of Tryst is "Chuck admitted to lying about seeing her."

MaiaResponse of Tryst is "'Chuck said that he slept with Rosalita last week.'

Maia nods, and says 'I could see that.' She looks off at the corner. 'Sometimes I wish that I had met him first... You won't tell him I said that, right?'

'No, I don't think I will.'

[if Absent is MaiaGiven]She looks thoughtful. 'You know, I could have sworn Danny said she was somewhere else last Wednesday...'[otherwise]She says, 'Wait, didn't you tell me something about this a second ago? Rosalita told everyone she was at the shelter, right?[end if]'"

MaiaSummary of Tryst is "Maia wishes she had met Chuck before Rosalita."

CindyResponse of Tryst is "'Cindy, Chuck admitted that he spent the night with Rosalita last week.'

'I knew it. I just knew it.' She shakes her head. 'He...But I can't fault him. They were married for a long, long time. They probably still loved each other, you know?'

'Did you know anything about it?'

'I mean, I saw the flowers he sent her, but I didn't know they were [italic type]together[roman type]. But it doesn't surprise me. Although now I wonder what Danny's up to; he told me she was at the shelter last Wednesday.'"

CindySummary of Tryst is "Cindy wasn't surprised."

ChiefResponse of Tryst is "'Chuck admitted to spending the night with Rosalita two days before the murder.'

'That's a pretty serious event to keep hidden. Do the others know about this?'

'I think he kept it secret from his coworkers,' you say.

'I'd be shocked if Danny doesn't know something about it.'"

ChiefSummary of Tryst is "The chief suggested talking to Danny about it."

Knocking is an action applying to one thing. Understand "knock [something]" or "knock on [something]" or "ring [something]" as knocking.

Carry out knocking something:
	if the noun is FrontDoor:
		say "You knock on the door and Rosalita answers, with Cindy in the background turning to watch. A smile flashes on her face. 'Chuck!' she says, and runs to embrace you. She steps back. 'I wanted to see you before you go, but I...'
		
		'Yeah, I know. I wasn't going to come here, but I just wanted to say goodbye.'

	'We talked all afternoon, remembering old times. It felt good. Now, I would never share this on my own, but just to be completely frank, we spent the night together, and the next day I sent flowers. I'd prefer not to go in more detail.' 

	'And that's what happened.'";
		try delivering Tryst;
[		try delivering Housekeeper;]
		try switcherooing cop;
	otherwise if the noun is cindystudiodoor:
		say "It's soundproof; no one can hear you.";
	otherwise if the noun is a door:
		say "[if persona is cop]You don't need to knock while you're here.[otherwise]You're comfortable enough here that you never knock.[end if]";
	otherwise:
		say "You don't feel like that's the right thing to do in this situation.";

Chapter 9 - Cindy's story

Section 1 - Basics and setup

[Cindy's story is based on Shade, cleaning up some sand]

CindyFear is a number that varies. CindyFear is 0.

CindyResponse of greeting is "'I didn't catch your name earlier.'

Cindy says, 'Oh! Cindy. Cindy Jalcek.'

You say, 'My name is Hector King.' She nods. You say, 'I've been assigned to investigate the murder of Rosalita Morales, and I'd like to ask you some questions.'

She blanches. 'I've been wondering... am I under arrest?'

'No, I'm just gathering information from everyone here at the station.'

She nods, relieved. 'Okay, like I said, my name is Cindy Jalcek. I'm the receptionist here at KOLR. I also helped Rosalita with some of her other activities, including her charity, Roses for Rehab. I was responsible for accounting for the charity.'
".
CindySummary of Greeting is "You have met Cindy Jalcek."

After speeching greeting to Cindy:
	try delivering Secretary;
	try delivering Accounting;

Check speeching a quip to Cindy:
	if the noun is Greeting:
		continue the action;
	otherwise if Greeting is MeihCindy:
		say "She looks about nervously. 'I'm not sure what's going on today...'";
		try speeching Greeting to Cindy;
		continue the action;

CindyStatementGiven is a number that varies. CindyStatementGiven is usually 0.

Instead of speeching Statement to Cindy:
	if greeting is MeihCindy:
		say "She looks about nervously. 'I'm not sure what's going on today...'";
		try speeching Greeting to Cindy;
	if CindyStatementGiven is 0:
 		say "'Tell me what you know about the death of Rosalita Morales.'

'All right. Here's what happened.'";
		now CindyStatementGiven is 1;
		add "[line break][line break]Cindy has already given you her statement" to history of Statement;
	otherwise:
		say "'Tell me your story again.'

'Fine.'";
	now StartRoom of cop is CopBreak;
	try switcherooing Cindy instead;

Before switcherooing Cindy:
	now HouseDirty is 1;
	now FanTaken is 0;
	now FoundRosalita is 0;
	now CindyMicrowave is closed;
	now CindyFan is in CindyCloset;
	now CindyFan is not powered;
	now CindyClosetDoor is closed;
	now CindyOfficeNow is 0;
	now burnt popcorn is in CindyMicrowave;

FanTaken is a number that varies. FanTaken is 0.

FoundRosalita is a number that varies. FoundRosalita is 0.

[Image found at http://yozirindik.xyz/yellow-kitchen-ideas/yellow-kitchen-cabinet-white-windows-beige-wall-paint-yellow-bar-stool/]

Accounting is a quip. The preview of accounting is "Cindy did the accounting for Roses for Rehab".

The DannyResponse of Accounting is "'Mr. Kostopolous, what was Cindy Jalcek's relationship with Roses for Rehab?'

He says, 'Oh, Ms. Jalcek; well, I'm sure you want me to be as specific as possible, so...if I recall, she would take the donations over the phone and in person, file them away, and do quarterly internal auditing and budget reports.'

'So she was closely involved in the accounting, right?'

He nods. 'That's correct.'"

DannySummary of accounting is "Danny said that Cindy was closely involved in accounting for Rosalita's charity."

ChuckResponse of accounting is "You ask, 'Mr. Lee, is it true that Rosalita's secretary took care of accounting for Roses for Rehab?'

Chuck rubs his forearm as he answers. 'Cindy? Yeah, she was the one who did the internal auditing. She would go through about four times a year and reconcile all of the records. She would also take most of the donations and record them.'

'So she was heavily involved.'

'Yes, I would say that's accurate.'"

ChuckSummary of accounting is "Chuck said that Cindy was heavily involved in accounting for Roses for Rehab."

CindyResponse of accounting is "'Ms. Jalcek, could you describe for me exactly what your responsibilities were for Roses for Rehab?'

She chews on her lower lip for a second, looking you directly in the eyes. 'I was the primary person in charge of receiving donations by phone or by check. I conducted quarterly audits, and kept the records.'

'Thank you Ms. Jalcek, it's important for the record that I had this in your own words.'"

CindySummary of accounting is "Cindy described how she was involved in every part of accounting for Roses for Rehab."

MaiaResponse of accounting is "'Ms. Morales-'

'Andromeda, please.'

'Ms. Morales, what do you know about the accounting for Roses for Rehab?'

Maia rolls her eyes and sighs. 'I know absolutely nothing about it.'"

MaiaSummary of accounting is "Maia didn't know anything about this."

ChiefResponse of accounting is "'Chief, apparently Cindy Jalcek did all of the accounting for Roses for Rehab.'

'That's the charity, right?'

'Yes.'

'They take non-profit law very seriously in this county. She's on thin ice if she's done anything serious.'"

ChiefSummary of accounting is "The Chief said that any violations of non-profit law would be treated seriously."

Section 2 - Driveway

After switcherooing Cindy:
	say "The bus drops you off, then gasps and wheezes away. You're lucky you got here in time; the police were closing off the streets for a parade. The next bus isn't until noon.";
	try delivering parade;
	say "Time to get to work.";
	now SteppedIn is 0.

Parade is a quip. The preview of Parade is "The streets were closed down for a parade all morning."

ParadeGiver is a person that varies. ParadeGiver is Cindy.

DannyResponse of parade is "'[ParadeGiver] claimed that the streets were closed down for a parade the morning Rosalita died.'

'Yes, that makes sense. I was watching it on the news. Is this important for the investigation, or are you just making idle chitchat?'

'We'll have to see, won't we?'"

DannySummary of Parade is "Danny confirmed that there was a parade the day that Rosalita died."

ChuckResponse of Parade is "You say, '[if ParadeGiver is Cindy]Cindy says that a parade shut down the streets the day Rosalita died.'[otherwise]You said that a parade shut down the streets the day Rosalita died, right?'[end if]

'Yes, the Founder's Parade. I was supposed to cover it for WESL but I was at home, sick.' Tears well up in Chuck's eyes. 'Sorry, I just haven't had any sleep, and this is just so exhausting.'

You say, 'But you were sick? The first week of work? That must have been very unfortunate. Do you feel better now?'

Chuck makes an equivocating gesture. 'Yes, I am. Well, mostly. Thanks for asking.'"

ChuckSummary of parade is "Chuck confirmed there was a parade, and said that he was too sick to cover it."

CindyResponse of Parade is "'You said there was a parade the morning of Rosalita's death?'

'Yes, the Founder's Day Parade. I saw the floats in a big parking lot, and they were putting up barricades.'

'So you're sure there were no buses after you?'

'Yes, I'm sure. The bus driver told me I was lucky to catch the last bus.'"

CindySummary of Parade is "Cindy was sure that no buses came after her."

MaiaResponse of Parade is "'Apparently there was a parade the day that Rosalita died, shutting down all of the buses in the morning.'

'So? What's your point?' She frowns.";

MaiaSummary of Parade is "Maia was defensive."

ChiefResponse of Parade is "'Apparently there was a Founder's Day parade the day that Rosalita died.'

'Yes, I know, I was in the motorcycle escort. Great floats, but terrible weather. What brought it up?'

'One of the suspects said that it shut down traffic to the station,' you say.

'That's right; we had to shut down all the traffic in the city center, and the station was in the parade zone.'"

ChiefSummary of parade is "The chief confirmed that the parade shut down all traffic."

CindyParking is south from CindyLobby. The printed name of CindyParking is "Parking Lot". Understand "parking lot" as CindyParking. The description of CindyParking is "[if foundRosalita is 0]The dawn sun smiles on you as you stand in the parking lot, and you smile back. Looks like you got here just in time; the lot is starting to fill up, and that means the studio will open soon. The wind cuts through your jacket like a knife, but the smell of rice and hot oil from the Chinese restaurant warms your heart. You know where you're getting lunch today. [otherwise]The day has begun to cloud over. The lot seems empty and bare, despite the cars. The wind cuts through your jacket like a knife. [end if]The lobby door is [bold type]north[roman type] from here." Startroom of Cindy is CindyParking.

Some Cindycars are in CindyParking. Some cindyCars are scenery. The printed name of cindycars is "cars". The description of Cindycars is "A variety of cars dot the parking lot." Understand "rows" or "row" or "car" or "cars" as some Cindycars.

Some CindyBuildings is scenery. Some CindyBuildings are in CindyParking. The description of cindybuildings is "The brick walls of the buildings to either side hem you in, the Meih Lee Chinese restaurant on one side, Wolfe Pawn and Check Cashing on the other." Understand "brick" or "chinese" or "wolfe" or "pawn" or "check" or "cashing" or "wall" or "brick walls" or "buildings" as cindybuildings.

The dawn sun is in CindyParking. The description of dawn sun is "The dawn sun beams brightly over the wet parking lot."

CindyStudios is scenery. CindyStudios is in CindyParking. Understand "KOLR" or "studio" or "lobby" or "door" or "studios" as CindyStudios. The printed name of CindyStudios is "KOLR studios". The description of CindyStudios is "KOLR studios, your place of employment, your artistic canvas, your mechanical workshop, your everything.".

Instead of touching the dawn sun:
	say "How sweet a dream it is, to touch the sun.";

SteppedIn is a number that varies. SteppedIn is 0.

After going north from CindyParking:
	try looking;
	if SteppedIn is 0:
		if Fraud is MeihCindy:
			say "You see Rosalita coming out of the break room. She smiles. 'Oh, Cindy, it's good to see you. Listen, my sister's in the break room again. Can you make sure she leaves? I've got to pick the lineup for my show tonight.' You nod, and she walks into the hallway. She turns around and touches the door. 'Oh, and can you take the trash out of my office?' With that, she walks to her office.";
		otherwise:
			say "You see Rosalita coming out of the break room. She seems lost in thought. 'Oh, Cindy, it's you!' She looks around, and leans in. 'Listen, my sister's in the break room again. Can you make sure she leaves? I have to talk to Chuck in a few minutes, and I don't want her to be here.' 
			
			You nod, and she walks into the hallway. She turns around and beckons you closer. 'I'm worried that he knows about Roses for Rehab. You know what will happen to both of us if he does.' With that, she walks to her office.";
		now SteppedIn is 1;

Instead of going nowhere from CindyParking:
	if FoundRosalita is 0:
		say "Now's not a good time to leave.";
	otherwise:
		say "You don't want to chase after a homicidal maniac! You should call the police.";
		
Instead of smelling Cindybuildings:
	try smelling CindyParking;

Instead of smelling CindyParking:
	say "You smell some incredible Chinese cooking.";
	

Section 3 - Lobby

SawMaia is a number that varies. SawMaia is 0.

Before going west from CindyLobby:
	if SawMaia is 0:
		try looking;
		say "As you rush in to find the source of the smell, you smack into someone and knock them to the ground!
	
	'Oh, I'm so sorry!' you say. Your victim rolls over, revealing a mop of green hair.
	
	'Oh,' you say. 'It's you.' You frown, but lift her up. You say, 'Rosalita said to make sure you left the station. I'm sorry, but you're going to have to go; I just have to take care of something real quick.' Maia frowns and walks away back to the lobby and out of sight.";
		now SawMaia is 1 instead;

Before switcherooing Maia:
	now SawMaia is 0.
	
Report going east from CindyBreak:
	if the burnt popcorn is carried by the player:
		say "You look around, but Maia seems to be gone."


CindyLobby is a room. The printed name of CindyLobby is "Lobby". The description of CindyLobby is "This lobby is much bigger than the one at your old receptionist job. You glance at the KOLR logo you stenciled on the wall your first week; it always comforts you. The ceiling in this room is ten feet high, and your desk is huge. You've vacuumed this carpet so many times that it's almost worn through in places. The lights in the ceiling are wigging out again, you'll have to replace them, but the stepladder has been missing for weeks. The doorway to the [bold type]north[roman type] leads to the hallway. The parking lot is back to the south, and the bathroom is to the [bold type]east[roman type].

The phone sits on the desk."

Before going north from CindyLobby:
	if the burnt popcorn is not in HoldingCell:
		say "There's more smoke in here than the Pan Am business section. You need to deal with it before Rosalita finds out." instead.

The popcorn stench is in CindyLobby. "The unmistakable stench of burning popcorn [if HouseDirty is 1]rolls in [otherwise]still leaks in [end if]from the break room to the [bold type]west[roman type].[first time] Oh no.[only]". The description of the popcorn stench is "[if HouseDirty is 1]That horrible smell, it's coming from the break room![otherwise]Ugh, it still stinks. It'll take days to air this place out.[end if]". Understand "horrible" or "smell" as popcorn stench.

HouseDirty is a number that varies. HouseDirty is 1.

Instead of taking the popcorn stench:
	say "You try to grab a smell with both hands, and you do not succeed."

Instead of touching the popcorn stench:
	say "You wave your hand futilely in the odor."

The cindyfluorescentlights are scenery. The cindyfluorescentlights are in CindyLobby. Understand "fluorescent" or "dingy" or "plastic" or "covers" or "lights" or "light" or "buzz" or "buzzing" or "flickering" as cindyfluorescentlights. The description of cindyfluorescentlights is "You've got to change these lights! But the studio has to buy a new stepladder first."

Instead of touching cindyfluorescentlights:
	say "It's too high!"

Instead of listening to the Cindyfluorescentlights:
	try examining the Cindyfluorescentlights;

The CindyStencil is in CindyLobby. The CindyStencil is scenery. Understand "faded" or "r" or "logo" or "stencil" or "rust" or "beige" or "washed-out" or "blue" or "green" as CindyStencil. The description of CindyStencil is "You bought paint and slapped together this stencil when you started at KOLR, three years ago. You notice that the R seems to have faded off; you probably shouldn't have used fingerpaint."

The CindyFrontDesk is in CindyLobby. The CindyFrontDesk is scenery. The printed name of CindyFrontDesk is "front desk". Understand "front" or "kingdom" or "neat" or "tidy" or "desk" as CindyFrontDesk. The description of the CindyFrontDesk is "This your kingdom. Your desk stretches from the wall almost to the door. You keep it neat and tidy; right now, it's empty except for your phone.". CindyFrontDesk is a supporter. CindyFrontDesk is not enterable.

The CindyPhone is on CindyFrontDesk. The CindyPhone is scenery. The printed name of CindyPhone is "telephone". The description of CindyPhone is "An ivory rotary telephone. All of the numbers have been rubbed off, but you can dial by memory. You definitely don't want to bother with it right now." Understand "phone" or "telephone" or "tele" or "rubbed" or "rotary" as CindyPhone.

The CindyFrontChair is in CindyLobby. The CindyFrontChair is scenery. Understand "chair" as CindyFrontChair. The printed name of CindyFrontChair is "chair". The description of CindyFrontChair is "Your chair is pushed up close to the desk. It's [italic type]so[roman type] comfortable." CindyFrontChair is an enterable supporter.

Instead of taking the CindyFrontChair:
	say "It's not time to sit here yet."
	
Instead of pushing the CindyFrontChair:
	say "It's not time to sit here yet."
	
Instead of pulling the CindyFrontChair:
	say "It's not time to sit here yet."

CindyHallDoor is a door.  CindyHallDoor is scenery. The printed name of CindyHallDoor is "hallway door". Understand "door" or "black" or "window" or "glass" or "reinforced" or "metal" or "chips" or "paint" or "rusty" or "rust" or "hallway" as CindyHallDoor. CindyHallDoor is north of CindyLobby and south of CindyHallway. CindyHallDoor is closed and openable. The description of CindyHallDoor is "A black metal door with a glass window leads to the studio and the offices. Part of your job is keeping unwanted visitors from getting in here." CindyHallDoor is not lockable.

Instead of listening to CindyLobby:
	say "You hear a buzzing noise in the background."

Instead of smelling CindyLobby:
	say "You smell burnt popcorn from the west."

CindyMuddyCarpet is a backdrop. CindyMuddyCarpet is in CindyLobby. CindyMuddyCarpet is scenery. Understand "muddy" or "carpet" or "floor" as CindyMuddyCarpet. The printed name of CindyMuddyCarpet is "carpet". The description of CindyMuddyCarpet is "The muddy carpet looks like it was tacked on poorly; it curls up at the edges." Understand "backing" or "curls" or "corners" as CindyMuddyCarpet.

Instead of taking the CindyMuddyCarpet:
	say "That's disgusting; you'd rather not touch that."

Some CindyBeigeWalls are a backdrop. Some CindyBeigeWalls are in CindyLobby. CindyBeigeWalls is scenery. Understand "beige" or "walls" or "wall" as CindyBeigeWalls. The printed name of BeigeWalls is "walls". The description of CindyBeigeWalls is "The walls are a patchy shade of beige."

Section 4 - Hallways

MaiaOut is a number that varies.

CindyHallway is a room. The printed name of CindyHallway is "Hallway". The description of CindyHallway is "You've scrubbed the walls in this hallway a hundred times, and they are clean as a whistle. Chuck's office lies to the [bold type]west[roman type]. Looking closer, it seems like someone's gouged his door. You'll have to repaint it. The studio to the [bold type]east[roman type] is closed, and the On-Air light is glowing. Someone must be recording. The hallway extends to the [bold type]north[roman type]. You can also go [bold type]south[roman type] to return to the lobby."

[PUT IN SOMETHING ABOUT THE On-Air LIGHT BEING OFF]

CindyMuddyCarpet is in CindyHallway. CindyBeigeWalls are in CindyHallway.

A CindyBlueDoor is a closed openable door. CindyBlueDoor is west of CindyHallway and east of CindyChuckOffice. The printed name of CindyBlueDoor is "blue door". CindyBlueDoor is scenery. The description of CindyBlueDoor is "Someone has completely scratched up this door. You shake your head. You know they won't pay for a new one, so you'll just have to repaint it.". Understand "battered" or "pleasant" or "blue" or "door" or "gouges" or "deep" or "scratched" or "scratches" or "window" as CindyBlueDoor.  The cindybluedoor is locked.

The CindyChuckPlate is part of the CindyBlueDoor. The CindyChuckPlate is scenery. The printed name of CindyChuckPlate is "name plate". Understand "empty" or "name" or "plate" or "nameplate" as CindyChuckPlate. The description of CindyChuckPlate is "You took out Chuck's name yesterday. No point in honoring a traitor. And maybe the empty office will lead to a promotion for somebody..."

The CindyAirLight is scenery. The printed name of CindyAirLight is "On-Air light". Understand "on" or "air" or "light" or "on-air" or "red" as CindyAirLight. The CindyAirLIght is in CindyHallway. The description of CindyAirLight is "That's odd; the on-air light is on. Someone must be recording here.".

The CindyStudioDoor is a door. The CindyStudioDoor is scenery. The CindyStudioDoor is locked. The CindyStudioDoor is east of CindyHallway and west of CindyStudio. CindyStudioDoor is closed. The printed name of CindyStudioDoor is "studio door". The description of CindyStudioDoor is "A thick, heavy green door with 'Studio' painted on it in white. You redid it last week.” Understand "green" or "thick" or "heavy" or "door" or "studio door" as CindyStudioDoor.

CindyHallway2 is north of CindyHallway. The printed name of CindyHallway2 is "End of Hallway". The description of CindyHallway2 is "The hallway from the [bold type]south[roman type] ends here. Rosalita's wooden door stands to the [bold type]north[roman type]. Danny's hand-carved door is to the [bold type]west[roman type]. And your very own bright yellow door lies to the [bold type]east[roman type]: the janitor's closet."

CindyMuddyCarpet is in CindyHallway2. CindyBeigeWalls are in CindyHallway2.

The cindydarkreddoor is a closed openable door. The printed name of cindydarkreddoor is "brown door". The cindydarkreddoor is scenery. The cindydarkreddoor is north from CindyHallway2 and south from CindyRosasOffice. Understand "brown" or "dark"  or "door" or "wood"  or "cherry wood" as the cindydarkreddoor. The description of the cindydarkreddoor is "The door to Rosalita's office is made of some light brown wood."

Instead of touching cindydarkreddoor:
	say "It's soft and pleasant to the touch."

The CindyRosaPlate is part of the CindydarkredDoor. The CindyRosaPlate is scenery. The printed name of CindyRosaPlate is "Rosalita's name plate". Understand "Rosalita" or "rosa" or "name" or "plate" or "nameplate" as CindyRosaPlate. The description of CindyRosaPlate is "The name plate says 'Rosalita Morales'."

CindyDannysdoor is a closed, openable door. CindyDannysDoor is locked. CindyDannysdoor is scenery. CindyDannysdoor is east from CindyDannyOffice and west from CindyHallway2. The description of CindyDannysdoor is "A light red wooden door. It might be made of oak; it's hard to judge.". The printed name of CindyDannysdoor is "plain door". Understand "plain" or "wooden" or "Danny" or "danny's" or "hand" or "carved" or "hand-carved" or "light" or "red" or "oak" or "solid" or "door"  as CindyDannysDoor. CindyDannyOffice is blocked.

The CindyDannyPlate is part of the CindydannysDoor. The CindydannyPlate is scenery. The printed name of CindydannyPlate is "Danny's name plate". Understand "Danny" or "name" or "plate" or "nameplate" as CindyDannyPlate. The description of CindyDannyPlate is "The name plate says 'Daniel Kostopolous'."

The Cindyclosetdoor is a closed, openable door. CindyClosetdoor is scenery. CindyClosetdoor is west from CindyCloset and east from CindyHallway2. The description of CindyClosetdoor is "You wear many hats at KOLR: receptionist, personal assistant, and, of course, janitor. The word 'Janitor' is stenciled on the heavy closet door in white paint.". The printed name of CindyClosetdoor is "closet door". Understand "heavy" or "yellow" or "bright" or "closet" or "door" or "janitor" or "stencil" or "white" or "frame" or "tall" or "narrow" as CindyClosetDoor. 

Section 5 - Break Room

The printed name of CindyChairs is "chairs". Some CindyChairs are scenery. Some CindyChairs are in CindyBreak. Understand "scattered" as some CindyChairs. The description of CindyChairs is "The chairs are dark plastic with absorbent cloth seats. You've had to scrub stains out of them dozens of times." Understand "dark" or "chairs" or "plastic" or "worn" or "cloth" or "absorbent" or "stains" or "seat" or "seats" or "chair" as CindyChairs. The CindyChairs are a supporter. The CindyChairs are enterable. 

CindyBreak is west of CindyLobby. "The vending machine is still on the fritz; you called the manufacturer three times in the last week, and you'll have to call them again. A few tables and several chairs are scattered around the room. The smell of burnt popcorn overpowers you. The lobby is back to the [bold type]east[roman type]. 

[if HouseDirty is 1]Smoke billows from the microwave. Something's burning![otherwise]The microwave sits smugly and smokily against the wall.[end if]". The printed name of CindyBreak is "Break Room".

Instead of smelling CindyBreak:
	say "Please, no."

Some CindyBeigeWalls are in CindyBreak. Some CindyMuddyCarpet is in CindyBreak. Some CindyChairs are in CindyBreak.

Some CindyTables are in CindyBreak. The printed name of CindyTables is "tables". Some CindyTables are scenery. Understand "table" or "tables" or "coffee" or "figures" or "figure" or "abstract" or "surface" or "surfaces" or "ketchup" or "mustard" or "heavy" or "plastic" or "stains" as CindyTables. The description of CindyTables is "The circular tables are built from heavy plastic. Their surfaces are stained into abstract figures and designs by coffee, ketchup, and mustard." The Cindytables are a supporter.

A CindyVendingMachine is in CindyBreak. The printed name of CindyVendingMachine is "vending machine". CindyVendingMachine is scenery. The description of CindyVendingMachine is "The vending machine leans in the corner. Half of the buttons are dark or flickering; all that works is a couple of rows of iced tea. Not your favorite." Understand "buttons" or "button" or "vending" or "machine" or "dark" or "flickering" or "couple" or "rows" as CindyVendingMachine. 

Instead of looking under CindyVendingMachine for the first time:
	say "[one of]You look under the vending machine, and see a shim.[or]It looks the same as it ever did.[stopping]";

The CindyShim is part of the CindyVendingMachine. The description of the CindyShim is "That seems to be keeping the vending machine from toppling over."

Before taking the CindyShim:
	say "That seems dangerous; let's not do that." instead.

The CindyMicrowave is on CindyTables. The CindyMicrowave is scenery. The CindyMicrowave is a container. The CindyMicrowave is openable. The CindyMicrowave is closed. Understand "smugly" or "smoke" or "greasy" or "popcorn" or "microwave" as CindyMIcrowave. The printed name of CindyMicrowave is "smoky microwave". The description of CindyMicrowave is "[if HouseDirty is 1]Smoke is pouring from inside the microwave![otherwise]The popcorn is gone, but the microwave is filthy, now. You don't have time to clean it right now, but you can do it later, after lunch.[end if] It stinks of burnt popcorn."  

The burnt popcorn is in CindyMicrowave. Understand "bag" as burnt popcorn. The printed name of burnt popcorn is "bag of burnt popcorn". The description of the burnt popcorn is "[first time]You sigh just looking at the bag; you told Rosalita that selling popcorn in a bag was a bad idea, but she always wants to try new products. [only]This popcorn bag has been incinerated. The whole bag is carbonized except for a few blue and white corners. [if burnt popcorn is in CindyToilet]The water is making it disintegrate into a foul black liquid.[otherwise]Smoke still billows from the bag.[end if]"

Instead of opening the burnt popcorn:
	say "It's a solid block of charcoal right now; I don't think it's going to open."

Every turn:
	if the burnt popcorn is carried by the player:
		say "The bag of popcorn is smoking in your hands."

After taking burnt popcorn:
	if HouseDirty is 1:
		say "You gingerly pick it up by the edges. You'll have to throw it away in the bathroom!";
		now HouseDirty is 0;
	otherwise:
		say "Taken."

Instead of going south from CindyLobby when the burnt popcorn is carried by the player:
	say "You try going outside for a moment, but a few passers-by stare at you and the burning bag. You rush back inside."

Instead of dropping the burnt popcorn:
	say "And burn the floor? No way!";
	
Instead of eating the burnt popcorn:
	say "When they say 'That's hardly edible', this what they're talking about. Ew!"

Instead of inserting the burnt popcorn into CindyMicrowave:
	say "Are you crazy? No way!";

Instead of switching on the CindyMicrowave:
	say "You would not eat something cooked in this microwave."

Instead of switching off the CindyMicrowave:
	say "It's already off."

Instead of touching CindyMicrowave:
	say "It feels greasy. You wipe you hands on your pants in disgust."


Section 6 - Bathroom

The Cindytilefloors are scenery. Understand "old" or "tiles" or "dingy" or "tile" or "floors" or "floor" as Cindytilefloors. The printed name of CindyTileFloors is "tile floors". The description of the Cindytilefloors is "The tiles used to some kind of brown or tan, but now it's hard to tell."
The printed name of Cindybathroomwalls is "bathroom walls". The Cindybathroomwalls are scenery. Understand "grey" or "bathroom" or "walls" or "wall" or "shades" as Cindybathroomwalls. The description of the Cindybathroomwalls is "The bathroom walls are painted in various shades of grey." [The bathroom walls are in Men's Room. The bathroom walls are in Women's Room.]

The Cindypapertoweldispenser is scenery. The Cindypapertoweldispenser is in CindyPrivateBathroom. Understand "combination" or "paper" or "towel" or "dispenser" as Cindypapertoweldispenser. The description of the Cindypapertoweldispenser is "A paper towel dispenser set over a trash can."[ The paper towel dispenser is in Men's Room and in Women's Room.]

Instead of taking the Cindypapertoweldispenser:
	say "The dispenser seems be empty; you need to buy more paper towels.";

Instead of pushing the Cindypapertoweldispenser:
	try taking the Cindypapertoweldispenser;

The Cindytrashcan is scenery. The Cindytrashcan is in CindyPrivateBathroom. Understand "battered" or "combination" or "trash" or "can" as Cindytrashcan. The description of Cindytrashcan is "A rectangular metal trash can that is attached to the wall." The CindyTrashcan is a container.

Instead of searching Cindytrashcan:
	say "Fortunately, the can has recently been emptied.";

CindyPrivatebathroom is a room. The printed name of CindyPrivateBathroom is "Private Bathroom".  The description of Cindyprivatebathroom is "At least the bathroom's moldy smell will help cover up the popcorn. You don't know why they won't let you remodel this bathroom, clearly the walls are crawling with mold. But at least it makes your job easier when nobody uses it. Even the sink is pretty clean, below the mirror, and the trash can is empty. Oh, looks like the paper towel dispenser is empty too. Oops. The toilet itself squats in a corner."

Instead of smelling CindyPrivateBathroom:
	say "No matter how much you clean, the moldy smell doesn't go away."

The Cindybathceiling is scenery. The Cindybathceiling is in CindyPrivateBathroom. The printed name of the Cindybathceiling is "ceiling". Understand "ceiling" or "low" or "bulb" or "fan" or "single" or "dim" or "ventilation" as Cindybathceiling. The description of Cindybathceiling is "The ceiling is lower in this room, with a single light bulb fixture and a ventilation fan."

Instead of opening the Cindybathceiling:
	say "You're a receptionist, not an acrobat."

Instead of switching off the Cindybathceiling:
	say "You'd rather not, although you've done it before."

The Cindytilefloors are in Cindyprivatebathroom. The Cindybathroomwalls are in Cindyprivatebathroom. The small mirror is in Cindyprivatebathroom. The bathroom sink is in CindyPrivateBathroom.

The Cindytoilet is scenery. The Cindytoilet is in the CindyPrivateBathroom. The printed name of CindyToilet is "toilet". Understand "toilet" or "bowl" or "loo" as Cindytoilet. The description of the Cindytoilet is "Your least favorite thing to clean in this office, when it gets dirty. Which isn't often.[if burnt popcorn is in CindyToilet] The water is black with ash, and the popcorn is floating in the bowl.[otherwise if burnt popcorn is in HoldingCell] The toilet is backed up, but it should drain by itself in a few hours.[end if]". The cindytoilet is a container.


CindyPrivatedoor is a closed openable door. CindyPrivatedoor is scenery. Understand "bathroom door" or "bathroom" or "door" as CindyPrivateDoor. The printed name of CindyPrivateDoor is "bathroom door".  CindyPrivatedoor is east from CindyLobby and west from CindyPrivatebathroom. The description of CindyPrivatedoor is "A door with a handicap symbol."

Understand "lock [something]" as locking keylessly. 
	
Check locking keylessly: 
	if the player is in the Cindylobby:
		if the noun is CindyPrivateDoor:
 			say "You can't lock the door from this side without the key." instead.

Flushing is an action applying to one thing. Understand "flush [something]" as flushing.

SelfFlushing is an action applying to nothing. Understand "flush" as selfflushing.

ThrowAwaying is an action applying to one thing. Understand "throw away [something]" or 
"throw out [something]" or "throw [something] away" as throwawaying.

Carry out throwawaying:
	if the player is in CindyPrivateBathroom:
		try inserting the noun into CindyTrashCan;
	otherwise if the player is carrying the burnt popcorn:
		say "You should throw it away in the bathroom.";
	otherwise:
		say "You don't need to throw that away."

Carry out flushing:
	if the noun is burnt popcorn:
		if the noun is not in CindyToilet:
			say "Good idea!";
			now the noun is in CindyToilet;
			try selfflushing;
		otherwise:
			try selfflushing;
	otherwise if the noun is the CindyToilet:
		try selfflushing;
	otherwise:
		say "That's probably not a very good idea."
	
Carry out selfflushing:
	if the burnt popcorn is in CindyToilet:
		say "You flush the popcorn down the toilet. It disappears, thankfully, but the toilet starts backing up. Crap! Well, actually, just water, but still, you're glad no one uses this toilet.
		
		You've got to deal with that smell in the break room, now. Time to get the fan from the closet.";
		now the burnt popcorn is in HoldingCell;
	otherwise:
		say "There's no need."

Instead of inserting the burnt popcorn into the CindyTrashCan:
	say "That's a good idea. You throw the popcorn into the toilet and turn to go. Wait! The toilet? Not again. You always mix the two up when you're in a hurry. It's caused a lot of messes.
	
	Now the burnt popcorn is floating in the toilet.";
	now the burnt popcorn is in CindyToilet;

Instead of inserting the burnt popcorn into the bathroom sink:
	say "That's a good idea. You throw the popcorn into the toilet and turn to go. Wait! The toilet? Not again. You always mix the two up when you're in a hurry. It's caused a lot of messes.";
	now the burnt popcorn is in CindyToilet;

After inserting the burnt popcorn into the CindyToilet:
	say "Good idea! Now you can flush it down.";

Before taking the burnt popcorn:
	if the burnt popcorn is in CindyToilet:
		say "Better to just flush, now." instead;

Check going west from CindyPrivateBathroom:
	if the burnt popcorn is in the Cindytoilet:
		say "You can't just leave that in the toilet! You should at least just flush it." instead;


Stranger is a quip. The preview of Stranger is "Cindy saw a strange man in a yellow poncho hovering over Rosalita Morales."

DannyResponse of Stranger is "'Cindy saw a strange man in a yellow poncho shoot Rosalita.'

Danny says, 'Did she see anything else about him? Was he old, young, heavy, thin? Did he have a scar, a limp? Did she see anything at all but a poncho?'

'She didn't get a good look at him.'

Danny leans back. 'Well, I shouldn't be too hard on her; it's a hard situation all around. But I wish she had seen more.'"

DannySummary of Stranger is "Danny wanted more information about the killer."

ChuckResponse of Stranger is "You say, 'Cindy saw Rosalita get shot by a man in a yellow poncho.'

Chuck looks like he's been kicked in the stomach, and nods. 'Yeah, I heard that.' He says, 'Do you know anything more about the killer? Are you close to finding him?'

'Not yet.'

'It makes me sick, thinking about some stranger breaking into KOLR and killing Rosalita.' He grimaces."

ChuckSummary of Stranger is "Chuck wanted more information about the killer."

MaiaResponse of Stranger is "'Cindy saw a man in a yellow poncho shoot Rosalita.'

'Who was he? Do you have any information about him?'

'No.'

'Do you guys have any leads on this guy? Any open cases? Serial killers running around? It's absolutely frightening that a murderer just broke into the studio and killed my sister. Are you doing anything?'

You say, 'The department has quite a few people working on this case. My job is just to get the facts from you and the other witnesses.'"

MaiaSummary of Stranger is "Maia wanted more information about the killer."

CindyResponse of Stranger is "'Did you see anything else about the killer?'

She shakes her head. 'No, he was gone so fast.'

'Was he tall, short, old, young?'

'Listen, I don't know! I was terrified!'

'Was there anything you remember?'

'He was wearing some sort of yellow poncho, and he had a gun. He was unshaven, and I think he smelled a bit like week-old leftovers.'"

CindySummary of Stranger is "Cindy said the man was unshaven and smelled bad."

ChiefResponse of Stranger is "'Cindy says she saw a man in a yellow poncho shoot Rosalita and run away.'

'Yes, that was her story when we arrived at the station after the murder.'

'So why am I talking to this group? Do you know something I don't?'

'Yes, I do: nothing was stolen from Rosalita's office.'

'And you didn't tell me?'

'I told you to investigate these suspects. We're exploring other options as well, and that's all you need to know.'"

ChiefSummary of Stranger is "The chief suggested you find other accounts of the murder."

Section 7 - Closet

The printed name of CindyCloset is "Janitor's Closet". The description of CindyCloset is "This cool, concrete closet is tiny, but it [italic type]is[roman type] neatly organized and perfectly sorted. Rags, a mop, a bucket, a straw broom, a push broom, bottles of cleaner, two dustpans, extra toilet paper, and so on."

A supply is a kind of thing. A supply is scenery. 

Instead of taking a supply:
	say "You always take out exactly what you need, and nothing more. And you don't need this right now."

The rags are in CindyCloset. The rags are a supply. The description of the rags is "You bought these in bulk in the auto-care section." Understand "rags" as rags.

The mop is in CindyCloset. The mop is a supply. The description of the mop is "This one has a washable head; you take it home and wash it yourself, frequently."
 
The washable head is part of the mop. The description of the washable head is "Looks pretty clean. Good job!"

The big metal bucket is in CindyCloset. The big metal bucket is a supply. The description of the big metal bucket is "A big metal bucket. You could make a washtub bass with this, like your grandpa's, but you're not into stealing office supplies."

The straw broom is in CindyCloset. The straw broom is a supply. The description of the straw broom is "This broom is for smaller jobs, and for cobwebs on the ceiling." 

The push broom is in CindyCloset. The push broom is a supply. The description of the push broom is "This broom is for bigger jobs, and for practicing dance routines." 

The bottles of cleaner are in CindyCloset. The bottles of cleaner are a supply. The description of the bottles of cleaner is "You use all sorts of cleaners, but you prefer plain vinegar when possible." Understand "bottle" or "sorts" or "sort" or "plain" or "vinegar" as bottles of cleaner. 

The dustpans are in CindyCloset. The dustpans are a supply. The description of the dustpans is "It's good to have two, because you never know when you might break one."  Understand "two" or "dustpan" or "dust" or "pan" or "pans" as dustpans.

The extra toilet paper is in CindyCloset. The extra toilet is a supply. The description of the extra toilet paper is "Okay, okay, you keep it in here because you don't like people using the bathroom."

SoOn is in CindyCloset. SoOn is a supply. Understand "so on" as SoOn. The description of SoOn is "So On is your favorite cleaner."

Before opening the CindyClosetDoor:
	say "You yank hard on the handle of the door. Everyone thinks you lock it, but it's just really, really hard to open."

The CindyFan is in CindyCloset. "[if CindyFan is in CindyCloset]Oh, and the fan you were looking for.[otherwise if the fan is powered]The fan sits here, clicking and humming as it spins around.[otherwise]You can see an oscillating fan here.[end if]". The printed name of CindyFan is "oscillating fan". The description of the CindyFan is "[if the cindyfan is not powered]It runs on batteries; you can just drop it anywhere you want and turn it on.[otherwise]The fan clicks and hums as it rotates about the room, trying to clear the air.[end if]". Understand "clicks" or "hums" or "oscillating" or "fan" as the CindyFan.

The cindyfan can be powered or not powered.

Instead of putting something on CindyMuddyCarpet:
	try dropping the noun.

Instead of switching on the cindyfan:
	if CindyFan is carried by the player:
		say "Not while you're carrying it.";
	otherwise if CindyFan is not powered:
		say "You turn on the fan. It sputters, then begins to blow.[if CindyFan is in CindyBreak][line break]The smell dissipates somewhat. Now you can finally get to cleaning Rosalita's office.[end if][if CindyFan is on CindyTables][line break]The smell dissipates somewhat. Now you can finally get to cleaning Rosalita's office.[end if]";
		now CindyFan is powered;
		if CindyFan is in CindyBreak:
			now FanTaken is 1;
		if CindyFan is on CindyTables:
			now FanTaken is 1;
	otherwise:
		say "It's already on."

Report putting CindyFan on CindyTables:
	if the fan is powered:
		say "The smell dissipates somewhat. 
			
		You rub your temples and sigh. Now you can finally get to cleaning Rosalita's office. She'll probably be be angry you took so long.";
		now FanTaken is 1;			

Report dropping CindyFan:
	if the location of the player is CindyBreak:
		if the fan is powered:
			say "The smell dissipates somewhat. 
			
			You rub your temples and sigh. Now you can finally get to cleaning Rosalita's office. She'll probably be be angry you took so long.";
			now FanTaken is 1;			
	otherwise if the location of the player is CindyPrivateBathroom:
		say "You could try using the fan in here, but most of the smoke is in the break room.";
			
Instead of switching off the cindyfan:
	if CindyFan is not powered:
		say "It's already off.";
	otherwise if CindyFan is in CindyBreak:
		say "You should leave that on.";
	otherwise:
		say "You turn it off. It sputters and slows.";
		now CindyFan is not powered.

Before taking CindyFan:
	if CindyFan is powered:
		say "You don't want to take it when it's turned on." instead;

Instead of listening to CindyFan:
	if CindyFan is in CindyBreak:
		say "It clicks and hums as it spins about.";
	otherwise:
		say "It is silent.".

Instead of listening to CindyBreak:
	if CindyFan is in CindyBreak:
		try listening to the Cindyfan;
	otherwise:
		say "You don't hear anything.";

Section 8 - Rosalita's office

The printed name of CindyRosasOffice is "Rosalita's Office".

Instead of smelling CindyRosasOffice:
	say "You smell leftover Chinese food."

The leftovers are scenery. The leftovers are in CindyRosasOffice. Understand "chinese" or "smell" or "food" as leftovers. The description of the leftovers is "You can't focus on that right now!". 

The description of CindyRosasOffice is "Rosalita's office stretches across the whole northern side of KOLR Studios. The room is lined with bookshelves full of pop psychology, exercise and diet guides, romance novels, and short story collections (you know because you've borrowed half of them). The bookshelves are split by two giant windows, covered in sumptuous drapes. The pride of her office, her red leather chair, sits behind her artsy desk."

Some CindyOfficeShelves are in CindyRosasOffice. Some CindyOfficeShelves are a backdrop. CindyOfficeShelves are scenery. The printed name of CindyOfficeShelves is "bookshelves". Understand "bookshelves" or "bookshelf" or "books" or "shelf" or "shelves" or "illuminated" or "pop" or "psychology" or "exercise" or "diet" or "guide" or "guides" or "romance" or "novel" or "short" or "story" or "collections" or "collection" as CindyOfficeShelves. The description of CindyOfficeShelves is "It's really hard to focus when there's a dead body lying on the floor. This really bad. This really really bad."

The Cindydrapes are in CindyRosasOffice. The Cindydrapes are scenery. The description of the Cindydrapes is "The light through the windows is bright and yellow, falling in shafts to illuminate Rosalita's body. The windows are closed and the lock is engaged. Unfortunately, you can't see anything but the empty alley through the windows.". The printed name of CindyDrapes is "windows". Understand "windows" or "drapes" or "window" or "light" or "bright" or "yellow" or "shafts" or "burgundy" or "giant" as Cindydrapes.

[Lock is a quip. The preview of Lock is "Cindy said that the office windows were closed and locked."

DannyResponse of Lock is "'Cindy said that Rosalita's office windows were locked when the killer was in the room.'

'Then how did the killer get in? Did he just walk in?'

'We're not sure. Cindy didn't see anyone coming in that day, but she spent a lot of time away from the main hallway.'

'I've tried for years to get Rosalita to spend more money on security, officer. She never listened to me. And look what happened, you know?'"

DannySummary of Lock is "Danny wants to know how the killer got in."

ChuckResponse of Lock is "'Cindy said that Rosalita's office windows were locked when Rosalita was shot.'

Chuck is startled. 'That's not-I mean, I heard that the police report said something different. When I went to identify her body, they told me otherwise. You should check it out.'

'What do you mean? Do you know something about this?'

'No, I'm just saying that I heard the windows weren't locked. I swear, it must be in the report.'"

ChuckSummary of Lock is "Chuck suggested checking out the police report."

MaiaResponse of Lock is "'Cindy said that the office windows were locked when Rosalita was shot.'

She looks surprised. 'Then how did the murderer get in?'

'Did you see anything when you were there? Anything suspicious?'

She says, 'No, no I didn't. I didn't even go to that part of the office.'"

MaiaSummary of Lock is "Maia wanted to know how the murderer got in."

CindyResponse of Lock is "'Are you sure you saw that the windows were locked?'

'Yes, why do you ask?'

'I wanted to make sure.'

'I always check because I'm worried about break-ins.'

'Well, there was a break-in.'

Cindy is silent."

CindySummary of Lock is "Cindy confirmed that the windows were locked."

ChiefResponse of Lock is "'Cindy said that the windows were locked when Rosalita was killed.'

'Hmmm..'

'What is it?'

'When we arrived at the station, the windows were open, I believe. Check the police report in your folder.'"
	
ChiefSummary of Lock is "The chief suggested checking your folder."

Report examining CindyDrapes:
	try delivering Lock;]


A CindyArtDecoDesk is in CindyRosasOffice. A CindyTallDeskChair is in CindyRosasOffice. CindyArtDecoDesk is scenery. Understand "desk" or "art"or "artsy" or "deco" as CindyArtDecoDesk. The printed name of CindyArtDecoDesk is "desk".  The description of CindyArtDecoDesk is "Rosalita's body lies sprawled by her desk."

The printed name of CindyTallDeskChair is "crimson chair". CindyTallDeskChair is scenery. Understand "tall" or "imposing" or "chair" or "red" or "crimson" as CindyTallDeskChair. The description of CindyTallDeskChair is "Oh gosh, her chair, you can't even think of her chair. She's dead!"

The CindyTallDeskChair is a supporter. The CindyTallDeskChair is enterable.

Instead of entering the CindyTallDeskChair:
	say "You don't want to sit in Rosalita's chair.".

[In this version, Rosalita is based on Marisol Valles García]

CindyOfficeRosalita is a thing. "Your employer, Rosalita, is sprawled on the floor over a pool of blood." CindyOfficeRosalita is in CindyRosasOffice. Understand "Rosalita" or "Rosa" or "pool" or "blood" or "body" as CindyOfficeRosalita. The printed name of CindyOfficeRosalita is "Rosalita's body". The description of CindyOfficeRosalita is "Rosalita lies face down on the ground, her black glasses hanging askew from one ear. Her lemon-colored pantsuit is stained on the edges by the pool of blood spreading over the office floor."

Instead of taking CindyOfficeRosalita:
	say "No, you shouldn't touch the body; You need to call the police; the killer might still be out there! 
	
	You should use the phone at your desk!";

[Fake intruder is based on homeless man and his dog]

Check going north from CindyHallway2:
	if ChuckThere is 0:
		if FanTaken is 0:
			say "You need to deal with that horrible popcorn smell first." instead;
		otherwise if FoundRosalita is 1:
			say "You're not going back in there!" instead;
		otherwise:
			now FoundRosalita is 1;
	otherwise:
		say "You really don't want to disturb Rosalita and Chuck." instead;

After going north from CindyHallway2:
	if ChuckThere is 0:
		say "As you open the door, you hear a loud gunshot, and you see Rosalita crumple to the floor.";
		[try looking;]
		now CindyOfficeNow is CindyOfficeNow plus 1;

CindyOfficeNow is a number that varies. CindyOfficeNow is 0.

CindyOfficeTime is a recurring scene. CindyOfficeTime begins when CindyOfficeNow is 1. CindyOfficeTime ends when the player is in CindyParking.

CindyTruth is a number that varies. CindyTruth is 0.

[The condition for the truth might be two quips, or a combined quip.]
Before speeching Accomplice to Cindy:
	now CindyTruth is 1.
	
[Have Rosa say she regretted hooking up again with Chuck, and arguing about the fraud.]
When CindyOfficeTime begins:
	say "You see an unfamiliar man wearing a yellow poncho hovering over Rosalita, holding a gold-handled revolver. Startled, he turns to look at you, and his bloodshot eyes widen in his stubble-covered face. He runs to the door and you shrink back in fear as he races down the hall outside.";
	now cindydarkreddoor is open;
	now CindyOfficeNow is 7;
	try delivering Stranger;

Report going east from CindyHallway2:
	if ChuckThere is 1:
		say "The walls are thin in this closet. You can make out snatches of conversation. You hear Chuck mention 'Rehab', 'charity', and 'money'. Just like you planned.
		
		You take the fan to the break room and turn it on, then come back to the office. You press your ear to the door.
		
		'I can't give you any more, Chuck. You're asking for too much. '
		
		'Rosalita, no!' There are the sounds of a struggle. Someone's getting hurt in there! You go to open the door when you hear a loud gunshot. You drop to the floor and remain motionless.
		
		Chuck opens the door, his face ashen and a gun in his hand. 'Cindy, it went bad, 'it went really bad. She had a gun. We have to figure something out.'
		
		'Maybe we can-'
		
		'Cindy, look!' Chuck says, pointing down the hall. You turn and look. The On-Air light is on!
		
		'Chuck, just go, go! Get rid of that thing! I'll call the cops and tell them it was an intruder. Just get out of here!'
		
		Chuck runs down the hall. You head to the lobby to call the police.
		
		'And now, officer, you know the truth.'";
		try switcherooing cop;
		try delivering Truth;
		say "Now that you know what happened, you have to do something.";

When CindyOfficeTime ends:
	if FoundRosalita is 1:
		say "As you burst out of the doorway, you're just in time to see the killer race around the corner. He's gone.";

Section 9 - Ending the scene

Every turn:
	if FoundRosalita is 1:
		if persona is Cindy:
			say "[one of]What do you do? What do you do? You have to call the police![or]You have to call the police NOW![or]Is the killer still out there? What's going on?[or]In the lobby--the phone-- you've got to call the police![or]Who would do this? Why did someone kill Rosalita?[or]Oh no oh no oh no oh no[line break][then at random]";

Understand "dial 911" or "use phone" or "dial" or "call 911" or "call police" or "call cops"  or "call the cops" or "call the gendarmerie" or "call the rhozzers"or "call the authorities" or "call law enforcement" or "make an emergency call" or "call emergency services" or "call homicide division" or "call robbery and homicide" or "call a private eye" or "call the sheriff" or "call the marshal" or "call the police" or "pick up receiver"  or "call chief" or "take receiver" or "telephone" or "call for help" as dialing. Dialing is an action applying to nothing.

Instead of taking the CindyPhone:
	try dialing;

Carry out dialing:
	if the player is in CindyLobby:
		if FoundRosalita is 1:
			say "'I called the police. And I waited up there until they showed up. It was horrible...'

Cindy falls silent, shivering.";
			try switcherooing cop;
		otherwise:
			say "You don't need to call anyone right now.";
	otherwise if the player is in CopLobby:
		say "You call up the chief. The chief answers. 'What is it?'"; 
		now FakeChief is in CopLobby;
	otherwise if the player is in MaiaLobby:
		say "You're not allowed to use the phones.";
	otherwise if the player is in ChuckLobby:
		say "You have no one to call.";
	otherwise if the player is in DannyLobby:
		say "Cindy's using the phone.";
	otherwise if the player is in the Phone Booth:
		say "You're already on the phone.";
	otherwise:
		say "You don't have a phone available at this time.";

Does the player mean HangUping:
	it is likely.

HangUping is an action applying to nothing. Understand "hang up" or "dial off" or "end call" or "drop phone" or "ring off" or "put down phone" or "put receiver on base" or "hang up phone" or "goodbye" as HangUping.

Before going a direction from CopLobby:
	if FakeChief is in CopLobby:
		try HangUping.

Carry out HangUping:
	if FakeChief is in CopLobby:
		say "'I'll talk to you later, Chief.'
		
		'You do that. Good luck.'
		
		You place the receiver back on the base.";
		now FakeChief is in holdingCell;
	otherwise if the player is in the Phone Booth:
		say "Not yet.";

The FakeChief is a person. "The Chief is on the phone." FakeChief is scenery. FakeChief is in HoldingCell. The printed name of FakeChief is "Chief". Understand "chief" or "schwarz" or "bulldog" or "voice" as FakeChief.

Instead of touching FakeChief:
	say "He's not here."
	
Section 10 - Cindy's breaking scene

[CindyBreaking is a scene. CindyBreaking begins when CindyFear is at least 7. CindyBreaking ends when Innocent is CindyGiven.]

[Confession is a quip. The preview of confession is "Cindy confessed to the murder."]

[When CindyBreaking begins:
	say "[line break]'Stop! Just stop! I did it!' She sobs until she can almost can't breathe. 'I murdered her!'";
	try delivering Confession;

Before speeching something to Cindy during CindyBreaking:
	if the noun is not innocent:
		say "[one of]Cindy just sobs, face in her hands[or]Cindy weeps, saying 'It was me, just take me in, just take me in'[or]Cindy can't talk right now, being overcome with emotion[at random]." instead;]

Chapter 10 - Maia's story

[Maia's story is based on 9:05]

Section 1 - Setup and basics

MaiaResponse of Greeting is "You glance at your folder for a moment. 'Maia Morales?'

She rises, extending her hand. 'Please, call me Andromeda.'

'For the purpose of this interview, I'd prefer to use your legal name.'

Her face falls, but she recovers. 'That's alright then.' She sits back down. She leans forward, looking at your badge. 'And you're Officer... King?'

'Detective King, actually. I'd like to ask you some questions.'";


After speeching Greeting to Maia:
	try delivering sister.

Check speeching a quip to Maia:
	if the noun is Greeting:
		continue the action;
	otherwise if Greeting is MeihMaia:
		say "The woman interrupts you. 'And you are?' she says, raising an eyebrow.";
		try speeching Greeting to Maia;
		continue the action;
		
MaiaSummary of Greeting is "You have met Maia Morales."

MaiaStatementGiven is a number that varies. MaiaStatementGiven is usually 0.

InStreet is a number that varies. InStreet is 0.

Instead of speeching Statement to Maia:
	if greeting is MeihMaia:
		say "The woman interrupts you. 'And you are?' she says, raising an eyebrow.";
		try speeching Greeting to Maia;
	if MaiaStatementGiven is 0:
 		say "'Tell me what you know about the death of Rosalita.'

'All right. Here's what happened.'";
		now MaiaStatementGiven is 1;
		add "[line break][line break]Maia has already given you her statement." to history of Statement;
	otherwise:
		say "'Tell me your story again.'

'Fine.'";
	now StartRoom of cop is CopStudio;
	try switcherooing Maia;

[	if StartRoom of Maia is MaiaBedroom:
		say "[line break][bold type]Asleep[roman type]
		
		Your alarm is beeping...beeping...beeping...beeping..beeping...
	
	Oh no! How long have you been asleep? This bad. Not very bad, but still bad.";
	try switcherooing Maia ;
	if StartRoom of Maia is MaiaBedroom:
		say "You stumble out of bed. You hear your phone beeping.";
		now Maia is unclothed;
	now cell phone is in Studio;
	now AlarmOff is 0 instead;]

StartRoom of Maia is MaiaBreak.

Section 2 - Breakroom

Before switcherooing Maia:
	now MaiaRosalita is in MaiaBreak;

MaiaMuddyCarpet is a backdrop. MaiaMuddyCarpet is in MaiaLobby. MaiaMuddyCarpet is scenery. Understand "muddy" or "carpet" or "floor" as MaiaMuddyCarpet. The printed name of MaiaMuddyCarpet is "carpet". The description of MaiaMuddyCarpet is "The chocolate-colored carpet is meticulously clean." Understand "backing" or "curls" or "corners" as MaiaMuddyCarpet.

Instead of touching the MaiaMuddyCarpet:
	say "You run your hand over the carpet."

Some MaiaBeigeWalls are a backdrop. Some MaiaBeigeWalls are in MaiaLobby. MaiaBeigeWalls is scenery. Understand "beige" or "walls" or "wall" as MaiaBeigeWalls. The printed name of MaiaBeigeWalls is "walls". The description of MaiaBeigeWalls is "The walls are a patchy shade of beige."

The printed name of MaiaChairs is "chairs". Some MaiaChairs are scenery. Some MaiaChairs are in MaiaBreak. Understand "scattered" as some MaiaChairs. The description of MaiaChairs is "The ebony chairs are scattered tastefully about the break room." Understand "dark" or "ebony" or "chairs" or "plastic" or "worn" or "cloth" or "seat" or "seats" or "chair" as MaiaChairs. The MaiaChairs are a supporter. The MaiaChairs are enterable. 

Instead of sleeping when the player is on MaiaChairs:
	say "You doze off for a moment."

[A CC camera is a backdrop. A CC camera is scenery. A CC camera is in DannyRoom. The description of CC camera is "The security cameras in this building  are small, unobtrusive black domes. The feeds can be accessed by the security guards, or, in this case, the chief." Understand "unobtrusive" or "small" or "black" or "domes" as CC camera.]

MaiaBreak is west of MaiaLobby. "Rosa's break room is your favorite room in the studio; the wainscoting on the outer walls and the stippled ceiling help you imagine the chapel this building used to be. The vending machine sells banana nut muffins, they have a microwave for bigger meals, and the chairs are great for naps. You never liked the color choice for the tables, though.[if MaiaRosaCounter is at least 1]

The microwave whirrs in the background.[end if]"

The printed name of MaiaBreak is "Break Room".

The wainscoting is a backdrop. The wainscoting is scenery. The wainscoting is in CopBreak. The wainscoting is in CindyBreak. The wainscoting is in MaiaBreak. The wainscoting is in MaiaControl. The description of the wainscoting is "The wainscoting is well preserved."

The stippled ceiling is a backdrop. The stippled ceiling is scenery. The stippled ceiling is in CopBreak. The stippled ceiling is in CindyBreak. The stippled ceiling is in MaiaBreak. The stippled ceiling is in MaiaControl. The description of the stippled ceiling is "The stippled ceiling is preserved from the original construction."

Instead of smelling MaiaBreak:
	say "It smells great in here."

Instead of listening to MaiaBreak:
	say "The microwave whirrs in the background."

Some MaiaBeigeWalls are in MaiaBreak. Some MaiaMuddyCarpet is in MaiaBreak. Some MaiaChairs are in MaiaBreak.

Some MaiaTables are in MaiaBreak. The printed name of MaiaTables is "tables". Some MaiaTables are scenery. Understand "table" or "tables" or "coffee" or "figures" or "figure" or "abstract" or "surface" or "surfaces" or "ketchup" or "mustard" or "forest" or "green" or "heavy" or "plastic" or "stains" as MaiaTables. The description of MaiaTables is "Underneath the stains, you can see the original forest green color of the tables." The Maiatables are a supporter.

A MaiaVendingMachine is in MaiaBreak. The printed name of MaiaVendingMachine is "vending machine". MaiaVendingMachine is scenery. The description of MaiaVendingMachine is "You love the banana nut muffins, but you're not hungry right now." Understand "buttons" or "button" or "vending" or "machine" or "dark" or "flickering" or "couple" or "rows" as MaiaVendingMachine. 

Instead of looking under MaiaVendingMachine for the first time:
	say "[one of]You look under the vending machine, and see a shim.[or]It looks the same as it ever did.[stopping]";

The maiashim is part of the MaiaVendingMachine. The description of the maiashim is "That seems to be keeping the vending machine from toppling over." The printed name of the maiashim is "shim". Understand "shim" as maiashim.

Before taking the maiashim:
	say "That seems dangerous; let's not do that." instead.

The MaiaMicrowave is on MaiaTables. The MaiaMicrowave is scenery. Understand "greasy" or "popcorn" or "microwave" as MaiaMIcrowave. The printed name of MaiaMicrowave is "greasy microwave". The description of MaiaMicrowave is "This microwave must have cost a pretty penny when Rosa bought it; you recognize the brand. You mostly use it for reheating sandwiches.

Oh, what's this? Looks like Rosa hit another 0 when she was setting the microwave. That's going to be one overcooked bag of popcorn. Serves her right."

The MaiaMicrowave is an openable container. 

Instead of opening the MaiaMicrowave:
	say "If Rosa wants to overcook her popcorn, that's her prerogative.".

Instead of switching on the MaiaMicrowave:
	say "It's already on."

Instead of switching off the MaiaMicrowave:
	say "If Rosa wants to overcook her popcorn, that's her prerogative."

Instead of touching MaiaMicrowave:
	say "You run your hand over the logo."

[Blackmail is a quip. The preview of Blackmail is "On Wednesday, Rosalita told Maia says that Danny was trying to blackmail her."]

MaiaRosalita is a person. MaiaRosalita is in MaiaBreak. The printed name of MaiaRosalita is "Rosa". The description of MaiaRosalita is "Rosalita is wearing a pantsuit with soft mint boots.  Her curled hair spills over one shoulder." Understand "rosa" or "pantsuit" or "hair" or "boots" or "shoulder" or "rosalita" as MaiaRosalita.

Report switcherooing Maia:
	say "Rosa punches a button a couple of times, then pounds the vending machine with her fist. The machine whirrs, and the popcorn packet thuds against the bottom of the slot, and she grabs it. She pops open the microwave and tosses in the packet. She turns on the microwave and sits at the table, looking at you expectantly. 'Well?'[if talkinghelpcounter is 0][line break][bracket]Use TALK TO [bracket]someone[close bracket] to talk during flashbacks.[close bracket][line break][end if]"
	

MaiaRosacounter is a number that varies. MaiaRosaCounter is 0.


TalkingHelpCounter is a number that varies. TalkingHelpCounter is 0.

Before talking to someone:
	if persona is not a speaker:
		now TalkingHelpCounter is TalkingHelpCounter plus one;

Instead of talking to MaiaRosalita:
	now MaiaRosaCounter is MaiaRosaCounter plus one;
	if MaiaRosaCounter is 1:
		say "'Hey Rosa, you know how hard it's been to get any recording ti-'
	
	'You can't use it.'
	
	'What do you mean?'
	
	'You can't use the studio.'
	
	You frown. This not good. Maybe you should try again.";
	if MaiaRosaCounter is 2:
		say "'Rosa, please.'
		
		The microwave whirrs quietly.
		
		'Rosa, this important. I don't even need a tech, I can do it mys-'
		
		Rosalita stands and glares at you. 'Maia, I have a real job to do. That's something you wouldn't know about, right? Do you have any idea what could happen to me if I let you screw around with the recording equipment? The audio mixer alone costs more money than you've made in your entire life. Go home and get a real job.' She stands up and walks out of the room.";
		now MaiaRosalita is in HoldingCell.

Before going east from MaiaBreak when MaiaRosalita is in MaiaBreak:
	say "You've got to try to convince her. Talk to her." instead;

SawCindy is a number that varies. SawCindy is 0.

After going east from MaiaBreak:
	now SawCindy is 1;
	say "As you exit the break room, you notice an odd smell from behind you. Is that...burning? You turn around, but someone rushes into and knocks you over!
	
	'Oh, I'm so sorry!' you hear someone say. You roll over to see the secretary standing above you.";
	now MaiaCindy is in MaiaLobby;
	now the player is on the MaiaFloor;

FloorCounter is a number that varies. FloorCounter is 0.

Every turn when the player is on the MaiaFloor:
	if FloorCounter is at least 1:
		try talking to MaiaCindy;
	now FloorCounter is FloorCounter plus one;

MaiaCindy is a person. MaiaCindy is in HoldingCell. The printed name of MaiaCindy is "Cindy". The description of MaiaCindy is "It's that secretary again. She stands here with her hands on her hips and a frown on her face." Understand "cindy" or "jalcek" or "secretary" or "receptionist" as MaiaCindy.

Instead of talking to MaiaCindy:
	try getting off the MaiaFloor.

Instead of going a direction when the player is on MaiaFloor:
	try getting off the MaiaFloor.

Report getting off the MaiaFloor:
	say "'Oh,' she says. 'It's you.' She frowns, but helps you up. 'Rosalita said to make sure you left the station. I'm sorry, but you're going to have to go; I just have to take care of something real quick.' She runs off to the west.";
	now MaiaCindy is in HoldingCell;

The standard report getting off rule response (A) is "[if the current action is getting off MaiaFloor]You dust yourself off.[otherwise][We] [get] off [the container exited from].[end if]"

Instead of entering the MaiaFloor:
	say "You don't feel like getting on the floor.";

Report switcherooing Maia:
	now SawCindy is 0;
	now FloorCounter is 0;
	
Instead of going west from MaiaLobby:
	say "Cindy's probably still in there; best not to antagonize her."

Section 3 - Lobby

MaiaLobby is a room. The printed name of MaiaLobby is "Lobby". The description of MaiaLobby is "The original ceiling in this comfortable room has been lowered for the installation of office lights and ceiling panels. A huge desk extends from one wall almost to the hallway, a telephone placed on its center. The black door to the hallway is solid, a throwback to times when security and safety mattered. KOLR has its own parking lot, to the [bold type]south[roman type]. The bathroom is to the east and the break room is to the west, but you don't want to even think about the first and that nosy secretary is in the other. The station logo has been stenciled on the wall in faded, tasteful colors." 

The MaiaFloor is a supporter. MaiaFloor is in MaiaLobby. MaiaFloor is scenery. The printed name of MaiaFloor is "floor". Understand "floor" as MaiaMuddyCarpet.

MaiaPrivatedoor is a closed openable door. MaiaPrivatedoor is scenery. Understand "bathroom door" or "bathroom" or "door" as MaiaPrivateDoor. The printed name of MaiaPrivateDoor is "bathroom door".  MaiaPrivatedoor is east from MaiaLobby and west from MaiaPrivatebathroom. The description of MaiaPrivatedoor is "A door with a handicap symbol.". MaiaPrivateBathroom is blocked.

Instead of going east from MaiaLobby:
	say "You don't have to go to the bathroom now, and you need to hurry.";

The Maiafluorescentlights are scenery. The Maiafluorescentlights are in MaiaLobby. Understand "fluorescent" or "dingy" or "ceiling" or "office" or "panels" or  "plastic" or "covers" or "lights" or "light" or "buzz" or "buzzing" or "flickering" as Maiafluorescentlights. The description of Maiafluorescentlights is "The ceiling is a checkerboard of flourescent lights and ceiling panels."

Instead of listening to the Maiafluorescentlights:
	try examining the Maiafluorescentlights;

Instead of touching Maiafluorescentlights:
	say "It's too high!"

Check going nowhere from the MaiaLobby when the noun is east:
	say "That bathroom smells horrible, and you suspect the receptionist is hoarding the toilet paper." instead;

The MaiaStencil is in MaiaLobby. The MaiaStencil is scenery. Understand "colors" or "tasteful" or "logo" or "faded" or "r" or "stencil" or "rust" or "beige" or "washed-out" or "blue" or "green" as MaiaStencil. The description of MaiaStencil is "'Welcome to' is written in precise black letters, while 'KOLR' is painted underneath in a gradually fading sequence of colors, so that the R blends into the studio walls itself, saying that all of this KOLR, that all are welcome. It's genius."

The MaiaFrontDesk is in MaiaLobby. The MaiaFrontDesk is scenery. The printed name of MaiaFrontDesk is "front desk". Understand "front" or "desk" as MaiaFrontDesk. The description of the MaiaFrontDesk is "This massive desk must have taken four people to move. Rosa always like status symbols. 

A phone sits on the desk.". MaiaFrontDesk is a supporter. MaiaFrontDesk is not enterable.

The MaiaPhone is on MaiaFrontDesk. The MaiaPhone is scenery. The printed name of MaiaPhone is "telephone". The description of MaiaPhone is "A vintage ivory rotary telephone; Rosa bought it with you at an antique mall in the suburbs.". Understand "ivory" or "telephone" or "rotary" or "numbers" or "rubbed" as MaiaPhone.

The MaiaFrontChair is in MaiaLobby. The MaiaFrontChair is scenery. The printed name of MaiaFrontChair is "chair". The description of MaiaFrontChair is "This low black chair is pushed up close to the desk."

Instead of taking the MaiaFrontChair:
	say "That's not really necessary."
	
Instead of pushing the MaiaFrontChair:
	say "You're not here to rearrange furniture."
	
Instead of pulling the MaiaFrontChair:
	say "You're not here to rearrange furniture."

MaiaHallDoor is a door.  MaiaHallDoor is scenery. The printed name of MaiaHallDoor is "hallway door". Understand "door" or "black" or "window" or "glass" or "reinforced" or "metal" or "chips" or "paint" or "rusty" or "rust" or "hallway" as MaiaHallDoor. MaiaHallDoor is north of MaiaLobby and south of MaiaHallway. MaiaHallDoor is closed and openable. The description of MaiaHallDoor is "The door to the studios; Rosa rarely lets you down here, if ever. The door looks like Rosa, frowning at you, daring you to pass it." MaiaHallDoor is not lockable.

Instead of listening to MaiaLobby:
	say "You hear a busy hum, like a beehive. It's probably from the lights."

Instead of smelling MaiaLobby:
	say "It smells like age and dust."

MaiaMuddyCarpet is in MaiaLobby. MaiaBeigeWalls are in MaiaLobby. 

MaiaLied is a number that varies. MaiaLied is 0.

Before going north from MaiaLobby:
	if MaiaLied is 0:
		say "You heard Rosa. She doesn't want you here." instead;

Before going south from MaiaLobby:
	if MaiaLied is 1:
		say "You pause at the door. Rosa's in her office now; if you're quick, you can make it to the studio without her noticing." instead;

Section  4 - Parking lot

MaiaParking is south from MaiaLobby. The printed name of MaiaParking is "Parking Lot". Understand "parking lot" as MaiaParking. The description of MaiaParking is "Rosa's studio has its own private parking lot. The sunlight glistens off the dripping bushes. A few cars are parked here and there, but all are empty and no one can be seen. Brick walls point up to an empty sky on either side. The lobby door is [bold type]north[roman type] from here.

You can go [bold type]south[roman type] to take the bus home."

Some Maiacars are in Parking Lot. Some MaiaCars are scenery. The printed name of Maiacars is "cars". The description of Maiacars is "The raindrops on the cars reflect the studio in a hundred ways." Understand "rows" or "cars" or "car" or "raindrop" or "raindrops" or "drops" or "drop" as some Maiacars.

Some bushes are a backdrop. Some bushes are scenery. Some bushes are in CindyParking. Some bushes are in parking lot. Some bushes are in MaiaParking. The description of bushes is "Green bushes line the front of the studio."

Some MaiaBuildings is scenery. Some MaiaBuildings are in MaiaParking. The description of Maiabuildings is "The brick walls of the buildings to either side hem you in, the Meih Lee Chinese restaurant on one side, Wolfe Pawn and Check Cashing on the other." Understand "brick" or "chinese" or "wolfe" or "pawn" or "check" or "cashing" or "wall" or "brick walls" or "buildings" as Maiabuildings.

The dawn sun is a backdrop. The dawn sun is in MaiaParking. The description of dawn sun is "The dawn sun beams brightly over the wet parking lot."

MaiaStudios is scenery. MaiaStudios is in MaiaParking. Understand "KOLR" or "studio" or "lobby" or "door" or "studios" as MaiaStudios. The printed name of MaiaStudios is "KOLR studios". The description of MaiaStudios is "Rosa's studio. How did she end up with everything you ever wanted? And not willing to share it...".

Instead of touching the dawn sun:
	say "How sweet a dream it is, to touch the sun.";

SteppedIn is a number that varies. SteppedIn is 0.

Instead of going nowhere from MaiaParking:
	if the noun is south:
		say "You hail the bus, and go back home.
		
		'And that's the last I saw of Rosa. I can't believe anyone would kill her,' she says, shaking her mane of green hair.";
		try delivering Home;
		try switcherooing cop;
	otherwise:
		say "Where will you go? You need time to think. Maybe you should just go home.";

Home is a quip. The preview of Home is "Maia took the bus home after talking with Rosalita."

DannyResponse of Home is "'Maia said that she just left and took the bus home right after talking to Rosalita, the day she died.'

'Maia says whatever she thinks people want to hear. She's not reliable in the slightest.'

'So you think she was there?'

'I'm not sure if she was there that day. But I'm sure I wouldn't trust her with my life.'"

DannySummary of Home is "Danny is suspicious of Maia."

ChuckResponse of Home is "You say, 'Maia claims that she took the bus home after talking with Rosalita, the morning she died.'

Chuck rubs his eyes and answers, 'I know that that's not true.'

'Oh?' you ask, 'How so?'

He says, 'There was a major parade that day that shut down the streets. No buses were running. Maia is hiding something.'

'You seem very confident about that.'

He frowns irritably. 'Yes; I was supposed to cover the parade that day.'"

After speeching Home to Chuck:
	if Parade is not carried by the player:
		now ParadeGiver is Chuck;
		try delivering Parade;

ChuckSummary of Home is "Chuck said that Maia's story was false."

CindyResponse of Home is "'Maia claims that she took a bus home after you got to work.'

'I really don't think so.'

'Why not?'

[if parade is carried by the player]'That parade shut everything down. There's no way she took a bus.'[otherwise]'Let me give you my statement; it may shed some light on the matter.'[end if]"

CindySummary of Home is "Cindy thought she was lying."

MaiaResponse of Home is "'So you claim that you took a bus home after talking to Rosalita?'[if buses is MeihMaia]

'Yes, I took the bus home right away. I could tell I wasn't wanted there.'

'And what time was that?' you ask.

She says, 'It was right after Cindy got to work, so around then.'

'That's not very specific.'

She shrugs, and says 'I never pay attention to the time. It drives Ro-it drove Rosalita crazy.'
[otherwise]

'Yes, that's what I said, but like I already admitted, it's not true. Okay?'[end if]"

MaiaSummary of Home is "Maia equivocated."

ChiefResponse of Home is "'Maia said that she took a bus home after she got to work.'

'Check the timelines; a lot of these people crossed paths during the last few days. Did anyone see her coming in?'

'I'll ask.'

'Good.'"

ChiefSummary of Home is "The Chief suggested talking to Cindy about this."

Instead of going north from MaiaParking:
	say "You heard her, she won't help you. You should go home."
		
Instead of smelling Maiabuildings:
	try smelling MaiaParking;

Instead of smelling MaiaParking:
	say "You smell a vaguely nauseating smell from one of the nearby buildings.";

Section 5 - Hallway

MaiaHallway is a room. The printed name of MaiaHallway is "Hallway". The description of MaiaHallway is "You hold your breath and step quietly through this hallway. The office to the west looks unused. A red 'On-Air' light is mounted next to the door. The hallway extends north to Rosa's office. The lobby is [bold type]south[roman type] from here.

Rosa's studio lies to the [bold type]east[roman type], behind a heavy black door."

MaiaMuddyCarpet is in MaiaHallway. MaiaBeigeWalls are in MaiaHallway.

A MaiaBlueDoor is a closed openable door. A MaiaBlueDoor is locked. MaiaBlueDoor is west of MaiaHallway and east of MaiaChuckOffice. The printed name of MaiaBlueDoor is "blue door". MaiaBlueDoor is scenery. The description of MaiaBlueDoor is "This blue door is banged up and scratched. A name plate is on the door, but it is currently empty. You vaguely recall that Chuck Lee used this office before.". Understand "battered" or "pleasant" or "blue" or "door" or "gouges" or "deep" or "scratches" or "banged" or "scratched" or "window" as MaiaBlueDoor. MaiaChuckOffice is blocked.

The MaiaChuckPlate is part of the MaiaBlueDoor. The MaiaChuckPlate is scenery. The printed name of MaiaChuckPlate is "name plate". Understand "empty" or "name" or "plate" or "nameplate" as MaiaChuckPlate. The description of MaiaChuckPlate is "The name plate is empty."

The MaiaAirLight is scenery. The printed name of MaiaAirLight is "'On-Air' light". Understand "on" or "air" or "light" or "on-air" or "red" as MaiaAirLight. The MaiaAirLIght is in MaiaHallway. The description of MaiaAirLight is "This must be the light they use to show when the studio is in use. It's currently turned off.".

The MaiaStudioDoor is a door. The MaiaStudioDoor is scenery. The MaiaStudioDoor is east of MaiaHallway and west of MaiaStudio. MaiaStudioDoor is closed. The printed name of MaiaStudioDoor is "studio door". The description of MaiaStudioDoor is "A thick, heavy green door with 'studio' painted on it in white." Understand "green" or "thick" or "heavy" or "door" or "studio door" as MaiaStudioDoor.

MaiaHallway2 is north from MaiaHallway. MaiaHallway2 is blocked.

Instead of going north from MaiaHallway:
	say "You don't want Rosa to see you!";

Report going east from MaiaHallway:
	now MaiaStudioDoor is closed;
	say "You close the studio door behind you."

Section 6 - Studio

The printed name of MaiaStudio is "Studio". The description of MaiaStudio is "This studio is incredible, decorated in gorgeous greens. The walls are specially built to provide the perfect sound. The microphones hanging from the ceiling are incredibly expensive, much better than the ones you use at Saul's cousin's place. You're not too crazy about the table and chairs, given that you're not doing a radio show, but they won't be a big issue. The soundproof door to the west leads to the hallway, where Rosa might be lurking. The control room is to the [bold type]east[roman type]."

The MaiaStudioWindows are in MaiaStudio. The MaiaStudioWindows are scenery. The printed name of MaiaStudioWindows is "bank of windows". Understand "bank" or "control" or "room" or "window" or "windows" or "intricate" or "electronics" or "electronic" or "soundproof" or "darkened" or "racks" or "tangles" or "tangle" or "wire" or "wires" or "audio" or "console" as MaiaStudioWindows.  The description of MaiaStudioWindows is "You press against the windows, looking through to the control room. You could never afford this much equipment, not even half of it."

The MaiaHardwoodFloor is in MaiaStudio. The MaiaHardwoodFloor is scenery. The description of  MaiaHardwoodFloor is "The hardwood floor is raised up several inches to boost the acoustics, and is covered in dark green carpet. The studio you've been using just plops you on a concrete floor."

The MaiaStudioWalls are scenery. The MaiaStudioWalls are in MaiaStudio. Understand "wall" or "walls" or "thicker" or "thick" as MaiaStudioWalls. The printed name of the MaiaStudioWalls is "studio walls". The description of the MaiaStudioWalls is "Wow, they actually built the walls askew, to keep down the echoes. This incredible!".

Instead of touching the MaiaStudioWalls:
	say "The wall feels smooth, while the boards feel soft.".

Instead of touching the MaiaStudioBoards:
	say "The green fabric is soft as you run your hand across it."

The MaiaStudioBoards are scenery. The MaiaStudioBoards are part of the MaiaStudioWalls. Understand "fabric" or "covered" or "fabric-covered" or "board" or "boards" as MaiaStudioBoards. The printed name of the MaiaStudioBoards is "fabric-covered boards". The description of the MaiaStudioBoards is "Soft green fabric covers a few scattered boards, deadening the sound in the room."

Instead of looking under the MaiaHardwoodFloor:
	say "You can't get a good look, considering the floor is in the way."

Instead of listening to MaiaStudio:
	say "The perfect silence fills you with peace."
	
The MaiaStudioTable is in MaiaStudio. The MaiaStudioTable is scenery. The printed name of the MaiaStudioTable is "table". Understand "single" or "table" or "gouges" or "hardwood" or "indentations" or "gouges" or "scratches" or "surface" or "center" as MaiaStudioTable. The description of MaiaStudioTable is "This hardwood table stretches across much of the room, with chairs to either side. Hopefully it won't mess up the acoustics." The MaiaStudioTable is a supporter.

The MaiaStudioChairs are in MaiaStudio. The MaiaStudioChairs are scenery. The printed name of the MaiaStudioChairs is "chairs". The MaiaStudioChairs are plural-named. Understand "chair" or "chairs" as MaiaStudioChairs. The description of MaiaStudioChairs is "These dingy chairs aren't worthy of the studio they're in." The MaiaStudioChairs are a supporter. The MaiaStudioChairs are enterable.

Report entering MaiaStudioChairs:
	say "You sit on a chair."

Instead of pushing the MaiaStudioChairs:
	say "You don't need to push the chairs around."

Understand "swivel" as turning.

Instead of turning the MaiaStudioChairs:
	say "You swivel the chairs back and forth; it's actually quite fun. They're surprisingly silent."

The MaiaStudioCeiling is scenery. The MaiaStudioCeiling is in MaiaStudio. The printed name of the MaiaStudioCeiling is "ceiling". Understand "acoustic" or "panels" or "panel" or "ceiling" as MaiaStudioCeiling. The description of the MaiaStudioCeiling is "The low ceiling is covered by green acoustic panels."

The MaiaMicrophones are scenery.The MaiaMicrophones are part of the MaiaStudioCeiling. The printed name of the MaiaMicrophones is "microphones". Understand "microphone" or "microphones" as MaiaMicrophones. The description of MaiaMIcrophones is "Microphones droop from the ceiling to the table, patiently waiting for their next use."

Instead of talking to MaiaMicrophones:
	try singing;

Instead of using MaiaMicrophones:
	try singing;

Instead of pushing MaiaMIcrophones:
	say "You push the microphone. Its well-oiled mechanisms glide silently."

Instead of pulling MaiaMicrophones:
	say "You push the microphone. Its well-oiled mechanisms glide silently.".

Instead of switching on the MaiaMicrophones:
		say "You have to turn on the microphone in the control room.";

The MaiaControlDoor is a door. The MaiaControlDoor is scenery. The MaiaControlDoor is east of MaiaStudio and west of MaiaControl. The printed name of the MaiaControlDoor is "control room door". Understand "control" or "room" or "door" or "slim" as MaiaControlDoor. The description of MaiaControlDoor is "This slim door leads to the control room, guarding the expensive equipment stored inside.".

Instead of going west from MaiaStudio:
	say "You don't want to leave yet, not while you can still record!"

Section 7 - Control Room

MaiaAlarm is a number that varies. MaiaAlarm is 0.

Report switcherooing Maia:
	now MaiaRosaCounter is 0;
	now the noise gate is powered;
	now the expander is not powered;
	now the limiter is not powered;
	now the compressor is not powered;
	now the delay is not powered;
	now the harmonizer is not powered;
	now the modulation is not powered;
	now the record button is not depressed;
	now the playback button is not depressed;
	now receipts are in HoldingCell;
	now MaiaAlarm is 0;
	now DidSing is 0;
	now LetsGo is 0;
	now the session tapes are in MaiaControl;
	

The printed name of MaiaControl is "Control Room". The description of MaiaControl is "This small room is packed with the best equipment available. The original plaster ceilings and wainscoted walls distinguish this room from the dumpy studios you've been using. A large patch bay in the corner is surrounded by a nest of wires."

The patch bay is in MaiaControl. The patch bay is scenery. The description of the patch bay is "This device and the wires flowing in and out of it are used to decide what inputs end up in the console. It seems to be set up just fine for a vocal solo."

Instead of taking the patch bay:
	say "You don't want to mess with the setup."

The stack of receipts are in HoldingCell. The description of the stack of receipts is "[if the stack of receipts is not carried by the player]A stack of receipts? What are they doing here? You pick them up. [end if]This is odd... You flip through the stack, kicking dust into the air. These receipts are for payments to KOLR. Why would Rosa keep them here?" Understand "something" as the stack of receipts.

Instead of dropping the stack of receipts:
	say "You don't want to leave evidence!";

Report examining the stack of receipts:
	now the stack of receipts is carried by the player;
	try delivering Hidden;
	now MaiaAlarm is 1;

After taking the stack of receipts:
	if MaiaAlarm is 0:
		try examining the receipts.

CopsComing is a recurring scene. CopsComing begins when MaiaAlarm is 1. CopsComing ends when persona is cop.

When CopsComing begins:
	say "You hear a siren wailing, far away but steadily approaching. Oh no... someone must have seen you. You have to get out of here!"
	
Instead of going west during CopsComing:
	say "Whoever called the cops on you is out there. You should go out the back way!";

Instead of listening to MaiaControl during CopsComing:
	say "You can hear sirens getting closer and closer."

Hidden is a quip. The preview of HIdden is "Maia found a bunch of receipts hidden in the studio."

DannyResponse of Hidden is "[if Missing is MeihDanny]'Mr. Kostopolous, do you have any idea why a stack of receipts for payments to KOLR would be hidden in the control room?'

He glowers at you. 'What do I look like, a janitor? It's none of my business if some idiot is stuffing paper in corners.' But you notice that his fist clenches for a moment as you ask.[otherwise]'Why were those receipts hidden in the control room?'

'I honestly don't know who would have put them there, it's bizarre.'";

DannySummary of Hidden is "Danny seemed nervous about the receipts.";

ChuckResponse of Hidden is "You say, 'Chuck, apparently the receipts for a very large number of payments to KOLR have been discovered in the studio recently. Do you know why they weren't filed with the others?'

Chuck furrows his brow, thinking. He shakes his head. 'No, well...Sorry, it's just so hard to think right now....the receipts...no, it's just so odd. You know, Danny always said the filing system here was perfect. He'd go nuts if you told him this.[if Fraud is ChuckGiven]I'm sure it's related to the fraud you discovered, though.[end if]'"

ChuckSummary of Hidden is "Chuck didn't know what happened to the money."

CindyResponse of Hidden is "'Apparently, a large stack of receipts for payments to KOLR were found stuffed in the control room of the studio.'

'What? Receipts? Who was in the control room?'

'That's not what I'm talking about right now. Do you know about these receipts?'

'No, but if it was Maia in the control room, I'm going to be extremely angry. Was it her?'

'Just tell me about the receipts.'

'I don't know anything about them. Rosalita usually handles them.'

'Is she the only one?'

'Danny always worked on the files, too; said he had the perfect system. I bet he's kicking himself now.'"

CindySummary of Hidden is "Cindy didn't know anything about the receipts."

MaiaResponse of HIdden is "'Tell me about the receipts again.'

'You see, I was just trying to get the session tapes out, and I pulled something too hard, and the whole console shifted.

'And the receipts were behind the console?'

She says 'Yes, that's exactly what happened.'

'Where are they now?'

'I tossed them in a dumpster when I was running. I wouldn't know which one.'"

Understand "plug in" or "unplug" as messing. Messing is an action applying to one thing.

MaiaSummary of Hidden is "Maia said she found the receipts, then tossed them in a dumpster."

ChiefResponse of Hidden is "'Maia said she found a stack of receipts for paments to KOLR hidden away.'

'That's fairly serious.'

'I'm not sure; they may have just been misplaced,' you say. 

'Find out. If I were you, I'd ask Danny about it. He's involved in all of the finances, so he should know what's going on.'"

ChiefSummary of Hidden is "The Chief suggested talking to Danny about this."

Carry out messing:
	if the noun is the patch bay:
		say "You don't want to mess with that.";
	otherwise if the noun is CindyFan:
		say "It's battery powered; you can just turn it on.";
	otherwise:
		say "That doesn't have a plug.".

The audio console is in MaiaControl. "You see the audio console here, filling up a third of the room." The description of the audio console is "This console is top of the line, with hundreds of switches and knobs. You could record anything in this studio; string ensemble, barbershop quartet, orchestra... well, if you could fit them in. You recognize a few of them: noise gate, expander, limiter, compressor, delay, harmonizer, modulation.

You also see a record button, a stop button, and a playback button. The session tapes are spooled here."

Instead of taking the audio console:
	say "You can't take that!";

The emergency door is a door. The emergency door is east of MaiaControl and west of FinishedCell. "There is an emergency door to the outside here." Understand "fire" as emergency door.

Instead of going east from MaiaControl:
	if MaiaAlarm is 0:
		say "You don't want to set off the alarm.";
	otherwise:
		say "You're out of here!
		
		'And so I left. I found out later that day that Rosalita was dead. I didn't tell the police because I knew how bad it looked.'
		
		'Do you still have the receipts?'
		
		'No, I tossed them in a dumpster in an alley. I don't even remember where they are.'";
		try switcherooing cop;
		now MaiaAlarm is 0;

A knob is a kind of thing. A knob is scenery. A knob can be powered or not powered. A knob is usually not powered.

Instead of switching on a knob:
	say "You turn on the [noun].";
	now the noun is powered;

Instead of switching off a knob:
	say "You turn off the [noun].";
	now the noun is not powered.

Instead of turning a knob:
	if the noun is powered:
		try switching off the noun;
	otherwise:
		try switching on the noun;

Instead of pushing a knob:
	try turning the noun;

The noise gate is in MaiaControl. The noise gate is a knob. The description of noise gate is "The noise gate allows you cut off all sound beneath a certain level. The radio shows here like to use it to keep the sound more pure, but you like the gritty details in the background of your recordings.[if the noise gate is powered] It is currently turned on.[otherwise] It is currently turned off.[end if]". The noise gate is powered.

The expander is in MaiaControl. The expander is a knob. The description of the expander is "The expander is a lot like the noise gate, but instead of cutting off the all sound below a threshold, it just dampens it. You've considered using it, but you're still debating whether you like it or not.[if the expander is powered] It is currently turned on.[otherwise] It is currently turned off.[end if]"

The limiter is in MaiaControl. The limiter is a knob. The description of the limiter is "The limiter is basically the opposite of a noise gate; it keeps the sound on the channel from getting past a certain amplitude. You are singing solo today, and you don't really care one way or another about the limiter.[if the limiter is powered] It is currently turned on.[otherwise] It is currently turned off.[end if]"

The compressor is in MaiaControl. The compressor is a knob. The description of the compressor is "The compressor is the opposite of an expander. When sound gets over a certain limit on the input channel, it lowers the amplitude a bit to keep it from getting out of control. It's not as strict as the limiter. You can use it if you feel like.[if the compressor is powered] It is currently turned on.[otherwise] It is currently turned off.[end if]". Understand "compresser" as compressor.

The delay is in MaiaControl. The delay is a knob. The description of the the delay is "The delay can be used to produce an echo effect. You tried it once and liked it, but you're not sure if it fits your new track.[if the delay is powered] It is currently turned on.[otherwise] It is currently turned off.[end if]". Understand "echo" as delay.

The harmonizer is in MaiaControl. The harmonizer is a knob. The description of the harmonizer is "This will take your vocals and play them back at a different pitch, combining the new version with the old version to create harmony. You've had some trouble with harmonizers before, but you can give it a go if you like. [if the harmonizer is powered] It is currently turned on.[otherwise] It is currently turned off.[end if]" 

The modulation is in MaiaControl. The modulation is knob. The description of the modulation is "You can use this to enable a 'wa wa' effect if you like.[if the modulation is powered] It is currently turned on.[otherwise] It is currently turned off.[end if]". Understand "modulator" or "wa" as the modulation.

A thing can be depressed or not depressed. A thing is usually not depressed.

The record button is in MaiaControl. The record button is scenery. The description of the record button is "[if the record button is not depressed]You can push record to start recording.[otherwise]You are currently recording.[end if]"

The playback button is in MaiaControl. The playback button is scenery. Understand "play" as playback button. The description of the playback button is "You can push playback to hear your recording."

The stop button is in MaiaControl. The stop button is scenery. The description of the stop button is "You can press stop to stop recording."

Happenings is a list of text that varies;

Singularechotext is some text that varies. Singularechotext is "[if delay is powered] that echoes.[otherwise].[end if]"

Pluralechotext is some text that varies. Pluralechotext is "[if delay is powered] that echo.[otherwise].[end if]"

[fix the multiple silences thing]

SilenceHappened is a number that varies. SilenceHappened is 0.

SilenceText is some text that varies. SilenceText is "The tape is quiet for a moment."

Every turn when the record button is depressed:
	if the player is in MaiaControl:
		if the current action is going west from MaiaControl:
			if noise gate is powered:
				if SilenceHappened is 0:
					add "[SilenceText]" to Happenings;
					now SilenceHappened is 1;
			otherwise:
				add "[if limiter is powered]You hear some faint sounds[Pluralechotext][otherwise]You hear movement[Singularechotext][end if]" to Happenings;
				now SilenceHappened is 0;
		otherwise:					
			if SilenceHappened is 0:
				add "[SilenceText]" to Happenings;					
				now SilenceHappened is 1;
	otherwise if the player is not in MaiaStudio:
		add "There is silence for a moment." to Happenings;
	otherwise:
		if the current action is jumping:
			if noise gate is powered:
				if SilenceHappened is 0:
					add "[SilenceText]" to Happenings;
					now SilenceHappened is 1;
			otherwise:
				add "[if limiter is powered]You hear some faint sounds[Pluralechotext][otherwise]There is a dull thud[Singularechotext][end if]" to Happenings;
				now SilenceHappened is 0;
		otherwise if the current action is going a direction:
			if noise gate is powered:
				if SilenceHappened is 0:
					add "[SilenceText]" to Happenings;
					now SilenceHappened is 1;
			otherwise:
				add "[if noise gate is powered]You hear silence.[otherwise if limiter is powered]You hear some faint sounds[pluralechotext][otherwise]You hear some shuffling about[Singularechotext][end if]" to Happenings;
				now SilenceHappened is 0;
		otherwise if the current action is entering something:
			if noise gate is powered:
				if SilenceHappened is 0:
					add "[SilenceText]" to Happenings;
					now SilenceHappened is 1;
			otherwise:
				add "[if limiter is powered]You hear some faint sounds[pluralechotext][otherwise]You hear some dull sounds[pluralechotext][end if]" to happenings;
				now SilenceHappened is 0;
		otherwise if the current action is getting off something:
			if noise gate is powered:
				if SilenceHappened is 0:
					add "[SilenceText]" to Happenings;
					now SilenceHappened is 1;
			otherwise:
				add "[if limiter is powered]You hear some faint sounds[pluralechotext][otherwise]You hear more movement[Singularechotext][end if]" to happenings;
				now SilenceHappened is 0;
		otherwise if the current action is dancing:
			if noise gate is powered:
				if SilenceHappened is 0:
					add "[SilenceText]" to Happenings;
					now SilenceHappened is 1;
			otherwise:
				add "[if limiter is powered]You hear some faint sounds[pluralechotext][otherwise]You hear more movement[Singularechotext][end if]" to happenings;
				now SilenceHappened is 0;
		otherwise if the current action is locking keylessly:
			if noise gate is powered:
				if SilenceHappened is 0:
					add "[SilenceText]" to Happenings;
					now SilenceHappened is 1;
			otherwise:
				add "[if limiter is powered]You hear some faint sounds[pluralechotext][otherwise]You hear a clicking sound[Singularechotext][end if]" to happenings;	
				now SilenceHappened is 0;
		otherwise if the current action is unlocking keylessly:
			if noise gate is powered:
				if SilenceHappened is 0:
					add "[SilenceText]" to Happenings;
					now SilenceHappened is 1;
			otherwise:
				add  "[if limiter is powered]You hear some faint sounds[pluralechotext][otherwise]You hear a clicking sound[Singularechotext][end if]." to happenings;	
				now SilenceHappened is 0;
		otherwise if the current action is humming:
			add "You hear yourself humming [one of]a short refrain[or]a rocking bridge[or]a bluegrass medley[or]a show tune[or]an advertisement jingle[at random][if delay is powered] that echoes[end if]." to happenings;
			now DidSing is 1;
			now SilenceHappened is 0;
		otherwise if the current action is praying:
			add "You hear yourself praying.[if delay is powered] It echoes[end if]." to happenings;
			now SilenceHappened is 0;
		otherwise if the current action is singing:
			add "You hear yourself singing [one of]some jazz scat[or]a rock ballad[or]a heartfelt alternative song[or]a show tune[or]an advertisement jingle[at random][if delay is powered] that echoes[end if]." to happenings;
			now DidSing is 1;
			now SilenceHappened is 0;
		otherwise if the current action is screaming:
			if limiter is powered:
				add "You hear a scream that is cut off." to happenings;
			otherwise:
				add "[if the compressor is powered]You hear a strangely muted scream.[otherwise]You hear yourself screaming.[end if][if delay is powered] It echoes.[end if]" to happenings;
			now SilenceHappened is 0;
		otherwise if the current action is shouting:
			if limiter is powered:
				add "You hear nothing." to happenings;
			otherwise:
				add "[if the compressor is powered]You hear a strangely muted shout.[otherwise]You hear yourself shouting.[end if][if delay is powered] It echoes.[end if]" to happenings;
			now SilenceHappened is 0;
		otherwise if the current action is waiting:
			if SilenceHappened is 0:
				add "[SilenceText]" to Happenings;
				now SilenceHappened is 1;
		otherwise:
			add "You hear some random noise." to happenings;
			now SilenceHappened is 0;
			
DidSing is a number that varies. DidSing is 0.

Instead of jumping when persona is Maia:
	say "You jump high, your green hair floating around you for a moment.";

Instead of pushing the record button:
	now the record button is depressed;
	say "The microphones in the studio are now recording; you'll have to get out of the control room.";
	now Happenings is {"The recording starts."};
	now DidSing is 0; 
	now LetsGo is 0;
	now SilenceHappened is 0;

Instead of pushing the stop button:
	if the record button is depressed:
		say "You stop the recording.";
		if harmonizer is powered:
			add "All of the sound is in perfect harmony." to happenings;
		if modulation is powered:
			add "All of the sound is distorted by a 'wa wa' effect." to happenings;
		add "The recording stops.[line break]" to happenings;
		now the record button is not depressed;
	otherwise:
		say "You aren't currently recording.";

After switching off the modulation:
	add "All of the previous sound had a 'wa wa' effect, but now it turns off." to Happenings;

After switching off the harmonizer:
	add "All of the previous sounds were in close harmony, but now the harmony turns off." to Happenings;

After switching on the modulation:
	add "A 'wa wa' effect kicks in." to Happenings;

After switching on the harmonizer:
	add "A harmonizing effect begins." to Happenings;

LetsGo is a number that varies. LetsGo is 0.

Understand "tape" as session tapes.

Instead of pushing the playback button:
	if session tapes are in MaiaControl:
		if the record button is depressed:
			try pushing the stop button;
		repeat with currentsentence running through Happenings:
			say "[currentsentence] ";
		if DidSing is 1:
			say "[line break]That take sounded great! You should grab the tapes and get out of here.";
			now LetsGo is 1;
		otherwise:
			say "Hmm... you need a recording that has you singing in it.";
	otherwise:
		say "The tapes aren't in."

The session tapes are in MaiaControl. "The session tapes are spooled here.". The description of the session tapes is "[if LetsGo is 0]The tape doesn't have a good recording yet.[otherwise]The tape has a recording of a great session.[end if]"

Before taking the session tapes:
	if LetsGo is 0:
		say "You haven't recorded a good session yet." instead;
	otherwise if the stack of receipts is in HoldingCell:
		now the session tapes are carried by the player;
		say "You go to grab the tapes, but they seem stuck. You yank hard, and the whole console shifts, knocking you to the floor. You rise and dust yourself off, holding the tapes.
		
		You seem to have dislodged something from behind the console.";
		now the stack of receipts is in MaiaControl instead;		

Backlooking is an action applying to one thing. Understand "look behind [something]" as backlooking. 

Carry out backlooking:
	say "You don't see anything there right now."

Instead of dropping the session tapes:
	say "You don't want to lose this.";

Before switcherooing cop:
	now the record button is not depressed;
	now happenings is {"Silence."};

Humming is an action applying to nothing. Understand "hum" as humming.

Carry out humming:
	if persona is Maia:
		say "You hum one of your favorite pieces.";
	otherwise:
		say "You hum tunelessly."

Dancing is an action applying to nothing. Understand "dance" as dancing.

Carry out dancing:
	if persona is cop:
		say "You haven't danced since Jayla died.";
	otherwise if persona is Danny:
		say "You're getting too old to dance.";
	otherwise if persona is Chuck:
		say "You're feeling too sad to dance.";
	otherwise if persona is Cindy:
		say "You've got more important things to do!";
	otherwise if persona is Maia:
		say "You dance on the spot.";		
	otherwise if persona is the shadowy figure:
		say "This is a really bad time to dance.";		

Singing is an action applying to nothing. Understand "sing" as singing.

Carry out singing:
	if persona is Maia:
		if the player is in MaiaControl:
			say "You sing with all your heart, but there are no microphones in the control room.";
		say "[one of]You belt out a song with all your heart.[or]You reach within yourself and burst out in song.[or]You sing with all your heart, tears pouring from your eyes.[or]You hit the rafters with your voice, shaking the windows.[then at random]";
	otherwise:
		say "You don't really feel like singing.";

Screaming is an action applying to nothing. Understand "scream" as screaming.

Carry out screaming:
	if persona is Maia:
		if the player is in MaiaStudio:
			say "You let out out an ear-piercing scream";
		otherwise:
			say "You don't want them to hear you!";
	otherwise if persona is Cindy:
		if FoundRosalita is 1:
			say "You scream loudly, overcome with fear.";
		otherwise:
			say "Why would you scream?";
	otherwise:
		say "That would hurt your throat.";			


Understand the command "shout" as something new. Shouting is an action applying to nothing. Understand "shout" as shouting.

Carry out shouting:
	if persona is Maia:
		if the player is in MaiaStudio:
			say "You shout out the chorus to a song.";
		otherwise:
			say "You don't want them to hear you!";
	otherwise if persona is Cindy:
		if FoundRosalita is 1:
			say "You shout loudly, overcome with fear.";
		otherwise:
			say "Why would you shout?";
	otherwise:
		say "That would hurt your throat.";			

Praying is an action applying to nothing. Understand "pray" as praying.

Carry out praying:
	if persona is cop:
		say "You don't pray much, but it seems as good a time as any. You bow your head for a moment before moving on.";
	otherwise if persona is Danny:
		say "Kyrie Iesou Christe, Huie tou Theou, eleeson me ton amartolon.";
	otherwise if persona is Chuck:
		say "You don't really feel like praying.";
	otherwise if persona is Cindy:
		if FoundRosalita is 1:
			say "You can't really think of anything to pray for right now.";
		otherwise:
			say "You pray that the killer won't come after you.";
	otherwise if persona is Maia:
		say "You've always taken life in your own hands.";
	otherwise:
		say "This isn't really a heavenly affair you're involved in."

Chapter 11 - The speakers and radio

The copradioarea is a region. CopHallway and CopHallway2 and CopLobby are in copradioarea. 

ChuckRadioArea is a region. ChuckHallway and ChuckLobby are in ChuckRadioArea. 

DannyRadioArea is a region. DannyHallway2 and DannyHallway are in DannyRadioArea.

CindyRadioArea is a region. CindyHallway2 and CindyHallway and CindyLobby are in CindyRadioArea. 

MaiaRadioArea is a region. MaiaLobby and MaiaHallway are in Maiaradioarea.

Speakersarea is a region. The copRadioarea and chuckradioarea and dannyradioarea and DannyLobby and cindyradioarea and maiaradioarea are in Speakersarea.

The small beige speakers are scenery. The small beige speakers are a backdrop. The small beige speakers are in speakersarea. The description of the small beige speakers is "Small beige speakers are hidden unobtrusively in the ceiling." Understand "radio" as the small beige speakers.

Instead of touching the small beige speakers:
	say "The speakers are too high up to touch.";

Instead of tasting the small beige speakers:
	say "The speakers are too high up to touch.";

Instead of rubbing the small beige speakers:
	say "The speakers are too high up to touch.";

Instead of switching on the small beige speakers:
	say "You don't see the controls anywhere."

Instead of switching off the small beige speakers:
	say "You don't see the controls anywhere."

Instead of listening to the small beige speakers:
	say "You listen carefully to the radio."
	
Instead of listening to a room in SpeakersArea:
	if the noun is DannyLobby:
		say "You hear Cindy talking, drowning out the radio.";
	otherwise:
		say "You hear the radio in the background.";

Section 1 - Cop Radio

CopRadio is a list of text that varies. CopRadio is {"I Love Rock And Roll", "[advert]", "We Got The Beat", "[advert]", "Through The Years", "[advert]", "Rapture", "[advert]", "Against the Wind", "[advert]", "Too Much Heaven", "[advert]", "MacArthur Park", "[advert]", "Think of Laura", "[advert]", "Time Will Reveal", "[advert]", "Round and Round", "[advert]", "Self Control"}.

CopCounter is a number that varies. CopCounter is 1.

Every turn when the player is in copradioarea:
	say "[first time]Some small speakers in the ceiling are tuned to KOLR. [only]From the speakers, you hear the radio quietly playing [entry copcounter of CopRadio].[line break]";
	
Every turn when persona is cop:
	if a random chance of one in three succeeds:
		now CopCounter is CopCounter plus one;
		if CopCounter is greater than the number of entries in CopRadio:
			now CopCounter is 1.

Section 2 - Danny Radio

DannyRadio is a list of text that varies. DannyRadio is {"Sister Christian", "[advert]", "Footloose", "[advert]", "Beat It", "[advert]", "Sweet Dreams", "[advert]", "Little Red Corvette", "[advert]", "Eye of the Tiger", "[advert]", "Call Me"}.

DannyCounter is a number that varies. DannyCounter is 1.

Every turn when the player is in Dannyradioarea:
	say "The radio is playing [entry dannycounter of dannyRadio].[line break]";
	
Every turn when persona is Danny:
	if a random chance of one in three succeeds:
		now DannyCounter is DannyCounter plus one;
		if DannyCounter is greater than the number of entries in DannyRadio:
			now DannyCounter is 1.
			
Section 3 - Chuck Radio

ChuckRadio is a list of text that varies. ChuckRadio is {"When Doves Cry", "[advert]", "I Guess That's Why They Call It The Blues", "[advert]", "Total Eclipse of the Heart", "[advert]", "Mr Roboto", "[advert]", "Waiting For a Girl Like You", "[advert]", "Even The Nights Are Better", "[advert]", "Lost in Love", "[advert]", "She's Out of My Life", "[advert]", "Sad Eyes"}.

ChuckCounter is a number that varies. ChuckCounter is 1.

Every turn when the player is in Chuckradioarea:
	say "The radio is playing [entry chuckcounter of chuckRadio].[line break]";

Every turn when persona is Chuck:
	if a random chance of one in three succeeds:
		now ChuckCounter is ChuckCounter plus one;
		if ChuckCounter is greater than the number of entries in ChuckRadio:
			now ChuckCounter is 1.

Section 4 - Maia Radio

MaiaRadio is a list of text that varies. MaiaRadio is {"Owner of a Lonely Heart", "[advert]", "What's Love Got to Do With It", "[advert]", "Every Breath You Take", "[advert]", "Maneater", "[advert]", "Why Do Fools Fall In Love", "[advert]", "Bette Davis Eyes", "[advert]", "Hold Me Now", "[advert]", "Say Say Say", "[advert]", "I've Been Waiting for a Girl Like You"}.

MaiaCounter is a number that varies. MaiaCounter is 1.

Every turn when the player is in Maiaradioarea:
	say "The radio is playing [entry maiacounter of maiaRadio].[line break]";

Every turn when persona is Maia:
	if a random chance of one in three succeeds:
		now MaiaCounter is MaiaCounter plus one;
		if MaiaCounter is greater than the number of entries in MaiaRadio:
			now MaiaCounter is 1.

Section 5 - Cindy Radio

CindyRadio is a list of text that varies. CindyRadio is {"Girls Just Want to Have Fun", "[advert]", "Uptown Girl", "[advert]", "She Works Hard For The Money", "[advert]",  "Do You Really Want to Hurt Me", "[advert]",
"I Can't Go For That", "[advert]", "the theme from Ghostbusters", "[advert]", "Karma Chameleon", "[advert]", "Harden My Heart", "[advert]", "Rosanna"}.

CindyCounter is a number that varies. CindyCounter is 1.

Every turn when the player is in Cindyradioarea:
	say "The radio is playing [entry cindycounter of cindyRadio].[line break]";

Every turn when persona is Cindy:
	if a random chance of one in three succeeds:
		now CindyCounter is CindyCounter plus one;
		if CindyCounter is greater than the number of entries in CindyRadio:
			now CindyCounter is 1.

Section 6 - Ads

To say advert:
	say "an ad for [one of]the Apple IIc[or]Infocom's The Hitchhiker's Guide to the Galaxy[or]Infocom's Sorcerer[or]Infocom's Seastalker[or]Infocom's Suspect[or]'Who Killed the Robins Family?'[no line break][or]'The Aquitane Progression'[or]'Full Circle'[or]'Pet Sematary'[or]'Ghostbusters'[or]'Gremlins'[or]'The Terminator'[or]'Indiana Jones and the Temple of Doom'[or]'Amadeus'[or]'A Nightmare on Elm Street'[or]robot action figures[or]a trivia board game[or]kid's dolls[or]Ronald Reagan[or]Walter Mondale[or]the CD-ROM[or]the Macintosh[or]Matlab[or]Jeopardy[or]Who's the Boss[or]Karate lessons[or]The Very Best of An Evening With Rosa (boxed set)[or]leg warmers[or]The Danny Kostopolous Show[or]WESL weather alerts[in random order]"

Part 3 - Quip linkages

Chapter 1 - Linkage rules

Conversation is a quip. The preview of Conversation is "Chuck said that he didn't talk to Morales, but Danny claims that they did talk.".

Buses is a quip. The preview of Buses is "Maia said she took a bus home, but Cindy said the buses weren't running.".

[Entry is a quip. The preview of Entry is "Cindy's statement that the windows were locked contradicts the police report.";]

[Ignorance is a quip. The preview of Ignorance is "Cindy claims that she hasn't seen Chuck Lee in years.".

Meeting is a quip. The preview of Meeting is "Chuck says he saw Cindy on Thursday, but she claims she hasn't seen him in years.".]

Linking it with is an action applying to two visible things. Understand "link [something] with [something]" or "link [something] to [something]" as linking it with.

Carry out linking something (called firstquip) with something (called secondquip):
	if firstquip is Flowers:
		if secondquip is Speechless:
			say "The statements don't match up; Chuck says that he never had a chance to talk to her, but Danny says that they did.";
			try delivering Conversation;
		otherwise if secondquip is volunteer:
			say "It's true that Chuck's note said he and Rosalita had dinner, but that doesn't contradict Danny's statement. You need something more.";
		otherwise:
			say "You can't link those.";
	otherwise if firstquip is Speechless:
		if secondquip is Flowers:
			try linking Flowers with Speechless;
		otherwise:
			say "You can't link those.";
	otherwise if firstquip is parade:
		if secondquip is home:
			say "The statements don't match up; Cindy said the streets were closed for a parade, but Maia said she took a bus home.";
			try delivering Buses;
		otherwise:
			say "You can't link those.";
	otherwise if firstquip is home:
		if secondquip is parade:
			say "The statements don't match up; Cindy said the streets were closed for a parade, but Maia said she took a bus home.";
			try delivering Buses;
		otherwise:
			say "You can't link those.";
[	otherwise if firstquip is forced:
		if secondquip is lock:
			say "Cindy's statement doesn't match up with the police report. She said that the windows were closed and locked, but the police report said that they were forced open.";
			try delivering Entry;
		otherwise:
			say "You can't link those.";
	otherwise if firstquip is lock:
		if secondquip is forced:
			say "Cindy's statement doesn't match up with the police report. She said that the windows were closed and locked, but the police report said that they were forced open.";
			try delivering Entry;
		otherwise:
			say "You can't link those.";]
	otherwise if firstquip is Broke:
		if secondquip is Charity:
			say "She says she's broke, but she's stolen more from Roses for Rehab than she's given you.";
			try delivering Funds;
		otherwise if secondquip is Sales:
			say "She says she's broke, but her cassettes have been selling better than ever.";
			try delivering Funds;
		otherwise:
			say "You can't link those.";
[	otherwise if firstquip is Housekeeper:
		if secondquip is Ignorance:
			say "Chuck claims he saw Cindy last Thursday, but Cindy claims she hasn't seen him in years.";
			try delivering Meeting;
		otherwise:
			say "You can't link those.";
	otherwise if firstquip is Ignorance:
		if secondquip is Housekeeper:
			say "Chuck claims he saw Cindy last Thursday, but Cindy claims she hasn't seen him in years.";
			try delivering Meeting;
		otherwise:
			say "You can't link those.";]
	otherwise if secondquip is Broke:		
		try linking secondquip with firstquip;	
	otherwise if firstquip is hidden:
		if secondquip is records:
			say "Maia found a stack of hidden receipts, but Danny claimed that all records were accounted for.";
			try delivering Missing;
		otherwise if secondquip is accounting:
			say "While the receipts are missing, you don't know that they were connected to the charity.";
		otherwise:
			say "You can't link those.";
	otherwise if firstquip is records:
		if secondquip is hidden:
			say "Maia found a stack of hidden receipts, but Danny claimed that all records were accounted for.";	
			try delivering Missing;
		otherwise:
			say "You can't link those.";
	otherwise if firstquip is accounting:
		if secondquip is Missing:
			say "While the receipts are missing, you don't know that they were connected to the charity.";
		otherwise if secondquip is hidden:
			say "While the receipts are missing, you don't know that they were connected to the charity.";
		otherwise if secondquip is fraud:
			say "Rosalita was defrauding Roses for Rehab, and Cindy did all of the accounting. Cindy must have been involved.";
			try delivering accomplice;
		otherwise:
			say "You can't link those.";
	otherwise if firstquip is missing:
		if secondquip is accounting:
			say "While the receipts are missing, you don't know that they were connected to the charity.";
		otherwise:
			say "You can't link those.";
	otherwise if firstquip is Fraud:
		if secondquip is accounting:
			say "Rosalita was defrauding Roses for Rehab, and Cindy did all of the accounting. Cindy must have been involved.";
			try delivering accomplice;
		otherwise:
			say "You can't link those.";
	otherwise if firstquip is volunteer:
		if secondquip is tryst:
			say "Danny claims that Rosalita was volunteering at a shelter on Wednesday, but Chuck says she was with him.";
			try delivering absent;
		otherwise if secondquip is flowers:
			say "It's true that Chuck's note said he and Rosalita had dinner, but that doesn't contradict Danny's statement. You need something more.";
		otherwise:
			say "You can't link those.";
	otherwise if firstquip is tryst:
		if secondquip is volunteer:
			say "Danny claims that Rosalita was volunteering at a shelter on Wednesday, but Chuck says she was with him.";
			try delivering absent;
		otherwise:
			say "You can't link those.";
	otherwise:
		say "You can't link those.";

Chapter 2 - Reactions to statements

Section 1 - Lies and blackmail

Absent is a quip. The preview of Absent is "Rosalita told Danny she was at the shelter, but she was with Chuck."

DannyResponse of Absent is "'Danny, you said that Rosalita volunteered at the shelter last Wednesday, but Chuck says they spent the night.'

[if tryst is DannyGiven]'I didn't realize that was the same day....'[otherwise]He shakes his head. 'I can't believe it...'[end if]

He looks up at you. 'Why would Rosalita lie to me? Why?' His face turns reddish. 'That woman...[if Missing is MeihDanny]I could tell you things about her, things she's done, but I can't. I can't hurt the reputation of the studio.'

'Mr. Kostopolous, this is very important to the investigation. You have to tell us everything.'

He slams his fist on the chair. 'No! This station is everything to me!' He calms himself down for a moment. 'Listen, if you figure out what's going on by yourself, that's different. Then I'll talk.'

'I can take you downtown for obstruction of justice, Mr. Kostopolous.'

'We'll see.[end if]'" 

DannySummary of Absent is "Danny was upset about Rosalita lying."

ChuckResponse of Absent is "You say, 'Chuck, you claimed you were with Rosalita the same night that Danny says she was at the shelter.'

Chuck frowns. 'I don't know why Danny would lie, or why Rosalita would lie to him. Maybe Rosalita was embarrassed about us getting back together.' A tear forms in the corner of his eye."

ChuckSummary of Absent is "Chuck thinks that Rosalita was embarassed by their relationship."

CindyResponse of Absent is "'Chuck claims that he was with Rosalita the same night that she was at a shelter, according to Danny.'

'Well, I don't know which I would trust there. Chuck is really hung up on Rosalita, or was, at least. And Danny wants to make the station look good.' She shrugs. 'I don't know,' she says.

'Do you know where she was last Wednesday night?'

'Not at all,' she says. 'I was at home.'"

CindySummary of Absent is "Cindy doesn't have an opinion on who is telling the truth."

MaiaResponse of Absent is "'Maia, Chuck claims he was with Rosalita last Wednesday night; but Rosalita told Danny she was at the shelter.'

'[if volunteer is Maiagiven]Didn't I tell you she was full of herself? [end if][if tryst is MaiaGiven]Although, maybe Chuck is lying, too,' she says with hope in her eyes. '[end if]I doubt she ever went near that shelter. Rosalita was just focused on herself, and didn't seem to care about anyone else.' Her eyes narrow as she looks lost in thought."

MaiaSummary of Absent is "Maia wasn't surprised that Rosalita was lying."

ChiefResponse of Absent is "'Danny's telling me that Rosalita was at a shelter last Wednesday, but Chuck says he was with her.'

'This is good, Hector. Real good. I want you to keep on these guys, push them. Let's find out who's behind this.'

'Sure thing, Chief.'"

ChiefSummary of Absent is "The Chief is pleased with your progress."

Accomplice is a quip. The preview of Accomplice is "Cindy must have been involved with Rosalita's fraud."

DannyResponse of Accomplice is "'Danny, I believe that Cindy was involved with Rosalita's fraud.'

Danny lowers his eyes and studies the floor. 'You're right, it's the only thing that makes sense.' He shakes his head. He says, 'I feel sorry for the girl; I always liked her.'

'It seems like a pretty serious offense.'

'Yeah it does. Poor girl."

DannySummary of Accomplice is "Danny believes that Cindy was involved."

ChuckResponse of Accomplice is "You say, 'Chuck, we have reason to believe that Cindy Jalcek was involved in fraud with Roses for Rehab.'

His face darkens. 'Well, I expected better from her. Stealing from a charity is a serious offense.'

'Do you have any information about this?' you ask.

He shakes his head vigorously. 'Me? I kept as far away from all that as I could. I didn't want to be involved.'

You ask, 'Why not?'

Chuck seems surprised. 'I mean, I was just a newscaster. I didn't care about that stuff.'"

ChuckSummary of Accomplice is "Chuck said he avoided everything related to the charity."

CindyResponse of accomplice is "'I've been informed that Rosalita was stealing money from Roses for Rehab before her death. And that you were involved.'

Cindy looks surprised for a moment. Then a look of panic crosses her face.

'Cindy, you were Rosalita's personal assistant. These are serious allegations. You may have to be taken into custody if you're involved.'

Her eyes harden, and she sits up straighter. 'What if I made you a deal? Gave you some bigger fish to fry?'

'I can't make any promises, but if you cooperate in this investigation, your sentence may be reduced.'

'Listen, I'm changing my statement. I have to tell you the truth. But you have to promise to work with me.'

'I'll do what I can.'";

After speeching Accomplice to Cindy:
	now CindyTruth is 1;

CindySummary of Accomplice is "Cindy decided to confess when she was inmplicated in fraud."

MaiaResponse of Accomplice is "'I strongly believe that Cindy Jalcek was involved in fraud with your sister.'

'Cindy? Really?' asks Maia. 'She always seemed so scatter brained. I can't believe she'd be involved in something like this.'

'Can you vouch for her?'

'Well, no, I don't know her that well. But I still feel bad for her.'"

MaiaSummary of Accomplice is "Maia found it hard to believe that Cindy was involved."

ChiefResponse of Accomplice is "'Chief, I think Cindy helped Rosalita steal money from Roses for Rehab.'

'Bring her in for questioning later. In the mean time, find out what she knows about the murder. If she was involved in the fraud, she has to know something about the murder.'"

ChiefSummary of accomplice is "The chief wants you to find out more about the murder from Rosalita."

Missing is a quip. The preview of Missing is "Maia found a stack of receipts that Danny said was accounted for."

DannyResponse of Missing is "'I have information that a large number of receipts for payments to KOLR were found in the control room recently. You said that all such receipts were kept filed away.'

Danny goes completely still except for the throbbing of a forehead vein. 'So you found them in the control room?'

'Mr. Kostopolous, we need you to tell us everything that you know.'[if Absent is MeihDanny][paragraph break]'Rosalita didn't keep secrets from me, and I don't want to betray her trust. She loved that charity. Two nights before she died, she was volunteering at one of the local shelters. Would a crook do that?'[end if]";

DannySummary of Missing is "Danny wants evidence that Rosalita kept secrets from him.";

ChuckResponse of Missing is "You say, 'Chuck, apparently the receipts for a very large number of payments to KOLR have been discovered in the studio recently. It looks like payments may have been underreported. Do you know anything about this?'

[if Fraud is MeihChuck]Chuck stands up and walks around the room. He stops and says, 'Wait, you're saying that someone's been stealing money from the studio?'

'Sort of. The money may have never made it to KOLR; some of the payments went...somewhere else. We don't know where. Do you?'

He sits down again.[end if]

Chuck is staring off into the distance.

'Chuck? Chuck, do you know what happened to that money?'

Chuck shakes his head no, still lost in thought.'"

ChuckSummary of Missing is "Chuck was quiet when the stolen money was mentioned."

CindyResponse of MIssing is "'Cindy, apparently a number of files connected with the studio were missing. What do you know about this?'

[if Fraud is MeihCindy]'What do you mean missing?'

'I mean they weren't filed with the others. Were you aware of this?'

'No, Rosalita handled most of that by herself.'[otherwise]'I won't go into details, but it's just what you'd think. She asked me to hide the receipts in there. I'll include it in my testimony, if our deal stands.'

'It does.'"

CindySummary of Missing is "Cindy said that Rosalita was the one involved."

MaiaResponse of Missing is "'Maia, Danny said that all the files were accounted for.'

[if Fraud is MaiaGiven]'Well, didn't you just say he found out Rosalita was committing fraud? Seems like he's lying to me.'[otherwise]'I saw what I saw. A huge stack of receipts, hidden behind the audio console.'

'Why do you think they were hidden?'

'Obviously someone had something to hide. If Danny was claiming that everything was perfect and tidy, he's the one to talk to.'"

MaiaSummary of Missing is "Maia felt like Danny had something to hide."

ChiefResponse of Missing is "'Chief, it looks like we have a bit of a mixup here.'

'What's going on?'

'Danny claims that every receipt and expenditure for the studio was filed away carefully, but Maia found a big stack of receipts hidden in the control room.'

'The what?'

'It's the room where they mix the audio from the recording room.'

'Oh. Interesting, though; I wonder what else Mr. Kostopolous is hiding.'"

Funds is a quip. The preview of Funds is "Rosalita says she's broke, but she has money leftover."

The RosaResponse of Funds is "'Cut it, Rosalita. I have the figures. You say you're broke, but you have plenty left to give. I'll expect my next payment in two days.'

Rosa is silent.

'Two days, Rosalita.' You hang up the phone.


***********************
[bracket]Press any key to switch characters[close bracket]
***********************

".

After speeching Funds to BoothRosa:
	now IntroOver is 1;

ChuckResponse of Conversation is "Chuck says, 'We have a witness who claims that you talked to Rosalita the day that you cleaned out your office.'

Chuck stares forward silently for an uncomfortably long amount of time. Without looking at you, he says 'You're right, I did talk to her.'

'Maybe we should go over your statement again.'

'Yeah, we might as well.'";


After speeching Conversation to Chuck:
   now Speechless is ChuckGiven;
   now flowers is ChuckGiven;
   now ChuckLiedOne is 1;

ChuckSummary of Conversation is "Chuck admitted that he talked to Rosalita that day."

DannyResponse of Conversation is "'Do you remember those flowers you saw? When I first asked Chuck if he'd seen Rosalita recently, he denied it.'

'That snake! Obviously he was lying. I saw those flowers! Who does he think he's kidding?'[if Conversation is ChuckGiven]

'He's admitted to lying.'

'He'd better admit to lying! What an absolute idiot.'[end if]"

After speeching Conversation to Danny for the first time:
	now DannyAnger is DannyAnger plus two;
	say "[AngerText]";

DannySummary of Conversation is "Danny believes Chuck was lying."

CindyResponse of Conversation is "'Chuck claims that he saw Rosalita last week but didn't talk to her, while [if flowers are DannyGiven]Danny said he saw[otherwise]you said you saw[end if] flowers from Chuck with a note implying he'd spent the night.'

'I'm sorry, but I have a horrible headache and this all very confusing. I stay out of Rosalita's-well, I used to stay out of Rosalita's love life.'"

CindySummary of Conversation is "Cindy didn't really want to talk about it."

MaiaResponse of Conversation is "'Chuck lied about seeing Rosalita last week, apparently.'

'What did he tell you?'

'He claimed he never talked to her last week, but [if Flowers is DannySaid]Danny[otherwise]Cindy[end if] found a note from him in Rosalita's office. It implied that they had gotten together the night before.'

She smiles tightly. 'Well, I hope they had a good time. I'm not one to pry.'"

MaiaSummary of Conversation is "Maia wanted to leave Rosalita's love life alone."

ChiefResponse of Conversation is "'Chuck lied about seeing Rosalita last week.'

'That doesn't surprise me at all.'

'And why's that?'

'It doesn't look good for him if he started reconnecting with Rosalita right before she died.'

'But he shouldn't be lying when murder is involved!'

'Keep an eye on him. Ask him about what really happened.'"

ChiefSummary of Conversation is "The chief was not surprised, and asked you to keep an eye on Chuck."

DannyResponse of Buses is "'Maia claims she took the bus home, but Cindy said that none of the buses were running. What are your thoughts?'

Danny frowns. 'Maia is a huge liar. That's one reason she changed her goes by Andromeda and dyes her hair, trying to rebrand herself after being caught too many ties.' 

He shakes his head, and goes on. 'No, I'm sure Cindy was right. Which means that Maia didn't go home.' He looks at you. 'What was Maia doing?' He pauses. 'You know, don't tell me. Just make sure you found out who killed Rosalita, okay?'" 

DannySummary of Buses is "Danny thinks that Maia was lying."

ChuckResponse of Buses is "You say, 'Maia lied about taking the bus home the day that Rosalita died. There were no buses running that morning.'

Chuck smoothes the creases of his pants. 'Yes, yes, I knew that, I was just going to tell you. Now why would she lie about that?

You ask, 'About taking a bus home?'

He scratches his ear and nods. 'Exactly. Why would the last person to see Rosalita alive lie about what she was doing that day? It's very...what's the word.... very suspicious.'"

ChuckSummary of Buses is "Chuck says you should not trust Maia."

ChiefResponse of Buses is "'Cindy and Maia's testimony disagree about how Maia left the studio.'

'In what way?' he asks.

'Cindy said that the parade shut down the streets, but Maia says that she took a bus home after Cindy got to work.'

'I'd tell them about it. Get them to sweat. One of them is lying; find out who.'"

ChiefSummary of Buses is "Chief Schwarz recommended discussing this with Cindy and Maia."

MaiaResponse of Buses is "'Cindy Jalcek said that there was a parade the day that Rosalita died, shutting down the buses. But you said you took the bus home.'

'I probably just remembered wrong. I think I actually took a taxi.'

'Ms. Morales, all of the traffic was shut down. What did you really do that morning?'

Maia looks panicked. She says, 'Listen, it's not what you think!'

'What is it, then?'

'I want to change my statement.'";

MaiaSummary of Buses is "Maia decided to change her statement."

CindyResponse of Buses is "'Your testimony doesn't match up with Maia Morales['].'

Cindy looks up sharply. 'What do you mean?'

'She says that she took a bus home after talking to Rosalita, but you said that the parade shut down all the buses.'

'Ha! That liar. Ask anyone, there was a parade that day.'"

CindySummary of Buses is "Cindy says that Maia was caught in a lie."

[After speeching Clothes to Cindy for the first time:
	now CindyFear is CindyFear plus three;]

After speeching buses to Maia for the first time:
	now MaiaLied is 1;

[DannyResponse of Entry is "'Cindy said that the windows were closed and locked, but the police report says they were forced open.'

Danny slams the table. 'What are you people doing? Just find out what happened! Stop fooling around! Are you a cop or not?' 

'We're doing the best we can.'

'You can't even decide if a window is open or not, how are you going to catch the killer?'";

DannySummary of Entry is "Danny was upset with the slowness of the investigation."

After speeching Entry to Danny for the first time:
	now DannyAnger is DannyAnger plus two;
	say "[AngerText]";

CindyResponse of Entry is "'Cindy, you said that the windows were closed and locked, right?'

'I believe so, yes.'

'The police report says that the windows were forced open by the intruder.'

'Oh, is that so?' she says, with a faint tremble in her voice. 'I must have misremembered it. Yes, yes I think that they were forced open.'"

CindySummary of Entry is "Cindy says she must have been mistaken about the windows."

[After speeching Entry to Cindy for the first time:
	now CindyFear is CindyFear plus two;]

ChuckResponse of Entry is "'Cindy said that the windows were closed and locked, but the police report says the windows were forced open.'

'Unless you have suspicions about your own department, it seems clear what happened. The windows must have been forced open, the killer must have come in, and something must have happened to set him off. I bet it was a crazed fan, stalking Rosalita.'

'You seem to have quite the well-developed theory there, Mr. Lee.'

'I've been thinking about it ever since she died. I want to know what happened.'"

ChuckSummary of Entry is "Chuck believes Cindy made a mistake."

ChiefResponse of Entry is "'Cindy said the windows of Rosalita's office were locked and closed, but the police report disagrees.'

'There are several points in Ms. Jalcek's story that don't add up. In fact, there are weak points in all of their stories.'

'If you knew all of this, why aren't you conducting this investigation?'

'A Chief's job is to manage and direct. I had a gut feeling one of these four are involved, so I assigned you to talk to them; but you're not the only iron in the fire. I'm pursuing several other pathways right now. Do your job right, and we can all move on.'"

ChiefSummary of Entry is "The chief suggests you discuss this with Cindy."

MaiaResponse of Entry is "'Cindy said that the windows were closed and locked, but the police report says the windows were forced open.'

'Well...I'm sure she just made a mistake. It was a hectic day for her, after all. I'm sure I haven't been able to remember everything accurately, either.'"

MaiaSummary of Entry is "Maia believes Cindy made a mistake."]

CindyResponse of Suspicion is "'We believe that someone close to Rosalita was involved in her death.'

'Well, I mean she is dead, right? I mean, that doesn't just happen out of nowhere. It must have... I mean, it probably was just a crazy fan or something, you know? You always hear about how obsessed those guys are.'

'So you don't think the death was connected to someone at the studio.'

'I mean, why would they kill her?'

'Money. You all stood to receive quite a bit of money from Rosalita.'

[if Testament is CindyGiven]'Yes, but $50,000 is not enough to murder someone for.'[otherwise]'Was I in the will?'";

CindySummary of Suspicion is "Cindy thinks it was an outsider who killed Rosalita."

Section 2 - Truth sequence

Arresting is an action applying to one thing. Understand "arrest [someone]" as arresting.

Carry out arresting:
	say "[if truth is carried by the player]You should talk to Chuck or the Chief about what you've learned, first.[otherwise]You won't arrest anyone until you have the entire truth.[end if]"

Truth is a quip. The preview of Truth is "Cindy claims that Chuck killed Rosalita after an argument about blackmail."

CindyResponse of truth is "'Tell me again what happened.'

She sighs. 'I hear her arguing with Chuck, and she tried to shoot him; he got the gun away from her and shot her instead. Listen, we have a deal, right?'

'I'll see what I can do.'"

CindySummary of Truth is "Cindy wants a plea deal."

ChiefResponse of Truth is "'Chief, Cindy's admitted to lying.'

'So what happened?'

'She says that she and Chuck were blackmailing Rosalita, and that Chuck shot her in self-defense.'

The Chief says, 'We'll take him in. Have her make a formal, recorded statement. Thanks for your work.'

You stay in the lobby until the squad cars arrive. As Chuck walks by in handcuffs, tears running down his cheeks, he turns to look at you. You tilt your head questioningly, and he nods.

You leave the folder behind on the desk, neatly placed, and walk to your car.

As you drive away, you turn on the radio; they're playing the end of another rerun of An Evening With Rosa:

'[italic type]Thank you so much for sharing the evening with me. From all of us at KOLR, I wish you a good night, and God bless[roman type].'
";

After speeching Truth to FakeChief:
	end the story finally saying "You have won.";

ChuckResponse of Truth is "Say, 'Chuck, it's over. I know that you killed Rosalita.'

Chuck looks you in the eyes. 'Cindy told you, didn't she.' 

You say, 'Mr. Lee, you're under arrest for the murder of Rosalita Morales.'

Chuck is quiet. Thunder rumbles outside. He says, 'Cindy...Did she tell you it was an accident? That it wasn't supposed to happen?'

You say, 'You can tell us all about it at the police station. Would you care to step this way?'

Chuck nods and stands up slowly. You shepherd him down the hallway, passing the open doorways where the others are sneaking glances. You walk through the station doors together and into the night.

As you drive away, you turn on the radio; they're playing the end of another rerun of An Evening With Rosa:

'[italic type]Thank you so much for sharing the evening with me. From all of us at KOLR, I wish you a good night, and God bless.[roman type].'";

After speeching truth to Chuck:
	end the story finally saying "You have won."

Before going east from CopHallway:
	if Truth is carried by the player:
		say "Maia is the least of your worries right now." instead;

Before going west from CopHallway2:
	if Truth is carried by the player:
		say "Danny is the least of your worries right now." instead;

Part 4 - Standard Responses

Understand "batter" as attacking.

 The block vaguely going rule response (A) is "You'll have to say which compass direction to go in (such as NORTH, NORTHWEST, or just abbreviations like S or SE)."
 The can't take yourself rule response (A) is "You already have yourself, you know."
 The can't take other people rule response (A) is "[the noun] isn't going anywhere with you right now."
  The can't drop yourself rule response (A) is "Don't give up on yourself now."
  The can't eat unless edible rule response (A) is "[if persona is Cindy]You're saving yourself for lunch at the Chinese restaurant.[otherwise]You've already eaten.[end if]".

  The examine undescribed things rule response (A) is "You look over [the noun], but don't see anything important."
   The standard looking under rule response (A) is "You crouch down and look, but you don't find anything."
	The block showing rule response (A) is "In this game, all interaction with other characters is done through conversation."
	The block throwing at rule response (A) is "You're not one to discard something so easily."
	The block attacking rule response (A) is "[if persona is Chuck]Honestly, you don't have the energy.[otherwise if persona is Cop]Your fighting days are over.[otherwise]You're not that kind of person.[end if]".

	The kissing yourself rule response (A) is "You press your fingers to your lips. It's soothing."
	The block kissing rule response (A) is "You're not in the mood for a kiss, and you doubt they are."

	The block answering rule response (A) is "[if persona is cop]In this game, speech is handled by using SAY [bracket]a topic[close bracket], where topics can be found by typing T.[otherwise if persona is the shadowy figure]In this game, speech is handled by using SAY [bracket]a topic[close bracket], where topics can be found by typing T.[otherwise]During flashbacks, conversation advances by using TALK TO [bracket]someone[close bracket].[end if]"

	The telling yourself rule response (A) is "If you want to think about a topic, try X [bracket]the topic[close bracket]."
	The block telling rule response (A) is "[if persona is cop]In this game, speech is handled by using SAY [bracket]a topic[close bracket], where topics can be found by typing T.[otherwise if persona is the shadowy figure]In this game, speech is handled by using SAY [bracket]a topic[close bracket], where topics can be found by typing T.[otherwise]During flashbacks, conversation advances by using TALK TO [bracket]someone[close bracket].[end if]"

	The block asking rule response (A) is "[if persona is cop]In this game, speech is handled by using SAY [bracket]a topic[close bracket], where topics can be found by typing T.[otherwise if persona is the shadowy figure]In this game, speech is handled by using SAY [bracket]a topic[close bracket], where topics can be found by typing T.[otherwise]During flashbacks, conversation advances by using TALK TO [bracket]someone[close bracket].[end if]"

	The standard report waiting rule response (A) is "[if persona is cop]You stop to think for a moment.[otherwise if persona is Danny]You tap your foot impatiently.[otherwise if persona is Chuck]You pause, lost in thought.[otherwise if persona is Cindy]You stop for a second, unsure what to do next.[otherwise if persona is Maia]You bide your time.[otherwise if persona is the shadowy figure]You wait for a moment, taking deep breaths to keep your calm.[end if]".

	The report touching yourself rule response (A) is "Your skin feels smooth."
	The report touching other people rule response (A) is "You decide not to, after a moment's thought."
	The report touching things rule response (A) is "You lay your fingers on it for a moment."
   The can't pull people rule response (A) is "Persuasion is better than physical force."
	The can't push people rule response (A) is "Persuasion is better than physical force."
	The can't turn people rule response (A) is "Persuasion is better than physical force."
	The innuendo about squeezing people rule response (A) is "Persuasion is better than physical force."
	 The block saying yes rule response (A) is "[if persona is cop]In this game, speech is handled by using SAY [bracket]a topic[close bracket], where topics can be found by typing T.[otherwise if persona is the shadowy figure]In this game, speech is handled by using SAY [bracket]a topic[close bracket], where topics can be found by typing T.[otherwise]During flashbacks, conversation advances by using TALK TO [bracket]someone[close bracket].[end if]".

	The block saying no rule response (A) is "[if persona is cop]In this game, speech is handled by using SAY [bracket]a topic[close bracket], where topics can be found by typing T.[otherwise if persona is the shadowy figure]In this game, speech is handled by using SAY [bracket]a topic[close bracket], where topics can be found by typing T.[otherwise]During flashbacks, conversation advances by using TALK TO [bracket]someone[close bracket].[end if]".

	The block burning rule response (A) is "You don't need a fire right now."
	The block waking up rule response (A) is "[if persona is cop]You take deep breaths and try to be more alert.[otherwise if persona is the shadowy figure]You have never been more alert.[otherwise]You can't exit a flashback until the suspect has finished their statement."
	The block thinking rule response (A) is "To see you current thoughts, try typing T."
	The report smelling rule response (A) is "You take a deep breath, but you don't learn anything new."
	The report listening rule response (A) is "You strain to hear something useful, but you can't make anything out."
	The report tasting rule response (A) is "You don't feel like tasting anything."
	The block cutting rule response (A) is "[if persona is Danny]You could get your woodworking knives, but you don't want to dull them.[otherwise]You don't have anything to cut with, and don't really feel like it anyway.[end if]".
   
The parser error internal rule response (E) is "[one of]You typed a valid action, but it was for something that isn't visible right now, something that isn't in the game, or possibly a typo. If you're stuck, type HINT repeatedly to find the next action you can try.[or]I didn't understand the object in that command.[stopping]";

The parser error internal rule response (X) is "[no line break][lookaction]";

The parser error internal rule response (N) is "[one of]Your first word had a typo or was a word not anticipated by the author. Try a different word or type HINT repeatedly to get suggestions.[or]The first word you typed was not recognized.[stopping]"; 

The parser error internal rule response (B) is "Your sentence had too many words; you can try typing just the part where you wanted to ";

The parser error internal rule response (C) is "Your sentence had too many words; you can try typing just the part where you wanted to (go) ";
	
	The report jumping rule response (A) is "You're worried what people would think if they saw you."
	The block tying rule response (A) is "You think about that idea, then discard it."
	The block drinking rule response (A) is "You're not thirsty at all."
	The block saying sorry rule response (A) is "Don't be."
	The block swinging rule response (A) is "Persuasion is better than force."
	The can't rub another person rule response (A) is "You're not that close."
	The block buying rule response (A) is "There's nothing left you want to buy."
	The block climbing rule response (A) is "You're not in shape enough for that."
	The block sleeping rule response (A) is "[if persona is cop]You wish you could, but it's not time.[otherwise if persona is Danny]You can take a nap when you go home.[otherwise if persona is Chuck]It's hard to sleep when you're thinking about Rosalita.[otherwise if persona is Cindy]You'd love to just lie down and stop thinking, but it's not the time for it.[otherwise if persona is Maia]You rest for a moment, eyes closed. It doesn't really do anything for now.[otherwise if persona is the shadowy figure]This is not the time for sleep.[end if]".
	The requested actions require persuasion rule response (A) is "[if persona is cop]In this game, speech is handled by using SAY [bracket]a topic[close bracket], where topics can be found by typing T.[otherwise]During flashbacks, conversation advances by using TALK TO [bracket]someone[close bracket].[end if]".

	The carry out requested actions rule response (A) is "[if persona is cop]In this game, speech is handled by using SAY [bracket]a topic[close bracket], where topics can be found by typing T.[otherwise]During flashbacks, conversation advances by using TALK TO [bracket]someone[close bracket].[end if]".

	The can't go that way rule response (A) is "You can't go that way."

To say lookaction:
	say "You look around.";
	try looking;

Part 5 - Work notes

[To do:

Add tics to in-narrative characters

add pockets? Make clothing person-specific?

Coloratura design notes: adaptive puzzles based on counting wrong attempts; puzzles must be player-centric (am I forcing the player to guess what Danny is thinking with Tryst and Hidden? (ha, make both of them contradict Danny's statements, and make Danny a real tough nut to crack. 'It won't be that easy, detective'.

]

Section 1 - Debug notes

ObjectListing is an action out of world. Understand "objectx" as ObjectListing.

Carry out objectListing:
	repeat with current running through portable things that are not quips:
		say "[current][line break]";
		
ThingDescribing is an action out of world. Understand "descriptx" as ThingDescribing.

Carry out thingdescribing:
	repeat with current running through things that are not quips:
		say "[bold type][current][roman type]: [description of current][line break]";

RoomListing is an action out of world. Understand "roomx" as RoomListing.

Carry out RoomListing:
	repeat with current running through rooms:
		say "[bold type][current][roman type]: [description of current][line break]";

QuipListing is an action out of world. Understand "quipx" as QuipListing.

Carry out QuipListing:
	repeat with current running through quips:
		say "[bold type][current][roman type]: [preview of current]
		
			[bold type]Chief[roman type]:[ChiefResponse of Current][line break]
					[ChiefSummary of Current][line break]";

Listing is an action applying to nothing. Understand "available" or "a" as listing.

Carry out listing:
	say "[bold type]Available topics:[roman type][line break]";
	if fakeChief is in the location of the player:
		if the number of MeihChief quips carried by the player is 0:
			say "You have said eveything you can.";
		repeat with current running through quips carried by the player that are MeihChief:
			say "    [current][line break]";
	otherwise if the player is in CopDannyOffice:
		if the number of MeihDanny quips carried by the player is 0:
			say "You have said eveything you can.";
		repeat with current running through quips carried by the player that are MeihDanny:
			say "    [current][line break]";
	otherwise if the player is in CopChuckOffice:
		if the number of MeihChuck quips carried by the player is 0:
			say "You have said eveything you can.";
		repeat with current running through quips carried by the player that are MeihChuck:
			say "    [current][line break]";
	Otherwise if the player is in CopBreak:
		if the number of MeihCindy quips carried by the player is 0:
			say "You have said eveything you can.";
		repeat with current running through quips carried by the player that are MeihCindy:
			say "    [current][line break]";
	otherwise if the player is in CopStudio:
		if the number of MeihMaia quips carried by the player is 0:
			say "You have said eveything you can.";
		repeat with current running through quips carried by the player that are MeihMaia:
			say "    [current][line break]";
	otherwise:
		say "You can't list available topics at this time.";
		
Section 2 - Easter Eggs


XYZZY is a quip. The preview of xyzzy is "You need to know more about xyzzy."

Xyzzying is an action applying to nothing. Understand "xyzzy" as xyzzying.

Carry out Xyzzying:
	if xyzzy is not carried by the player:
		say "You think deeply about xyzzy.";
		try delivering xyzzy;
	otherwise:
		try singlespeeching XYZZY;
	
RosaResponse of xyzzy is "'Rosa, there's something I've been meaning to say.'

She hesitates before answering. 'What is it?'

You sigh. 'Xyzzy.'

You hear her sobbing through the phone. Your stomach sinks as even you, the blackmailer, are filled with regret for bringing up this painful episode in Rosalita's life. Xyzzy is best left forgotten.";

RosaSummary of xyzzy is "[bracket]Redacted[close bracket]";

ChiefResponse of xyzzy is "'Chief, I know it's not supposed to exist, but I think it's time that we open the old case file on XYZZY.'

The chief is silent for a long time.

'Chief, are you listening?'

He says, 'Hector, you don't know what you're messing with here. Believe me: leave XYZZY alone. Forget about it. It never happened.'";

ChiefSummary of xyzzy is "[bracket]Redacted[close bracket]";

CindyResponse of xyzzy is "You wait until Cindy has let her guard down before surprising her. 'Cindy, I think this murder is connected to XYZZY.'

Cindy bolts from the table, knocking over her chair. You follow her into the lobby, where she runs into the bathroom and locks the door. Standing outside, you hear her vomiting and sobbing. You go back to the break room, right her chair, and sit down patiently.

She comes out and sits in her chair, as if nothing had happened."

CindySummary of xyzzy is "[bracket]Redacted[close bracket]";

DannyResponse of xyzzy is "'Danny, tell me about XYZZY.'

Danny's face turns a brilliant scarlet. 'You think you can talk to me like that!' he bellows, and swings a fist at you. It connects smashing into your skull. Your vison blurs and your ears ring. He grabs you by the lapels and slams you against the wall. 'Never talk to me about XYZZY! It didn't happen! Do you get that? Do you!'

You nod, and he releases you.";

DannySummary of xyzzy is "[bracket]Redacted[close bracket]";

ChuckResponse of xyzzy is "You lean close and whisper, 'XYZZY.'

Chuck breaks down, sobbing. He slumps from his chair to the floor and crawls to a corner, crying. 

You turn out the lights and walk out. A few minutes later, you come back in. The lights are on, and he is seated as usual.";

ChuckSummary of xyzzy is "[bracket]Redacted[close bracket]";

MaiaResponse of xyzzy is "'Hey, Maia,' you begin, 'about XYZZY-'

Her mouth twists in disdain. 'Rosalita was-she-' A look of pure bitterness tinges her face. 'No. No. Not her. Not here. Not now. Not XYZZY.";

MaiaSummary of xyzzy is "[bracket]Redacted[close bracket]";

Instead of eating the player:
	say "You look at your arm for a moment, and imagine what it would be like. You turn away and shudder."

Section 3 - Amusing

Rule for amusing a victorious player: say "Have you tried:

...listening to all of Cindy's phone conversation?

...typing XYZZY?

...discovering Danny's hobby?

...praying, sleeping, or waiting as the different characters?

...turning on the faucets in the bathroom?

...using the advanced features of the audio console?

...finding out why the walls are faded and scuffed?

...reading the books in the Chief's office?

...looking at the stencil in the lobby as different characters?

...looking at the microwave as Maia?

...learning about the previous use of the building the station is in?

...examining yourself as various characters?

...eating the burnt bag of popcorn?

...finding all the songs and ads the radio plays?"