"Invisible Parties" by Sam Kabo Ashwell

When play begins: say "An old resolution: [i]no dealings with the Three Rebeccas.[/i] But clade-mates must be civil, and civility made you careless. More importantly, Jave was going to be there: Jave, who you haven't managed to see in close on a year, as though the ways were twisting against you. And they don't have any [i]good[/i] reasons to want to fuck you over, so you should really give them the benefit of the doubt, right?[p]But as you cross the threshold of the tangle, every sense screams [i]trap[/i]. Of fucking course."

[The source, originally laid out under deadline conditions, is kind of a mess; I have commented it up somewhat, but not to a standard I'm happy with. Read at your own peril.

This source code is provided for personal, educational and critical use only. It is not released under an open-source licence. You can read it to understand the game better or to learn its code techniques (though I don't recommend the latter), but copyright is retained; you still need permission to create derivative works, unless usual fair-use conditions apply.]

[TODO

USE waymouths

what is up with reba-act-2

]

Book 1 - Indexical

Include Basic Screen Effects by Emily Short.

Include Menus by Wade Clarke.

Include Exit Lister by Gavin Lambert. [At time of publication this will not compile out of the box, due to some use of deprecated phrases; but it should be a ten-minute fix, if that.]

[The following allows EXITS to work as a command out-of-world, taking no time and unblocked by conditional rules.]

Release along with cover art ("An ornate 4 x 4 grid, with the text INVISIBLE PARTIES.").
 
Release along with the source text, a website and an interpreter.

Understand the command "exits" as something new.

The story genre is "Fantasy". The story headline is "An Interdimensional Romance".

New-exiting is an action out of world. Understand "exits" as new-exiting.

Carry out new-exiting: carry out the listing exits activity.

Use no scoring.

The release number is 3.

The story description is "You are a walker of the ways between the worlds. It is not an arrangement conducive to straightforward relationships. The Three Rebeccas have created a tangle, a temporary artifice woven from parts of many worlds, in order to hold a party. You don't trust them, but this might be your only chance to see Jave again."

This is the supplementary listing exits rule:
	let exits be the number of exit-listable directions;
	if exits is 0, do nothing;
	otherwise say "[one of]Orient yourself. Picture the woven ways as ground underfoot, and they'll become walkable[run paragraph on]. [or][stopping]The tangle extends to the [list of exit-listable directions][one of]. [co]You can turn off exit notifications with EXITS OFF, or get a listing with EXITS.[/co][or].[stopping]".
	
The supplementary listing exits rule is listed instead of the standard exit listing rule in the for listing exits rules.

Abooting is an action out of world applying to nothing. Understand "about" or "info" or "credits" as abooting.

After printing the banner text, say "Credits, help, further information: see ABOUT."

Carry out abooting: say "[co]Invisible Parties was made for ShuffleComp in April/May 2014, an event that randomly assigned songs to authors to inspire games. It was originally published under the pseudonym Psychopup. The work was very loosely inspired by [/i]Symphony 10: Could This Really Be The End[i] by Emily Wells, from the album [/i]The Symphonies: Dreams Memories & Parties[i]; the song was suggested by Jennifer Earl. (Actually, this was sort of a cop-out, because I was already familiar with the song, owned the album, and had seen the artist play live.)[p]Thanks are due to Matt Weiner and Doug Orleans for first-round testing, to Neil Butters, Jacqueline Lott, Emily Short and Caleb Wilson for second-round testing, and to Jason Dyer, Dan Shiovitz and Carl Muckenhoupt for brainstorming post-comp improvements.[p]Finally,  thanks to Bedlam Coffee, Drip City Coffee Company, Fremont Coffee Company, the Red Door, the Cyclops CafÃ© & Lounge, and Anna Armstrong; because I would never get a writing project finished if I did it all at home.[p]Complaints, bug reports, comments to magadog@gmail.com . HELP may also be of interest.[/co]"

Hinting is an action out of world applying to nothing. Understand "hint" or "help" as hinting.
Carry out hinting:
	carry out the displaying activity;
	 clear the screen;
	 try looking.

Table of Help Contents
title (text)	subtable (table name)	description (text)	used (number)	bookpage (number)	localpage (number)
"Am I missing an important command?"	--	"The most important command is USE, which only applies to gifts. You can shorten this to just the name of the gift.[p]Other than that, the most important commands are directions and TALK TO."
"I'm confused. What on earth is going on?"	--	"That's okay. This is not a story where you understand everything immediately." 	--	--
"I'm lost."	table of hinting	"Making a map is a very good idea in Invisible Parties."	--	--
"hint"	--	"Although the map changes over the course of play, its basic layout is fairly simple."
"hint"	--	"The map is initially a 3x3 grid connected by cardinal directions."
"hint"	--	"Half the rooms of the grid are connected to outside exits. Not all of these exits are at the map's edge."
"hint"	--	"As play progresses, its northern and eastern edges gradually expand; and some rooms will be destroyed, making them permanently inaccessible."
"hint"	--	"The game's cover art is a simple map."
"hint"	--	"The exits are at the central four squares of the map (minus the way you came in), and its corners. Focus your efforts on the other rooms."
"How do I find Jave?"	--	"If the game is still winnable, Jave will be in an accessible area. Explore the map thoroughly. Rooms that she passed through recently will offer clues."	--	--
"What am I meant to do with these gifts?"	table of hinting	"Your gifts are powerful abilities that apply in wide-ranging circumstances."
"hint"	--	"In some rooms, gifts may produce interesting results, but they won't accomplish anything."
"hint"	--	"Gifts are also of limited use when Jave is not around. Focus on using them in her presence."
"hint"	--	"Rooms that feature ways out of the weave are too firmly-rooted to mess about with. Focus on the rooms that don't have outside connections."
"hint"	--	"Both you and Jave have gifts. Each suitable room has two different solutions; some of these will be more obvious than others."
	--	--	--	--

This is the Zarfian cruelty violation rule:
if the number of blank rows in Table of Help Contents > 0 begin;
	choose a blank row in the Table of Help Contents;
	now the title entry is "I think I've trapped myself. How do I get back in?";
	now the description entry is "If you're in one of the entrance areas and the room immediately inside is destroyed, you cannot get back in: the connection has been severed. This is the main way in which you can make the game unwinnable.";
end if;

[Understand "* [text]" as a mistake ("[co]Noted.[/co]"). [Everyone should do this. I often forget.]

Understand "*[text]" as a mistake ("[co]Noted.[/co]"). [And some people, unaccountably, don't use a space.]]

Part 1 - Not for release

[When play begins, seed the random-number generator with 1234.]

Textchecking is an action applying to nothing. Understand "text" as textchecking.

grindlist is a number that varies. grindlist is 0.

To say grind: now grindlist is grindlist + 1.

Finding is an action applying to nothing. Understand "find" as finding. Instead of finding: say "Jave is in [the location of Jave].";

[An automated script to keep track of how many responses still needed writing.]

Instead of textchecking:
	now grindlist is 0;
	repeat with N running through parties begin;
		say "[b][N]: [/b]";
		if the closeclue of N is "", say "closeclue[grind], ";
		if the desolation of N is "", say "desolation[grind], ";
		if the tsadesc of N is "", say "tsadesc[grind], ";
		if the javedesc of N is "", say "javedesc[grind], ";
		if the rivcostume of N is "", say "rivcostume[grind], ";
		if the listendesc of N is "", say "listendesc[grind], ";
		if the boozedesc of N is "", say "boozedesc[grind], ";
		if N is challenging and the javecostume of N is "", say "javecostume [grind], ";
		if the rivdesc of N is "", say "rivdesc[grind], ";
		if the javeinit of N is "", say "javeinit[grind], ";
		if the javeclue of N is the wineglass and N is not startroom, say "javeclue[grind], ";
		if the description of N is "", say "description[grind], ";
		if there is no crowd in N, say "crowd[grind], ";
		if N is a party and the demise of N is "", say "demise[grind]";
		if the singdesc of N is "", say "singdesc[grind], ";
		if the dancedesc of N is "", say "dancedesc[grind], ";
		say "[p]";
	end repeat;
	repeat with X running through egresses begin;
		say "[b][X]: [/b]";
		if the rebadesc of X is "", say "rebadesc[grind],";
		if the tsadesc of X is "", say "tsadesc[grind], ";
		if the listendesc of X is "", say "listendesc[grind], ";
		if the javeapp of X is "", say "javeapp[grind], ";
		if the singdesc of X is "", say "singdesc[grind], ";
		if the dancedesc of X is "", say "dancedesc[grind], ";
		say "[p]";
	end repeat;
	repeat with Q running through unimplements begin;
		if the description of Q is "", say "[Q][br][grind]";
	end repeat;
		say "TO DO: [grindlist]";

Destroying is an action applying to nothing. Understand "destroy" as destroying.

Instead of destroying:
	ruin everything;

Part 2 - Text Subs

[The first thing I copy into any new project.]

To say i -- running on: say "[italic type]".
To say /i -- running on: say "[roman type]".
To say p -- running on: say "[paragraph break]".
To say br -- running on: say "[line break]".
To say b -- running on: say "[bold type]".
To say /b -- running on: say "[roman type]".
To say ob -- running on: say "[bracket]".
To say cb -- running on: say "[close bracket]".
To say co -- running on: say "[i][ob]";
To say /co -- running on: say "[no line break][cb][/i][line break]";
To say wait for any key: wait for any key.
To say clear the screen: clear the screen.

The printed name of outside is "Outside".

Part 3 - Responses

The parser error internal rule response (E) is "[if the location is doomed]No time. You should really be getting the hell out of here[otherwise]Nothing like that here[end if]."

The can't take other people rule response (A) is "That's kind of ambiguous. Are we talking physically lifting [regarding the noun][them]? Sex? Ass-kicking?"

The immediately undo rule response (A) is "[ob]No more 'undo' available.[cb]"

The can't take yourself rule response (A) is "'You can't take it with you' is a daily reality among walkers; you shift a little more than most, which seems to correspond to not giving a great many shits. There are horror stories of long walkers who lose their centre, become nothing more than drifting expressions of whatever world they happen to occupy. That one's not high on your list of fears: you feel you've got a pretty solid grasp on the shit that actually matters."

The parser error internal rule response (E) is "[if a lady is referred to]She's nowhere to be seen[otherwise][We] [can't] see any such thing[end if]."

The parser error internal rule response (X) is "Sorry, say again?"

To decide whether a lady is referred to:
	if the player's command matches the regular expression "(?i)Rivka|(?i)Strossi|(?i)Reba|(?i)Nasri|(?i)Becky|(?i)Baumgartner|(?i)Rebecca|(?i)Jave|(?i)Kambei", yes;
	no.
	
The the block attacking rule response (A) is "Wanton violence wears one out, and makes one feel like an ass[one of]. [i]Purposeful[/i] violence is another matter[or][stopping]."	

Volume - Worldkennings

[A room has a number called heat. The heat of a room is usually 0.

A room has a number called business. The business of a room is usually 0.

Originally, the idea was that the game would work something like travel through Zelazny's Amber: by navigating in directions, you'd change variables that would determine the world, phasing stuff into existence around certain loci. This quickly got abandoned as too experimental to adequately test in the allotted time.

The idea is preserved, sort of, in the distribution of the rooms: the idea was that the east-west active would run from activity to inactivity, and the north-south one from emotionally intense to cool. This is preserved a *little* - the top-right cluster are all kind of crazy in the same way, and the bottom-left similarly washed-out, but I'm not sure that it's an obvious arrangement, and it doesn't hold up in all its particulars.]

A room has some text called closeclue.

A room has some text called desolation.

A room has some text called demise.

A room has some text called tsadesc.

A room has some text called javeinit.

A room has some text called javecostume.

A room has some text called singdesc. The singdesc of a room is usually "You don't have much of a voice, and this doesn't seem like the place for it."

A room has some text called dancedesc. The dancedesc of a room is usually "Not the place, not the mood."

Book - Gifts

A gift is a kind of thing. Understand "gift" as a gift.

A gift can be promissory. A gift is usually not promissory.

A gift has some text called introduction.

Instead of doing something other than examining or using to a gift:
	say "Gifts inhere within the gifted; you can examine or use them, but not treat them as separate from the individual.";

[Tsawac knows all her abilities at the outset; Jave's are gradually revealed over the course of the game. At first I had thought that endings would vary depending on how heavily the player relied on Jave's abilities as opposed to Tsawac's, but that seemed overcomplicated and not quite in keeping with the tone I was after.]

To say giftintro: say "[one of]As for your resources... well, physical things don't always translate reliably world-to-world, and walkers tend to be wary of them. Most learned skills rely too heavily on specific circumstances that rarely obtain in other worlds. The range and life expectancy of a wayfarer are circumscribed by [i]gifts[/i], talents in the bone, uncannily irrespective of culture, climate or metaphysics. Gifts are inborn, or hard-bargained from subdivine powers.[p][or][stopping]".

the Warrior is a gift. The player carries warrior. The description of warrior is "With the warrior gift, you win fights. People often describe it as innate skill, and that's partly it, but it's something more than that; it also, somehow, shields you from the kinds of bad luck that get even the strong and experienced killed. It is not something to be relied upon, given that it makes you neither bulletproof nor inexhaustible, but it's one hell of an edge.[p]For some, the talent manifests in sublime grace, a whirl of dancer's rhythms and brush-stroke arcs. It does not work that way for you. You're more about short stabbing motions, nasty little twists and locks, low defensive stances, staying in cover, controlled fire. Looks like a dog's breakfast." Understand "fight" as warrior.

the Forager is a gift. The player carries forager. The description of forager is "One of the most common gifts, if only because those without it tend to die off fast. A forager can find food and water if there's any to be found. You know, somehow, which berries are best left uneaten, which meat is full of hookworm, which water is a cholera vector[one of].[p]The forager gift is often given as the reason why so many of the Walk are ravening foodies. You're doubtful of the correlation, yourself: travel's reason enough. But a professional-quality meal [i]does[/i] seem more enticing when you've been living on grass seeds for a month[or].[p]Jave lacks it. On the one hand, this makes her even more insanely impressive. On the other, it's something to be anxious about whenever she's out of sight[or][stopping]."
Understand "forage" as forager.

the Textual Critic is a gift. The player carries critic. The description of textual critic is "Some gifts are of, well, fairly marginal utility. You met a guy once whose only talent, other than wayfinding itself, was juggling. He could juggle pretty much anything he could lift one-handed, mind you, and had thought of several unexpected applications, but it didn't go all that far.[p]Textual criticism is a good bit more useful, because - like most gifts - it's truly cross-cultural, which means that you can read, say, a pamplet of pornographic 18th-century French political satire, or an oral history of land rights among indigenous Tasmanians, or a zine circulated among otaku in 1980s NorCal high schools, and you will actually [i]grasp[/i] all the layers of meaning.[p]Apart from making grad school embarrassingly easy, it's very useful for grappling with the otherwise-incomprehensible peoples that populate odd corners of the ways and worlds - as long as they're literate, or willing to tell you stories."
Understand "criticise" or "criticize" as critic.

the Troublemaker is a gift. The player carries Troublemaker. The description of troublemaker is "It's called [i]trickster[/i] by some, and the stories that feature it usually depict someone with a pathological need to start shit, who glories in shit-starting for its own sake, even at vast personal cost. Yeah, there are people like that all over, and the ones who have the gift are fucking [i]awful[/i]. But you're not, by inclination, someone who [i]lives[/i] to make trouble. Not since you were younger and more confused, anyway. Nor do you create trouble by accident, at least not any more than the average person. What the gift [i]does[/i] is to make the trouble you start [i]highly effective[/i]; if any potential exists for a riot or a fist-fight or a shouting-match, you can pretty much ensure it'll be an [i]epic[/i] one. Which may have something to do with this whole business, come to think of it." Understand "trickster" or "trouble" as troublemaker.

[In the first release Tsawac only had three gifts, a result of running completely out of creative energy. Troublemaker came out of a pick-up brainstorming session on ifMUD.]

the Commander is a gift. Jave carries commander. The description of commander is "There's a more common Leader gift, which is just the ability to motivate and organise people you're in charge of. The commander's gift is rarer, and considerably more complicated: it inspires some of the same confidence, the same deft touch for managing people, but it's also about putting all that to effective use. A leader is perfectly capable of leading everyone off a cliff: a commander sees the cliff coming and can quickly understand the best way to deploy a range of resources to deal with it. It is, in short, very highly sought after.[p]From your perspective, it has the mystery and alien allure of black magic. From Jave's, it's just basic common sense."

the Abider is a promissory gift. The description of abider is "This may not even count as a gift, but it's certainly as useful as one. People get themselves in all kinds of trouble, generally, because they're not prepared or able to wait things out, from a compulsion to do [i]anything[/i] rather than doing nothing. Hurry-up-and-wait drives you up the walls, but to Jave it's one of the standard modes of life. If something bothers or pains her but there's no good way to fix it, she doesn't beat her head against it."

The introduction of Abider is "A memory:[p]You'd come off a particularly lousy patrol, and the depot was just on the other side of the river; but the river was swollen with unseasonal rain, and had turned from a clear sheet of water a stone's throw broad to a turbid chocolate torrent, full of broken-off trees. It was still raining. A couple of grunts had taken noncritical wounds in a skirmish, but more to the point, the antifungals supplied by command were about as effective as talcum powder, so you were all itching furiously in inconvenient places. You had fuel and ration bars and tents, so there was nothing to do but wait; and everyone knew that as soon as command spotted you setting up camp, they'd be starting the clock on your four days off. Nothing to do but sit and itch and stink until the water went down, cheek-by-jowl with a bunch of exhausted, grouchy grunts.[p]Jave was just about the only reason why the group didn't disintegrate by the second day. She sat quietly, did stretches, and made tea. She didn't lose her temper when a brawl broke out among the grunts, and she didn't run herself ragged trying to figure out a fording method that was, plainly, never going to work. And somehow her calm transferred to the rest of you, and made it all [i]endurably[/i] shitty."

the Laughing One is a promissory gift. The description of laughing is "Humour's tough. It's pretty much a cultural universal, and it even tends to refer to the same topics, but really catching all the threads of something funny is a tall order. And there's a big difference between [i]understanding[/i] it and [i]getting[/i] it. Jave's gift is, therefore, a very special kind of fluency, a kind that gets deep at the heart of what people are concerned about, and potentially a very direct route to a powerful species of trust. It's instant, totally unanalytical, completely without artifice. It's not exactly a matter of making jokes - she's not a raconteur or a wit, she's understated rather than showy. It's primarily reactive, an ability to laugh fully and know why, complete freedom from uncertainty and second-guessing. You are not given to envy, on the whole, but if anything could spark it it'd be this."

the introduction of Laughing One is "A memory:[p]Two-thirds of the way along Mirror Branch, the only time that an easy job got a little twitchy. Along the river there was, unexpectedly, a stockade, set up by a Romani band, out of Upside via Kosovo. They were well-armed, and had one or two single-gifted among them; it was not clear whether they were set up as bandits, or toll-collectors, or were simply protecting their settlement. They bristled; accustomed to persecution, they assumed trade along this river would be followed by Mission, and if they were wrong it was only through happy accident. You still don't really have any idea about how their humour [i]worked[/i] - you had the vague sense that dark humour was part of everything, something in the vein of Soviet political jokes, and that there was a subtle distinction between the kinds of joke told by women and men, but really all that you [i]knew[/i] was that there was a vast, vast amount of vital meaning that was shooting back and forth over your head.[p]And Jave just sort of waded into all this - look, the first time you really remember her, remember her as a distinct person rather than a template Mission-vet, is when she burst out in full-bellied laughs at an aside from the headman.[p]They didn't suddenly become her bosom friends. The scene did not end with a guitar sing-along over potent drinks, worse luck. They knew that something was going on with her, and you're not completely sure it changed the outcome. But they had started thinking of her (and only her) as a person rather than a problem. She waved when you left, and some of them waved back."

the Antinominalist is a promissory gift. The description of antinominalist is "To the pure all things are pure. Jave has a sort of translation field for social mores up around her, enabling her to kiss girls in Riyadh or smoke weed in Singapore or walk around armed in Upside or unarmed in the steader worlds, just because [i]she[/i] doesn't see anything wrong with it. She doesn't take advantage of it anywhere near as much as you would - which is perhaps part of how it works. In any case, it's a powerful survival tool."

The introduction of Antinominalist is "A memory:[p]In Masdita you almost never saw a woman outside without a man or several other women. Every girl above the age of five or so wore long skirts, and hats above about twelve. But nobody took exception to the two of you wandering around in shorts and going all goopy-eyed over one another. You vaguely ascribed it to genial tolerance of the strangeness of outsiders (which was, in other respects, true: they had a strong hospitality code and a deep appreciation of the ridiculous), but once you started paying attention it became clear that they were decidedly unhumourous on the subject."

[EXAMINE PLAYER and INVENTORY were the same command in the first version. This kind of confused people, at least to the extent that they didn't really register gifts as things that were inventory items (and thus worth examining.)]	

selfex is a truth-state that varies. selfex is false.

Instead of examining the player:
if selfex is false, say "Worlds change wayfarers to a certain extent, and you're used to that. But a tangle is a messy superposition of many worlds, so it's liable to be more abrupt. Watch yourself.[p]";
now selfex is true;
	say "[the tsadesc of the location][br]";
	
Understand "tsawac" or "poitier" as yourself.
	
Instead of taking inventory:
	let L be the list of things carried by the player;
	say "[giftintro]You are [L with indefinite articles].[one of] [co]To use a gift, USE GIFT or just GIFT. You can EXAMINE gifts for more information.[/co][or][stopping]";
	if Jave is in the location and Jave carries a gift begin;
		let X be the list of gifts carried by Jave;
		say "[br]Jave is [X with indefinite articles].";
	end if;
	
your hair is part of yourself. The description of your hair is "Now is not the time to be worrying about it."

your clothes are a scenery wearable thing. When play begins: now the player wears your clothes. [For some inexplicable reason 'your clothes are worn by yourself' threw up a bizarre bug.] Instead of examining your clothes: try examining yourself. Instead of taking off your clothes: say "Nudity is better-reserved for less hazardous situations." Instead of dropping your clothes: try taking off your clothes.
Understand "clothing" as your clothes.	
	
Understand "shirt" or "skirt" or "pencil skirt" or "shoes" or "sensible" or "hose" as your clothes when the location is office party.
Understand "red" or "scarlet" or "crimson" or "dress" or "shoes" or "heels" as your clothes when the location is beautiful people.
Understand "sweatshirt" or "hoodie" or "pants" or "trousers" or "carhartts" or "keds" or "socks" or "stripy" or "striped" as your clothes when the location is gamesnight.
Understand "tactical" or "pants" or "trousers" or "tank" or "top" or "tank-top" or "keds" or "shoes" as your clothes when the location is gamejam.
Understand "turtleneck" or "cargo" or "pants" or "trousers" or "shoes" or "approach shoes" as your clothes when the location is turning stair.
Understand "bridesmaid" or "dress" or "confection" or "train" or "gown" or "frock" as your clothes when the location is wedding.
Understand "tunic" or "sandal" or "sandals" as your clothes when the location is wine-dark sea.
Understand "tux" or "tuxedo" or "suit" or "shoes" or "dance shoes" as your clothes when the location is ballroom.
Understand "suit" or "formal" as your clothes when the location is wakeroom.
Understand "cloak" or "desert cloak" or "indigo" or "shoes" or "hiking" as your clothes when the location is Barren Way.
Understand "shorts" or "sandals" or "t-shirt" or "shirt" as your clothes when the location is hotrock.
Understand "waistcoat" as your clothes when the location is maying.
Understand "hiking" or "boots" as your clothes when the location is Hollow Way.
Understand "jeans" or "army boots" or "boots" or "hoodie" or "gloves" or "black" or "shooting gloves" as your clothes when the location is Empty City.
Understand "black" or "dress" or "shirt" or "pants" or "waistcoat" or "tie" as your clothes when the location is DoubleTwisted.
Understand "gown" or "dressing gown" or "dressing-gown" as your clothes when the location is orgy.
Understand "rags" as your clothes when the location is bacchanal.
Understand "hiking boots" or "pants" or "tank top" or "top" or "tank" or "tank-top" as your clothes when the location is Into the Woods.

[This is the basic mechanic:

Rooms can have exits or puzzles; there are eight of each. Four puzzles must be solved to get out.

After solving a puzzle, that room and the next room the player moves to will be destroyed. When the fourth puzzle is solved, *every* room the player moves to will fall apart, aside from exits.

The easternmost edges will be locked until the first puzzle has been solved; the north section until the second puzzle is solved.
]

Book - Plot Mechanics

Puzzlecount is a number that varies. Puzzlecount is 0.

A room has a number called threshold. The threshold of a room is usually 0.

A party is a kind of room.

A party has some text called rivcostume.
a party has some text called rivweapon.

A room can be destroyed or undestroyed. A room is usually undestroyed.

An exit obviousness rule for a room (called place):
	if place is destroyed, rule fails.

Definition: a room (called foo) is available if the threshold of foo <= puzzlecount.

A room can be doomed or safe. A room is usually safe.

Definition: a room is suitable if it is undestroyed and it is available and it is safe.

Definition: a room is escapable if there is a safe available party adjacent to it.

Definition: a room is playerless if it is not the location of the player.

[Jave will only go to rooms presently accessible, undestroyed and not about to be, where the player isn't.

If possible, she will avoid rooms which are about to become dead-ends - unless they are the only rooms available. This makes it considerably less likely - though certainly not impossible - that she will end up on the wrong side of a bisected map.]

Definition: a room (called foo) is desirable:
	if it is suitable begin;
		if it is escapable begin;
			if it is playerless, yes;
			no;
		otherwise;
			if there is an escapable suitable party adjacent to the location of Jave begin;
				[say "[foo] is inescapable, and there are better options. NO.";
				say "([a random escapable suitable party adjacent to the location of Jave] is escapable.)";]
				no;
			otherwise; [say "[foo] is inescapable, but there are no good options. YES.";]
				yes;
			end if;
		end if;
	otherwise; [say "[foo] is unsuitable. NO.";]
	no;
	end if;

Check going to a party (called foo):
	if the threshold of foo > puzzlecount begin;
		say "[one of]You're unable to pass. But this is a completely different feeling to reaching the [i]edge[/i] of a space. The tangle extends further in that direction, and its makers certainly traversed here[or]Even though this clearly leads to other parts of the tangle, you can't make it through. Yet[stopping].";
		stop the action;
	end if;

Instead of going nowhere:
	say "The sharp, scraping-bone feel of an edge sends crawly feelings up your spine[one of]. Ugh, tangles are horrible things. Though you can see how you might feel differently if you could make them yourself[or]. There's no way through there[stopping]."
	
Going northeast is diagonalism. Going northwest is diagonalism. Going southeast is diagonalism. Going southwest is diagonalism.

Instead of diagonalism in a party: say "Traversing along two axes at once is a reasonable approach in the true ways, but within the artificial structure of a tangle it's near-impossible."

Instead of exiting: try going outside.

movecount is a number that varies. movecount is 3.

To make the lady run:
if puzzlecount < 4 begin;
	let N be the location;
	repeat with F running through clues begin;
		remove F from play;
	end repeat;
	let Q be 0;
	let Z be a random number from 1 to movecount;
	while Q < Z begin;
		now Q is Q + 1;
		if Jave is in a party (called the current space) and there is a desirable party which is adjacent to the current space begin;
			let next space be a random desirable party which is adjacent to the current space;
			if the player is in current space, move shadow Jave to current space;
			move Jave to next space;
			let X be the javeclue of next space;
			move X to next space;
		end if;
	end while;
	let NPQ be the location of Jave;
	now the initial appearance of Jave is the javeinit of NPQ;
	if there is a clue (called foo) in NPQ 	begin;
		remove foo from play;
	end if;
	if NPQ is meadhall begin;
		now Orvar is in meadhall;
	otherwise;
		remove Orvar from play;
	end if;
end if;
	
shadow Jave is a woman. shadow Jave is scenery. The printed name of shadow Jave is "Jave".

Instead of doing something to shadow Jave: say "She's gone."

Before doing something when shadow Jave is the second noun and the current action is not asking:
	say "She's gone.";
	stop the action.
	
[In the first release, Jave always moved two rooms. Given that all directions are cardinal, this meant that she could never be in half the rooms on the map, so a great deal of work went unseen. This is the sort of utter design failure one makes when crunching.]

When play begins:
	now Jave is in startroom;
	make the lady run.
	
Second-destruction is a truth-state that varies. Second-destruction is false.

To ruin everything:
	now puzzlecount is puzzlecount + 1;
	if puzzlecount is 2, now movecount is 4;
	if puzzlecount is 3, now movecount is 5;
	wait for any key;
	bring down doom;
	now second-destruction is true;
	if there is a promissory gift begin;
		gift-expansion happens in four turns from now;
	end if;

[We want there to be an indication that Jave's gifts have been revealed, but not to get it all tangled up with the apocalypse text. I'm also keeping it out of situations where Jave is, to cut down on the noise.]

At the time when gift-expansion happens:
	if Jave is not in the location and the location is not doomed and doublespeak is false begin;
		let N be a random promissory gift;
		now Jave carries N;
		now N is not promissory;
		say "[the introduction of N][br]";
	otherwise;
		gift-expansion happens in two turns from now;
	end if;
	
murdercount is a number that varies. murdercount is 0.

dressfix is a number that varies. dressfix is 0.

To bring down doom:
now murdercount is 0;
say "[if second-destruction is false][br][end if][the desolation of the location][br]"; [Oh, I7 line breaks, so fickle.]
now the location is doomed;
if there is an suitable room adjacent to the location begin;
	if Jave is in the location begin;
		if puzzlecount is 4 begin;
			say "[br][one of]You're aware of Jave at your elbow. A hand brushes lightly up your forearm, and it takes you a minute to recognise your old signal: [i]this is your bailiwick, take lead.[/i] Not strictly part of the code, but implicit: [i]I trust you[/i][or]Jave has made it through after you[or]Jave's still with you[stopping].";
			repeat with F running through clues begin;
				remove F from play;
			end repeat;
			if dressfix is 0, now the description of Jave is the substituted form of "[javecostume of the location]";
			now dressfix is 1;
		otherwise;
			make the lady run;
			if Jave is in the location of the player begin;
				[A special case: the player may be able to herd Jave out of the tangle in only three attempts. I thought about making this a different ending, but decided that enough was enough.]
				try silently Jave going outside;
				if Jave is in an egress begin;
					now puzzlecount is 4;
					now the initial appearance of Jave is the substituted form of  "[javeapp of the location].";
				end if;
			end if;
		end if;
	end if;
otherwise;
		if Jave is in the location begin;
			wait for any key;
			say "[br]The dread and panic, oddly, go away entirely when you confirm the unsettling hunch: there is no way out of here any more.[p]Jave takes your hand. 'Shit.'[p]'Yeah. It'd be one thing if there was [i]time[/i]...'[p]'Yeah. Look, I love you.'[p]'I -'[p]";
		otherwise;
			wait for any key;
			say "[br]The dread and panic, oddly, go away entirely when you confirm the unsettling hunch: there is no way out of here any more.[p]You don't age in normal ways, but you're not immortal. Like most wayfarers, you'd made your peace with the likelihood that, when the time came, it'd be a lonely death. It's unusual, among the clades, for a lost friend's fate to be confirmed; when they recovered Hoff-Owen's bones, it was such an unusual event that you had to make it all up as you went along. Fine. That's the walk. You make your peace.[p]But this, this is bullshit. Utter fucking bullshit.";
		end if;
			end the story; 
end if;
	
	[For some in explicable reason, this prints an extra line break, when the first destruction does not do so. I have been unable to diagnose this.]
Every turn:
	if second-destruction is true and the player is not in a doomed room and player is not in an egress begin;
		bring down doom;
		if puzzlecount < 4 and player is not in an egress, now second-destruction is false;
	end if;
	
Before going from a doomed party:
	if the room gone to is a party, move Rivka to the room gone to;
	continue the action;
	
Carry out going from a doomed party:
	now the room gone from is destroyed;
	
doublespeak is a truth state that varies. doublespeak is false.
	
After going:
try looking; [After would normally prevent this; 'continue the action' would get it, but would put the following message *before* looking, which we don't want.]
if the room gone from is destroyed and second-destruction is false begin;
	say "[one of]Your waysense is still lurching around, making you a little nauseous. The tangle's temporary structure isn't robust enough to cope with  this - it was probably designed to fail, like luring someone inside a condemned building and encouraging them to lean against a joist. Your very passage may be disrupting it.[p]There might be an up side, though. When the walls are coming down, it's hard to keep doors locked[or]Your guts do some zero-gravity acrobatics. A lot worse this time. A tangle's a big lie, a trompe l'oeil, a cloth woven from borrowed threads. Usually they're static dioramas, set-dressing, sometimes a den made for a single occupant. Using one for this is... excessive[or]The nausea has been at a low background level for a while; you expected it to come back, but this is like being in a car-crash. Holding up fine one moment, then [i]snap[/i], knocked reeling. You're throwing up before you realise it's coming, almost breathe it in, undergo some truly revolting coughing and spluttering for a few moments. By the time you've spat out the last of it, though, the adrenalin has kicked in and you feel vastly better. Bile mouth, watering eyes, still kind of dizzy, but solid and alive[or]The gut plunge doesn't come. This makes you a lot more nervous, somehow, though you're all out of theories why[stopping].";
	now doublespeak is true;
end if;
if the puzzlecount is 4 and Jave is in the room gone from begin;
	move Jave to the location;
	if the location is an egress begin;
		now the initial appearance of Jave is the substituted form of  "[javeapp of the location].";
		say "[the initial appearance of Jave][br]";
	end if;
end if;
remove shadow Jave from play.
	
[This prevents this message from doubling up with memories of Jave's gifts, if e.g. the player didn't flee immediately.]	
	
Before reading a command:
		if doublespeak is true, now doublespeak is false;
	
Instead of going:
	if the room gone to is a destroyed party begin;
	if Jave is not in the location and the best route from the location of Jave to the location of the player through undestroyed rooms is nothing begin;
		say "[one of]It dawns on you: she's not here. You've cut her off. She's on the other side[or]Somehow you've torn the tangle right across, with you in one half and Jave in the other. You've failed[or]Nothing remains but to get the hell out of here[stopping].";
	otherwise;
		say "[one of]You flinch away, a plummeting sensation in your gut. You can feel how the substance of the tangle is torn there: if you pushed harder at it, you'd shred yourself to pieces[or]Seriously, you'd be obliterated if you tried to go through there[or]The nasty thought surfaces that perhaps the Three have somehow gotten hold of some therapist's notes from a decade or so back, are trying to tempt you with a simple, mess-free way out.[p]If so, they got the notes from one of the [i]stupid[/i] therapists. Not that way, not ever[or]No. So very no[stopping].";
	end if;
	otherwise;
		continue the action;
	end if;
	
Instead of going when the puzzlecount is 0:
	if the room gone to is an egress begin;
		say "You try and walk outwards, and - oh, [i]unpleasant[/i]. There's a way out there, no question, the ways are the loom-strings that hold the weave in place - but the whole thing's wound so tightly that they're constricted[one of].[p]A really strong wayfarer, on a familiar way, might be able to squeeze out. But the Rebeccas, for all their other gifts, are in your estimation rather unexceptional walkers. So, there needs to be a trick to loosing them, or another exit[or][stopping].";	
	otherwise;
		continue the action;
	end if;
	
Instead of doing something other than going or exiting when the location is a doomed party:
	now murdercount is murdercount + 1;
	if murdercount > 2 begin;
		say the demise of the location;
		end the story;
	otherwise if the current action is looking;
		say "[one of]No time for sightseeing. [or][stopping]You confine your attention to avenues of escape: [list of exit-listable directions].";
	otherwise;
	say "No time. You should really be getting the hell out of here.";
	end if;
	
Last after printing the name of a direction (called way) when the location is a doomed party (this is the doomed exit rule):
	let place be the room way from the location;
	if place is visited and room memory is true, say " to [the place]".
	
chequerhint is a number that varies. chequerhint is 0.
	
weave1 is a truth state that varies. weave1 is false.
weave2 is a truth state that varies. weave2 is false.	
	
To say weavewin:
	now weave1 is true;
	if weave2 is true, explain the chequerboard;
	
To say weaveloss:
	now weave2 is true;
	if weave1 is true, explain the chequerboard;
		
	
To explain the chequerboard:
	if chequerhint is 0 begin;
		say "[br]So why [if the location is challenging]did[otherwise]didn't[end if] that work, when before... okay, right. The tangle is strong in some places, fragile in others. It's strongest at the anchor-points, the exits and entrances, where the ways feed into it. In between, the weave is looser, more negotiable. Still too strong to tear through alone, perhaps, but with two...";
		now chequerhint is 1;
	end if;

Volume - NPCs

[I didn't really set out to write a game with almost-entirely female characters. Jave and Tsawac are characters I've had knocking around forever; both have been protagonists of unfinished games before, though Tsawac has seen rather more light as a leading light of my snarky comic strip Oxbridge Pissheads. The Three Rebeccas are of rather more recent coinage, from a Varytale game that was also about gifts and parties, albeit rather differently. A lot of the references to the Sheaf clade and to potlatch come from that.

Rebecca is, particularly in the UK, a ridiculously common name in my generation. It also happens to be one of those names with a great many linguistic and nicknamey variations - not on the scale of an Elizabeth or a Catherine, exactly, but quite enough to offer a range of aesthetic choice when selecting for a trio.]

Becky Baumgartner is a woman. Becky Baumgartner is in startroom. The description of Becky Baumgartner is "Tall and solid, with acres of jawline, Becky Baumgartner has always put you in mind of a matron of the Roman Republic: soft-armed, but with bones you could anchor ships to, and a sense of unaffected superiority that could anchor small planets. She says little and emotes less, but when she does she damn well means it."

Understand "woman" or "lady" as a woman. [Testers, man.]

beckytalk is a number that varies. beckytalk is 0.

Instead of talking to Becky Baumgartner:
	now beckytalk is beckytalk + 1;
	if beckytalk is 1 begin;
		say "There are a number of questions that occur to you, but 'What the everliving fuck is going on?' seems to cover most of it. Poor diplomacy, possibly, but playing nice seems moot at this point.[p]'Forgo her.'[p]'That's not a - wait, [i]what?[/i]'[p]'Jave. We object to the match. Forgo her.'";
	otherwise if beckytalk is 2;
		say "It takes a little work to bring your temper down. One of the consistent points of the Sheaf clade, and of clades in general, is that nobody gives orders. Oh, there's an unspoken hierarchy, and leaders of various kinds, but... outside a life-and-death situation, even the [i]appearance[/i] of a command is a room-clearing insult. There's a lot of 'If you could' and 'Would you mind please', which drives you insane at times, but apparently you care about the underlying premise a great deal. Hunh.[p]'What's the idea here? If I say no, you... what does this place [i]do[/i], anyway?'[p]'A monkey trap. You are only held here by holding on. Let go, get out.'[p]'Hold on, and... what? Wait until the trapper shows up?'[p]A languid blink.";
	otherwise if beckytalk is 3;
		say "'Jesus shit, look: one, this counts as an attack on a clademate and I'll fucking [i]wreck[/i] you for it, two, what the fuck makes it even slightly okay for you to decide who I see, and three... what's going on here? If it's let go you want, why is there no way out?'[p]With an expression that says [i]enough[/i], she stands, walks, and fades away into nothingness.[p]Shit. Okay. Take stock. [expostul1]";
		remove Becky Baumgartner from play;
	end if;

Onholding is an action applying to nothing. Understand "hold on" or "let go" as onholding.

Carry out onholding: say "That's too abstract. You need a more concrete course of action."

[A common worldbuilding problem I have is that I am seriously not keen on the Tolkien-style encyclopedia approach, and would rather plunge into _media res_ and let the audience figure everything out. Generally I go over the top on this, which sucks, because people need to have some grasp of what's going on in order to care about it and to feel confident about interaction. This next is an attempt at succinctly summing up the situation.]

numbat is a number that varies. numbat is 0.

To say expostul1:
	if numbat is 0, say "The Rebeccas, taken together, are not just walkers but weavers: they can not only traverse the ways and worlds, they can... take bits out of them, temporarily rearrange them into new ways and worlds. It's a rare gift. One of the things that can be crafted in this way is a [i]tangle[/i], a bounded array of tightly finite worlds that complement one another, packed very closely. This is not a stable arrangement, and they're usually set up as spectacles, exhibitions of craft and talent. Extremely elaborate party tricks.[p]But they're unstable. Imperfect. Whatever they've done, you can get around it.";
	now numbat is 1;

Instead of talking to the player:
	say "Every waking hour, man. Not as though you need to plan it.";

The initial appearance of Becky is "Becky Baumgartner is seated - enthroned, really, like a heavy-handed neoclassical allegory - with a snow leopard draped at her feet[one of]. She cocks her chin up at your approach[or][stopping]."

the snow leopard is scenery. the snow leopard is a person. the snow leopard is in startroom. The description of snow leopard is "Holy fuck but these things are huge up close. It looks... lean, alert, not a zoo animal. But of course it's completely relaxed."

Understand "pet [something]" as touching. Instead of touching the snow leopard: say "It tenses up ever so slightly at your approach, and you think better of it."

Instead of kissing Becky: say "The idea is a little disquieting."

Rivka Strossi is a woman. The description of Rivka is "[one of]Rivka has the air of a self-satisfied cat: she's petite, broad-hipped, with a constant slight smirk. You don't think you've ever seen her eyes: she's always veiled, or masked. Today it's oversized aviator sunglasses. Fearsome reputation, absolutely none of it confirmable. If Becky grounds the three, it's Rivka who makes them what they are. If she decides you're her enemy, she will crush you and not give the tiniest shit about it; and her definition of [i]enemy[/i] is a lot looser than most people's.[p][or][stopping][rivcostume of location]"

Rivka's aviator shades are carried by Rivka. Understand "glasses" or "sunglasses" as aviator shades. The description of aviator shades is "That style of big-lens sunglasses with a shape calculated to make their wearer look like aloof and dickish. On Rivka this is coals to Newcastle."

Rivka's clothes are worn by Rivka. Instead of examining Rivka's clothes: try examining Rivka.

Understand "biker" or "jacket" or "jeans" or "purple" or "skinny jeans" or "shoe" as Rivka's clothes when the location is startroom.
Understand "shirt" or "tie" as Rivka's clothes when the location is office party.
Understand "suit" or "cream" or "tie" as Rivka's clothes when the location is beautiful people.
Understand "hoodie" as Rivka's clothes when the location is gamesnight.
Understand "tattoos" or "tank" or "top" or "tank-top" as Rivka's clothes when the location is Under My Roof.
Understand "surplice" or "collar" or "ring" as Rivka's clothes when the location is wedding.
Understand "ballgown" or "gown" as Rivka's clothes when the location is ballroom.
Understand "veil" or "bolero" or "jacket" or "cuban" or "heels" as Rivka's clothes when the location is wakeroom.
Understand "ashes" or "ash" or "staff" as Rivka's clothes when the location is hotrock.
Understand "costume" or "red" or "black" as Rivka's clothes when the location is maying.
Understand "suit" or "thirties" or "30s" as Rivka's clothes when the location is DoubleTwisted.
Understand "earpiece" as Rivka's clothes when the location is orgy.

Instead of kissing Rivka: say "Or, y'know, you could dry-hump a hornet's nest."

The initial appearance of Rivka Strossi is "[the rivdesc of the location]".

Reba al-Nasri is a woman. The description of Reba is "Little aquiline nose, teeth a little too large for a mouth always smiling, really good at choosing scarves[one of]. Reba is the least obnoxious of the three, the one who you actually get along with when distinct from her cohorts. Which is ultimately why they've got a problem with you: while the motives and interests of the Rebeccas are a matter of carefully-tended ambiguity, the one thing they do not forgive is any attempt to divide the Rebeccas.[p]True, the possibility of schtupping Reba wasn't [i]entirely[/i] absent from your mind, and at least [i]part[/i] of the appeal of that prospect was that it would fluster the others. But it's not as though that formed an [i]objective[/i] so much as a routine possibility, a basic consequence of her being kind of cute and not conspicuously a jerk[or].[p][the rebalook of the location][stopping]."

The initial appearance of Reba al-Nasri is "[the rebadesc of the location]".

rebacount is a number that varies. rebacount is 1.

Instead of talking to Reba when Jave is in the location:
	if rebacount is 1 begin;
	say "[if the location is rebaseated][reba-act-3 of location] [otherwise]Reba bounds into an enthusiastic hug which you don't return. [end if]'You made it out together! Oh, I'm so happy you did.'[p]'Yeah.'[p][if the location is rebaseated][reba-act-4 of location] [otherwise]She pulls back, beaming, face a little too close. [end if]'Walk safe - and be happy. I'll see you at the next potlatch. ' There isn't any double meaning, no veiled threat; you might almost be friends.[p]'You... you don't really get to set us up for something like -' you wave an arm behind you, 'all [i]that[/i], and then play nice afterwards.'[p]She cocks her head to one side and does that little smile, as if you've said something adorably clueless. 'Our space, Tsawac. Walk safe.'";
	now rebacount is 2;
	otherwise;
		say "You've nothing more to say to her. Well, you have several things, but mostly they're preambles to violence, so you'd better refrain.";
	end if;
	
Instead of talking to Reba when puzzlecount < 4:
	say "[one of]'What the shit are your cronies up to, al-Nasri?'[p]She actually looks hurt. 'Nice to see you too, T. How have you been?'[p]'Oh, same old - grouchy, lovelorn, intolerant of horsecrap. Seriously, what's the idea here?'[p]Reba straightens herself a little. 'It is... not easy to express our motives succinctly. We act as one, but we cast many shadows. Any single explanation would fall short. This one may serve you: we are offering you an opportunity for closure.'[or]'Closure is nonsense, Reba. Spin me another story.'[p]She bobs her head at an angle, beaming. 'If you must. In you and Jave we perceive the potential, perhaps, for a counterpart to we Three. Your gifts align in worrisome ways. You both tend to form attachments fiercely, firmly and to strong walkers. Harmless now, maybe, but introduce Nusku to the dynamic, or Dlamini, or the Shadow Minister, or, no offence meant, Thalud -'[p]'Wow. Just... you have some [i]really[/i] incomplete information.'[p]'Care to enlighten us? No? Then admit that the principle has some merit.'[or]'Jesus, Reba, Jave isn't... my conspiracy to come up with some crazy-ass dynastic threesome or whatever. Hard as it is to imagine, not everybody aspires to be you.'[p]'I'm sure you intend nothing of the sort; the potential is what matters. This is a long-term aspect. A shorter one: the match would bring Jave into the Sheaf, and she is ex-Mission.'[p]Ah. Yeah, that one has some clout. Among a certain mindset, ex-Mission is about as cuddly as ex-KGB.[or]'Look, Reba, she's not Mission. Completely severed. Doesn't draw pension, she doesn't even [i]like[/i] hanging out with vets, and she'd never rat if it might possibly hurt me.'[p]'Love is the falsest witness. Mission is Mission. Also, we understand she's [i]not interested in food.[/i]' She twinkles at that, but it's actually a pretty serious indictment for a potential clademate. Food's at least half of what holds the Sheaf together.[or]'Can you please cut the crap, Reba? For me? The Rebeccas aren't God, I'm not Job. What's the [i]real[/i] reason? If you tell me then maybe I can, you know, negotiate, figure something out that works for everyone.'[p][if the location is rebaseated][reba-act-1 of the location][otherwise]She draws closer, raises her brows.[end if] 'Maybe I'm just jealous.' It's a joke, yes, but only half one, and you both know it.[p][i]Shit.[/i] [if the location is rebaseated][reba-act-2 of location] [otherwise]You refrain from slipping your hands over the crest of her hips. [end if]This would be an awful lot easier if she was tidily [i]wrong[/i].[or]You're sure she could come up with a hundred more reasons, but it's clear there's no negotiation possible, and it's only going to make your heart ache.[stopping]";
	
Reba wears Reba's clothes. Instead of examining Reba's clothes: try examining Reba.
Understand "drapery" or "drapes" or "drape" as Reba's clothes when the location is Turning Stair.
Understand "dress" or "sundress" as Reba's clothes when the location is Wine-Dark Sea.
Understand "hijab" or "headscarf" or "scarf" or "head scarf" as Reba's clothes when the location is Barren Way.
Understand "straw" or "hat" or "dress" or "floral" or "print" or "scarf" or "crock" as Reba's clothes when the location is Hollow Way.
Understand "gown" or "wedding" or "wedding-gown" or "makeup" or "fishnets" or "tights" as Reba's clothes when the location is Empty City.
Understand "boots" or "boot" or "dress" or "skirt" as Reba's clothes when the location is Into the Woods.
	
Instead of kissing Reba:
	if Jave is not in the location and the best route from the location of Jave to the location of the player through undestroyed rooms is nothing begin;
		say "[one of]For almost two seconds she kisses back, and then firmly pulls away. 'Consolation prize, Tsa? Revenge? Insult to my loyalty aside, you don't exactly flatter.' She looks genuinely disappointed and uncomfortable. So hooray, now you've conclusively ruined [i]that[/i] as well[or]She's right. You're just making yourself look like a fool[stopping].";
	otherwise;
		say "You're not sure how she'd react. And it'd certainly elicit reprisals from the other two.";
	end if;

Instead of attacking Reba:
	say "Becky could probably throw a solid punch, at least, and you suspect Rivka of being warrior-gifted. But you doubt Reba would be able to fight back at all. That's a sticking-point, regardless of how angry you are with her.";

Book - Scenery

Tangle is a region.

An unimplement is a kind of thing. An unimplement is scenery. The description of an unimplement is usually "".

Instead of doing something other than examining with an unimplement: say "Real, solid, but still just a shadow-trick."

A waymouth is a kind of thing. A waymouth is scenery. [The description of a waymouth is usually "It's not anything you can see or touch. Just a thread feeding into the tangle, a way outwards."] Understand "way" or "exit" or "road" or "road out" as a waymouth.

Instead of entering a waymouth: try going outside.

Instead of taking a waymouth: try going outside.

Instead of following a waymouth: try going outside.

the drinks are a backdrop in tangle. Instead of examining drinks: say "[the boozedesc of the location][br]". Understand "booze" or "beer" or "lager" or "wine" or "spirits" or "cocktail" or "cocktails" or "drink" or "liquor" or "punch" or "punchbowl" or "bottle" or "bottles" or "champagne" or "cider" or "scotch"  as drinks.

Instead of taking the drinks: try drinking the drinks.

Instead of drinking the drinks when the location is not doomed:
	say "[shortbooze of location] [one of]It's really rather good: the Rebeccas can't be accused of scrimping on their parties[or]May as well take advantage of it while it's available[or]You're maintaining a nice even buzz[or]You don't appear to be getting any drunker[stopping]."
	
Rule for supplying a missing noun while drinking:
	if the player is regionally in tangle, now the noun is the drinks.

the tangle-ghost is a backdrop. tangle-ghost is everywhere. The printed name of tangle-ghost is "tangle". Understand "tangle" or "weave" or "ways" or "way" or "reality" or "fabric" or "here" or "room" or "world" as the tangle-ghost.

Instead of taking tangle-ghost: say "You can't manipulate the tangle directly, since you're not a weaver. You'll have to subvert it in other ways."

Instead of pushing tangle-ghost: try taking tangle-ghost.

Instead of pulling tangle-ghost: try taking tangle-ghost.

Instead of attacking tangle-ghost: try taking tangle-ghost.

Instead of entering tangle-ghost:
	if the player is in an egress begin;
		try going inside;
	otherwise;
		say "You're already very much inside the tangle.";
	end if;

Instead of examining tangle-ghost:
	if the location is an egress begin;
	let X be a random room which is adjacent to the location;
		if puzzlecount is 4 begin;
			say "The tangle is collapsing behind you. You're probably safe here, but it wouldn't do to stick around.";
		otherwise if X is destroyed;
			say "You're cut off from the tangle, having irreparably damaged it at the opening-point of the Way. You could go around, link to a different way, and re-approach from another angle - but that might take days or months, and you rather doubt that it'll still be standing tomorrow.";
		otherwise;
			say "Inwards, you can sense the many-layered, artificial mess of the tangle.";
		end if;
	otherwise;
		if the location is unchallenging begin;
			say "[if puzzlecount > 1]Despite the damage you've done to it, t[otherwise]T[end if]he tangle feels firmly-anchored at this position.";
		otherwise;
			say "The tangle feels weaker here. It should be easier to get through to Jave.";
		end if;
	end if;

Book - The Beloved

Jave is a woman. Understand "kambei" as Jave.

Instead of doing something to Jave:
	 if the location is an unchallenging party and the current action is not examining begin;
	say "[one of]'Jave! Fuck, I'm glad you're here - wasn't sure you really were. Look, this place is a tangle, a place woven from bits of the ways and worlds. Super unstable, but I've seen some exits -'[p]She hasn't reacted at all. You're in her line of sight, she'd have to be deaf and blind to miss you. Something's up.[p]Then she jumps up, beaming, and runs with arms outstretched in a direction about forty-five degrees away from you. She fades as she moves, until she's quite gone. Shit[or]'Hey. Jave. Can you hear me, or are you - '[p]Evidently not. This time she spaces out on you for several minutes of gesticulating and yelling, and then calmly walks away, brushing you with her elbow as she goes[or]Again you try to contact Jave, and again she fails to register you, sees something else, moves to chase or follow it, and disappears[stopping].";
	make the lady run;
	say weaveloss;
	otherwise;
	continue the action;
	end if;
	
After going from a challenging room:
	if Jave is in the room gone from, make the lady run;
	continue the action;

Instead of talking to Jave in an egress:
	say "[one of]'Are you OK?'[p]'Yeh. Little shocky. I'll be fine.'[p]'We're still in range of the tangle, I think - keep moving?'[p]'Sure[or]'Still with me?'[p]'Yeah[stopping].'";

Instead of kissing Jave in an egress: say "You draw her in close; she dips her head down and holds you and you kiss. You're too drained to get all that turned on, at this point, but you feel immeasurably reassured.".

Instead of examining Jave when the location is an egress:
	say "[the description of Jave] She's red-cheeked from sprinting, and she's grinning like a gold medalist."

Instead of touching Jave: try kissing Jave.

Instead of kissing Jave in a party: try talking to Jave.

Instead of doing something to Jave when puzzlecount is 4 and the location is not an egress:
	say "Later, maybe, once you're in the clear."

Instead of talking to Jave in a challenging party:
	say "[one of]You start, and... it's not that you can't make sounds, or articulate your mouth. It's not even that you can't form thoughts into words. But the process that connects the two parts doesn't work - what comes out is a garble, uninflected even by emotion. Jave attends closely, but without comprehension[or]You learned some basic operational sign-language from her in the Lacmore Slope, and you can still remember parts of it, but it's not exactly a language designed for emotional nuance. Or anything much beyond commands, really. [p][i]Acknowledge?[/i][br][i]Acknowledge.[br]Area is dangerous but no imminent threat.[br]Acknowledge.[/i] (There is no sign for 'well obviously,' or even 'yes, I know.')[br][i]Regroup.[br]Negative. Impassable terrain.[br]Hold position.[br]Negative. Area is dangerous but no imminent threat.[/i][p]Not the reunion conversation you had in mind, but it makes you feel better anyway[or]You can't really get anything across that'll [i]help[/i], and the conversation you want to have will have to wait until you're both out of here[stopping].";
	say weavewin;
	
Rule for writing a paragraph about Jave when second-destruction is true and puzzlecount < 4:
	say "You catch sight of [Jave], running as fast as she can. You can't tell to where.";
	
Jave wears Jave's clothes.
Understand "jumpsuit" or "vault" or "dweller" as Jave's clothes when the location is startroom.
Understand "skirt" or "updo" or "herringbone" or "glasses" as Jave's clothes when the location is office party.
Understand "heels" as Jave's clothes when the location is beautiful people.
Understand "dreads" or "dreadlocks" as Jave's clothes when the location is Under My Roof.
Understand "wedding dress" or "dress" or "veil" as Jave's clothes when the location is wedding.
Understand "weeds" or "widow's weeds" or "veil" or "handbag" as Jave's clothes when the location is wakeroom.
Understand "tank-top" or "tank top" or "top" or "tank" or "skirt" or "sandals" or "wraparound" or "wrap-around" or "cap" or "military cap" or "army cap" as Jave's clothes when the location is hotrock.
Understand "dress" or "peasant dress" as Jave's clothes when the location is maying.
Understand "bedsheet" or "sheet" or "bed" or "armlet" or "anklet" or "tiara" or "goblet" as Jave's clothes when the location is meadhall.
Understand "dress" or "art" or "deco" or "cocktail dress" as Jave's clothes when the location is doubletwisted.
Understand "dress" or "white" or "clingy" as Jave's clothes when the location is doubletwisted.

Instead of examining Jave's clothes: try examining Jave.

Instead of taking off Jave's clothes: say "You'd rather do so while not under observation."

Instead of attacking Jave: say "You have sparred a little in the past. But she's the last person you'd ever actually [i]attack[/i]."

Instead of attacking Jave when the location is wakeroom and wakeroom is not doomed: try using warrior.

Volume  - New Verbs

Book - Fuck Off You Can't Use These

['But should the vulgar feel dissent,
It's such fun to reimplement.'

This is the first time I have had reasonable occasion to do so. Tsawac is not one to mince words. It took some struggle to refrain from figuring out a regular-expressions way of detecting cusses; the following is just meant to capture expressions of frustration.]

Swearing is an action applying to nothing. Understand "fuck" or "shit" or "bollocks" or "motherfucker" or "damn" or "jesus" or "christ" or "goddamn" or "balls" or "bullshit" as swearing.

Praying is an action applying to nothing. Understand "pray" or "meditate" as praying.
Carry out praying:
	say "[one of]Walkers are rarely given to structured religion. A substantial minority adhere, or mostly adhere, to customised versions of the major faiths. Most carry out rituals, invented, borrowed or inherited, in very much the same way that they seek out childhood recipes or favourite books; they tend towards ancestor-worship, symbols of identity, of memory. Your particular set of rituals are for quieter times than this, when you're not being watched[or]This is really a time for cursing, not reverence[stopping].";
	
Instead of praying when the location is wakeroom and wakeroom is not doomed:
	say "A good portion of the mourners are doing so. It feels disrespectful to fake it.";

Report swearing:
	if the location of Jave is an egress begin;
	say "You've still got a hot little ball of rage going, but... just for now, let's set it aside and not ruin the moment.";
	otherwise;
	say "You curse [one of]a blue streak[or]at length and with feeling[or]ferociously[or]like a sailor[or]the Rebeccas, the worlds and the ways[or]with venom and vigour[at random][one of], coining some inventive compound words in the process[or] in [if a random chance of 1 in 3 succeeds][randomlang], [randomlang] and [end if][if a random chance of 1 in 4 succeeds][langadj] [end if][randomlang][or][or][or][at random][one of]. You're still pissed off, but you feel a satisfying ownership of your anger[or]. You'll see them thrown out of the clade for this. You'll see them fucking [i]burn[/i][or]. [cussresponse][or]. Got to keep that anger at a steady low burn, where you can make use of it[or]. Seriously, what a bunch of [i]assholes[/i][or][or][at random].";
	end if;
	
To say randomlang:
	say "[one of]Xhosa[or]Mandarin[or]Scouse[or]Weegie[or]Cantonese[or]Flemish[or]Welsh[or]Tlingit[or]Navajo[or]Ibo[or]Khoi[or]Haitian Creole[or]Quebecois[or]Sanskrit[or]Hindi[or]Sinhalese[or]Old Church Slavonic[or]Urdu[or]Afrikaans[or]Galician[or]Luo[or]Ainu[or]Persian[or]Magyar[or]Quechua[or]Maori[or]German[or]Yiddish[at random]";
	
To say langadj:
	say "[one of]archaic[or]thickly-accented[or]densely idiomatic[or]pidgin[or]high literary[or]formal[or]an obscure dialect of[at random]";
	
To say cussresponse:
		if Jave is in the location and the location is challenging and the location is not orgy begin;
			say "Jave gives you an incomprehending smile[one of]. She doesn't even seem to have grasped your tone of voice[or][stopping]";
		otherwise if Reba is in the location;
			say "Reba attempts to suppress an amused smile[one of]. It's one of her more becoming expressions, and you've never been sure how much it's consciously so[or][stopping]";
		otherwise if Becky is in the location;
			say "Becky's look implies that she holds you in much the same regard as a small and ill-favoured child who has just deliberately shat on the rug";
		otherwise;
			say "Nobody appears to notice";
		end if;

Understand "make out with [someone]" as kissing.

[The following dispenses with the infamous 'I only understood you as far as wanting to fuck' error, which, while perhaps descriptive of a number of relationships, does not apply to any depicted in this game.]

Rule for printing a parser error when the latest parser error is only understood as far as error:
let Z be the player's command;
if Z matches the regular expression "fuck|shit|bollocks|motherfucker|damn|jesus|christ|goddamn|balls|bullshit" begin;
	try swearing instead;
otherwise;
continue the activity;
end if;

Rule for printing a parser error when the latest parser error is not a verb I recognise error:
let Z be the player's command;
if Z matches the regular expression "fuck|shit|cunt|dick|cock|piss" begin;
	try swearing instead;
otherwise;
continue the activity;
end if;

Talking to is an action applying to one thing. Understand "talk to [a person]" as talking to. Understand "talk [a person]" as talking to.

Topicing is an action applying to one topic. Understand "a [text]" as topicing.

[One of my pet peeves in IF is when you enter a command - say, TALK TO - and the game replies with 'Use CONVERSE for conversation.' If there's ambiguity, or you need a second noun or whatever, fine - but otherwise, just take the action that the player clearly intends, already.]

Instead of saying yes: try topicing.
	
Instead of saying no: try topicing.

Instead of topicing:
	let N be a person;
	if there are two not scenery people in the location begin;
		let Z be a random not scenery person in the location that is not the player;
		try talking to Z;
	otherwise if there are two people in the location;
		let Z be a random person in the location that is not the player;
		try talking to Z;
	otherwise;
		say "There's nobody here to talk to.";
	end if;

Instead of asking a person about some text: try talking to the noun.

Instead of asking a person for something: try talking to the noun.

After deciding the scope of the player while asking:
	repeat with X running through things begin;
		place X in scope;
	end repeat;

Instead of telling a person about some text: try talking to the noun.

Instead of answering someone that some text: try talking to the noun.

Following is an action applying to one thing. Understand "follow [something]" as following.

Instead of following the player: say "You generally do. The question is to where."

Instead of following a crowd: say "They're not going anywhere. And wherever they would go, you doubt you could follow."

Instead of following a person: say "But [the noun] isn't going anywhere."

Instead of following Jave:
	if the location is an egress begin;
		try going outside;
	otherwise if puzzlecount is 4;
		say "She gave you the signal to take the lead. Incident command principle: whoever's in charge stays in charge until the situation's stable - even if there's someone around who might be better at it, changes of command are horribly inefficient. There are exceptions, as always, but they all involve the in-charge being incapable of fulfilling the role. This not being the case, it's all on you.";		
	otherwise;
		try talking to Jave;
	end if;
	
Instead of following shadow Jave: say "You didn't see which way she went."
	
Following Reba is witchfollowing. Following Rivka is witchfollowing. Following Becky is witchfollowing. Instead of witchfollowing: say "There's no hope of chasing down the Rebeccas in a tangle of their own creation."

Instead of taking a crowd: say "Yeah, you could probably take the whole lot of [']em.";

Instead of taking Jave:
	if the location is an unchallenging party begin;
		continue the action;
	otherwise if the location is an egress;
		try kissing Jave;
	otherwise if the puzzlecount is 4;
		say "Just go. She'll follow.";
	otherwise;
		say "Oh, if it were that simple.";
	end if;
	
Instead of touching Jave when the location is mutualslump and puzzlecount < 4: try taking Jave.	
	
Instead of taking Jave when the location is mutualslump and puzzlecount < 4:
	say "You reach over and give her shoulder a gentle I'm-still-here squeeze. Pause. Then she springs to her feet, stumbly, hair falling across her face; mutters something apologetic that makes it clear she doesn't recognise you; and runs off while you're still struggling for a response.";
	make the lady run;
	
Persuasion rule for asking Jave to try following the player:
	if the location is an unchallenging party begin;
		try talking to Jave;
	otherwise if the location is an egress;
		try going outside;
	otherwise if the puzzlecount is 4;
		say "You trust her to do so.";
	otherwise;
		try talking to Jave;
	end if;
	
Loosening is an action applying to nothing. Understand "loose" or "loosen" or "loose strings" or "loosen strings" as loosening. Instead of loosening: say "As you're not a weaver, you can't do so directly. You'll have to find other ways to weaken the tangle."

Instead of attacking the tangle-ghost: try loosening.

Singing is an action applying to nothing. Understand "sing" or "hum" as singing.

Understand "yell" or "scream" or "howl" or "shout" as singing when the location is bacchanal.

Carry out singing: say "[the singdesc of location][br]".

Dancing is an action applying to nothing. Understand "dance" as dancing.

Dancing with is an action applying to one thing. Understand "dance with [someone]" as dancing with.

Instead of dancing with: try dancing.

Instead of dancing with Jave when the location is ballroom and ballroom is not doomed: say "You would very much like to, but the dance - or the tangle, more likely - is preventing it. You need to change the order of things."

Instead of dancing with Jave when the location is maying and maying is not doomed: try touching Jave.

Carry out dancing: say "[the dancedesc of location][br]".

Instead of thinking: say "You generally do. Pray, continue."; 

Understand "remember" as thinking.

It-remembering is an action applying to a topic. Understand "remember [text]" or "think about [text]" as it-remembering.

Instead of it-remembering: say "Much as you're inclined towards it, this is not a good time for navel-gazing."

Book - That Annoying Direction Thing

Going unto is an action applying to one thing. Understand "go to [any room]" as going unto. Understand "[any room]" as going unto.

Check going unto:
	if the noun is not adjacent begin;
		say "That's far-off, and not all ways are alike. You'll have to go step by step.";
		stop the action;
	end if;

Carry out going unto:
	let N be the noun;
	let F be the best route from the location to the noun;
	try going F;
	

Book - Using

Using is an action applying to one visible thing. Understand "use [a gift]" as using. Understand "[gift]" as using.

Instead of using warrior in the presence of Becky Baumgartner:
	say "Polite time [i]way[/i] over. Clademates aren't allowed to attack one another, but this is absolutely self-defence at this point. You close, feint left, and stab a short front-kick towards her solar plexus. She responds with a bland stare, and fades out of existence moments before the blow lands. You stumble, not dangerously but enough to feel like a prat.[p]Well, of course, they [i]made[/i] this place, naturally they know how to get around it. If they'd wanted to make a battleground - and you're not sure any of them are even gifted in that department - they'd have set one up to suit themselves.";
	remove Becky from play;

Instead of attacking Becky: try using warrior.

Check using:
		if the puzzlecount is 4 and the location is not an egress begin;
			say "The only thing left is to run.";
			stop the action;
		end if;
	
Check using:
		if  the location is doomed begin;
			say "There's nothing you can do to save this place. Save your skin.";
		end if;
		
Check using:
	if Rivka is in the location and the noun is warrior begin;
		say "[one of]'You want to play games? All right, let's fucking play,' you snarl, lunging. Rivka... withdraws. It's a neat little trick, a looking-glass effect where you seem to be traveling towards her at speed, and she seems to remain in place, yet she gets further and further away. Before she disappears entirely, she makes a pass with one hand, which now holds [the rivweapon of location], and tips it in sardonic salute: [i]some other time[/i][or]No point in breaking a sweat if she's unwilling to face you[stopping].";
		remove Rivka from play;
		stop the action;
	end if;
	
Instead of attacking Rivka: try using warrior.	
		
Check using:
if Jave carries the noun and the location is unchallenging and puzzlecount < 4 begin;
	say "[one of]You attempt to signal Jave, and she makes no response whatsoever. You repeat the signal. Nothing. And then - oh, great, she springs up, yelling acknowledgement to someone. And then she traverses out[or]Jave ignores you completely for roughly ten minutes. Then she starts as if stung, whirls around, takes three quick steps and vanishes[or]Right on cue, Jave hurries away[stopping].";
	make the lady run;
	say weaveloss;
	stop the action;
end if;

Check using:
	if the location is not a challenging room or Jave is not in the location begin; 
		repeat through the Table of Unchallenging Responses begin;
			if the locus entry is the location and the power entry is the noun begin;
				say "[the response entry][br]";
				stop the action;
			end if;
		end repeat;
end if;
	
Check using:
	if Jave is not in the location begin;
		say "There's nothing you really need to deal with here. You need to [if Jave is seen]find Jave[otherwise]work out the bounds of this place, see if Jave's really around[end if].";
		stop the action;
	end if;
	
Check using:
	if the player is not in a challenging party begin;
		[We need a message here that progressively clues the player in to how they should approach things.]
		say "[one of]Hm, how would that work, exactly? You have an idea or two, but -[p]A moment of confusion. Something's not right with the weave. Jave strides away, intent on something, fading as she goes. You're not sure what just happened[or]You're trying to figure out how to get through to her, when the weave [i]shifts[/i] again. You're more alert to it this time: first the fabric of the place convulses, and then Jave and you kind of flow apart, until she is entirely vanished.[p]No, that makes sense. The weave is anchored by the ways, so it stands to reason it'd be strongest where they connect. Most resistant to subversion. If the point of the thing is to keep you and Jave on opposite sides of the glass, it'll work best here[or]That lurch, and Jave takes off again. You'll have to try in a less stable area, away from the entry points[or]It's not going to work here. You make a token attempt to interact with Jave, and she predictably moves on[stopping].";
		make the lady run;
		stop the action;
	end if;

[Check using:
	if the location is not a challenging room begin;
		say "You can't figure out how that would apply here.";
		stop the action;
	end if;	
]

Instead of using in an egress:
	if Jave is not in the location and the best route from the location of Jave to the location of the player through undestroyed rooms is nothing begin;
		if the noun is warrior begin;
			say "You're tempted to strike one last blow, just as a fuck-you on the way out. Just one good punch to the stomach, winding her.[p]It's never as satisfying as it is in your imagination, though. ";
		otherwise;
			say "There's no point. You've failed. All that remains is to get out of here.";
		end if;
	otherwise if Jave is in the location;
	say "No time to mess around. You're still perilously close to the tangle. Maybe once you're further out.";
	otherwise;
	say "You can't afford to mess around out here. You should [if Jave is seen]get to Jave and see if you can disrupt the tangle[otherwise]see if Jave is inside the tangle[end if].";
	end if;

Volume - 16 Parties

A room has some text called listendesc.

a crowd is a kind of person. a crowd is scenery. a crowd is plural-named. Understand "people" or "crowd" or "guests" or "guest" or "man" or "woman" or "men" or "women" as a crowd.

Instead of kissing a crowd: try talking to the noun.

Instead of talking to a crowd: say "They're not really here; they're drawn from the worlds, somehow, though whether they're facsimiles, composites or projections of real people isn't clear. You could make light conversation with them, manipulate, harm; but you'll never be able to learn anything important from them, or understand them as individuals. Frankly, it gives you the creeps."

Instead of kissing a crowd: try talking to the noun.

Check attacking a crowd:
	if Reba is not in the location and Rivka is not in the location and Becky is not in the location, try using warrior instead.

[This is a crude hack: an alt-crowd is just a crowd that doesn't cause a multiple-objects command issue when there are two of them in a room together.]

An alt-crowd is a kind of person. an alt-crowd is plural-named. an alt-crowd is scenery.

Instead of talking to an alt-crowd: say "They're not really here; they're drawn from the worlds, somehow, though whether they're facsimiles, composites or projections of real people isn't clear. You could make light conversation with them, manipulate, harm; but you'll never be able to learn anything important from them, or understand them as individuals. Frankly, it gives you the creeps."

Check attacking an alt-crowd:
	if Reba is not in the location and Rivka is not in the location and Becky is not in the location, try using warrior instead.

A party has some text called boozedesc.
A party has some text called shortbooze.

A party can be challenging or unchallenging. A party is usually unchallenging.

A room has some text called javedesc.

A party has some text called rivdesc.

A party has some text called deathknell.

A clue is a kind of thing.

A clue can be unreachable or solid. A clue is usually solid.

Instead of taking a solid clue: say "You run a wistful finger over [the noun]. Keepsakes are not something that anybody gets to keep for long in the Ways - for that matter, practical equipment doesn't have much of a lifespan. That's never more true than in the constant transition of a tangle; little point in hanging on to it[one of][or][or]. When you were new and a fool, you kept a lock of Nusku's hair for nearly two years, subjective. Human tissue seems to be more constant. Eventually it turned into sloughed-off snakeskin and disintegrated[stopping]."

Instead of taking an unreachable clue: say "Not exactly the kind of thing you can lug around, even if it weren't ephemeral."

A party has a clue called javeclue.

The initial appearance of a clue is "[closeclue of the location]".

Instead of examining Jave:
	if the puzzlecount is 4 begin;
	say the description of Jave;
	otherwise;
	say "[the javedesc of location][br]";
	end if;

The description of Jave is "".
	
Jave can be seen or unseen. Jave is unseen.	
	
Carry out looking:
	if Jave is in the location, now Jave is seen;

Instead of listening:
	say "[the listendesc of the location][br]".

An egress is a kind of room.

Carry out going to an egress (this is the cruelty-check rule):
	if Jave is not in the room gone to and the best route from the location of Jave to the room gone to through undestroyed rooms is nothing begin;
		follow the Zarfian cruelty violation rule;
	end if;

Before going (this is the Reba placement rule): 
	if the room gone to is an egress, move Reba al-Nasri to the room gone to.

The singdesc of an egress is usually "You hum a soft walking rhythm to yourself."

An egress has some text called roadahead.

An egress has some text called rebadesc.

An egress has some text called rebalook.

An egress has some text called javeapp.

An egress can be rebaseated. An egress is usually not rebaseated.

An egress has some text called reba-act-1.
An egress has some text called reba-act-2.
An egress has some text called reba-act-3.
An egress has some text called reba-act-4.

Before going inside in an egress:
	if the room gone to is destroyed begin;
		say "You know without trying. No possible way back in; it's destroyed on that side. You've sealed yourself out[if Jave is in the location]. And probably for the best[otherwise]. Reba catches your glance, tightens her lips in sympathy, and gives a so-it-goes shrug[end if].";
		the mercy-kill happens in three turns from now;
		stop the action;
	end if;
	
At the time when the mercy-kill happens:
	wait for any key;
	try going outside;
	
Instead of going nowhere in an egress:
	say "The choices are a lot simpler: either dive back into the tangle, or head out of it entirely[one of]. The latter, you're reasonably certain, would be a final decision[or][stopping]."
	
triedtoleave is a truth state that varies. triedtoleave is false.	
	
Instead of going outside in an egress:
	if Jave is in the location begin;
		say "[roadahead of the location] Jave slips one hand into yours, which makes the going a little slower, but who fucking cares.[p]You might walk together for a month, or a few days, or years. But that's not something you have to worry about just yet. You know that you can talk, now, and that matters a lot more than what gets said.";
		end the story saying "On and up.";
	otherwise if the best route from the location of Jave to the location of the player through undestroyed rooms is nothing;
		say "Well, no point in delaying the inevitable. The tangle's collapsing on itself. Maybe Jave will be caught in it, and maybe she won't, but hovering at the threshold isn't going to help her. [i]Fuck.[/i][p][roadahead of the location] You'll be fine, eventually. You're good at being fine. You'll grow and change in the ways, and fall for someone else, and be a different kind of happy. It'll be fine.";
		end the story saying "It's still shit, though.";
	otherwise if triedtoleave is false;
		say "You fucking hate the idea. This is, you're sure, the last chance you may have to contact Jave in a very long time, and that could always mean never. You don't know if she can make it out of here on her own, either - [i]probably[/i] she can, if you can get out then likely so can she, but still. And, fuck, you hate the idea of running away with your tail between your legs, and seeing the Rebeccas at the next clade potlatch and smirking to one another about it.[p]But you could fucking [i]die.[/i] That is a point of some consequence. And you're not sure if you're still young and stupid enough to die for love or spite.";
		now triedtoleave is true;
	otherwise;
		say "Fine. Fuck it. Jave can handle herself, and getting yourself killed in a pocket apocalypse isn't going to fix anything. But you have the sinking sense that your paths may not cross any time soon.[p][roadahead of the location] You'll be fine, eventually. You're good at being fine. You'll grow and change in the ways, and fall for someone else, and be a different kind of happy. It'll be fine.";
		end the story saying "It's still shit, though.";
	end if;

Book - None Know The Hour

startroom is a party in tangle.  The printed name of startroom is "None Know The Hour". Understand "none know the hour" or "none" or "know" or "hour" as startroom.

The description of startroom is "Feels like the apartments of a well-off academic couple who entertain a lot. Lots of one-of-a-kind furniture in nice old wood, very full bookcases, art conspicuously esoteric and ethnic. Would feel kind of crowded if it wasn't so unnaturally tidy. More, similar rooms sprawling out through odd-angled spaces[one of].[p]It was themed as a maybe-the-apocalypse party - a small joke on the difficulty of scheduling social events in a multiverse. There's a mushroom-cloud cake (already partially eaten), a planet-shaped piÃ±ata, and a glittery banner declaring HAPPY END OF DAYS. A few of the guests are costumed[or][stopping]."

the nice wooden furniture is an unimplement in startroom. The description of nice wooden furniture is "The furniture is evidently the result of long, dedicated and tasteful (but not over-planned) questing in antique shops and odd corners of the world; congruent styles and colours (lots of weathered-brick red and teak brown), but just mismatched enough to let you know."

the bookcases are an unimplement in startroom. The description of bookcases is "There are a lot of coffee-table books in here, largely in the field of Photogenic Anthropology. A lot of hardcore-scholarly Upside treatises on magic and liminology, of the kind that walkers largely ignore. The complete Book of Eight Houses, including apocrypha. A bunch of things with hand-tooled leather covers. If there are any paperbacks in the house, they're not where visitors can see [']em." Understand "books" or "book" or "shelves" or "shelf" or "bookshelf" or "bookshelves" or "treatise" or "treatises" or "book of eight" or "eight houses" as the bookcases.

the esoteric art is an unimplement in startroom. The description of esoteric art is "A trio of Central African masks. An outsider-art-ish scene in coloured pencils, with hundreds of naively drawn figures with horribly distorted heads. The head of a steel drum, intricately cut out into birds and tree-branches. A frame drum the size of a truck wheel, with a raven and an eagle in solid panels of red and black. A fetish of dark wood, vaguely human but almost completely obscured by the narrow iron wedges pincushioning its body. A set of clay cylinder seals, framed behind glass." Understand "masks" or "mask" or "scene" or "drum" or "steel" or "birds" or "branches" or "frame" or "raven" or "eagle" or "fetish" or "seals" as esoteric art.

the mushroom cloud cake is an unimplement in startroom. The description of mushroom cloud cake is "It represents quite an impressive feat of construction, though you suspect the central column of hiding supports. The icing is the most striking feature, a beautifully-brushed grey that somehow suggests fiery destruction glowering from within the choking dust; the effect is spoiled somewhat by the parts that have been cut away, revealing a nondescript white sponge."

Instead of eating the mushroom cloud cake: say "It's just a dry old sponge with fancy icing. Not worth the hassle of working out which part to hack off."

[Mushroom cloud cakes existed as early as 1946, when a truly hideous one was made to celebrate the conclusion of Operation Crossroads. Given that they had just sent a bunch of native Bikinians to starve on an inhospitable island, it is a cake with many layers of tastelessness.]

the planet-shaped pinata is an unimplement in startroom. The printed name of planet-shaped pinata is "planet-shaped piÃ±ata". Understand "planet" or "shaped" as pinata. The description of pinata is "A crude papier-mache globe, with green blobby continents looking rather crowded for space."

Instead of attacking the pinata: say "You're not generally squeamish about symbolism, but for some reason the thought of it makes your skin crawl a little."

the glittery End of Days banner is an unimplement in startroom. The description of glittery banner is "HAPPY END OF DAYS, in a cheerful font with little rainbow-reflective sparklies all over it. Mass-produced, evidently, which raises a question or two."

the tsadesc of startroom is "A politely-deployed tangle will often have a sort of foyer, a not-too-outlandish area in which guests can orient themselves. With a discreetly-positioned mirror. Yeah, you look pretty close to your default: soft butch woman, five-three-ish, strong but totally non-graceful, spiky hair dyed crimson."

the academics are a crowd. academics are in startroom. The description of academics is "They're mostly between twenty and forty, predominantly white or East or South Asian, about three-quarters female, above-average queer quotient. Slightly-tweedy business-casual. It's very tempting to assume that this represents the Three Rebeccas['] clique, if they even have such a thing. (Or what they'd like their clique to look like. Or some version thereof skewed towards you.)[p]A handful of them are in fancy dress: two of the Horsemen of the Apocalypse are necking in the kitchen, and you count two Jesuses and one Antichrist, all women. Best concept is definitely Loki, a cheekbony young man with a plush wolf on one shoulder and a plastic snake on the other." Understand "Jesus" or "Jesuses" or "Loki" or "Antichrist" or "horsemen" or "horseman" as the academics.

The listendesc of startroom is "The music here is lush and busy, earwormy string motifs darting through it like silver schools of fish in dark water. The vocals are a high, honeyed drawl, almost a meow, the singer's voice looped back to provide her own accompaniment." [This is intended to be Emily Wells.]

The boozedesc of startroom is "This is evidently a red-wine kind of affair; you sense that the expensive spirits will be brought forth later in the evening, once the faithful have been sufficiently winnowed."

The shortbooze of startroom is "You slosh out a half-glass of Shiraz."

The desolation of startroom is "The guests are gathering around the windows. (Were there windows before?) A cityscape, a crescent of glittering high-rises along a shoreline, with an orange-purple sunset filling the sky over the sea. The sea itself, though, has a dark line across its face, a band that grows thicker even as you watch it.[p]Time bends. Roaring. The wall of water engulfs the waterfront, swallows onetwothree blocks."

The demise of startroom is "The tidal wave rolls into the building, tearing it from the foundation and crumpling it like paper. There's a brief, lucid moment where the floor is crazy-angled and there's darkness all round, but most of the walls are intact and there's still air. And then it's a laundry cycle full of concrete."

The javeinit of startroom is "Jave is loitering by an end-table, looking out-of-place."

The javedesc of startroom is "Jave is wearing a Vault Dweller jumpsuit - the armoured version, with shoulderpad, bandolier and kneepad, and 34 stenciled across the upper back. On her, it looks like work clothes rather than a costume."

Before going from startroom:
	remove Becky Baumgartner from play;
	if numbat is 0, grand expostulation happens in two turns from now
	
At the time when grand expostulation happens:
		if puzzlecount is 0, say "[expostul1]";
	
The rivdesc of startroom is "Rivka Strossi is kicked back on the couch, arms slung around two randoms, her feet up on the coffee-table. She appears to be sipping Kool-Aid. Jeez, way to keep it classy."

The rivcostume of startroom is "She looks like a gatecrasher, what with the scuffed-up biker jacket and the purple skinny jeans and one shoe with the sole beginning to peel off. If anyone could pull off gatecrashing her own damn party, it'd be Rivka."

The rivweapon of startroom is "a scythe".

Instead of talking to Rivka Strossi: say "All the response you get is the impassive regard of her aviator shades. Maybe the beginnings of a smirk."

The closeclue of startroom is "Over on a cabinet, a wineglass with red lees in the bottom. And one of those tasteless little charms around the stem, in the shape of an osprey."

the wineglass is a clue.  the javeclue of startroom is wineglass. Understand "osprey" or "hawk" or "charm" or "glass" or "lees" as wineglass. The description of wineglass is "A wide-bowled crystal wineglass, the sort that are meant to only ever be a third or so full. A small pool of wine and lees in the bottom. The osprey wine-charm is - nobody else in the room is using wine-charms, they're tacky as fuck. It's made of pewter and not very accurately-cast, but is still recognisably a mid-sized bird of prey."

The player is in startroom.

The player is female. [One or two reviewers elected to treat Tsawac as ambiguously-gendered, and I see why. It's probably more accurate to describe her as a woman of flexible gender.]

Book - Ex Officio

office party is a challenging party in tangle.  office party is south of startroom. The description of office party is "Paneled ceilings, carpet tile, decor that's dead inside. A table of snacks and a sad-looking punchbowl. The non-smell of recycled air. Party decorations from a party store[one of]. You've never had a job in a context remotely like this, so it seems unlikely that they're constructing this out of your memories. That plastic-y air smell is too solid. Has to be something else[or][stopping]." Understand "function" as office party.

the paneled ceiling is an unimplement in office party. The description of the paneled ceiling is "These things always make you think of piled-up dust and dessicated dead birds. Which is unfair, since those appear in other ceiling-spaces too."

the carpet tile is an unimplement in office party. The description of carpet tile is "It's one of those horrendous colours that exists weirdly in between actual colours that a human being might appreciate."

the party decorations are an unimplement in office party. The description of party decorations is "A few helium balloons already beginning to deflate, some hologram-sparkle streamers, an unused stack of conical hats." Understand "balloon" or "balloons" or "streamers" or "hat" or "hats" or "conical" as party decorations.

The printed name of office party is "Function".

the buffet table is an unimplement in office party. Understand "snacks" or "snack" as buffet table. Instead of eating buffet table: say "You take a small nibble of something that doesn't look too offensive. Appearances prove deceptive."

The desolation of office party is "Hunh. There's a pile of empty clothes in one corner. That's probably not good. And there's another.[p]You catch the next one in the act: a crystal-clear radiant glow, an understated celestial hum, and a pasty young man is gone. The problem is that the radiant glow is [i]so[/i] clear that it illuminates the details of the process, which seems to operate at slightly different rates on different tissues: it's possible that you imagined the look of total agony in his eyes, but you're unwilling to wager on it.[p]The pace picks up almost immediately; three more are gone before you can think."

The demise of office party is "You begin to glow, first at the extremities but rapidly spreading up your limbs to the core. Skin disintegrates, and your nerve-endings have time for one unified, maximal howl of pain before they're gone. You're still conscious after all your senses are gone, but only for a mercifully brief moment."

the office workers are a crowd. The description of office workers is "They're tired, kept going by social duty and quiet desperation. This, and the light, makes them look ugly." The office workers are in office party.

The tsadesc of office party is "Periwinkle-blue shirt, grey pencil skirt. The sort of shoes called sensible by people who aren't. Hose that fall into that narrow band between Too Sexy To Be Appropriate and Too Unsexy To Be Appropriate - which you can recognise in others, but have never bothered to learn how to select for yourself."

The listendesc of office party is "Upbeat, vacuous elevator rock. Gives you the crawls."

The boozedesc of office party is "Mass-market lager and anaemic punch. Avoid."

The shortbooze of office party is "You ignore the punch and seek out the least offensive of the lagers."

The rivdesc of office party is "Rivka is perched atop a photocopier, necktie akimbo, regarding you with bland amusement."

The rivcostume of office party is "Rivka's shirt is untucked and two sizes too big for her, and her tie is in punkish disarray. She isn't smoking, but she might as well be."

The rivweapon of office party is "a .40 automatic".

The javeinit of office party is "Jave is hovering by the buffet table, avoiding making eye contact with anyone."

The javedesc of office party is "She's fetchingly dowdy. Herringbone skirt. Chunky-rimmed glasses. Inexpert updo. She's hovering by the refreshments table, trying not to engage with anyone."

The javecostume of office party is "Herringbone skirt. Chunky-rimmed glasses. Inexpert updo, beginning to fall apart."

The closeclue of office party is "There's a paper plate abandoned on the edge of the buffet table."

the paper plate is a clue. the javeclue of office party is paper plate. The description of paper plate is "The plate's been picked over, with a pile of rejected items - some limp celery sticks, an ill-favoured species of cracker - over on one side. She loves food, but she has a habit of throwing everything on her plate and sorting out what she actually wants to eat later. You've been curious about that, and haven't found the moment to ask: is it a military mess-hall habit, or is there something going on from further back? There's a lot of undramatic, ordinary mystery left to go over. Or, to put it another way: you don't really know one another all that well."

Carry out using abider in office party:
	say "Jave marshalls an office chair and several bottles of white wine over to a desk, puts her feet up, and stares at the ceiling. The message to the Rebeccas is clear: [i]your move[/i]. Oh, gods and monsters. She can do this for [i]days.[/i] And whatever reaction they want to get out of you, it ain't this.[p]You watch with interest. After perhaps half an hour, a youngish guy with bad hair attempts to start a conversation with her and is repelled with a firm 'voetsak'. Maybe an hour after that, a knot of women make an effort to draw her into a conversation, and are met with indifferent silence. (Nobody comes up to you, but you perfected your go-tae-fuck glower long ago.) Time drags. This would be insufferable if you didn't have Jave to watch, and if you didn't have a delightful picture of the Rebeccas getting steadily more irked.";
	ruin everything;

[make the goal of foraging a little clearer]

Carry out using forager in office party:
	say "You once went a month eating nothing but sedge roots, and it wasn't quite as dispiriting as this snack table. Maybe if you treat this as a blasted wilderness it'll all seem better.[p]Fire, first. Plenty of paper here, but not a lot of untreated wood. You'll just have to keep feeding it, then. Punch holes in a metal wastebasket for air-draw. A piece of tinfoil scrunches into a pot. There are packets of butter and salt around, and water, and really that's all you need for most cooking. You prop the wastebasket on a windowsill, start a fire, and start turning sad vegetable assortments and lacklustre salami into something worth getting enthused about. Braise, then turn into stew. There's plenty of starchy food to thicken it with; you grind crackers into powder with a beer-bottle pestle.[p]You're conscious of Jave watching, quietly amused. Nobody else appears to have noticed. What you're left with is a somewhat-blackened, thick, greasy soup, and smoky clothes. You've eaten similar-looking things a thousand times, and often it tastes like arse, but there's always a small sense of the magic of the process, of how the cauldron transforms the raw into the cooked.";
	ruin everything;
	
Carry out using antinominalist in office party:
	say "Well, obviously. The rules are what choke this place, and if she doesn't give a shit -[p]She heads straight for the door, and is gone before you can do anything about it. Yeah, given your druthers you'd ditch this party as soon as you humanly could, too, but given that she didn't leave with [i]you[/i] it's not exactly doing you any favours.";
	make the lady run;

Carry out using laughing one in office party:
	say "She could adapt to the style of humour prevalent here, assuredly - but that's the kind of humour that functions as a wall, when you really need a bridge.";

Carry out using warrior in office party:
	say "Wiping the floor with this lot would get Jave's attention, but not in a [i]good[/i] way. And you're not sure if these are illusions, or reflections, or real people.";

Carry out using commander in office party:
	say "She looks smaller, closed-in; you can't imagine this version of her directing anyone. Younger? Less assured, regardless. Is this why she was always horrified at the idea of being out of the field?";
	
Carry out using troublemaker in office party:
	say "You're more interested in salvaging something from this godawful place than in making things [i]worse[/i]."
	
Carry out using critic in office party:
	say "There's documentation everywhere here. You glance at an arbitrary selection of notices, memos and reports. People are tired and untrusting, is the short version. Almost all the language is oriented around professional self-defence; people scrupulously avoid anything that might make them vulnerable. So the problem is about how to pierce through that."

Book - Beautiful People

beautiful people is a party in tangle. beautiful people is west of office party. The printed name of beautiful people is "Granite and Glass". Understand "granite" or "glass" or "granite and glass" as beautiful people.

The description of beautiful people is "This is a home, you think, but a home out of a spy movie or a photoshoot, not intended to be lived in by humans. Ceiling and floor are vast oceans of polished black granite; the walls on two sides are sheer glass, so the overall effect is of being between two crushing plates. There is no clutter at all, each item of furniture spaced out from the others like a piece in a gallery. The guests are beautiful in a marble-and-porcelain, expensive-tailor manner; they aren't so much mingling as posing in groups.[p]There'll be a garage somewhere, and a ridiculous prestige car, and the road out."

the way to the garage is a waymouth in beautiful people.  Understand "car" or "prestige car" or "prestige" as the way to the garage. The description of garage is "You can feel that outwards from here is the Night Road: it gives you a feeling of burger-grease and petrol fumes, high-beam glare, the squeeze of a brake pedal."

[The specific image I was thinking of here was a vague recollection of the cover of the Skunk Anansie album _Post Orgasmic Chill_, which now that I actually reference it looks almost nothing like the image I had in mind - it's a scabby hotel room with ceiling tile, gross furniture and an aging carpet. The only commonalities are the leggy-model pose and dress Skin is assuming, and the blocky French-windows shape of the room. Probably it got merged with something more Frank Lloyd Wright-ish in memory.]

the black granite floor is an unimplement in beautiful people. The description of black granite floor is "It's as though the whole room is an expensive kitchen countertop."

the sheer glass windows are an unimplement in beautiful people. The description of sheer glass windows is "Seamless, frameless, without blemish. There's a good-sized drop on the other side, and it's easy for your fear-brain to forget the glass is there."

the expensive minimalist furniture is an unimplement in beautiful people. Understand "chaise" or "longue" as expensive minimalist furniture. The description of minimalist furniture is "Simple, clean shapes and dark tones matching the black granite; meant to draw the eye towards their occupants. Probably about as comfortable as a cinderblock."

The tsadesc of beautiful people is "You're perhaps a shade more slightly built and a couple of inches taller. Six inches taller, counting these awful strappy shoes. Even so, you're still at about cleavage height for most of the women around here. There's a sort of airbrushed quality to your skin, and your hair's gelled back against your scalp."

bellas are a crowd in beautiful people. The description of bellas is "Tall, graceful men and women, all slightly androgynous in an elfin, decorative manner. Crisply and expensively dressed. Everyone in carefully-chosen greys and blues, and you in scarlet. You can tell that, in other settings, each would be alarmingly beeautiful; taken together, they feel like a school of predatory fish." The printed name of bellas is "beautiful people." Understand "elves" or "elf" or "tall" or "graceful" as people.

The listendesc of beautiful people is "A nightingale-voiced woman sings flowing glossolalia over an electronic soundscape that's all spangles, chimes and glitter. Nightingale-voiced in the way you'd interpret it if you'd never actually heard one." [I'm thinking of Cocteau Twins here.]

The boozedesc of beautiful people is "You see no bottles, bar or anything else that could be used to procure alcohol, but a number of the guests hold cocktail glasses, their contents either icewater-clear or a gleaming mahogany. You venture a sniff at the latter: it's cold tea."

Instead of drinking the drinks in beautiful people when beautiful people is not doomed: say "It's not for actually drinking. It's for posing with."

The singdesc of beautiful people is "The music here is so floaty and ethereal that you can't really imagine that the voice is produced by the same set of organs that you use for talking."

The dancedesc of beautiful people is "The posed stillness of the room is getting on your nerves. You fail to work up the courage to disrupt it with some crumping or something equally fast and angry."

The desolation of beautiful people is "There's something wrong about the man next to you. His perfect face is... yeah, that's a blemish. No, a full-on rash. The woman next to him has it, too, a patch of cracked, flaking skin spreading across one shoulderblade. The woman next to her - shit, they've all got it.[p]Lesions blossom on flesh, suppurate into craters. Skin sags, spines hunch, eyes yellow. Beside you, a woman coughs and splutters, then spits out a mouthful of teeth and blood-streaked mucus. There's a splattering sound as someone's bowels void. One by one, the guests crumple to the ground, crawl awkwardly into corners. You don't seem to be affected, yet, but your skin itches furiously, phantom spikes of pain crawling around your body. Go. Go."

The demise of beautiful people is "Your face itches. Your eyes itch, watering uncontrollably. You're unable to resist scratching, and when you do something rips, bursts wetly down your face. The nausea hits all at once, dizzying, so you vomit as you're falling over, which would probably look really impressive from some other perspective. You don't have the energy to fall properly, and something in your forearm snaps as you hit the floor.[p]It's largely downhill from there; if you picture every disgusting thing your body has ever done and then compile them into a twenty-minute clip-show, it's pretty close to that."

The rivdesc of beautiful people is "Rivka Strossi is immediately obvious, being the only other person in the room under six feet tall. She's leaning in a corner, head cocked to gaze out of the window."

The rivcostume of beautiful people is "She's wearing an cream suit, collar and tie aggressively immaculate. Her dark hair is magazine-cover perfect. It takes you a moment to notice the brass knuckles."

The rivweapon of beautiful people is "a telescoping baton".

The closeclue of beautiful people is "Beside the mantelpiece of a wholly decorative fireplace, someone - evidently as bored with this place as you are - has scratched a series of graffiti into the slate walls."

the graffiti is an unreachable clue. the javeclue of beautiful people is graffiti. The description of graffiti is "A little South African flag, the word 'shit' in various fonts, several rather inexpert faces, a recognisable Blackhawk helicopter, and some stick-figures fucking." Understand "flag" or "faces" or "helicopter" or "blackhawk" or "stick figures" or "figures" or "stick-figures" as graffiti.

The javeinit of beautiful people is "Jave is standing by the arm of a chaise longue, flanked by a pair of perfect, bored-looking drones[one of]. Is the idea here to make you jealous? Someone didn't do their research[or][stopping]."

The javedesc of beautiful people is "Jave is normally a good head taller than you. In heels, and elongated to scale, she towers[one of]. [p]You haven't ever seen her in heels before. And with her skin airbrushed by context, she fits in with the aesthetic here a whole lot better than you do: she has the fine bones, the long graceful limbs, the ability to contendedly sit still for more than twenty seconds... but, ah, that's too obvious a trick. They'll have to do a whole lot better than body-image wank to get to you[or][stopping]."

The javecostume of beautiful people is "Jave has kicked off her heels and seems closer to her regular height, but she's still wearing a scoop-back dress, shimmery and deep aubergine."

The Night Ride is outside from beautiful people. Night Ride is an egress. The description of Night Ride is "A dangerous way, but a swift one. There are branches of the Night Ride that are unlit mud tracks that you go down a-horse, post to post, watching the woods; there are gun-barrels through desert or taiga, mountain dragonbacks, or - as here - an inhuman world of neon signs, snaking lines of tail-lights, a grimy trash-strewn darkness. They're all the same way, and you're one of those who endorse its other name, the Hell Road.[p]You're behind the wheel of a vehicle: fuck knows what make or model, but it seems low-slung and expensive. Something rhythmic and poncy with a lot of synthesizers is playing on the stereo." Understand "hell" and "road" as Night Ride. Understand "car" or "garage" as Night Ride.

The javeapp of Night Ride is "Jave is in the passenger seat".

The listendesc of Night Ride is "Consciously-electronic music. Soft, sterile emotions, heartbeat rhythms. Lyrics that repeat until they assume deep meaning. A feeling of... celestial attenuation."

The roadahead of Night Ride is "The road glares with red eyes, and the radio murmurs love-hymns to software."

The rebadesc of Night Ride is "Reba al-Nasri is in the darkness of the back seat, face contoured by streetlight shadow."

The rebalook of Night Ride is "You can make out no more of her than her neck, half an arm, one side of her face with the eye reflecting traffic-light amber. She is very still"

The singdesc of Night Ride is "You sing along with the stereo, which mostly involves subvocalising the harmonies."

The dancedesc of Night Ride is "You chair-dance a little in your seat, until you make the car swerve and return to your senses."

Night Ride is rebaseated.
The reba-act-1 of Night Ride is "She leans over the back of your seat, her breath on your ear."
The reba-act-2 of Night Ride is "You do your best not to draw her in to your neck."
The reba-act-3 of Night Ride is "Reba leans forwards in her seat and squeezes your shoulder."
The reba-act-4 of Night Ride is "She settles herself, arranging her hands neatly in her lap."

The tsadesc of Night Ride is "In the Night Ride, one is little more than an extension of one's vehicle. You fit into the driver's seat like a hand in a glove, and you're only aware of your flesh as connections to steering-wheel and pedals, the flickering rotation of head and eyes between road, mirrors, instruments."

Book - Games Night

gamesnight is a challenging party in tangle. gamesnight is east of office party. The printed name of gamesnight is "Solitaire With Friends". Understand "solitaire" or "friends" or "solitaire with friends" as gamesnight.

The desolation of gamesnight is "The player opposite you cracks his knuckles. 'Endgame.'[p]The rules change. Armies crawl of their own volition across the map, meet and consume one another. A cluster of fire-blossoms burst across one stretch of the map; in another, an inland sea bursts a channel into the ocean. A vast serpent drags itself from the sea, scattering pieces before it.[p]The room has been replaced by darkness. The other players are lit only by the light of the world spreading below: jackal-head, skull, empty hood, lion.[p]There are fewer and fewer pieces. You don't want to find out what happens when one of them wins."

The demise of gamesnight is "The empty hood directs counters across the board, throwing all its reserves in a surge to fend off the jackal-head. A narrow section of the map boils with activity, and one of the spreading ripples obliterates your last few counters.[p]Nobody appears to notice. The game continues, but it seems further and further away, until it's swallowed up in an all-consuming dark."

The listendesc of gamesnight is "A stereo plays droney white guys with guitars singing about quirky mathematics esoterica with the less-than-covert implication that this is the only way they know how to articulate their interpersonal angst. Fortunately, not turned up all that high." [This is probably TMBG, but it could be any number of geek-rock groups.]

the gamers are a crowd in gamesnight. The description of gamers is "Convivially withdrawn, is how you'd put it. Unkempt."

The description of gamesnight is "A comfortably cluttered room: warm but clear lighting, lots of shelves. You're seated with a half-dozen others around what is probably a dining table, and on the dining table is spread out a board-game with approximately a billion pieces. Beer-bottles have competed ineffectively for real estate, and are relegated to precarious edges." 

the game shelves are an unimplement in gamesnight. The description of game shelves is "You crane your neck to read off titles. Pandemic. Arkham Horror. Illuminati. Twilight Struggle. Gloom. Betrayal at House on the Hill. Risk. A ratty old printout of MetrofinÃ¡l."

the dining table is an unimplement in gamesnight. The description of the dining table is "It's rather too large for the room, but just the right size for the game."

the board game is scenery in gamesnight. The description of board game is "The board takes up about half the table's real estate, and is clearly a map of a city - maybe Renaissance? There's a scoring track around the outside, and a number of areas set aside for little stacks of cards and particular tokens to stipulate special things, and a whole lot of coloured cubes positioned across it. Every player has a hand of cards and other cards in front of them, a little stack of cardboard coins, and their own brightly-coloured set of tokens in front of them. (You're red.)" Understand "pieces" or "cards" or "cubes" as board game.

Does the player mean examining the board game: it is likely.

The tsadesc of gamesnight is "You're about the same as you generally are, though maybe with a little less muscle. Hooded chocolate-brown sweatshirt with a microbrew logo on it, knackered old Carhartts, stripy socks."

The boozedesc of gamesnight is "Microbrews and ciders, in six-packs."

The shortbooze of gamesnight is "You crack a Fremont IPA from its six-pack holder."

The rivdesc of gamesnight is "Rivka Strossi is seated opposite you. She has a great many more cards in hand than anyone else."

The rivcostume of gamesnight is "She's wearing a navy-blue hoodie with ILLUMINATI in big white letters across the chest. She's toying with a gaming figure, hunched and hooded and carrying a pair of oversized shears."

The rivweapon of gamesnight is "a flaming sword"

The closeclue of gamesnight is "There's an empty seat at the table, with a set of yellow-orange markers stacked up in front of it. She was just here."

the game tokens are an unimplement in gamesnight. Understand "token" or "marker" or "markers" or "seat" or "pieces" or "empty seat" as the game tokens. Does the player mean examining the game tokens: it is unlikely. The description of game tokens is "There are at least a dozen different shapes of token, some associated with individual players by colour, some not. Little stick-figure people, coins and cubes and shields. You hope you don't get stuck with the job of putting this all back in the box."

Playing is an action applying to one visible thing. Understand "play [something]" as playing. Understand "play with [something]" as playing. Instead of playing: say "That's not a game." Does the player mean playing the board game: it is very likely. Instead of playing the board game: say "You're already dealt in. You're playing. That's not the issue here."

Instead of playing Jave when the location is gamesnight: try playing the board game.

[PLAY GAME WITH]

Playing it with is an action applying to two things. Understand "play [something] with [something]" as playing it with.

Carry out playing it with:
	say "This is not a game.";
	
Instead of playing the board game with something:
	try playing the board game;

Instead of playing the game tokens with something:
	try playing the board game;
	
Instead of playing the orange markers with something:
	try playing the board game;

Does the player mean playing the board game with something: it is very likely.

the orange markers are a clue. The javeclue of gamesnight is markers. The description of markers is "Little coloured wood tokens in various shapes, a stack of cardboard coins. Rather a lot of them, compared to everyone else at the table. They've been arranged into a tidy little rank-and-file parade." Understand "yellow" or "token" or "marker" or "tokens" as the orange markers.

The javedesc of gamesnight is "Jave is leaning forward, elbows on the table, expression unreadable. There's an untouched beer at her elbow[one of], and her eyes are flickering from player to player, scanning tokens. Ah. Yeah. You can see why she would be inclined to take this way, way too seriously[or][stopping]."

The javecostume of gamesnight is "She's still carrying a beer-bottle, grip reversed to make it a weapon."

The javeinit of gamesnight is "Jave is seated opposite."

Carry out using commander in gamesnight:
	say "You catch Jave's expression. There's that slightly spaced-out look she gets, focused on thin air, when she's roughing out the shape of a plan. Then she looks up, catches you watching, and grins.[p]She takes her moves quickly, almost carelessly, dictating actions in a brisk monotone. She accumulates points and tokens in small increments, spends them on minor advantages before they begin to pile up and look threatening. You do your best to do likewise.[p]She catches the tipping-point; one retained card amplifies another, tokens start changing hands too quickly for you to follow it all, and - well, from the looks on their faces, two of the players have had their hopes and dreams dropped into a meat-grinder, and the rest are scrambling to adjust.";
	ruin everything;
	
Carry out using troublemaker in gamesnight:
	say "You don't grasp the rules, or the unspoken approaches supervening on them, well enough to win. Fair enough: then you can allow Jave to do her thing while you play spoiler.[p]You start out with some simple sacrifices that gain you nothing but inconvenience other players. They don't bite. You kick it up, withdrawing at an easily-defended point to expose one player to another, leading to frantic scrabbling to reprioritise; you keep as far out of the ensuing mess as possible, sniping at easy objectives.[p]Jave, who has been quietly orthodox up to this point, executes a rapid series of force-multiplier manoeuvres, making serious gains and drawing everyone's attention. You affect to ignore her, continuing with your bootless pestering and forcing everyone to devote resources to coping with your bullshit. One gets grouchy, devoting unnecessary resources to try and crush you completely, and you make small sacrifices and hedgehog up while Jave ravages their unprotected holdings. You're doomed, but Jave looks comfortably in control of things.";
	ruin everything;

Carry out using laughing one in gamesnight:
	say "She grins appreciatively at the referential snark that's occasionally thrown around, but this isn't a game overly reliant on social cues.";

Carry out using antinominalist in gamesnight:
	say "In certain situations, rules are not just constraints on activity: they [i]form[/i] the activity, and disregarding them makes the act pointless. Maybe if Jave was one of those assholes who consider cheating a metagame always inherent. She's not.";
	
Carry out using abider in gamesnight:
	say "There are games where you can win by treading water, minimising risk, and avoiding unforced errors. You have a strong intuition that this is not one of them.";
	
Carry out using warrior in gamesnight:
	say "Physical prowess isn't much help within the game, and flipping the table would probably count as failure.";
	
Carry out using forager in gamesnight:
	say "You feel rather full, honestly, with the slightly gross feeling that comes from overindulging in junk food.";

Carry out using critic in gamesnight:
	say "You find the rulebook and leaf through it; there are a [i]lot[/i] of pages. It's structurally haphazard and has a sort of rhetorical pedantry that muddies as much as it clarifies. You derive some salient insights into the cultural fetishes of its designer and his (definitely his) community, none of which are very helpful in actually understanding how to [i]win[/i] the damn thing."
	
Book - Game Jam

gamejam is a party in tangle. gamejam is east of gamesnight. The printed name of gamejam is "Parallel Crunch". Understand "jam" or "crunch" or "parallel" or "convention" as gamejam.

The description of gamejam is "A convention kind of space, a room that feels low-ceilinged because it's so big on the other axes. Resilient carpet and inoffensive lighting accommodate an ocean of folding tables. Said tables are occupied by nerds of varying degrees of hipness, some furiously typing at laptops and others circulating busily within small groups.[p]An emergency exit leads outwards to a familiar stair."

The folding tables are an unimplement in gamejam. The description of folding tables is "Blocky, unremarkable."

the emergency exit is a waymouth in gamejam. The description of emergency exit is "Unremarkable double-doors with a plastic lightbox declaring EXIT above them, but beyond them you sense the Stairways, their hallway-echo and smell of cleaning products." Understand "doors" or "door" or "lightbox" or "plastic lightbox" as emergency exit.

the devs are a crowd in gamejam. Understand "nerd" or "nerds" as devs. The description of devs is "They're sun-starved, mostly young, trendy in an aggressively anti-stylish manner, and in a state of ferociously contained panic."

the closeclue of gamejam is "A map has been sketched on a whiteboard, annotated in a familiar scrawl."

the whiteboard map is an unreachable clue. the description of whiteboard map is "Jave's always had the picture formed in her head, and she only drew when there was a need to explain it. This is a four-by-four grid, like a tiny chessboard. You can't make out the labels (it was meant to go with a spoken explanation) but half of the squares have arrows leading from them to points outside the grid, and the other half have exclamation points."

the javeclue of gamejam is whiteboard map.

The tsadesc of gamejam is "Close to normal, except that you're full of endorphins. Digicammo tactical pants. GDC tanktop. Keds."

The threshold of gamejam is 1.

The boozedesc of gamejam is "Most of the people here seem to be drinking energy drinks in vividly-coloured cans. You suspect that at least some of them are supplementing this with vodka, but if so they're not sharing."

Instead of drinking the drinks in gamejam when gamejam is not doomed: say "You're not chugging someone else's energy drink just in the hope that it's got some liquor in it. Well, not in front of them, anyway."

The javeinit of gamejam is "Jave is definitely not part of this set; she's moving around to some ulterior purpose."

The javedesc of gamejam is "She's strolling easily around, with clothes and body-language that scream [i]security[/i]. A bland lack of interest in the interesting details of what people are working on, a sweeping, tactical gaze. Jave's brain doesn't really register artistic creation as in any way interesting; she [i]likes[/i] a great many things, but has a maddening indifference regarding their ontogeny."

The javecostume of gamejam is "Black cargo pants, black T-shirt, a now-useless earpiece mic dangling from its wire."

The listendesc of gamejam is "Self-consciously digital music, with lots of little arcade-game bleeps and show-off fiddly quickness. No lyrics, or noises abrasive enough to break concentration."

The rivdesc of gamejam is "Rivka Strossi has a posse. She's standing, leather-jacketed, at the shoulder of a panda-eyed blonde androgyne who's typing furiously, totally intent on their laptop. Other kids are in a buzzing cluster around them, pointing things out or making suggestions. Rivka says nothing, but she's clearly the real audience for everything being said."

The rivcostume of gamejam is "She's formed much as she usually is, when you see her at Sheafhome or along the Empty City. Her adornment, here, is the group. They're frenetic, weird, gorgeous, and so young; and in a thousand small ways they constantly, silently defer to her. You silently hope that they're not really hers, that their real heroes are worth it."

The rivweapon of gamejam is "a taser".

The dancedesc of gamejam is "You anxiously bounce on the balls of your feet, then throw in a little twirl. Man, this place makes you jittery."

The desolation of gamejam is "There's a cheap-looking clock up on one of the walls, its rim a sort of scabby plastic-plate blue. You only notice it because everybody's shooting hurried glances at it. It's on the final minute, each tick of the second hand somehow audible across the entire space. Hands are flying across keyboards, curses muttered under tight breath. One by one, groups hit a moment of final tension, then relax, snap lids down, walk out."

The demise of gamejam is "The second hand clicks to the top of the hour. The few groups remaining throw up their hands in resignation, pile their stuff together, congratulate and commiserate. You catch the signals: old friends who don't get to see one another enough, passing acquaintances transforming into friends, colleagues affirming respect, rivals uncomfortably playing nice, incipient hookups. From appearances, the real party happens now, in a dozen different places for as many reasons. None of which you are part of. Nowhere to go.[p]As the last of them leave, the lights start going out row by row, until you're left the dark, alone."

The Turning Stair is outside from gamejam. The Turning Stair is an egress. The description of The Turning Stair is "A landing, quite ordinary, dim and unhealthy-looking in the manner of something institutional from in the post-war decades. A staircase goes down, double-doors lead this way and that, two others lead up in different directions.[p]And each will lead to another staircase, and another, a door, a landing. The labyrinth at Minos was just a big building, seen through the eyes of uncultured Greeks for whom a three-room house was a palace: the Stairways and Doorways are that sense of mazed awe, the feeling that the corridors might go on forever, the wildwood in the dorm block." Understand "stairway" or "stairways" or "doorway" or "doorways" as turning stair.

The javeapp of Turning Stair is "Jave is sitting on a step, catching her breath".

The listendesc of Turning Stair is "Near-silence. The faint buzz of a light on the go, distant footsteps echoing in an empty hall."

The tsadesc of turning stair is "Very close to your default: red turtleneck, black cargo pants, approach shoes. You always wish that the Turning Stair would give you beefier thigh muscles, but it only does so in the conventional, unpleasant way."

the rebadesc of Turning Stair is "Arrayed in theatrical drapery, Reba al-Nasri stands at the top of one stair."

The rebalook of Turning Stair is "Reba is swamped in vaguely-classical drapery, not corresponding to the dress of any particular era, culture, class or gender so much as to painterly imagination. It cascades down in folds and pleats and counter-folds, spills across rather a lot of the staircase, and exposes one carefully-arranged arm"

the roadahead of turning stair is "A familiar burn builds up in your quads as the stairs turn upward."

Book - Teens

Under My Roof is a challenging party in tangle. Under My Roof is north of beautiful people and west of startroom. The printed name of Under My Roof is "Under My Roof".

The description of Under My Roof is "Middle-class living-room, with couches. The teenagers variously slouched around the couches and the floor are clearly the occupant's spawn and comrades thereof. Big blocky stereo from back when CDs were new and exciting. The air's full of the reek of cheap weed, half-empty drinks are scattered over every surface and niche, and over at one end of the couch there's a couple pointedly making out."

the couches are an unimplement in Under My Roof. Understand "couch" or "sofa" as couches. The description of the couches is "Highly comfortable. Not cheap, but not new."

the blocky stereo is an unimplement in Under My Roof. The description of blocky stereo is "From an era when everybody felt the need to own gigantic hi-fi tower speakers, then play recopied tapes on them."

The tsadesc of Under My Roof is "Either the Rebeccas are pulling on your experience or they're [i]murderously[/i] good at guessing: this is basically you at seventeen or so, gangly and raw-boned. You dressed in a similar style back then, only with worse taste and an aggressive indifference to whether clothes fit properly. Or needed to be washed. Or repaired. Or burned."

The desolation of Under My Roof is "There's a muffled explosion. It takes you a second to realise that it's the door being kicked in.[p]Then there are red-and-blues flashing harsh through the windows, and everyone's scrambling in one direction or another. There's muffled shouting through a megaphone, but the words are broken up by further crashes and yells of fear or pain. [i]Thump[/i], a window breaks and the room's filling with smoke, and some of the shadows moving through it have gas-masks and clubs."

Instead of using warrior in Under My Roof when Under My Roof is doomed:
say "This is an unsustainable fight. You've got no allies, a surprise attack by what look like LE professionals, no weapons or defences worth speaking of. Chances are you're surrounded. Escalate and they'll just fucking shoot you. Your only way out is through the ways.";

The demise of Under My Roof is "A gas-masked figure grabs you, wrenches your arm around behind your back. He gets about halfway through the motion before reflex decides for you, twisting from the hold, reversing the grip, and finally driving your weight against the extended arm to break the elbow. You're not really watching as you do this; you're scanning the room for the next one.[p]The next one unholsters and shoots you, snap-snap snap-snap, in the chest.[p]Intellectually you know you're dead, but the warrior gift (or pure adrenaline) gets you far enough to shoulder-throw him into a wall before your body stops doing what you tell it. As a final act, it should have been a lot more gratifying."

The javeinit of Under My Roof is "Jave is sitting in a big armchair, blonde-dreadlocked, entirely comfortable but a little apart from the group."

The javedesc of Under My Roof is "Yeah, holy fucking shit, those are some [i]masterful[/i] blonde dreads. Given how long they must have taken to cultivate, and that your first impression of them isn't [i]greasy louse-farm[/i], she has to have at least five years on anyone here. Which, now that you consider it, is probably about right: the others have body-language with the slight, self-conscious deference due to the Older, Unfathomably Cool One."

The javecostume of Under My Roof is "Her hair is still in those spectacular dreads, and a small, impractical part of you hopes she'll be moved to keep them."

The listendesc of Under My Roof is "Peppy, punk-tinged white-girl ska. Summer tunes. You've got enough of a buzz on that they feel agreeably cheerful rather than irritating."

The singdesc of Under My Roof is "You don't really know the words well enough to follow along."

The dancedesc of Under My Roof is "You get to your feet and pull of a few steps of a skank, shuffling self-consciously. Nobody passes comment."

The boozedesc of Under My Roof is "An ill-conceived assortment of pre-mixed, syrupy cocktails-in-a-bottle, cheap beer, and - oh, god, tequila shots. You didn't know tequila could come in plastic."

The shortbooze of Under My Roof is "You reluctantly snag a wine cooler."

The rivdesc of Under My Roof is "Rivka Strossi sits cross-legged on the floor at the head of the coffee table, skinning up."

The rivcostume of Under My Roof is "One temple is buzz-cut over the ear, and she's wearing a stringy tank-top that exposes impossibly detailed tattoos across both arms, a random explosion of patterns and pop-culture iconography that you can't decipher."

The teenagers are a crowd in Under My Roof. The description of teenagers is "You estimate the age range to be between fourteen and twenty, with the mode about sixteen; this is unreliable, though, because everybody's trying very hard to look older than they are, principally through the medium of dirt."

The rivweapon of Under My Roof is "a katana".

Understand "couple" or "kids" or "kid" or "teenager" as teenagers.

The closeclue of Under My Roof is "That smell. The weed's Dun Thunder, no question about it."

the Dun Thunder is an unreachable clue. the javeclue of Under My Roof is Dun Thunder. The description of Dun Thunder is "Lacmore was like prison's meant to be, in that you needed something to trade. You figured boredom would be the lethal part, brought books. Jave showed up with several thousand Sharpies and about ten kilos of Dun Thunder, a smooth bud grown by one of the far-flung freeway communes. The good thing about Dun Thunder was that it wasn't very exciting, but if you smoked a one-skin, very slowly, about every four hours, it worked about as well as the horse pills without making you feel like a carcass the next morning. In short order, everything in barracks smelled of it." Understand "weed" or "pot" or "marijuana" as Dun Thunder.

Instead of smelling in Under My Roof:
	if the location of Dun Thunder is Under My Roof begin;
		try examining Dun Thunder;
	otherwise;
		say "People whose sense of hygiene is a work in progress. Weed, largely cheap resin. Bad beer growing stale.";
	end if;

Carry out using laughing one in Under My Roof:
	say "You don't think she knows you, in this context. Nothing to suggest she picks you out from the rest of these schmucks. So how did you two actually connect, at first, anyhow? Not the first time you technically knew one another. Lacmore, at some point, but... fuck, you try not to focus on those circumstances too much, and you spent enough time in one form of escapism or another that memory is not to be relied upon.[p]To some extent, you're pretty sure that you started hanging out in Lacmore because you were the only people you knew who weren't painfully hopeless kids, losing their shit, or true believers. You'd quirk an eye at her during the more murderously nonsensical moments of officer briefings, and once you realised that you could make her laugh... everything else follows.";
	wait for any key;
		say "If she could laugh about fuckwitted middle-managers running a bloody bush-war, she can laugh about stoned teenagers desperately trying to be cool. So you bide, and take a toke or two, and watch for her gaze. At length you catch it and aim an 'aww, bless' side-eye at a narrow-trousered bumfluff delivering sagacities on Palestine, and she tightens her mouth to suppress a snigger. And god, the sparkle in those eyes.";
	ruin everything;
	
Carry out using troublemaker in Under My Roof:
	say "You hate to mess with them. They're just kids, they're raw and stupid in so many different overlapping ways, and... somehow they're comfortable. You don't grow up by being comfortable, is the thing. This group's never going to change or grow without a major disruptive influence.[p]The fact that this justification seems completely reasonable to you is probably a sign that this place is making you raw and stupid yourself, but when in Rome, what the fuck, right?[p]Your overture is sequentially snogging both halves of a couple in the upstairs hall, letting yourself get caught the second time. You extract some stoned secrets from one guy and deliver them to the worst possible recipient, with certain shifts of emphasis. You identify the guy with bottled-up fury and subtly needle him until he has a spittle-spraying outburst at someone less subtle. You allow the drama to overlap and interweave.[p]You don't feel [i]good[/i] about it. But at the end of the evening, there are [i]two[/i] people who are at the heart of everything, and Jave is quietly watching you, awaiting your next move. It's a start.";
	ruin everything;
	
Carry out using warrior in Under My Roof:
	say "Jeez, they're just kids. Drunken roughhousing seems socially plausible, but you'd probably break one.";
	
Carry out using abider in Under My Roof:
	say "She seems to be handling that fine already. The question here, seems to you, is not whether you get through the party; it's whether the two of you connect.";
	
Carry out using commander in Under My Roof:
	say "This is not an evening that would be improved by leadership.";
	
Carry out using antinominalist in Under My Roof:
	say "She's cool enough as it is.";
	
Carry out using critic in Under My Roof:
	say "You dread to think what insights you would derive from any writing this lot produced.  Mercifully, if any of them have artistic aspirations, they're not leaving their manuscripts lying around."
	
Carry out using forager in Under My Roof:
	say "[one of]FOOD. YES. The best idea. With unerring instincts, you locate a kitchen, raid the fridge, find a frying-pan, and produce a fried-egg sandwich stacked with an ill-considered assortment of larder loot - blue cheese, cashew nuts, mayonnaise, dill pickles, probably some other stuff. It's too sloppy to consume with your hands, so you eat it over the sink, off the blade of a steak-knife. It is the finest meal.[p]On returning to the living-room, your situation has not markedly improved.[or]You could make another sandwich, sure, but then you'd eat it again. Which would be great, yes, but maybe you should focus[stopping]."

Book - The Wedding

wedding is a party in tangle. wedding is east of startroom and north of gamesnight. The printed name of wedding is "Wedding on the Water". The description of wedding is "A still and starry night. You're amongst a chaotic flotilla of smallish pleasure-boats, their masts and rails all hung with lights. Beyond the clustered vessels, inky-dark water reflects the more distant lights of a city, on three sides: a lake or a bay. The boats are full of people in suits and gowns, chattering fit to burst.[p]Outside the shelter of land, the sea at the bay mouth is dark and inviting." Understand "water" or "wedding on the water" or "flotilla" as wedding.

the bay mouth is a waymouth in wedding. Understand "ocean" or "sea" or "dark" or "inviting" as the bay mouth. The description of the bay mouth is "A break in the lights, a patch of darkness where sky and sea merge. Not, you sense, to the great open ocean, the whale-road: this opens onto the island sea, the wine-dark. You get a feeling of wooden keels crunching on coral, of a cormorant's dive, of how a sail fills."

the pleasure boats are an unimplement in wedding. Understand "mast" or "masts" or "rail" or "rails" or "vessels" or "vessel" or "barge" or "flotilla" or "coracle" or "coracles" as pleasure boats. The description of pleasure boats is "The main ones are all the same kind of flat-bottomed fancied-up barge, but the smaller ones are of a great many different varieties, as though everybody showed up in whatever was ready to hand."

the wedding punt is an unimplement in wedding. Understand "boat" as the wedding punt. The description of wedding punt is "It's a long, narrow, flat-bottomed craft, large enough to take perhaps six passengers, controlled by a single person standing on the back end. Not the kind of vessel you would trust in anything more vigorous than a canal or a mill-pond."

the long-bladed oar is a backdrop. the long-bladed oar is in wedding. The description of long-bladed oar is "Some light but robust wood; narrow enough to use handily as a pole in shallow waters, but with enough total area to make a tolerable paddle."

the decorative lights are an unimplement in wedding. Understand "party" as decorative lights. The description of decorative lights is "They have a soft, moon-like glow that only illuminates anything through the sheer number of bulbs."

the dark water is scenery in wedding. The description of dark water is "Very still for a body of water so broad, it glimmers with the reflected party lights."

the wedding guests are a crowd in wedding. The description of guests is "They have the patchy, heterogenous quality of much-extended families, of circles tenuously connected. So, a wedding, then. That, and there's great deal of drinking going on, but there are also plenty of children charging about. At least two of the adults and a good half of the children have fallen in at some point, and if you're any judge that number is likely to climb."

the wedding guests wear the wedding clothes. Understand "suits" or "gowns" or "frocks" as wedding clothes. The description of wedding clothes is "You grimly identify a number of young women wearing the same dreadful frock as you. It apparently looks ghastly even if you have the appropriate shape for it."

The tsadesc of wedding is "You're wearing an ill-advised confection that crawled out of a Disney princess catalogue to die. It's a sort of shimmery lilac, there is lace and ribbon and poofy bits, and while it technically fits, it does so with a tone of thinly-veiled annoyance regarding the configuration of your tits and waist. The train keeps slipping into the water, and is thoroughly soggy.[p]More practically, you're standing at the rear of something resembling a gondola or punt, and in possession of a long, single-bladed oar."

The rivdesc of wedding is "Rivka Strossi appears to be a priest, moving among the guests with smiles and nods."

The rivcostume of wedding is "She's wearing a black surplice, a white flash of clerical collar at her throat, and those aviator shades. One hand clasps the neck of a beer-bottle, a death's-head ring glinting from one finger."

The rivweapon of wedding is "a boat-hook"

The closeclue of wedding is "Clearly a wedding, yet no bride. But someone's left a distinctive bouquet behind."

the bouquet is a clue. the javeclue of wedding is the bouquet. The description of the bouquet is "It doesn't look much like a bridal bouquet, honestly: too contrasty and strongly-coloured, it gives the impression of a cluster of weeds or a Carnival accessory. Soft white frangipani, African daisies all dusty-red and dark-honey-gold, sprays of dark cerise bougainvillea complete with leaf and thorn, huge crimson hibiscus. Flame-orange honeysuckle, corn-gold monkeyflower, crimson poppy, dusky-purple chocolate lily. The whole bunch is knotted together at the base by dandelion stems, which... yeah. Jave."

The desolation of wedding is "Bubbles burst on the still lake surface - first marble-sized, then larger and larger, bringing with them the stench of anaerobic mud. The guests make disgusted noises, and one boat turns for shore. And then one of the boats cracks in half and abruptly goes under, too fast to have swamped. The water's full of splashing people, and everyone on the boats is shouting, and then you spot the first tentacle.[p]It's black as the black water, and covered in barnacles, and the portion you can see is about the size of a small commuter train."

The demise of wedding is "You don't see what tips your boat over, so sudden is the impact: hitting the water knocks the air out of you, and you have to struggle to avoid gulping water. It's dark and you don't know which way is up, and there are other people thrashing in the water around you. Someone kicks you in the face, or that's what it feels like. Panic. You claw at the water, desperate, lungs bursting.[p]Something snakes around your leg, clasps it, and pulls with inexorable strength. Downwards."

The javeinit of wedding is "And of course Jave is the bride, big white dress and all, all the party orbiting slowly around her."

The javedesc of wedding is "Jave is in a dress that looks like cubist origami, a narrow triangle of sharp, improbable folds and hard white panels; her neck and shoulders, emerging from the apex, are startlingly human. The veil, by contrast, is a thing of drifting smoke and calligraphy-brushwork, almost weightless[one of]. The whole production is so eye-catching that it takes you a second glance to notice her expression: she's quietly, radiantly happy, face relaxed, eyes bright. It's not an expression you've ever seen on her before, and it produces an immediate elevator-drop feeling around your upper abdomen. Calm down. This is a palace of lies. That reaction is just what they want[or]. You focus on her hair to avoid catching her face again: it's intricately braided in a circlet around her head, with frangipani blossoms tucked into the weave[stopping]."

The javecostume of wedding is "Jave has shed the veil and torn off the less practical parts of the wedding-dress, which is to say most of it. She looks quite prepared to take on the multiverse in a sports bra, though."

The boozedesc of wedding is "One of the larger, more barge-like boats seems to be an open bar, and waiters circulate - somehow balancing on tiny coracles - with champagne buckets. Champagne is the urine of a particularly feeble-assed demon; fuck that."

The shortbooze of wedding is "You manouvre the boat over to the open bar and avail yourself of some Scotch."

The listendesc of wedding is "No band, no music: just the chatter and laugh of a crowd two-thirds of the way to drunk, and beneath it the water lapping."

The dancedesc of wedding is "You'd rather stay in control of the boat."

The Wine-Dark Sea is an egress. Wine-Dark Sea is outside from wedding. The description of wine-dark sea is "A sky perfectly clear, moonless, cream-thick with stars. Air agreeably cool, suggesting a warm day to come. Below, water so clear and shallow that you catch starlit glimpses of its floor. And the horizon, gods, a horizon densely studded with islands and islets, a horizon that will go as far as you need it to. The little wedding-punt will serve.[p]This is not a way you have any feel for at all - walking is in your blood, rivers you can handle, but trackless open water loses you. But unlike the Great Open Sea, you love this place as a strangeway, a road that's always fresh because you'll never understand it." Understand "wine" or "dark" as wine-dark sea.

the javeapp of wine-dark sea is "Jave is seated at the middle of the craft, facing sternwards".

The listendesc of wine-dark sea is "The waves lap gently against the punt."

the dancedesc of wine-dark sea is "You'd rock the boat."

the singdesc of wine-dark sea is "You hum softly to yourself as you ply the oar."

the long-bladed oar is in wine-dark sea.

the islands are scenery in wine-dark sea. understand "island" or "islet" or "islets" or "horizon" as the islands. The description of islands is "Every kind of island is found in the Sea: barnacled reefs like a whale's back, lone sea-stacks with secret gardens at their peak, cliff-bounded fortresses, atolls with scorching sand and brilliant blue lagoons, grumbling volcanoes with thick-forested flanks, muddy sandbars held together by stilt-houses, derelict oil rigs, floating ship-cities, canal-crossed merchant cities, islands forged from mangrove and delta-mud, the peaks of drowned skyscrapers."

the little punt is scenery in wine-dark sea. The description of little punt is "It's a long, narrow, flat-bottomed craft, large enough to take perhaps six passengers, controlled by a single person standing on the back end. Not the kind of vessel you would trust in anything more vigorous than a canal or a mill-pond."

The tsadesc of wine-dark sea is "A light tunic and sandals. You can't see in this light, but your skin always goes a glowing bronze colour here, your hair closer to black than red."

The roadahead of wine-dark sea is "The water laps softly at your prow, and the sky teems with unfamiliar constellations."

The rebadesc of Wine-Dark Sea is "Reba al-Nasri sits quietly in the prow, turned half-away, fingertips trailing in the water."

The rebalook of Wine-Dark Sea is "The starlight suggests a French braid and a sundress, but mostly you see the arm extended over the water, turning one way and another, the fingers playing with the wavelets as if idly picking out a tune on the piano"

The Wine-Dark Sea is rebaseated.
The reba-act-1 of Wine-Dark Sea is "She tilts her head into the starlight, exposing a long sweep of neck and shoulder."
The reba-act-2 of Wine-Dark Sea is "You try not to linger on that neck."
The reba-act-3 of Wine-Dark Sea is "Reba bounces up and down in the prow, clapping her hands."
The reba-act-4 of Wine-Dark Sea is "She resettles herself, picking at her skirts."

the waiters are a backdrop. The waiters are in wedding and ballroom. Instead of doing something to waiters: say "They're even less substantial than the others: out of the corner of your eye they seem like your basic penguin-suited waiter, but look directly at them and they're blurry, out of focus, all more or less the same.".
Understand "waiter" as waiters.

Book - Ballroom

ballroom is a challenging party in tangle. ballroom is east of wedding and north of gamejam. The printed name of ballroom is "Trad Romance". The description of ballroom is "A high-ceilinged ballroom, decorated in full-on shameless rococo with fussy strapwork and gold-enameled nymphs and all the rest of it. A ring of galleries overlook the dance floor, and you would really rather prefer to be up there and [i]watching[/i] people prance about, but instead you're stuck down here getting handed off from one twirling ninny to the next." Understand "trad" or "romance" or "dance" or "dancefloor" as ballroom.

the closeclue of ballroom is "There's something a little off about one of the nymphs."

the troublesome nymph is an unreachable clue. The description of troublesome nymph is "One of the gold-enameled nymphs in bas-relief on the balcony trim is wearing combat boots. Another is applying a throat-punch to a handsy triton, her feet planted in a familiar stance." Understand "boots" or "combat" as troublesome nymph.
the javeclue of ballroom is the troublesome nymph.

the architectural ornamentation is an unimplement in ballroom. Understand "strapwork" or "fussy" or "nymphs" or "gold" or "gold-enameled" or "gold enameled" or "rococo" or "decoration" as architectural ornamentation. The description of ornamentation is "Gaudy, ostentatious, tits-out affluence. The colour scheme starts at gold and works out from there."

the galleries are an unimplement in ballroom. Understand "gallery" as galleries. The description of galleries is "Evidently this is where you go when dancing doesn't appeal, to watch the dancers and flirt and mingle. Still doesn't really look like your scene, but it's about 1000% more appealing than prancing about in a pattern."

your mask is an unimplement in ballroom. The description of mask is "A domino, covering only the middle of your face; it's well-made and not uncomfortable, but it maddeningly limits your peripheral vision. And you have no idea how it looks on the outside. Weird, no doubt, since nobody else is wearing one."

Instead of taking off your mask: say "Somehow, you can't."

the dancers are a crowd in ballroom. The description of dancers is "Immaculately-dressed. Women like tropical flowers, men in monochrome. Every one of them more capable of self-expression through movement than you are. And none of them are wearing masks."

The tsadesc of ballroom is "The vagaries of this corner of the tangle have put you in a tux and men's dance shoes, and put a good nine inches on your height, but have also given you hair so long it's in danger of reaching your knees. It's mercifully pulled back, but god that shit is heavy. Your peripheral vision is vexingly limited by a mask."

The threshold of ballroom is 1.

The rivdesc of ballroom is "Rivka Strossi is perched on the big square pillar at the end of a balustrade, skirts in picturesque disarray, smoking a cigarette."

The rivcostume of ballroom is "Rivka is wearing an exquisite slate-grey ballgown with complicated embroidered panels, but is very separate from it somehow, as if it's inhabiting the same approximate space as her by coincidence. Her cigarette-smoke is cheap and acrid."

The rivweapon of ballroom is "a cavalry sabre"

The boozedesc of ballroom is "Everyone appears to be drinking the same dry white wine, in vexingly small glasses. It's not bad - crisp mid-mouth fruitiness, maybe a pinot grigio? - but you don't feel moved to go for a second glass."

The shortbooze of ballroom is "You deftly swipe a glass of white wine as the dance takes you to the edge of the floor."

The listendesc of ballroom is "You can't make out the band, but it's definitely live music in a classical-you-can-dance-to vein - a string quartet, maybe? Is this a waltz? Gods, you know fuck-all about anything that happened before jazz."

The dancedesc of ballroom is "Alas, you already are."

The singdesc of ballroom is "You'd rather save your breath for dancing. And this isn't the kind of music that really goes with lyrics."

The javeinit of ballroom is "Jave is in the dance, now closer, now nearer, a flame-gold dress flickering in and out among the penguin suits."

The javedesc of ballroom is "You keep catching sight of the sweeping fire of her dress - but the way that partners switch, like some kind of rotating-discs puzzlebox, seems to be set up to keep you apart: whenever she seems to be getting closer she gets drawn away again."

The javecostume of ballroom is "The advantage of ballroom dresses is that they're [i]intended[/i] for moderately athletic activity, so with the shoes discarded and a foot of material torn off the hem, Jave seems prepared for anything. Or anything that doesn't involve concealment: the colour is a beacon-flame."

The desolation of ballroom is "The music's quality has shifted somehow, grown... more full-spectrum, is the only way you can describe it, like maybe a complete orchestra snuck up and progressively infiltrated a string quartet. Faster, too, rising towards an ever-delayed crescendo. The dancers are speeding up: a woman trips on her skirts, goes down, you're whirled away and don't see what happens to her. You can't tell if those are masks or the actual faces of the dancers any more. There's no ceiling, no walls, just a floor surrounded by darkness, and you know that as soon as the dance ends -"

The demise of ballroom is "The music reaches its final crescendo, and the world winks out of existence."

Carry out using warrior in ballroom:
	say "Well, fine, fuck it. You can't dance, and you're not going to be able to figure out how this one works in a hurry, but this is all basically just footwork, right? Footwork you can do.[p]As you're handed off from one partner to another, you put your hands behind your back, hook one foot behind his, step into him, and shift your bodyweight just so. No hands required: not expecting anything of the kind, he goes down smartly onto his rear. You translate the motion into a broad turn, scanning the room. [i]There she is.[/i] You turn through the dance. It throws another partner at you - you evade her, sweeping around to the far side of another couple. You're getting some really good glares now, but without a partner you can position yourself far more easily.[p]Interception. You judge the next arc of movement correctly, get a grip on her partner's upper hand. Then it's just the right twist to  peel him off her and away and down to his knees, and a step to rejoin her, hand to her waist, and [i]there.[/i][p]She's trying not to crack up. 'You'll spoil the party.'[p]'I think it's made to ruin itself. Look, if we get separated, meet up at the -'[p]'Wait, who's leading?' You've slowed to a sort of skippy shuffle. Well, if you don't know how to do this, why did you think that she would?";
	ruin everything;

Carry out using commander in ballroom:
	say "As Jave spins past you, she flashes a hand-signal: [i]remain in place.[/i] That has to mean [i]don't try switching partners[/i], right? You try to follow what she's doing. She's... stepping off the floor with a curtsey to her partner. Ah. Obviously.[p]The dance's structure is a series of constantly-moving, interlocking rings, with partner-changes switching dancers from one circuit to another; if you were up in the galleries and had a pen and paper maybe you could figure out the periodicity, but you'd be willing to bet that it's something that ensures you and Jave are never within range at a switch. But while the dance is [i]internally[/i] tightly determined, it has to allow for dancers leaving and joining - which means that if she just rejoins the dance at a suitable spot...[p]It doesn't work the first time: she steps out, and you can see her tracking where the error was, her eyes plotting out arcs across the floor. And the second time she nails it, the current of the dance bringing the two of you smoothly together with no apparent effort at all, like a spaceship docking. She looks enormously pleased with herself.";
	ruin everything;
	
Carry out using abider in ballroom:
	say "If you just wait around, you'll never run into one another here.";
	
Carry out using laughing one in ballroom:
	say "There's plenty that's ridiculous about the situation - this hair, for instance, good lord - but you can't really make her laugh if you're seperated in the crowd.";
	
Carry out using antinominalist in ballroom:
	say "Jave attempts to simply abandon her partner and walk up to you. The guy she shrugs off doesn't object, but somehow the pattern of the dance compensates for the error, and in a moment she's clasped in the arms of another partner, whirling away again. They assume she's dancing even when she's not. Hrm.";
	
Carry out using forager in ballroom:
	say "Your forager sense picks out waiters with trays of frilly confections, circulating well [i]outside[/i] the dance. Unlikely to be of much use.";
	
Carry out using critic in ballroom:
	say "There's nothing to interpret. Oh, someone with more of a focus on data rather than meaning might be able to make something out of the mathematics of the dance, but that's not your deal.";
	
Carry out using troublemaker in ballroom:
	say "You can think of several grades of shit to start here. First, and most obviously, you could spark a fight, and you'd probably be able to link up with Jave in the chaos; but that's a lot of damage. Secondly, a dance is obviously a ritualisation of all [i]kinds[/i] of juicy social motives that, if you knew anything about them, would make for fertile ground: but you're not good enough at reading people to identify their individual, underlying needs and motives without actually [i]talking[/i] to them. And if this was a normal place, you could disrupt the dance with the aim of ending it - but you suspect that the dance is built into the nature of this place, and thus impervious to normal attempts.";

Book - The Wake

wakeroom is a challenging party in tangle. wakeroom is north of Under My Roof. The printed name of wakeroom is "No Place To Rest Your Head". Understand "wake" or "no place" or "rest your head" or "head" or "your" or "no place to rest your head" as wakeroom.

The description of wakeroom is "A rough-plastered room that you suspect of being in the back of a pub somewhere. Deep in the night. A patchy assortment of people are sitting silent, a respectful distance away from the open coffin laid out on a table."

The tsadesc of wakeroom is "You're dressed formally after the manner of someone who only dresses up for weddings and funerals. And, hunh, this doesn't usually happen in a mixed-sex context, you're definitely male."

the mourners are a crowd in wakeroom. The description of the mourners is "A mixed crowd. Plenty of old people, no kids. Some in white, most in black, a lot of it shabby. What conversation is going on is held in whispers, and many of them are lost in silent melancholy. And hard liquor, which is near enough the same thing."

The rivdesc of wakeroom is "Rivka Strossi is at the back of the room, scarlet lipstick beneath the veil, checking her watch every ten seconds."

The rivcostume of wakeroom is "Rivka's wearing a veil, a bolero jacket over a black blouse, and dress pants. And Cuban-heeled boots. Her lipstick is a savage smear."

The rivweapon of wakeroom is "a switchblade".

The closeclue of wakeroom is "There's a single sprig of frangipani in the corpse's buttonhole."

the frangipani is a clue. the javeclue of wakeroom is frangipani. The description of frangipani is "Five waxy petals, white blushed with gold, and an impossibly voluptuous, nocturnal perfume[one of].[p]The plant seems like a dinosaur, too lumpen and exotic to live, yet it grew everywhere in Masdita. On one of the long evenings lazily searching for a safe route out, hoping not to find one, she tried to arrange as many as possible of them in your hair. Your hair was far too short, and frangipani blooms have no real stem to speak of, so every time you moved your head half the flowers would escape, but the fragrance clung until the next day[or][stopping]."

The javeinit of wakeroom is "Jave occupies the single chair beside the body, veiled, motionless."

The javedesc of wakeroom is "She's in widow's weeds, with a veil more fetching than modest. She sits by the body, a little apart from everyone, face downcast and tear-streaked. She's... older? Her face doesn't look it, but she has the neat, tidied-away posture of an old woman, or a woman from an age when being respectable was the law and the prophets. Grasping a purse for support."

The javecostume of wakeroom is "She's still wearing mourning black, hasn't even removed the veil, but the body language is her own again, deft, sure, taking up space."

the deceased is scenery in wakeroom. The description of the deceased is "[one of]For a moment you have the crawly suspicion that it'll be you in the casket, but as it turns out, no. [or][stopping]He's not old, not young. White, shortish, wearing a dress suit grown a little ratty. His face won't stick in your memory, and you suspect that he's not the point of this exercise." Understand "coffin" or "corpse" or "dead man" or "casket" as the deceased.

Instead of kissing the deceased: say "He's not anything to you.";

The listendesc of wakeroom is "Outside, muffled by windows and a gusty wind, you can make out the keening of a lone violin - a little scratchy, a cheap instrument and a sozzled fiddler who was probably never world-class in the first place, but [i]fuck[/i] that's some raw sorrow."

The boozedesc of wakeroom is "There are crocks and jars and unevenly-blown bottles all over the place, filled with various shades of ruinous liquor, from peaty-dark liquid smoke to madness-clear grain alcohol. Water-jugs, too, but nobody seems to be taking more than a splash of that."

The shortbooze of wakeroom is "You slosh yourself out a generous tumbler of peat-dark whisky. It tastes of wood-smoke and rich brown soil and leather."

The desolation of wakeroom is "A rosy light grows outside the window. Dawn.[wait for any key][p]Very casually, as if there is nothing strange about the situation at all, the corpse climbs out of the coffin, draws itself up a little, and walks to the door. The doorknob squeaks. Everyone is getting up, now, but there's no panic or shock: patiently, a little dreamily, but quite under their own volition, they're following the dead man out of the door.[p]You hurry to the window; in the pale morning light, the graveyard soil is heaving, the tombs opening, their inhabitants clambering out to walk steadily towards the gate. In the street, the quick and the dead walk shoulder to shoulder, softly, as you would on a still morning with most people still in bed. Uphill. You raise up your eyes.[p]The first sliver of the rising sun crests the hilltop. And a few at a time, people are walking into it, and are gone."

The demise of wakeroom is "The hill is steep, but the climb easy. You are amid thousands, now, not speaking or touching, in steady procession. At the hill's top, silhouetted against the sun and brighter than it, a figure is dividing the procession in two.[p]Left and right do not seem to matter all that much, except that you catch sight of Jave on the opposite side. You turn against the current - [i]no, there's been a mistake[/i] - but the mass of humanity flows on, and takes you with it."

Carry out using abider in wakeroom:
	say "The instinct is to go and comfort her, but perhaps that's not what's needed right now. Or, maybe, it's more what you have a need to do, rather than what's most useful to her right now. That chair's set apart for a reason. You'll wait. You're confident that she knows what she's doing, that this is a part of the process that you don't have to play a starring role in.[p]The night stretches out. You pour yourself a drink and nurse it. Some people quietly arrive, a few quietly depart. Normally you'd be climbing the walls by this point, or compulsively gnawing at thoughts better left alone, but the stillness is contagious. If she can do this, you'd sure as fuck better be able to.[p]After what seems like hours, she stands, walks carefully over to you, and gives your shoulder a gentle squeeze. 'Thanks.'";
	now rising sun is in wakeroom;
	ruin everything;
	
the rising sun is an unimplement.

Carry out using warrior in wakeroom:
	say "Fuck this. [i]Fuck[/i] this. Fuck this with its [i]trousers[/i] on.[p]There's a point in night, sometimes, when you have to choose between despair and fury, and you're [i]done[/i] with choosing despair. Therefore.[p]You stand up in what you hope is a dramatic fashion, and toss back your whisky, or a whisky which you retroactively hope was yours. 'Goodfolks,' you growl, 'we are gathered here to remember that it is shit to be dead: job [i]thoroughly[/i] done, good show, now if anyone here's got two fists and wanster remember that it's really pretty fucking non-shit to be alive, I suggest we take it outside.'[p]'Here's fine.' A big lad, no question, in a big-pawed puppy way. Non-lethal, you'll have him in fifteen seconds max.[p]'Awright. No gouges, biting, strikes to the head, low and indecent behaviour,' you say, counting off fingers. You have a moment of concern that you're wrecking everything, but a quick scan of the room suggests otherwise: little old ladies are rearranging chairs with a bloodthirsty glint.[p]The puppy is used to winning with one big punch, and is squealing in an arm-bar inside five seconds. Enough for the adrenaline rush, though. You're breathing hard, soaring: and then widow-Jave stands up, kicks off her shoes, sweeps her stockinged back foot around into a fighting stance, and makes everything [i]perfect[/i].";
	wait for any key;
	say "[br]Or would have, if the Rebeccas weren't intent on ruining everything.";
	wait for any key;
	ruin everything.
	
Carry out using laughing one in wakeroom:
	say "There are points at which laughter isn't an emotionally honest response. Well, maybe not for you; humour's kind of at the heart of your emotional resilience. But for Jave, absolutely.";
	
Carry out using commander in wakeroom:
	say "There is no war to fight here. Death is not something you can confront stragetically."
	
Carry out using troublemaker in wakeroom:
	say "Yeah, sure, there are absolutely people for whom births, weddings and funerals are sort of a drama water-hole, drawing in a rich concentration of prey in an emotionally charged and thus vulnerable state. Those people are dicks. Relationships are not advanced by gratuitous dickery.";
	
Carry out using forager in wakeroom:
	say "Now you consider it, your stomach bears evidence of a very substantial meal earlier in the evening. Good thing too, considering all the spirits.";

Carry out using critic in wakeroom:
	say "An anthropologist would have a field-day with this, but unless they write down their grief it's no help to you.";
	
Carry out using antinominalist in wakeroom:
	say "The rules don't seem like the immediate problem here. (This is, you sense, a context where widowhood offers a rather greater degree of autonomy.) And the mores at play here are offering structure to grief. The pain is the problem."

Book - Hot Rock

hotrock is a party in tangle. hotrock is north of startroom and east of wakeroom. The printed name of hotrock is "Sun-sleeps-in-the-rock". Understand "rock" and "sun" and "sleeps" and "sun-sleeps-in-the-rock" or "africa" or "koppie" as hotrock.

The description of hotrock is "Slickrock hillside, orange-red, a hot sun low in a cloudless sky, a hard dry heat. Far downhill, the rock gives way to thornveldt, the plain punctuated by distant outcrops.[p]Beneath a lone, broad-spreading shade-tree, people kick back on boulders and blankets and camp-chairs. There are meaty things sizzling over a fire, a couple of coolers.[p]And outwards, across the sun-baked rock, you can sense the Barren Way beckoning."

the barrens is a waymouth in hotrock. Understand "barren" or "way" as barrens. The printed name of barrens is "Barren Way". The description of barrens is "Outwards, the familiar feel of the Barren Way. It feels like a hard-shelled insect scurrying under a rock; like the steady trudge of a camel, caribou or oryx; like dry skin."

[This is based on various koppies, and braais conducted therein, around the southern Africa of my childhood. Perhaps the foremost one in my mind was Bloedkoppie in Namibia, but the Matopos Hills of Zimbabwe and numerous places in the bush outside Molepolole, Botswana are also involved.]

the sinking sun is an unimplement in hotrock. Understand "hot" or "low" or "sunset" as sinking sun. The description of sinking sun is "The sun's a deep, peachy colour, beginning to shade towards red. Some optical illusion - dust in the atmosphere? - makes it seem gibbous on the lower side. There are not enough clouds for a truly spectacular sunset, but the warm red glow still paints a third of the sky." Understand "sunset" or "cloud" or "clouds" as sinking sun.

the cloudless sky is an unimplement in hotrock. The description of cloudless sky is "Not a cloud to be seen; the dome of the heavens is a single, vast gradient of colour, red to brilliant purple to cool dark blue. You can already see stars populating the eastern horizon." Understand "stars" or "star" as the cloudless sky.

the shade tree is scenery in hotrock. The description of shade tree is "You don't quite recognise the species, but you've seen its kind. Hard, slow-growing wood, waxy leaves, branches spreading wide and airy. A tree to survive drought."

Instead of climbing shade tree: say "Given less pressing circumstances, of finding a comfortable perch and retiring there with a six-pack and a book would be magnificent. The thought occurs, not for the first time, that the Rebeccas are wasting their talents on petty revenge."

the boulders are an unimplement in hotrock. Understand "rocks" or "rock" or "boulder" as boulders. The description of boulders is "Great rocks, from about the size of a house down to that of a desk, are strewn about the slope seemingly at random: stacked on one another, jumbled in heaps, or standing like lone monoliths. They're of the same red, grainy stone as the hillside."

the thornveldt is an unimplement in hotrock. The description of thornveldt is "A rambling, broken landscape, dry and rocky. 'Rocky' undersells it, really: the landscape is rocks, into the cracks of which some soil and thorn-trees are jammed. From your high vantage, you can see a very long way." Understand "veldt" or "plains" or "plain" or "outcrop" or "outcrops" as thornveldt.

the hillside is an unimplement in hotrock. Understand "slickrock" or "slick" or "rock" or "red" as hillside. The description of the hillside is "Although cracked and fissured, this hill is plainly one single, vast piece of rock."

the blankets are an unimplement in hotrock. The description of blankets is "Old, worn-out looking blankets with a vague institutional flavour, or else brightly-coloured patterned cloths."

the camp chairs are an unimplement in hotrock. The description of camp chairs is "They look uncomfortable and awkward to put away. Lots of metal tubing."

the coolers are an unimplement in hotrock. Understand "cooler" or "coldbox" or "coldboxes" or "water" or "jugs" or "water-jugs" as coolers. The description of coolers is "Big, beaten-up-looking coldboxes, stacked densely with beer and large water-jugs."

the meat is an unimplement in hotrock. The description of meat is "There are some baked potatoes roasting in the embers, and a big pot of thick corn pap over to the side of the fire, but the focus is clearly on the meat. There are some slabs of lean beef on the campfire grill, but most of the real estate is taken up by thick, smoky, grease-dripping sausages." Understand "potato" or "potatoes" or "corn" or "pap" or "pot" or "beef" or "slab" or "slabs" or "sausage" or "sausages" as the meat.

Instead of eating the meat: say "You snag a sausage, still-hissing, from a plate by the fire. It tastes of wood-smoke and grease and hot spicy beef."

the fire is an unimplement in hotrock. The description of the fire is "At this stage of the day the fire's purpose is solely for cooking, and most of the group stay well clear of it. It's a simple rock ring fueled by old, dry down-wood, the dense wood of the desert margin that burns hot and slow and sweet-smelling. It's just low, hot coals, quiet except for the sizzle of dripping sausage-fat." Understand "wood" as the fire.

The listendesc of hotrock is "The only music is the regular chirp of insects."

The closeclue of hotrock is "A half-drunk bottle of Castle lager is propped on a rock, still dotted with beads of condensation."

the bottle of Castle is a clue. the javeclue of hotrock is the bottle of Castle. Understand "lager" or "beer" as the bottle of castle. The description of the bottle of castle is "The only beer she actually likes. (Philistine. But certain allowances have to be made for South Africans)." 

The javeinit of hotrock is "Jave seems a natural part of the group; if you didn't know her already, you wouldn't have picked her out."

The javedesc of hotrock is "Jave's sitting on the edge of a boulder, dividing her attention between a beer and a plate of greasy sausages. She's wearing a tired-looking beige tank-top with oily black stains on it, a wrap-around skirt in a bright pattern of yellow, red and white, beat-up leather sandals, an army cap."

The javecostume of hotrock is "She's wearing a tired-looking beige tank-top with oily black stains on it, a wrap-around skirt in a bright pattern of yellow, red and white, beat-up leather sandals, an army cap."

The boozedesc of hotrock is "The coolers are full of six-packs of lager, mostly cans. The kind of beer which, consumed steadily and continuously over the course of a hot day, will maintain a mild buzz while keeping you hydrated."

The shortbooze of hotrock is "You crack open a can of cold beer. It's recognisably beer, and fairly cold. In this heat, that makes it ambrosia."

Barren Way is outside from hotrock. The description of Barren Way is "It goes by other names - [i]high places, silk road, the tracks of the goats[/i]. It's old, bones-of-the-earth stuff. It leads through mountain, tundra, steppe, high desert: dry harsh land, where missing a cairn can kill you. It scares the everliving fuck out of you, every time, but your feet understand its course, it is one of [i]yours[/i], and a walker cannot help but love their way." Barren Way is an egress. Understand "silk road" or "barrens" or "high places" or "goats" or "tracks of the goats" as Barren Way.

The javeapp of Barren Way is "Jave regards the road ahead with some apprehension".

The roadahead of Barren Way is "The sun is just a dull purple tint around the western horizon, and the stars are coming out. You shuck off your shoes, the better to grip the rock, and feel the trapped warmth of the sun in the stone."

The rebadesc of Barren Way is "Reba is looking out over the rock, wearing the least hijab-ish hijab you've ever seen."

The rebalook of Barren Way is "The mission statement of hijab, you're given to understand, is modesty: primarily about one's body, secondarily about wealth. The hijab Reba is assuming is almost perfectly opposed to that: it's of an exceptionally fine black material. The fit is so flawless - intensely flattering, understating its own effect, suggesting the body beneath in a manner both subtle and achingly beautiful - that an expensive custom dressmaker is unavoidably implied. The neutral black of the headscarf only highlights her face, like a portrait by a Dutch master"

The tsadesc of Barren Way is "You're several shades tanner than normal, and protected by an indigo desert cloak. Light hiking shoes with a deep tread, and the buzz of anticipation that you always get before setting out into the Barrens."

The listendesc of Barren Way is "A steady chirp of insects. Off in the distance, you think you can make out the worried yip of a jackal."

The tsadesc of hotrock is "You're in shorts, sandals, a ratty white T-shirt and a farmer's tan."

The rivdesc of hotrock is "A little way up the hill, half-silhouetted against the sunset, Rivka Strossi stands on one leg and a staff."

The rivcostume of hotrock is "[one of]It takes you a minute to realise that s[or]S[stopping]he's naked except for thick-daubed ashes. (And those fucking aviator shades.) Her head's shaved clean. She's gazing out at the horizon, all her weight on one leg, the staff barely needed for balance."

The rivweapon of hotrock is "a knobkerrie".

The desolation of hotrock is "In the darkening portion of the sky, there's a blue streak, brighter than the setting sun. For about five minutes it shifts towards the horizon, then fades out.[p]Then there's a blinding flash, so intense that it seems to come from inside your head. You're still blinking when the shockwave punches you in the chest and sends you sprawling.[p]You make it to your feet, blood spattering from your nose, ears ringing. A dust-cloud is growing to fill the eastern sky with darkness; and after the dust-cloud, it will rain ash for weeks, and who knows how long it'll be before there's a clear sun again."

The demise of hotrock is "You try to walk out, but there's nothing; this section has separated out, and the meteor-strike must have disrupted your sense of the ways somehow, because it's just... the rock, and the sky heavy with ash, and the land about to die[if Jave is in the location]Jave squeezes your hand. 'Well, I suppose we need to start walking away from the point of impact, right?'[p]'I think it depends on prevailing winds. If we're downwind of it, might be better to take a right-angle. But absent a meteorologist, yes.'[p]'How fucked d'you think we are, Tsawac?'[p]'My surmise is pretty damn fucked. Still. Better walking than sitting in the dark waiting to starve. Let's see about that cooler.'[p]You walk faster alone. So does she, no doubt. But that's not a possibility either of you raise[end if]."

the loungers are a crowd in hotrock. The description of loungers is "A conspicuously mixed-race group; maybe a third each of unambiguous white and black. Dressed for a hot climate that's rough on clothes. There's some low-key chatter and occasional trips to the cooler or the fire, but your general impression is of a pride of very well-fed lions taking naps." The printed name of loungers is "guests".

Book - Maying

maying is a party in tangle. maying is north of wedding and east of hotrock. The printed name of maying is "The Pleasant Month of May". Understand "may" or "pleasant" or "month" or "pleasant month of may" or "england" or "village" or "green" as maying.

The description of maying is "You're on a village green: in England, or something very close to it. It's a flawless day in late spring, a blue sky dappled with lazy white clouds, air clean, crisp and just on the pleasant side of cool. Fresh green all over the trees, including the big dancing-lime spreading its shade over the green's heart. And evidently someone decided that the only thing that could possibly improve this was an accordion. You're amidst a rustic and jubilant crowd, dancing with more enthusiasm than choreography.[p]And there, at the edges of the green, the mouth of a hollow way. A way out."

the holloway is a waymouth in maying. The description of holloway is "Only three sides of the green are ringed by buildings; the fourth is fields, and up them, like a seam, runs a hedge so broad and dense as to accomodate a sunken track at its heart. A hollow way, an ancient road. You feel the accumulated tread of the long-passed, of words spoken in tongues long-buried." Understand "hollow" or "hedge" or "track" or "sunken" or "mouth" or "road" or "ancient" or "way" as holloway.

the dancing lime is scenery in maying. The description of dancing lime is "Not the citrus lime, but the linden or basswood, a beautiful, densely-foliated tree that, later in the summer, will be clouded by a sweet clean fragrance and much-frequented by bees. This one has been trained so that the lowest boughs clear the dancers['] heads, while still allowing climbing. A number of the musicians have been hoisted into the lower branches and are playing overhead." Understand "tree" or "basswood" or "linden" as dancing lime.

Instead of climbing dancing lime: say "If you were here to enjoy the party, that's exactly what you would be doing. But this place isn't designed for your enjoyment; they would probably send wasps."

The dancedesc of maying is "The dance is a simple one, and allows for a wide range of permutations: geriatric shuffles, elaborate moves from young show-offs, artless enthusiasm from very small children and the embarrassingly middle-aged. The overall effect is chaos, and you take a few bumps before deciding to stick to a vaguely rhythmic head bob."

the blue sky is an unimplement in maying. Understand "clouds" or "white clouds" or "air" as the blue sky. The description of the blue sky is "Little-white-sheep clouds proceed sedately across a gloriously blue sky. There's the occasional giant mutant sheep, but nothing with the grey underbelly of a raincloud."

the village green is an unimplement in maying. The description of village green is "Three sides of the green are tree-shaded and framed by houses - nestled among them you pick out a town hall, church, post office, a couple of pubs - and on the other by hedges and fields on a hillside, with a little stream running alongside. It's a good-sized area, big enough for a couple of sports fields."
 Understand "pub" or "village hall" or "hall" or "church" or "post office" or "stream" or "building" or "buildings" or "house" or "houses" as the village green.

the greenery is an unimplement in maying. Understand "trees" or "leaves" or "leaf" or "tree" or "hedge" or "hedges" as the greenery. The description of the greenery is "The trees and hedges are in the first flush of summer, leaves impossibly bright and green and unblemished. There are a great many of them, as if the houses squeezed up like mushrooms between the roots of a forest."

The tsadesc of maying is "You're dressed (not bodied, but passing) as a youth, neatly turned-out and hopelessly rustic; the waistcoat is one layer too many, but at least you don't have to deal with skirts."

The listendesc of maying is "Folk music of the livelier variety. The band are more enthusiastic than talented, but it all comes together well enough. It's mostly accordion and drums, and there're some pipes in there too."

The rivdesc of maying is "The keyboard accordionist is Rivka[one of]. She catches your eye and flashes you a Cheshire Cat grin; even with the shades you can tell that the smile doesn't reach her eyes[stopping]."

The rivcostume of maying is "She's dressed in much the same red-white-and-black outfit as the other musicians, but the colours and lines are stronger, making it feel more like a samurai's formal robes than a mummer's getup. She stands absolutely still as she plays, only one arm and one hand working."

The rivweapon of maying is "a two-foot cudgel".

The boozedesc of maying is "Everyone appears to be drinking that special variety of cider that seems to have been aged in a badger's rectum."

The singdesc of maying is "The music has no words, though if they did they would probably involve sexual innuendo and livestock."

The shortbooze of maying is "You acquire a generous mug of cloudy cider."

The javeinit of maying is "You catch Jave's face as she passes in the dance, laughing."

The javedesc of maying is "It's hard to get a good look at Jave in the bedlam of the dance, but she's dressed in much the same 18th-century-peasant style as the locals. She looks younger, too, tending a little towards puppy-fat, and good holy fuck her hair's in [i]curls[/i]. And she's giggling at something, which she [i]never[/i] does in public."

The javecostume of maying is "She has bound back the blonde curls into a ponytail, and discarded several layers of petticoats to reveal white bloomers, already rather dirt-scuffed."

The closeclue of maying is "You recognise one of the scraps of cloth tied around the tree's lower branches: a scarf."

the mayers are a crowd in maying. The description of mayers is "A sort of idealised version of jolly peasants from somewhere between the Reformation and the First World War, they comprise every age capable of walking."

the musicians are an alt-crowd in maying. Understand "band" or "accordion" or "accordian" or "musician" or "drum" or "pipes" as the musicians. The description of musicians is "A group covering a wide range of ages, musical proficiency and stages of drunkenness. The music is of a kind that mostly accomodates this and staggers onwards, though. They're dressed in a motley of red and black."

the scarf is a clue. The javeclue of maying is the scarf. The description of the scarf is "The scarf is burnt orange, crude-woven. You all wore them in Lacmore, partly for unit identification but mostly because it's useful to have a piece of cloth around. She was the only one who managed to keep hers consistently clean."

The desolation of maying is "The fresh warm air is cut by a billow of icy wind, and a moment later the air's full of snow. Hard, sideways snow, a blizzard. The dance breaks up, people huddling and scuttling for shelter: the snow, already ankle-deep, closes over their tracks. The sky's too dark-clouded to say whether night's coming on, but the temperature's dropping hard, and you're in shirt-sleeves; you catch yourself making hypothermia calculations, but they're all going to add up to one thing."

The demise of maying is "The snow's up to your knees, and the wind shows no sign of abating. The cold no longer hurts. You dimly understand that this is bad, but you're not getting many thoughts any more, and each one of them takes a very long time to form. All the world is now is blurred difficulty: it is difficult to move, to draw breath, to focus on anything. It would be better to lie down, curl up, and maybe things will be better when you wake up."

The Hollow Way is an egress. Hollow Way is outside from maying.

The description of Hollow Way is "A deep-sunken lane, not wide enough for two to comfortably walk abreast: stumpy trees of thorn and ash, hazel and oak turn it into a sea-green tunnel. There are fields on either side, a bright spring sun, but the path slopes upwards, steeply, promising a hilltop wood.[p]You've trodden this many times before. A generous way, if not an easy one. You could walk it in your sleep."
Understand "tunnel" or "hedge" as Hollow Way.

the javeapp of hollow way is "Jave stands beside you, her face dappled with leaf-broken light".

The roadahead of hollow way is "Fallen leaves crunch underfoot. A strident lamb and bored ewe are bleating back and forth across a nearby field, and a gentle breeze is shifting the boughs."

the spring sun is scenery in Hollow Way. The description of spring sun is "Bright, warm, fresh. Warm in cool."

the listendesc of Hollow Way is "You can still make out the sounds of the maying further down the hill, but off in the hilltop wood a thrush is singing."

the trees along the way are scenery in Hollow Way. Understand "stumpy" or "thorn" or "ash" or "hazel" or "oak" as the trees along the way.

the rebadesc of Hollow Way is "Reba al-Nasri sits atop a stile, balancing an improbably-sized crock on one knee."

The rebalook of Hollow Way is "She has a straw hat, a vaguely French-looking floral-print dress, an extremely French-looking scarf and the beginnings of a tan. The huge earthenware crock she's lugging seems heavy enough to pull her arms from their sockets"

The tsadesc of Hollow Way is "You feel very familiar. Hiking boots, lightweight but sturdy trousers, linen shirt."

Book - Mead Hall

meadhall is a challenging party in tangle. meadhall is north of ballroom and east of maying. The printed name of meadhall is "The Raftered Hall". Understand "mead" or "hall" or "raftered" or "norse" or "viking" as meadhall.

the vikings are a crowd in meadhall. The description of vikings is "Mostly men, almost exclusively white. Mostly big burly guys, or guys whose burliness has sagged into fat. The sort of wolf-pack conviviality common to groups brought together by gang violence. The ones scurrying about serving are probably slaves. Not a scene in which to let anyone sense you're an outsider." Understand "crew" or "gruff" or "gnarly" as the vikings.

the slaves are an alt-crowd in meadhall. The description of slaves is "They're shorter, skinnier, less well-fed than the feasting warriors, and drawn from a broad range of ethnicities. They don't catch your eye." Understand "slave" or "servants" or "servant" as slaves.

Does the player mean doing something with the slaves: it is unlikely.

The description of meadhall is "A long wooden building with a complex structure of knotwork-carved beams supporting a high roof. The only light are fires and greasy candles; the air is smoke-filled, laced with the smells of sweat and roasted meat, fuggy and far too warm. A line of tables and benches runs down the length of the hall, and on the benches sit a gnarly and gruff crew."

the carved beams are an unimplement in meadhall. Understand "beam" or "knotwork" or "roof" or "high" as carved beams. The description of carved beams is "Almost every piece of the building's structure is intricately, deeply carved with knotwork. The quality varies somewhat - some of the smaller beams in higher places were clearly left to lesser talents - but it's all very much in the same style. Animal heads, paws and hooves and tails, emerge from the woven wood in ways you can't follow."

the fires are an unimplement in meadhall. Understand "fire" or "fireplace" as fires. The description of fires is "There are hearths but no chimneys; the fires are being maintained at the stage of intense coals, to minimise smoke and roast meat more effectively, but the smoke is still choking. You don't envy the servants tasked with tending those furnaces."

the candles are an unimplement in meadhall. Understand "candle" or "light" as candles. The description of candles is "They're greasy and smell of scorched pork."

the smoky air is an unimplement in meadhall. Understand "smoke" as smoky air. The description of smoky air is "You've spent a great deal of time around campfires, and a certain amount more in fire-warmed buildings. The former beats the shit out of the latter, and when there's no chimney it's just a bad, bad time."

the meadhall tables are an unimplement in meadhall. Understand "table" as meadhall tables. The description of meadhall tables is "Rough workmanship, and you're pretty sure that the timbers are re-used from old ships. You can make out the scars of barnacles here and there."

the meadhall benches are an unimplement in meadhall. understand "bench" as meadhall benches.  The description of meadhall benches is "Rough workmanship, and you're pretty sure that the timbers are re-used from old ships. You can make out the scars of barnacles here and there."

The tsadesc of meadhall is "You're bigger, gnarlier: bare arms knotted with muscle, hands blunt-fingered, all calluses and scars. Your hair's long, thick, treated with some sort of ill-smelling grease, closer to the colour of clotting blood than your usual crimson. The clothing's coarse-woven, with a big fur-lined cloak. Gold rings."

gold rings are an unimplement in meadhall. Understand "ring" as gold rings. The description of gold rings is "There are a [i]lot[/i] of rings being worn here, almost all of them gold. Some are simple bands, but many are shaped into complex designs. There are far too many to be practical on the hands of a fighter: this is a night for special displays[if Jave is in the location]. Jave wears several that are considerably too large for her fingers - taken from larger hands, to be bestowed upon larger hands[end if]."

The listendesc of meadhall is "One big frame drum, occasionally supplemented with a cracked voice raised in a kind of stylised, wordless wail."

The closeclue of meadhall is "The throne is empty, and draped across it is a cloak."

the cloak is a clue. the javeclue of meadhall is cloak. The description of cloak is "It's lined with... you don't know what kind of animal fur, but it's a short-haired, orange-yellow shading to black, not anything that lived in this kind of climate. It has to be hers."

The threshold of meadhall is 1.

The rivdesc of meadhall is "Rivka Strossi is up in the rafters, paring her fingernails with a wicked little knife, one leg swinging in time with the drummer."

The rivweapon of meadhall is "the wicked little knife".

The rivcostume of meadhall is "She's got a grin so wicked that it's in danger of bisecting her head, and the flames of the hearth are reflected in her shades. The dangling leg is barefoot, and has a coil of rope tied around the ankle, the loose end neatly cut."

The boozedesc of meadhall is "Everyone's drinking a spiced and - to your palate - somewhat watery mead, in great quantities. A steady trail of carousers wend their way out of the hall to relieve themselves; the smell suggests that, by the end of the evening, many of them will start finding this trek too arduous."

The shortbooze of meadhall is "A servant hurries up to refill your drinking-horn with mead."

The desolation of meadhall is "The great doors at the hall's end tear open, and through it comes the night and the dark, made flesh in a monster so vast that it fills the whole breadth of the room. It is hard to get a clear impression: long, sinewy, dark-haired limbs, a red maw, the cold eyes of a frog. It snatches up a man, tears him, reaches for another. There's a scramble, with everyone rushing either to defend the hall or get away from the great hands, but you can hear nothing except the winter wind.[p]One by one, the torches are going out."

The demise of meadhall is "You wait in defensive stance, as you always have, waiting to counter the blow. But it's too fast, too huge; an arm broad as a roof-beam lashes around the room and seizes you, cracking ribs and arms. You're lifted aloft, head and shoulders sticking out of the monster's fist, towards that blood-gaping mouth. Your head's engulfed, the teeth close about your neck, and the hand shifts grip slightly for one final, ugly twist."

Instead of using warrior in meadhall when meadhall is doomed:
	say "You read opponents at a glance. This one doesn't follow any of the rules: no fear, no hesitation, the rhythms and coordination all wrong. It may not have a nervous system; you doubt it's physical in anything like the same way that you are. Conclusion: the only idea you have about how to fight this thing is 'from a distance, with high explosives.' Absent that, your best idea is to get the fuck away from it.";

The javeinit of meadhall is "On a throne at the table's head, Jave, every inch a queen."

The javedesc of meadhall is "Jave is enthroned a little way back from the head of the table, on a dais. She looks like a Nordic goddess as imagined by a late-19th century neoclassical: elegantly heavy-limbed, expression impassive, dressed in a perfectly white bedsheet held together by gold pins. Lots of gold in general: armlets, anklets, rings, a tiara. She's running one finger around the rim of a gold-chased goblet perched on a throne-arm, and it takes you a second to realise that it's a cranium."

The javecostume of meadhall is "Jave has ditched all of the jewelry except for a single golden armlet, and has rearranged bedsheet into something that makes her look more like a medieval Japanese labourer. Or a huge baby. She somehow looks radiant regardless."

Orvar is a man. The initial appearance of Orvar is "A burly viking stands across the table from you, red-faced and angry." The description of Orvar is "An exemplar of his fellows: well over six feet tall and heavily-built, with a face like a butcher's block. Everyone here dresses much the same, but his features a good deal more gold." Understand "viking" or "burly" as Orvar.

Carry out using warrior in meadhall: say "You glance across at Jave: [i]should I deck this asshole?[/i] She makes a cut with her hand: [i]no[/i]."

Instead of attacking Orvar: try using warrior.

[Orvarr is straightforwardly lifted from Ãrvar-Oddr, Arrow-Odd, hero of a 13th-century Icelandic saga that includes a flyting sequence. Like most sagas, it's written by Christians looking back on a heroic pagan past, and possibly slightly exaggerating how awesome their forebears were. Odd converts to Christianity mid-saga, but this has no visible effects on his behaviour except to protect him against the battle magic of volvas.]

Carry out using laughing one in meadhall:
	say "Orvar chugs, and starts in on another round of poetic invective. You ignore the substance of it to glance over to Jave, who is... smirking.[p]Not her usual smirk, the understated, momentary one that seems as if she's been briefly amused by a passing thought too subtle to explain: this is a public smirk, smeared all across one side of her face, threatening to peel back and become a snarl. Someone else's expression. Wolf queen.[p]The sneer deepens, gets settled in. Down into the long grass, waiting for the deer. Orvarr plunges on heedless.[p]He missteps - you can't tell how, maybe a liberty was taken with geography or time, maybe he claimed a connection that others have reason to doubt. The laugh rushes onto him like a great wave, scattering his words overboard. It fills the room, both theatrical and spontaneous, deeper than her natural range. It commands other laughs, brings the whole room along with it, and Orvarr stumbles back to his chair as though physically struck.";
	ruin everything;
	
Carry out using forager in meadhall:
	say "Whatever else is wrong with this place, there's no need to scrape up food. It's hearty stuff, oats and barley, lots of stockfish, and - evidently prestige food, this - roast pork and game. And there's no shortage of drink.";
	
Carry out using abider in meadhall:
	say "Doubtless she [i]could[/i] abide sitting there while you get rhetorically shredded, but you'd much rather [i]win[/i].";
	
Carry out using commander in meadhall:
	say "She's definitely in charge here, but this is a morale exercise. And it wouldn't be sound leadership to call the contest off; this lot are ravenous for honour, and well-accustomed to bloodshed."
	
Carry out using antinominalist in meadhall:
	say "The gift only works where it lines up with [i]Jave's[/i] sense of ethics, and its major constraint is her very firm sense of the duties of leadership. Particularly the military variety. And she's queen here, and this bunch are very clearly her warriors. Even if they wouldn't care, she would.";
	
Carry out using troublemaker in meadhall:
	say "Trouble has, quite definitively, already begun. And escalating conflict in a room full of drunk, bloodthirsty hulks is probably ill-advised.";

meadcount is a number that varies. meadcount is 0.

Instead of talking to Orvar: say "This is not a casual conversation."
	
Every turn when the location of player is meadhall and the location of Jave is meadhall:
	if meadhall is not doomed begin;
		now meadcount is meadcount + 1;
		if meadcount is 3, say "The burliest and most forbidding of the men stands to face you across the table; holding up a hand for silence, he knocks back his beer and then begins snarling stanzas in your face.[p]'You weren't there, Tsawac[br]when we cut down the Wend-king,[br]on that day shields burst[br]helms crushed in the fray.[br]Ten times was I wounded[br]in that hot hour.[p]'Meanwhile you warmed benches,[br]your chin all beer-dribble[br]we roved out from Rostock[br]ship heavy with spoils.'[p]He sits down, looking very pleased with himself. Someone shoves a ridiculously large mug of mead at you. Jave catches your eye, and makes a bottoms-up motion with one hand. Fine. You drink. The mead is stronger, drier and spicier than the rest of the stuff going around here, and has less of a yeasty kick; this is the good stuff.";
		if meadcount is 6, say "Orvar stands, chugs his drink, and recites.[p]'Tsawac, you weren't there[br]at the worm's feast in Anglia.[br]Swords ploughed a folk-furrow,[br]the shield-wall we shattered;[br]great the red slaughter.[p]'More to your taste the mead-hall[br]rolling drunk in hearth-ashes,[br]with the kitchen-scut snuggled;[br]as we broke proud Eadwulf,[br]cowed battle-hard Redwald.'[p]Meaty hands thrust a filled goblet in front of you; you drink, to whoops and jeers.";
		if meadcount is 9, say "Orvar stands, drinks. 'Where will you be, Tsa[br]when the swan-maidens seek us?[br]when I go to my sword-sleep[br]such a feeding of eagles,[br]they'll push out my pyre-ship[br]on a river of foe-blood.[p]And you? Lost in the mountains[br]twisted ankle, a rock-fall,[br]with some luck, a skrael's arrow![br]No pyre; mice, worms, beetles[br]are all who will mourn you,[br]and I'll feast, drink and laugh long[br]as roots chew your carcass.'[p]You chug dutifully. Way to be subtle about your mouthpiece, girls.";
		if meadcount > 9 and the remainder after dividing meadcount by 3 is 0 begin;
			say "Orvar unleashes another verse, retreading similar territory with different wording. He still seems to get credit for this; you both drink.";
		end if;
	end if;

Carry out using textual critic in meadhall:
	say "Without it being written down it's hard to pick apart the specifics of his insult-poetry, but you can get a tolerable grasp of the form in general. It's extemporaneous, an easy structure, but with bonus points for certain simple elaborations - mostly alliteration and circumlocution. A boast is twinned with an insult; exaggeration of various kinds is permissible, but while the insults can be fabrications the boasts must bear some degree of relationship to real deeds.[p]That, and there's a lot of drinking involved. Which you can absolutely handle. You rise to your feet, and Orvar is obliged to sit.[p]'Orvar, we didn't see you[br]
in the witches['] ambush:[br]
In that hour metal[br]
was trickery's answer.[br]
As the worlds blew down[br]
we paid back our feud-gold;[br]
three skulls I bore back[br]
to high-honoured Sheaf-home.[p]
'No, you spodded out[br]
on the couch like a grampus,[br]
neckbearding your way[br]
through a life of Ayn Rand.'[p]You toss back your mead, upend the cup on the table, and lean back in your chair. The assembled host erupt in roars; Jave's smile is triumphant. Orvar chugs, looking a touch wobbly, and gets to his feet.";
	ruin everything;


Book - Mutual Slump

mutualslump is a party in tangle. mutualslump is north of wakeroom. The printed name of mutualslump is "Mutual Slump". Understand "mutual" or "slump" or "bedroom" as mutualslump.

The description of mutualslump is "The room is safely small. There's a big bed, and soft carpeting, and it's agreeably warm. You are in good company. Details beyond this come lightly and go away lightly.[p]There are ways out, but they're less comfortable."

the wayout is a waymouth in mutualslump. Understand "way out" as wayout. The printed name of wayout is "way out". The description of wayout is "You sense a way through to the Empty City; in your mind it's the taste of soot and stagnant water, a dull red, a bone-ache."

the big bed is an unimplement in mutualslump. the soft carpeting is an unimplement in mutualslump. The description of big bed is "Soft. Accomodating." The description of soft carpeting is "It's not the nicest carpet, but holy hells it seems welcoming."

The tsadesc of mutualslump is "You're ambiguous and floppy, every minor tension and ache drained out of you. Gender indistinct, and distinctly less important."

the prone people are a crowd in mutualslump. The description of the prone people is "They're safe. Reliable. That's all that really matters. They're slouched and snuggled about the place like an overcrowded seal colony; some are asleep, but most are just pleasantly on sleep's border." Understand "company" as prone people.

The listendesc of mutualslump is "The music is soft, warbly, the singer plaintive - no, not so much plaintive as beautifully resigned to sadness." [I am thinking primarily of Belle & Sebastian here, though Nick Drake may be implied.]

The boozedesc of mutualslump is "Mostly water. You have a vague sense that there's some peppermint tea somewhere, but no inclination to find out."

Instead of drinking the drinks in mutualslump when mutualslump is not doomed: say "You sip some water."

The threshold of mutualslump is 2.

The rivdesc of mutualslump is "The stomach your head is resting on is, you're [one of]suddenly aware[or]almost certain[stopping], Rivka Strossi's."

The rivcostume of mutualslump is "She's softer than you would have expected. One hand is playing with your hair. It's unnervingly pleasant."

The rivweapon of mutualslump is "a baseball bat".

The closeclue of mutualslump is "Hey, there's your copy of [i]The Baron in the Trees[/i] that you lent to Jave and never got back."

the Calvino book is a clue. the javeclue of mutualslump is the calvino book. Understand "baron" or "trees" or "baron in the trees" as Calvino book. The printed name of Calvino book is "[i]The Baron in the Trees[/i]".


The description of calvino book is "It has become considerably more dog-eared[one of].[p]Lacmore Slope was a bad, bad fucking job. The only safe route out was a five-day hoof, and there were no places safe enough for R&R leave, so days off were spent dossed out in the dorms, desperate for something to think about other than what you'd seen during your days on.[p] They knew it was shit, though, and the employment terms included two crates of personal supplies and a generous advance. You converted one crate and most of the advance into reading material[or]One of your favourite books, long-lent[stopping]."

Instead of sleeping: say "Seriously not the time for it."

Instead of sleeping in mutualslump: say "Under the all-encompassing feeling of comfort, very deep down, is the suspicion that if you went to sleep here you might not wake up."

Instead of using textual critic in the presence of Calvino book:
	say "Most obviously it's concerned with escapism, exile, and with the Wood. The wood touches every part of Ombrosa, yet it is not Ombrosa. Cosimo is a sort of fairy, an inhabitant of a land adjacent but fundamentally separate; thus able to retain a certain attitude of youth. The bargain he makes is an end in itself, not a temporary sojourn in the wilderness; unlike Mowgli or Crusoe, he is not destined, nor is he willing, to return to an ordained place in civilised society - about which Calvino, unlike Defoe or Kipling, is generally pessimistic, yet Cosimo's exile is in some limited senses a triumph, not a reduction. It's a book tied to the age of profound disappointment that follows an age of idealism, particularly intellectual idealism.[p]You could expand at length upon this to unpack the significance to post-WW2 Italy, to mid-C20th socialism, to the relationship between sainted hermit and ivory-tower intellectual, to clade non-interventionism, and to your own special turmoils; but you have more pressing objectives.";

The javeinit of mutualslump is "Jave is curled up like a cat, her head resting on your shoulder, breathing slow and regular."

The javedesc of mutualslump is "You can't see her face from this angle, but you don't think she's asleep. Her touch is immensely soothing."

The javecostume of mutualslump is "She's wearing grey sweat-pants, purple socks with unicorns on them, and a sweatshirt with the caption [i]DOGS WITHOUT GODS - Long Patrol Refresher [']09[/i] on the back."

the desolation of mutualslump is "The walls are fading into a nondescript grey. The soft, gentle feel of the world is turning into an unbearable fug. There is no longer very much distinction between the sleepers and the floor and bed: you cannot make out where one begins and another ends, and you can't even say what they ought to look like. The light, already soft, is flattening into a uniform fuzzy glow, the background noise into a single steady heartbeat."

the demise of mutualslump is "Distinctions fade. No judgement. No boundary. No self.[p]Which is to say, no you."

The Empty City is an egress. Empty City is outside from mutualslump. The description of Empty City is "A cobbled and overhung street, leading upwards to a line of boarded-up storefronts; the air has the faint taste of a sea-mist, and the dull red glow of the sky accomodates no stars. The city is sometimes long-ruined, sometimes temporarily abandoned, sometimes merely shuttered against the night or the storm; you will meet only strays and transients here. Faceless cars might pass, but you'll always be on foot, in the interstices of the city and the road. A hard way, perhaps the loneliest."

The javedesc of Empty City is "In this light Jave is little more than a shadowy presence beside you, but she's here."

The listendesc of Empty City is "The distant roar of a motorway. Raised voices, you're not sure in which direction. The occasional siren."

The roadahead of the Empty City is "Glass crunches underfoot. Far off, a dog tries to howl but cracks into a whine."

The rebadesc of Empty City is "Reba al-Nasri congenially lurks in a gutted doorway."

The javeapp of Empty City is "Jave slumps against a wall, catching her breath".

[I really should have submitted Congenially Lurks as a pseudonym.]

The tsadesc of Empty City is "Black jeans going out at the knee. Army boots. Grey hoodie, neoprene shooting gloves. Wardrobe aside, you feel close to your default, only more worn-down and grimy."

The rebalook of Empty City is "Reba is gothed up in a style best-described as a burlesque version of Miss Havisham, decaying wedding-gown and artificially tear-streaked makeup. The torn fishnets are perhaps overselling it, but you can't deny that it's working for her"

Book - Cocktail Bar

DoubleTwisted is a challenging party in tangle. DoubleTwisted is north of hotrock and east of mutualslump. The printed name of DoubleTwisted is "Double and Twisted". Understand "double" or "twisted" or "bar" or "cocktail" as doubletwisted.

['Double and twisted' is an Appalachian moonshining term, referring to the higher-quality, fusel-oil-free liquor produced by running the shine through the still a second time. If I ever get around to making a moonshiner game, that'll be the title.]

The description of DoubleTwisted is "A long cocktail bar, with a certain degree of wood-panelling but not all stuffy about it. Populated, but not crowded: it's late[one of], but not yet that desperate hour of the black morning when all is ruin[or][stopping]. Windows overlook a rain-soaked street."

[The image I had in mind was of the Bookstore Bar in downtown Seattle, though there's little here to identify it. Or so I thought, until Emily Short and Jacqueline Lott, the people with whom I most regularly frequented the Bookstore, independently identified it.]

the cocktail bar is an unimplement in doubletwisted. The description of cocktail bar is "A long slab of dark-chocolate wood, with the usual assembly of taps, nozzles, drip-trays and implements on your side."

the wood-panelling is an unimplement in doubletwisted. Understand "wood" or "paneling" or "panels" or "panel" or "panelling" as wood-panelling. The description of wood-panelling is "Pale, stripped wood. This plainly isn't an old gentleman's club, but it's trying to evoke that general flavour."

the DT windows are an unimplement in doubletwisted. The description of DT windows is "Ceiling-to-floor windows overlook a broad, rain-spattered street, sloping gently." The printed name of DT windows is "windows".

the rain-soaked street is an unimplement in doubletwisted. The description of rain-soaked street is "It's a hard rain; you can endure most kinds, but this is a full-on deluge. The buildings on the other side of the street, the neon of another bar, are smeared by water pouring down the glass."

the barflies are a crowd in DoubleTwisted. The description of barflies is "Professional-looking types, on the whole, if not particularly dressy. Not a place to get your cocktails cheap." Understand "patrons" as barflies.

The tsadesc of DoubleTwisted is "Black dress shirt, black pants, waistcoat and tie; androgyne-going-on-femme. Maybe a little taller than you were. Hair black, slicked-back."

The listendesc of DoubleTwisted is "Jazz played at a speed suitable for dancing, probably from around the 40s."

The threshold of DoubleTwisted is 2.

The closeclue of DoubleTwisted is "There's an empty martini glass on the bar, holding down a fifty and a scrawled-on napkin."

the napkin is a clue. the javeclue of DoubleTwisted is napkin. The description of napkin is "The martini is just a martini. (She has a near-religious aversion to lipstick, to the point of avoiding lip balm because it's kind of like gloss, and it's adorable when she bites her lip but not so much when it results in [i]scabbing.[/i]) The handwriting unambiguously hers, though, although it's so hastily-scrawled as to be illegible." Understand "martini" or "fifty" as napkin.

Instead of drinking napkin: say "The martini glass is empty, with just a faint aroma of top-shelf gin.";
Instead of smelling napkin: try drinking napkin.

Instead of using Textual Critic in the presence of napkin: say "If you can't make out the letters, there's not much analysis that can be done. It's Jave's handwriting, no question, and it was hastily written. Contextually it has to have been intended for you. Under the circumstances, she wouldn't waste time on anything but crucial instructions: where to wait for her, how to deal with something. But that's all you're getting."

The boozedesc of DoubleTwisted is "The bar goes beyond full and makes a very close run at comprehensive; it towers so high that ladders are required. The actual guests, however, seem to mostly be drinking the dozen-or-so proper classic cocktails.[p]But on closer inspection, the bottles are all labeled in an incomprehensible alphabet, one that you're inexplicably unable to comprehend."

The javeinit of DoubleTwisted is "Jave leans against the bar, toying with a martini on its last legs. Her hair has shrunk to a flapper bob, and she's wearing an arresting red-gold dress of some shimmery, perilously-thin material."

The shortbooze of DoubleTwisted is "You pour a shot from a random bottle - it tastes of [one of]aniseed[or]raspberries[or]gym stank[or]old paperback glue[or]oak barrels[or]ammonia[or]acrid herbs[or]pepper[or]sardines[or]burning[or]marijuana smoke[or]freshly cured leather[or]fresh rain[or]stinkbugs[or]cleaning products[or]steel[or]lavender[at random]."

The javedesc of DoubleTwisted is "You suspect the tangle of having set this entire scenario up to show Jave off to good effect. The art-deco-y dress is a natural match for her long neck and boyish torso, and if the designer was thinking of someone a little, well, twiggier, she's only improved on the original intent. She's near the end of her martini, and has her eyes locked on you."

The javecostume of DoubleTwisted is "Jave has thrown a trenchcoat over the cocktail dress, which will do for now."

The rivdesc of DoubleTwisted is "Rivka sits at the end of the bar in full-on suit-and-tie drag, a sharp-angled fedora, lipstick the colour of falling ashes."

The rivcostume of DoubleTwisted is "Rivka's suit is more 30s menswear than anything: loose pants with a razor-edge crease, broad high lapels, high waist and broad shoulders, tie with a dark lavender pattern. The fedora doesn't match the aviator glasses at all, and the corpse-white lipstick also doesn't fit, and it's all quite conscious. She has a Mason jar full of muddled lemon and clear spirits at one elbow, a pack of cigarettes, a Zippo."

The rivweapon of DoubleTwisted is "an oversized revolver".

The desolation of DoubleTwisted is "The high windows explode inwards: a second later, the chatter of automatic fire from the street. Reflex takes you [if Jave is in the location]and Jave[end if] to the floor behind the bar - [i] useless as cover, find a supporting wall[/i] - and spares you the sight of whatever's happening to the patrons. One gun cuts off, another two start up, rhythms mismatched - [i]Uzi, AR, AK. Irregulars. There were... two, maybe three vehicles pulled up outside, big SUVs, different colours. Gang? Militia?[/i] Shattered glass is raining down from above. [i]No equivalent weapon, no allies, no position of advantage. Get the fuck out.[/i]"

Instead of using warrior in DoubleTwisted when DoubleTwisted is doomed:
	say the demise of DoubleTwisted;
	end the story;

The demise of DoubleTwisted is "If you're going to stay here, you'll need a weapon. They have weapons, QED. More immediately, you need some real hard cover, somewhere to mount an ambush. There's a supporting pillar over by the doorway. You pick a break in the fire and hurl yourself up and over the bar, sprinting low and hard. They spot you, and the gunfire gets a lot heavier very quickly. But somehow you make it, cannoning into the little corner of blessedly solid brick. You roll over, bruised and scraped, to catch your breath, and this enables you to see the grenade's final bounce."

Carry out using critic in DoubleTwisted:
	say "There's not enough text on the labels for you to bring an interpretation together. And something's preventing you from reading any of them, which you should normally be able to do.";

Carry out using forager in DoubleTwisted:
	say "You don't understand any of the labels, but if you [i]sample[/i] - you grab a shot glass and fill a larger one with water.[p]What does she like? So much of your time together was spent in places where food was just fuel. Coffee, absolutely. Citrus: in Masdita she ate grapefruit and oranges at every chance she could. Otherwise, usually not sweet things. Awful beer, let's not go down that road [i]just[/i] this moment. Something cold. As you're thinking, you're popping open every bottle in sequence, pouring just a few drops: sniff, sip, rinse and spit. (If anyone objects to your indecorous use of a spare bus tray as a spitoon, they can go fuck themselves.) The first three taste of... nothing you've had before, definitely acquired tastes. The fourth is a sickly fake-hazelnut thing. You wrinkle your nose and spit with gusto.[p]Before you've moved more than three feet down the rows, your tastebuds are no longer to be trusted, but an idea's formed: a couple of fingers of the crisp, faintly-fruity clear stuff, one of the golden thing that [i]might[/i] be pear brandy, a lemon's worth of juice, a muddle of those aromatic herbs, stir in ice, strain. Not going to win any awards for originality, but hopefully not something you'd send back. You place it lightly before her.[p]Her eyes go hooded for a moment after the first sip, then go to you, smiling. 'Yeah, that should do it.'";
	wait for any key;
	say "[p]Her hand's lying, palm-up, on the bar. You move to touch it.";
	ruin everything;
	
Carry out using antinominalist in DoubleTwisted:
	say "Well, if she wants a drink, her guess is probably as good as yours. You extend a hand. 'Here, come and get your own.'[p]She takes your hand, steps out of her shoes, and hops up onto the bar and down onto your side, cat-smooth. She evidently follows these names better than you do, somehow, because she darts from bottle to bottle, hand fluttering along the row before snatching up something as though she recognises it. Something starts coming together in a shaker, something milky-green and aromatic. She stirs once, strains it into a teacup, and boosts herself up to sit on the bar and drink it.[p]You haven't seen her cross her legs in a dress before, and you would have liked a lot more time to appreciate the effect.";
	ruin everything;
	
Carry out using warrior in DoubleTwisted:
	say "Barfights are a lot less romantic than generally assumed. You don't care to start any.";

Carry out using commander in DoubleTwisted:
	say "Lead whom? To what end?"
	
Carry out using troublemaker in DoubleTwisted:
	say "A bartender's meant to defuse conflict, not start it. That, and provide drinks."
	
Carry out using laughing one in DoubleTwisted:
	say "You smile and raise your eyebrows at Jave. She returns the smile, with rather more teeth and eyelashes than you expected, and raises a finger above her empty glass.";
	
Carry out using abider in DoubleTwisted:
	say "Well, you're sure she could endure not getting another drink, but that would defeat the purpose rather.";

Book - Orgy

orgy is a challenging party in tangle. orgy is north of maying and east of DoubleTwisted. The printed name of orgy is "Ashteroth Karnaim". Understand "club" or "sex" or "fetish" or "kink" or "ashteroth" or "karnaim" as orgy.

[The issue of this room is ]

The description of orgy is "It's sort of a mixture between a hotel lobby and a singles bar, just in terms of how it's laid out; there's a lot of wine-red velvet drapes and abstract erotic photography, just in case anybody was confused about it being a sex club, but really it's not as tacky as that sounds. The light's soft and distributed, and somehow the infographic posters not-so-discreetly positioned all over the place produce a kind of Toulouse-Lautrec feel rather than the sense that you're here to do something akin to buying a house or undergoing major surgery. There's a balcony section above, around which are distributed windowed booths."

the erotic photography is scenery in orgy. The description of erotic photography is "Oh, you know. Black-and-white close-ups at odd angles, of body-parts and activities that are sexy once you turn your head the right way and realise what they are. To the club's credit, there's a pretty decent variety of sexes and body-types." Understand "photo" or "photos" as erotic photography.

the wine-red velvet drapes are an unimplement in orgy. The description of drapes is "They're meant to evoke sensuous decadence, though to you they suggest old-timey cinema."

the balcony is an unimplement in orgy. The description of the balcony is "It's really just a walkway to connect to the second-floor booths, but since it's also a good point for visibility, a good number of people are loitering around up there."

the windowed booths are an unimplement in orgy. Understand "booth" or "window" as windowed booths.

The description of windowed booths is "Each booth is a small room - a little larger than a double bed - separated by from the main room by a door, a window, and a curtain. In about two-thirds of the occupied booths, the curtains are closed; the remaining third might arouse more of your interest given less stressful circumstances."

The threshold of orgy is 2.

The tsadesc of orgy is "You're in a voluminous, ankle-length dressing-gown of padded silk, the sort of thing a leading man might wear at breakfast in a 30s comedy[one of].[p]They may think it's light entertainment to threaten your life and treat your private relationships as the field for a stupid game, but the Rebeccas have at least the basic decency to not drop you naked into a sex club. Which is a shame, really, because that would [i]really[/i] have given you a reason for bloody revenge[or][stopping]."

The listendesc of orgy is "Too loud for conversation: langorous electronica, heavy on the bass end, as if heard through deep waters: over which a husky-voiced woman is crooning extraordinarily dirty things in... man, that's [i]Old Occitan[/i]. OK, that's just showing off."

the club patrons are a crowd in orgy. The description of club patrons is "A variable and polite bunch, mostly mid-twenties to mid-forties. Too ethnically diverse for almost anywhere on Earth; you suspect this is mostly drawn from Upside. The disconnect between those dressed in high fetish gear and those who look as if they're just on a low-key night out is a lot odder than the minority who are just naked.[p]Nobody is [i]quite[/i] naked, though; everyone's right arm contains a variety of coloured metal bracelets."

bracelets are scenery in orgy. The description of bracelets is "Can't be decoration; they're too varied. A code."

[This was, fairly obviously, meant to be a puzzle, but I didn't have time and it didn't really fit in with the tone and pacing of the game anyway.]

The javeinit of orgy is "She's here, but you can't say where. And she might not be here for you."

The javedesc of orgy is "Jave is - you're not sure where, you catch a glimpse of her on the upper level, talking to a man whose face you can't see, but then she's at the bar, hair worn up to show off a collar, but then she turns into profile and it's not her at all, just a girl with a similar jawline. A beautiful boy has exactly her hip-jive mannerism of slipping around someone in a crowd, a generously-built redhead in a vinyl corset has her gentle, private little smirk. She's here, she's imminent, she is just moments away from to coalescing from the crowd."

The javecostume of orgy is "Jave's wearing black silk pyjamas of a vaguely-Chinese cut, and matching slippers. There are bracelets jangling beneath one sleeve."

The rivdesc of orgy is "Rivka Strossi is over by the doors, dressed as a bouncer."

The rivcostume of orgy is "Shades, earpiece, big suit, no-nonsense hair, face impassive as a monument. The other bouncers start out at about twice her weight, but are somehow less imposing."

The rivweapon of orgy is "a set of brass knuckles".

The boozedesc of orgy is "There's a full bar, complete with a craggy, dented mountain of a bartender, but nobody seems to be having more than one or two."

The shortbooze of orgy is "You order a gin and tonic from the bar. They don't skimp on the lime, which is important."

The desolation of orgy is "From outside, very far away, a bell begins to toll, deep and slow. Very matter-of-fact, the patrons set down drinks and curtail embraces, straighten their clothes, and fade into nothing.[p]The lights go out, one by one; the curtains fade to reveal shattered windows and the lifeless amber cut of streetlights. The floor and couches are strewn with fallen plaster. Your bones ache with someone else's nostalgia."

The demise of orgy is "You do not leave, and you do not fade. You don't need food or sleep. It is always night, and it is always a ruin, and you drift aimlessly through the hollow shell of a community that you never fully understood. Forever."

the infographic posters are scenery in orgy. The description of inforgraphic posters is "This one describes the importance of [one of]using prophylactics on shared sex toys[or]phonotactically efficient safewords[or]responding promptly to reports of harrassment[or]appropriate lubrication choices[or]quick-release bondage plans[or]literacy in consent semiotics[or]regular STD screening[or]care and patience in various forms of penetration[at random]."

the closeclue of orgy is "Somehow worked into the decor, a familiar and out-of-place window."

the Masdita window is an unreachable clue. the javeclue of orgy is Masdita window. The description of Masdita window is "Her room in the boarding-house in Masdita was a shoebox - a comfy, clean shoebox, sure, but the one grand thing was this window, old carved wood with little geometric insets around the edges. It was high enough that from the bed all you could see was the sky, broken up by the highest branches of a pepper-tree. On the last week, which neither of you knew was the last week, the week after you abandoned the separate room, you'd lie in bed and watch the morning sun angle across the room. (The mornings were always gloriously sunny in Masdita. Weather like clockwork.) The pepper-tree was popular with songbirds, too early and too loud. You can't remember what any of them sounded like."

Carry out using critic in orgy:
	say "Nobody's wearing long sleeves, or else they have the right sleeve rolled. Those bracelets are obviously code for something, which means that they're a language, a script. You scan the room. Here a man holds up a forearm for the perusal of a young couple - brisk apologetic smiles all round, and they part. There two men shake hands, glance down at the wrist, promptly link arms and head for a room. A [i]simple[/i] language, with abundant context and certain things that very clearly have to be encoded. Hardly cryptography; barely even a puzzle.[p]Some of the bracelets have little bands on them, and these exchanges seem to take longer, with gestures at the banded ring: [i]clarify[/i] or [i]with some conditions.[/i] You quickly figure out and select [i]female-identified, seeking women, no groups, no spectators.[/i] There are a bunch so little-used that you can't triangulate, but for that reason they're probably safely ignored. You suspect, too, that there's some implication in the order and position of the bracelets, but that can wait. That leaves an abundant middle ground.  Watching the circulation at the tables gives you [i]would prefer to have a conversation first[/i] and - for your purposes, most importantly - the flat bronze armlet that means [i]let's go somewhere else together[/i].[p]That still leaves a huge scope of bracelets that are moderately common but can't be guessed at without perving on their bearers in action, up at the windowed booths. You'd be fine with this, if it weren't for the sense that this is precisely the point at which the Rebeccas would start sneaking in misleading clues for shits and giggles.[p]Well, you're not going to get turned on by anything with [i]them[/i] looking over your shoulder, and this makes watching other people fuck either disturbing or hilarious. You opt for hilarious - [i]this[/i] bracelet obviously means A Rubbish Top But Will Insist On Trying, [i]that[/i] one means Hot Enough That You'll Put Up With My Adolescent Posturing - and you're beginning to feel a little bit bad for being so uncharitable, when Jave slips her arm into yours.[p]'Lets ditch this joint.'";
	ruin everything;
	
Carry out using antinominalist in orgy:
	say "This place is built on rules, an intricate, sensitive codification of caution and consent. It's largely sensible stuff articulated as dogma, of the kind necessary when doing heavy construction over a volcano crater. And in another time, another place, another person, you might need that. But for the handful of people who you've truly cared about, the establishment of trust didn't require anything so procedural. You don't need bracelets to figure one another out, and there are always some things that are crushed or obscured by codes.[p]Jave slips from the crowd, tilts your chin up and draws you in for a kiss. It's long, slow and thorough, and your bodies kind of pressure-melt into one another. At length she draws back, eyes locked on yours. 'Let's ditch.'[p]'Shit, I'm going to do [i]such[/i] bad things to you once we get out of here.'[p]";
	ruin everything;
	
Carry out using laughing one in orgy:
	say "You hear a laugh that you're sure is hers, but following it leads you back and forth across the club. You suspect that she needs to find you, rather than the other way about."
	
Carry out using commander in orgy:
	say "You briefly consider the possibilities of treating an orgy as an exercise in military command. You're not entirely sure whether the idea is disturbing, arousing or just a kind of category error; regardless, it'd be a distraction."
	
Carry out using abider in orgy:
	say "She can, it's true, manage quite well without you. Or anyone, you suspect. That is not the problem here."

Carry out using warrior in orgy:
	say "You do not possess any special expertise in the varieties of violence that are appropriate here."
	
Carry out using troublemaker in orgy:
	say "God, the drama you could wring out of this place if you had a mind to. But... well, it's not so much that you're a good person, per se, it's mostly that the idea of being mixed up in that makes your stomach turn. Oh, sure, and they seem like decent people whom you have no reason to injure, but the determining factor is mostly the gut-dread."
	
Carry out using forager in orgy:
	say "There's a stocked bar. A handful of people are carrying whipped-cream-in-a-can, honey and the like, and one guy has roughly twenty pounds of raw steaks in a grocery bag. You'd hoped for entertaining root vegetables and gourds, but apparently the classics died with the advent of silicone."

Book - Bacchanal

bacchanal is a party in tangle. bacchanal is north of meadhall and east of orgy. The printed name of bacchanal is "The Goat's Wood". Understand "goat's" or "wood" or "goats" or "goat" or "forest" as bacchanal.

The description of bacchanal is "It's night, and you're in the forest, tree limbs thick in a moonless sky. Figures dart and stagger at the edges of your vision. The ground is wet with dew.[p]The woods get deeper, further up. They might lead anywhere."

the deep woods are a waymouth in bacchanal. The description of deep woods is "You are, at heart, a walker in the wood. Given three trees and a shrubbery, you can walk out. A wild forest like this is not so much a door left ajar as it is a triumphal arch."

the darkened trees are an unimplement in bacchanal. the moonless sky is an unimplement in bacchanal. the dewy ground is an unimplement in bacchanal.

The description of darkened trees is "The trees are old, but of moderate size and open spacing, and they all seem to be of the same species; you have the sense of being in a hillside orchard left to go wild. Their limbs mesh into one another overhead, choking out the sky."

Instead of climbing trees: say "You have a nasty feeling that it'd be best to avoid precipitous heights."

The description of moonless sky is "What you can make out of it is very, very black. No stars, even."

The description of dewy ground is "The grass is overgrown, full of snags and dripping with dew, chill despite the mild air."

The threshold of bacchanal is 3.

The tsadesc of bacchanal is "Barefoot. Clothes torn to rags. Covered in fresh blood, almost certainly not yours. Horny, furious, incandescent."

The listendesc of bacchanal is "Drums so deep and reverberant that they seem to come from everywhere, or from your bones. Fiddle deliriously fast and complicated, bounding over its mistakes and keeping on, whirling through the woods on every side. Pipes a mourning murmur, soft and very far off, surfacing with killing softness in the spaces between."

The desolation of bacchanal is "The horizon is growing pink. You hadn't known that there [i]was[/i] a horizon.[p]In the grey dawn-light, figures are scampering off into the woods. The light makes everything solid, sharp-focused. This tree is just a tree, unmoving, knowable. The fragment of wine-jug in the leaf-litter at your feet - you crouch and run your fingers along the grainy broken edge - is an object, a thing with its own shape, not a thing of dream and shadows. The people - oh, shit.[p]They're scattered out over the ground, like so many broken vessels and cast-off garlands. This man is throwing up wine, his back a pulpy mess of whip-marks. This woman is huddled at the foot of a tree, naked and foetal, and howls whenever you look at her. There are others, and not all live.[p]And you - you're gored up to the armpits, there's crap under your fingernails and the greasy taste of iron in your mouth. Nausea's creeping up, and with it that special hangover feeling of [i]knowing[/i] you did something unforgiveably awful, and it's just a matter of time until you remember what."

The demise of bacchanal is "Life is, you dimly realise, a kind of emulsion of madness and reason, unified by a fragile and exacting process. The goat-wood clarifies the cloudy substance, briefly, but at the cost of stability: afterwards, it curdles, clots, begins to putresce. You're rocking back and forth, fingers clawed around your shoulders. There was a sense, a while ago, of what the next thing to do was. You try to remember what that felt like, how that was walked, how one action could thread into the next to serve a calmly chosen end. It doesn't come."

Into the Woods is an egress. Into the Woods is outside from bacchanal. The description of Into the Woods is "The fringes of the Old Forest, the most ancient and powerful of the Great Ways. You're amid mixed-up thickets of rowan and thorn, holly and beech; beyond them, distant and deeper, the crowns of vast oaks spread to the sky, every leaf picked out in the sunlight. Mossy grass underfoot, soft and damp. There are no paths here, not so much as a rabbit-trail: but a wanderer among the trees will always find [i]something[/i], sooner or later.[p]Nobody is entirely comfortable with it, but every walker has to deal with it at some point. It's a way home. A long one, but it'd be one hell of an adventure." Understand "forest" or "old" as Into the Woods.

the woodland trees are scenery in Into the Woods. The description of woodland trees is "It's said that the Old Forest contains every tree that ever was, and you can testify that it has several not strictly included in that category." Understand "oak" or "beech" or "holly" or "rowan" or "thorn" as woodland trees. Instead of climbing the woodland trees, say "Yes. But another day."

The javeapp of Into the Woods is "Jave is leaning against a tree to catch her breath".

The tsadesc of Into the Woods is "You feel very familiar. Hiking boots, lightweight but sturdy pants, tank-top."

The listendesc of Into the Woods is "It's very still. A light wind in the tree-tops, but so soft that it might be imagined."

The rebadesc of Into the Woods is "Reba al-Nasri waits, dressed too ornamentally to go far."

The rebalook of Into the Woods is "You'd describe her outfit here as Elven Druid Princess. There's a muted palette of pale greens and browns (her eyes have shifted emerald to match), a lot of leaf-themed cut-outs and vaguely-Celtic jewelry, and a skirt that's part fairytale-wedding and part ghillie-suit. Setting aside that it'd last three seconds of actual woodland travel before being snagged and muddied and torn, those boots are essentially a blister factory. She would look very spirit-of-the-forest in a photo, though"

The roadahead of Into the Woods is "You scramble over a mossy log, disentangle yourself from the brambles that snagged you in the process, and find your way downhill to a tiny creek, a good step wide and barely ankle-deep. The air's cool and fresh and full of earthy scent."

the rivdesc of bacchanal is "Rivka Strossi is perched in the crook of a tree. One arm dangles a brace of severed heads by the scalps."

The rivcostume of bacchanal is "The dark hides it at first, but she's naked except for a mane of hair that sweeps down, like a cloak or a waterfall, to below her gripping feet. The limbs that emerge from it are death-white, model-thin, with fingers and toes too long to be human. Her lips tweak in a dreamy smile, but you sense fangs beneath."

The rivweapon of bacchanal is "a butcher's knife".

The boozedesc of bacchanal is "Wine the temperature of blood. New wine, sugary as grape-juice and with a vague mustiness."

The shortbooze of bacchanal is "You crush grapes between your fingers, suck at the fresh-fermented liquor as it runs down your arm."

The javeinit of bacchanal is "Jave is a moon-glow white under a moonless sky."

The singdesc of bacchanal is "An exultant ululation escapes your throat, two parts wolf-howl and one part hacking sob."

a discarded sandal is a clue.  the javeclue of bacchanal is discarded sandal. The description of discarded sandal is "These were her camp-shoes when you met, on the duffer escort along Mirror Branch. You barely spoke for most of it - the Long Patrol vets mostly had their own fiercely-ingrown social circle, and later you learned that there was some kind of rumour attached to you, a wayvine-distorted version of the Krusenstern business, and fuck, it's not as though you get along with military at the best of times. You clocked her as someone perhaps a little special, but that was all. But you remember the shoes, because people were leaving them outside their tents until some big critter, a porcupine or a giant capybara or something, snuck around in the night and gnawed a chunk out of hers. This is the one that doesn't have the bite taken out of it.[p]You had assumed she'd just thrown them away after that gig. Very likely she did."

The closeclue of bacchanal is "A single familiar sandal is lying amid the leaves."

The javedesc of bacchanal is "Jave's wearing something white and clingy and - the [i]mot juste[/i] would be [i]diaphanous[/i], the sort of thing a silent-film starlet would wear to flee from the monster. It suits her hilariously badly, in part because it exposes enough of her limbs to make it very clear that she possesses the werewithal to snap the monster's collarbones with one sharp blow. That, and the [i]seriously?[/i] expression on her face, do a great deal to make this place seem less like a murderous sex nightmare and more like a dumb haunted-house sideshow."

The dancedesc of bacchanal is "You execute a series of wild twirls and bounds. It does not have a calming effect: more the feeling that you might transform into a leopard."

the bacchantes are a crowd in bacchanal. The description of bacchantes is "You aren't able to see them all the time, but these woods are populated. A group that has been scattered, to hunt, dance, stagger blind-drunk, flee through the woods. Their cries and whoops ring on all sides, and the music - some of it, only some - is theirs. You hope not to run into any of them." Understand "maenad" or "maenads" or "satyr" or "satyrs" as bacchantes.

Table of Unchallenging Responses
locus	power		response
startroom	textual critic		"So many books here, grabbing one at random wouldn't reveal all that much. Consider them as a corpus. First, presentation: these are a set of books selected for their effect on an audience. Fiction forms a minority, is entirely hardback literature, often collector's editions. A great many gorgeously expensive coffee-table books, mostly with anthropological overtones: portraits of modern African scarification, street food scenes across the worlds, confiscated prison weapons in the West 1800-1960. The liminology section is dense, technical, and tightly organised, suggesting expertise; the magic stuff, on the other hand, leans hard towards the esoteric and apocryphal, scholarly works jumbled up with mystic ravings - so, either one person with very different approaches to neighbouring subjects, or, more likely, at least two contributors."
startroom	forager	"There's an abundance of finger-food here, most of it pretty good. You scarf down some devilled eggs."
mutualslump	warrior	"You're feeling about as aggressive as a throw pillow."
mutualslump	troublemaker	"Suffused as you are with a feeling of goodwill, you have no inclination to cause problems for anyone."
bacchanal	troublemaker	"Trouble's been made. So much trouble."
bacchanal	warrior	"[one of]You adopt a fighting stance, and spontaneously execute a rolling breakfall. Your form's inexcusably wobbly, and you stagger as you roll to your feet. There's laughter off in the woods[or]You box with the shadows. The blood on your arms mocks you[stopping]."
bacchanal	textual critic	"They don't write things down about this place."
hotrock	forager	"You could find food in this kind of country, though it'd be laborious and unappetising - lizards, insects, roots, buried water. Fortunately, there's a cooler full of drinks and hot sausages on the grill."
wedding	forager	"You spend some time considering setting up fishing lines with wire from the party lights, or using lights to draw in fish and spearing them with... well, you can always make up a spear somehow or other. Given the waiters, the question is largely academic, however."
wedding	troublemaker	"Weddings of this sort are big concentrations of extended family who may very well not like one another; a great deal of money is riding on the thing, and a lot of people are going to get drunk. It would not be excessively hard to torpedo the entire thing. It's not clear how that would [i]help[/i] you, though."
beautiful people	forager	"You're not entirely convinced that this body eats."
beautiful people	warrior	"You may not like them very much, but they probably don't deserve an unprovoked beating."
beautiful people	troublemaker	"If you were even minimally part of the group, there'd surely be potential here - people this all-consumingly beautiful have to have sensitive spots like [i]whoa[/i] - but they barely even acknowledge you."
gamejam	forager	"You scavenge up some cold pizza, and greasily consume it. It was probably pretty good when it was warm."
gamejam	troublemaker	"Everyone's strung up so tight that you're confident that you could ruin some friendships forever with a couple of nudges in the right place. But you don't hurt people just for the sake of it."
gamejam	textual critic	"It's impolite to criticise a work in progress unless you're invited to do so. You feel quite strongly about this."
DoubleTwisted	forager	"Not a bad idea - you have no idea what all this alcohol is, but you could probably find out. It doesn't seem too relevant now, though."
orgy	textual critic	"There's obviously some subtext to the bracelets everyone's wearing. A code, there. If you had the time and the inclination to try and pick up here, it'd be highly useful."
orgy	troublemaker	"Gods yes, this place has serious drama potential. But that'd be wanton destruction."
wakeroom	warrior	"This place [i]does[/i] make you feel like a good palate-cleansing brawl. And disrupting the tone crafter by the Rebeccas would be a definite bonus. But disrupting the weave is a calculated risk, and without Jave it's not worth it."
meadhall	textual critic	"This lot seem straight out of a saga. But, as yet, there aren't any skalds in evidence."
meadhall	warrior	"There are a lot of big, hard-bitten bastards in here. In a pinch, you could take a few of them down and beat a fighting retreat. But that'd be playing their game."
Under My Roof	troublemaker	"The group seems ripe for it. But you're not into causing squabbles without some clear purpose."
ballroom	warrior	"Yes - if you think about the dance as a footwork exercise it clicks better with your brain. It still doesn't make it [i]fun[/i], though - the dance-like martial arts are very much not your style. But it's a thought worth stowing away for later."
office party	forager	"Hah. Yeah, if you were sticking around that's not a terrible idea. Food's bad enough here. But you need to get to Jave first."
gamesnight	troublemaker	"You've got enough of a sense of the rules that you [i]could[/i] play spoiler if you had to. But you're not sure you want to end this game without Jave around."
