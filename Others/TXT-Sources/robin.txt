"Robin & Orchid" by "Ryan Veeder & Emily Boegheim"



The story headline is "An exercise in photojournalism".

When play begins:
	now the right hand status line is "";
	[this rule is here for dramatic reasons, and also I couldn't think of why to put it anywhere else]
	say "Casey glances around the newspaper office. 'Orchid is gone, right?'[paragraph break]'Yes, she left. Why do you ask?'[paragraph break]'I wanted to give you this while she wasn't around.' He hands you a thick, well-leafed notebook. 'It's just kind of a guide kind of thing, with some general information and directions, just to help you get around. Don't let Orchid see it.'[paragraph break]'Are you sure I'll require so much background material? And why shouldn't Orchid see it?'[paragraph break]'Well, Robin,' Casey says, and he spins around a chair to sit on it backwards in what he surely thinks is a very casual and friendly pose, 'Orchid is a church member, and you're not, and you missed the lock-in, so you don't have a lot of the context that Orchid has. I just wanted to put you on an even footing. And for your second question, I might have written some things in there that I wouldn't have said to Orchid's face.'[paragraph break]You nod, and slip the notebook into your backpack. 'I'm sure you've adequately prepared me for my mission.'[paragraph break]'Yes, I have. You're welcome. No, wait, I've got one more thing to say. Make sure you take lots of pictures, obviously. It's your job. That's one.' He starts counting on his fingers. 'Um, it's more fun if you run off by yourself, so make sure you get Sharon off your back. That's two. And keep an open mind. I mean, just because you don't believe in ghosts doesn't mean you're not gonna see one.[paragraph break]'So, yeah. That's three things.'";
	wait for any key.
		
[SPECIAL FEATURE: ALTERNATE OPENING
When play begins:
	now the right hand status line is "";
	say "Hey, Robin! Hold up![paragraph break]I wanted to wait until Orchid wasn't around before I gave you this. It's for your investigation tonight.[paragraph break]No, it's not that I don't trust Orchid; I just trust you more. I trust you [italic type]unconditionally,[roman type] Robin. So, I'm trusting you, with these notes I wrote about the church. It's just kind of a guide kind of thing, with some general information and directions, just to help you get around. Don't let Orchid see it.[paragraph break]I wish I could go with you! You know, the no boy-girl overnight thing is the school's policy, not the church's. We've had youth group lock-ins over there with boys and girls; it's not a big deal. I invited you to one. If you remember. I think you do.[paragraph break]Yeah. It was a lot of fun, and you missed out.[paragraph break]But I guess you'll get some idea while you're locked in with Orchid. And Sharon, har har. You'll want to get [italic type]her[roman type] off your back as soon as you can.[paragraph break]What else do I want to say? Take lots of pictures. Run off by yourself when you can; it's more fun to sneak around alone.[paragraph break]And keep an open mind! Just because you don't believe in ghosts doesn't mean you're not gonna see one.";
	[wait for any key;]
]
	

[When play begins: [DEBUG]
	say "Things without notes at the moment:[line break]";
	let l be a list of objects;
	let l be the list of things;
	let rl be the list of rooms;
	add rl to l;
	repeat with item running through l:
		if the note of item is empty:
			say " - [the item]: [description of the item][line break]";
	say "[line break]".]

[Last after reading a command:
	say "(I just read a command! It was '[the player's command][quotation mark]!)[command clarification break]".]
	
	
Volume - Set Up

Part - Extensions

[So we can "wait for any key". Ryan just loves doing this.]
Include Basic Screen Effects by Emily Short.

[For the photography code.]
Include Dynamic Objects by Jesse McGrew.

[Conversation]
Include Conversation Framework by Eric Eve.

[Small conveniences]
Include Plurality by Emily Short.
Include Punctuation Removal by Emily Short.
Include Second Gender by Felix Larsson.

Part - Use Options

Use American dialect.
Use the serial comma.
Use no scoring.

[Increase the maximum word length so "photographs" doesn't get truncated and confused with "photograph". ALSO A PROBLEM IN TADS, WHAT IS THE PROBLEM WITH THESE IF AUTHORING SYSTEMS]
Use DICT_WORD_SIZE of 11.

Part - Release Options

Release along with cover art.
Release along with a website and the "Quixe" interpreter.
Release along with the source text.

Part - Metadata

[This is stuff like the version number, genre, etc. Not important to deal with for now, I'm just putting this in as a reminder.]

The release number is 4. [I wish Inform didn't insist on this being an integer. Let's use the serial number to identify beta releases and only increment the release number for actual releases.]

The story creation year is 2013.

The story description is "High school journalists spend the night in a church, investigating reports of a ghost."

Volume - Actions

[The photographing action is in the Photography volume instead, just to keep all that code together.] 

Part - Actions out of world

[about, help, credits, etc]

Abouting is an action out of world. Understand "credits" and "author" and "about" and "info" and "contact" and "help" [?] as abouting.

Carry out abouting:
	say "Ryan Veeder and Emily Boegheim began work on [italic type]Robin & Orchid[roman type] in July of 2012. Their original intention was to enter it in the 2012 Interactive Fiction Competition. Then their intention was to enter it in the 2013 Spring Thing. If you’re reading this, that means they have successfully entered it in the 2013 Interactive Fiction Competition.[paragraph break]An adaptive walkthrough has been built into the game; you can enter the command 'WALKTHROUGH' to get some directions if you're stuck. Something might also happen if you try 'HINT[quotation mark].[paragraph break]If the mood strikes you, you can email one or both of the authors at rcveeder@me.com or emily.boegheim@gmail.com[unicode 8212]or say hi on Twitter, at @rcveeder or @emilyboegheim.[paragraph break]CREDITS[paragraph break][italic type]Lead Designer/Head Writer:[roman type] Ryan Veeder[line break][italic type]Head Programmer/Lead Voice of Reason:[roman type] Emily Boegheim[paragraph break][italic type]Beta Testers:[roman type] Jonathan Blask, Colin Djukic, Amanda Lange, CEJ Pacian, Michael Wayne Phipps Jr, Zach[paragraph break][italic type]Bible quotes are from the New Revised Standard Version.[roman type][paragraph break][italic type]Special Thanks:[roman type] Ryan Veeder’s church, Emily Boegheim’s churches, Ryan Veeder’s high school, and all the jerks he graduated with[paragraph break]"

Report requesting the story file version:
	say "Plus modified versions of:[line break]Secret Doors version 1 by Andrew Owen[line break]".
	
Rule for amusing a victorious player:
	say "Have you...[paragraph break]...taken any pictures of Orchid?[line break]...tried hiding?[line break]...learned all about Gwen?[line break]...checked out the pastor's office?[line break]...read all four entries in the epic Nativity Pageant Saga?[line break]...tested Casey's favorite napping spot?[line break]...looked up Jesus in the Bible?[line break]...read all of Casey's notes? I sincerely doubt it."

Understand "load" and "load game" and "load the/a game" as restoring the game.

Section - Hints

Hinting is an action out of world.
Understand "hint" and "hints" as hinting.

Check hinting:
	if Uncovering a Hoax is not happening or no puzzles are available:
		say "Sorry, there are no hints available for this section. Keep looking around, or if you're really stuck, type WALKTHROUGH for specific instructions." instead.

Check hinting when Cat Hints is happening:
	if the cat is in the location:
		say "The cat stares at you and meows loudly.";
	otherwise:
		let dir be the best route from the location to the location of the cat, using doors;
		say "You hear an impatient 'mreow' from [if the dir is down]downstairs[otherwise if the dir is up]upstairs[otherwise][the dir][end if].";
	stop the action.

Carry out hinting:
	now the time since progress counter is the time to hints;
	consider the scene changing rules;
	if the Cat Hints is not happening:
		say "Sorry, the hints cannot reach you here."

Section - Walkthrough

Requesting the walkthrough is an action out of world.
Understand "walkthrough" and "walkthru" and "walk through" and "walk thru" and "walk-through" and "walk-thru" and "solution" as requesting the walkthrough.

Putatively standing on is a truth state that varies.

Walkthrough explained is a truth state that varies. Walkthrough explained is false.

Setting action variables for requesting the walkthrough:
	if the player is standing on something:
		now putatively standing on is true;
	otherwise:
		now putatively standing on is false.

First carry out requesting the walkthrough when walkthrough explained is false:
	now walkthrough explained is true;
	say "To save you from the horrors of unwanted spoilers, we've implemented an adaptive walkthrough system which only shows you the walkthrough for the section of the game that you're up to. If there's more than one puzzle you could be stuck on, we'll ask you which you want a walkthrough for. Once you've completed that section you can type in WALKTHROUGH again for a further set of instructions, though hopefully you won't need to![paragraph break]Oh hey, and don't feel bad about asking for help! The walkthrough only covers the main parts of the game, so there's still plenty of optional stuff for you to discover on your own."

The walkthrough-loc is a room that varies.

Carry out requesting the walkthrough during Tutorial:
	[Orchid's monologue]
	if Sharon is not in the Nursery:
		say "Orchid has some stuff to say, so just look around and read Casey's notes for a bit while she talks at you, or:[paragraph break]";
		repeat with x running from 0 to the number of filled rows in the Table of Orchid's Bossiness:
			say "WAIT. ";
		say paragraph break;
		stop the action;
	[finding the blankets for Sharon]
	otherwise if Sharon is in the nursery and Sharon is awake and Sharon does not carry the blankets:
		say "Sharon wants you to get some blankets for her. ";
		if the blankets are not seen and the blankets are not discovered:
			say "[paragraph break]First you've got to find the blankets. You could hunt through the room for them yourself, but there's an easier way:[paragraph break]LOOK UP BLANKETS IN CASEY'S NOTES. ";
		if the blankets are in the nursery cupboards and (the player is not on the recliner or the posture of the player is not standing):
			say "[paragraph break]You're not tall enough to reach the blankets, so you'll have to[paragraph break]STAND ON THE RECLINER.";
		otherwise:
			say paragraph break;
		if the player does not carry the blankets:
			say "[if the blankets are in the nursery cupboards and the cupboards are closed]OPEN THE CUPBOARD. [end if]GET THE BLANKETS.";
		otherwise:
			say "You've already found the blankets, so just[paragraph break]";
		say "[if the player is on the recliner or the blankets are in the nursery cupboards]GET OFF THE RECLINER. [end if]GIVE THE BLANKETS TO SHARON.";
		stop the action;
	[the end of Sharon's monologue, if the player somehow finishes the blanket puzzle before the end of her monologue and then requests a walkthrough - which NO ONE WILL EVER DO, BECAUSE WHAT ON EARTH]
	otherwise if Sharon is in the Nursery and Sharon carries the blankets and the number of filled rows in the Table of Sharon's Chatter > 0:
		say "You'll have to let Sharon finish her spiel.[paragraph break]";
		repeat with x running from 1 to the number of filled rows in the Table of Sharon's Chatter:
			say "WAIT. ";
		say paragraph break;
		stop the action;
	[the spooky face at the window]
	otherwise if Tutorial is happening and Blanket Quest has ended:
		if the cat is in the nursery window:
			say "Hey, a spooky face at the window![paragraph break]TAKE A PHOTO OF THE FACE.[paragraph break]Then[run paragraph on]";
		otherwise:
			say "Now[run paragraph on]";
		say " you just have to wait for Orchid to finish making her plans:[paragraph break]";
		if Ghost Photography 101 has ended and the time since Ghost Photography 101 ended is 2 minutes:
			say "WAIT.";
		otherwise:
			say "WAIT. WAIT.";
		stop the action.

To give directions from (loc - a room) to (dest - a room):
	if putatively standing on is true:
		say "GET OFF [the holder of the player].[line break]" in upper case;
		now putatively standing on is false;
	let step be 0;
	let travel time be the number of moves from loc to dest, using doors;
	while loc is not dest:
		let dir be an object;
		if dest is an obvious room and loc is an obvious room:
			let dir be the best route from the loc to the dest through obvious rooms, using doors;
		otherwise:
			let dir be the best route from the loc to the dest, using doors;
		if dir is nothing:
			stop;
		increment step;
		say "[dir][run paragraph on]" in upper case;
		[insert a line break after every six directions, because the game has trouble processing command lines with a lot of commands on them]
		if step < travel time and the remainder after dividing step by 6 is 0:
			say ".[line break]";
		otherwise:
			say ". [run paragraph on]";
		let loc be the room dir from loc.

To give directions to (dest - a room) from (loc - a room):
	give directions from loc to dest.

Carry out requesting the walkthrough during Ghost Hunt:
	[flickering lights]
	if the flickering lights are on-stage:
		if the flickering lights are not in the location:
			say "Time to explore the basement. From here, you should head:[paragraph break]";
			give directions from the location to Hall1;
			say paragraph break;
		if the player does not have evidence of the flickering lights:
			say "Those lights look kind of ghostly. Better get some evidence of them, and take a closer look.[paragraph break]";
			say "TAKE A PHOTO OF THE LIGHTS.[line break]";
		otherwise:
			say "Maybe you should take a closer look at those spooky lights.[paragraph break]";
		if the player is in a hallway:
			give directions from the location to Hall4;
		otherwise:
			give directions from Hall1 to Hall4;
		say paragraph break;
		stop the action;
	[ectoplasm]
	otherwise if the flickering lights are off-stage and the ghostly figure is off-stage and the player does not have evidence of a random ectoplasm:
		[if you've already triggered the ghostly figure encounter without getting evidence of ectoplasm, too bad. you don't get a walkthrough for the ectoplasm]
		now the walkthrough-loc is the location;
		if the player is in Hall4 and the player does not carry ectoplasm and no ectoplasm is handled:
			say "Time to head back to the light.[paragraph break]SOUTH.[paragraph break]";
		otherwise if the player does not carry ectoplasm:
			say "You should have another look at that goop you found in the basement hallway.[paragraph break]";
			if the location is not Hall4:
				give directions from the location to Hall4;
				now the walkthrough-loc is Hall4;
				say line break;
			say "TOUCH WALL.[paragraph break]";
		if the walkthrough-loc is a dark room or the walkthrough-loc is Hall3 or the walkthrough-loc is Hall2:
			say "You'll need more light to get a proper look at this gunk on your hand.[paragraph break]";
			give directions from the walkthrough-loc to Hall1;
			say paragraph break;
		say "LOOK AT THE SLIME.[paragraph break]Ectoplasm, huh? Better get a photo of that.[paragraph break]TAKE A PHOTO OF THE ECTOPLASM.";
		stop the action;
	[ghostly figure]
	otherwise if the ghostly figure is off-stage:
		if the ghostly figure is off-stage:
			say "It's time to tell Orchid what you've found down here.[paragraph break]";
			if the location is not the sanctuary:
				give directions from the location to the Sanctuary;
				say line break;
			say "SAY YES.[paragraph break]";
		stop the action;
	otherwise:
		say "An actual ghost![run paragraph on]";
		if the player can see the ghostly figure and the player does not have evidence of the ghostly figure:
			say "[paragraph break]TAKE A PHOTO OF THE GHOSTLY FIGURE. [run paragraph on]";
		if (the ghostly figure is in the sanctuary for the second turn and the player does not have evidence of the ghostly figure) or the ghostly figure is in the sanctuary for the third turn:
			say "[paragraph break]LOOK AT THE GHOSTLY FIGURE.[paragraph break]";
		otherwise:
			say paragraph break;
		say "You'd better follow it, see if you can find out anything else.[paragraph break]";
		give directions from the location to the Back Room;
		say paragraph break;
		stop the action.

Carry out requesting the walkthrough during Uncovering a Hoax:
	[sliding door]
	if the sliding door is not comprehended:
		follow the opening the sliding door walkthrough rule;
		stop the action;
	[fuse box]
	if the west basement switch is switched off:
		follow the lighting the basement walkthrough rule;
		stop the action;
	[lighting rig]
	if the black sphere is not comprehended or the player does not have evidence of the black sphere:
		follow the lighting rig walkthrough rule;
		stop the action;
	[ALL THE OTHER HOAX EVIDENCE PUZZLES]
	let chosen puzzle be the closest puzzle;
	if chosen puzzle is not a puzzle:
		continue the action; [whoops, bug]
	if more than one puzzle is available:
		say "There are a few things you could be investigating at this point, but let's focus on [the non-spoilery name of the chosen puzzle].[paragraph break]";
	if the chosen puzzle is Scary Male Bathroom:
		follow the men's bathroom walkthrough rule;
		stop the action;
	otherwise if the chosen puzzle is Towers of OCD:
		follow the towers of ocd walkthrough rule;
		stop the action;
	otherwise if the chosen puzzle is Flying Rig:
		follow the Engelgurt walkthrough rule;
		stop the action;
	otherwise if the chosen puzzle is Final Confrontation:
		follow the trapdoor walkthrough rule;
		stop the action;
	[otherwise: bug]

To decide which object is the closest puzzle:
	if exactly one walkthroughable puzzle (called the-one) is available:
		decide on the-one;
	let l be the list of walkthroughable available puzzles;
	let nearest puzzle be nothing;
	let nearest distance be 99;
	repeat with p running through l:
		if the location encloses the linked evidence of p:
			decide on p;
		let new distance be the number of moves from the location to the location of the linked evidence of p, using doors;
		if new distance is less than nearest distance:
			now the nearest distance is the new distance;
			now the nearest puzzle is p;
	decide on the nearest puzzle.

This is the opening the sliding door walkthrough rule:
	if the location is not the back room or the sliding door is not revealed:
		say "Remember how the ghost vanished through the wall in the back room? You should take a closer look at that.[paragraph break]";
	if the location is not the Back Room:
		give directions from the location to the back room;
		say paragraph break;
	if the sliding door is not revealed:
		say "LOOK AT THE BACK WALL. LOOK AT THE PANEL.[paragraph break]";
	say "You could mess around figuring out how the jutting-out panel works yourself, but it's easier to just:[paragraph break]LOOK UP THE PANEL IN CASEY'S NOTES. OPEN THE PANEL. DOWN.[paragraph break]"

This is the lighting the basement walkthrough rule:
	say "You need to do something about the dark rooms in the basement.[paragraph break]";
	if the location is not the tunnel:
		say "First head down to the tunnel:[paragraph break]";
		give directions from the location to the tunnel;
		say paragraph break;
	say "Aha, a fuse box![paragraph break][if the fuse box is closed]OPEN THE FUSE BOX. [end if]LOOK AT THE FUSE BOX. SWITCH THE WEST BASEMENT SWITCH ON."

This is the lighting rig walkthrough rule:
	say "You should take [if the black sphere is comprehended]another[otherwise]a closer[end if] look at the weird collection of items you found in the tunnel.[paragraph break]";
	if the location is not the Tunnel:
		give directions from the location to the Tunnel;
		say line break;
	if the black sphere is not comprehended:
		say "LOOK AT THE BLACK SPHERE. LOOK AT THE MIRROR. LOOK AT THE BLACK CLOTH.[paragraph break]There's a button on the black sphere. Let's find out what it does.[paragraph break]PUSH THE BUTTON.[paragraph break]";
	otherwise if the black sphere is not lit:
		say "TURN THE PARTY LIGHT ON.[paragraph break]";
	otherwise:
		say "LOOK AT THE PARTY LIGHT.[paragraph break]";
	say "You've seen a blue light somewhere before...[paragraph break]TAKE A PHOTO OF THE PARTY LIGHT.";

This is the men's bathroom walkthrough rule:
	if the location is not Mens:
		if the location is not Main Floor Stairway:
			say "To get to the men's restroom:[paragraph break]";
		if Mens is visited:
			give directions from the location to Mens;
			say paragraph break;
		otherwise if the location is not Main Floor Stairway:
			give directions from the location to Main Floor Stairway;
			say paragraph break;
		if steeling <= 2:
			say "You're feeling a little uncomfortable about entering the men's room, seeing as you're not a man, but you just have to push through that.[paragraph break]";
			let tries be 3 - steeling;
			repeat with x running from 1 to tries:
				say "NORTHEAST. ";
			say paragraph break;
	say "There's something interesting in the garbage can.[paragraph break]LOOK AT THE GARBAGE CAN.[line break]LOOK IN THE GARBAGE CAN.[line break]LOOK AT THE GLOWING TISSUE.[line break]TAKE A PHOTO OF THE TISSUE.[line break]TAKE THE TISSUE."

This is the Towers of OCD walkthrough rule:
	now the walkthrough-loc is the location;
	[try to figure out the optimal solution to this dang puzzle]
	let the second step be the height 2 step;
	let the fourth step be the height 4 step;
	let the bottom ark be nothing;
	if the fourth step is nothing or the second step is nothing:
		if no ark is in Storage East and the player encloses an ark:
			now the bottom ark is a random ark enclosed by the player;
			now the fourth step is a random ark not enclosed by the player;
		otherwise if exactly one ark is in Storage East:
			now the bottom ark is a random ark in Storage East;
			now the fourth step is a random ark not in Storage East;
		otherwise if exactly one ark is enclosed by Storage East:
			now the bottom ark is a random ark not enclosed by Storage East;
			now the fourth step is a random ark enclosed by Storage East;
		otherwise if an ark (called a1) is on an ark (called a2):
			now the bottom ark is a1;
			now the fourth step is a2;
		otherwise if no stepper is on Noah's ark and (the player does not carry the ark of the covenant or the location is not Storage East or Noah's Ark is not in Storage East):
			now the fourth step is Noah's ark;
			now the bottom ark is the Ark of the Covenant;
		otherwise:
			now the fourth step is the Ark of the Covenant;
			now the bottom ark is Noah's ark;
		now the second step is the amplifier;
	[say "The bottom ark is [the bottom ark]. The fourth step is [the fourth step].";]
	now every non-stepper thing on a stepper is not notionally cleared;
	now every ark is not notionally carried;
	if the player carries an ark (called bother):
		now the bother is notionally carried;
	[summarise the puzzle]
	if the total height of the holder of the player < 4:
		say "Your aim here is to get up close enough to the glistening container to take a look at it.[paragraph break]";
	[get the player to the right place if necessary]
	if the walkthrough-loc is not Storage East and all arks are enclosed by Storage East:
		say "You'll need to go down to the eastern storage room:[paragraph break]";
		give directions from the walkthrough-loc to Storage East;
		now the walkthrough-loc is Storage East;
		say paragraph break;
	[if the player is on something, get off it]
	if the location is Storage East and the player is on something:
		if the steppers need clearing or the steppers need moving:
			say "You need to shift some stuff around, so you'll have to[paragraph break]";
			say "GET OFF [the holder of the player].[paragraph break]" in upper case;
	[if the player is carrying the handbells case, get rid of it]
	if the player carries the case labelled handbells:
		say "[The case labelled handbells] is pretty bulky, and you'll need your hands free for all this, so let's get it out of your way.[paragraph break]";
		if the walkthrough-loc is not Storage East:
			give directions from the walkthrough-loc to Storage East;
			now the walkthrough-loc is Storage East;
		say "PUT THE CASE LABELLED HANDBELLS ON THE LOWEST SHELF.[paragraph break]";
		now the case labelled handbells is notionally cleared;
	[if not all the arks are in Storage East, direct the player to fetch them]
	if the steppers need moving:
		if (the bottom ark is a thing and the bottom ark is not enclosed by Storage East and the player does not carry the bottom ark) or the fourth step is not enclosed by Storage East:
			say "You need to collect a thing or two first and then head to the eastern storage room.[paragraph break]";
		if the bottom ark is a thing and the bottom ark is not in Storage East:
			if Storage East does not enclose the bottom ark:
				if the player does not carry the bottom ark:
					give directions from the walkthrough-loc to the location of the bottom ark;
					now the walkthrough-loc is the location of the bottom ark;
					clear and take the bottom ark;
				give directions from the location of the bottom ark to Storage East;
				now the walkthrough-loc is Storage East;
			otherwise if the player does not carry the bottom ark:
				clear and take the bottom ark;
			say "PUT [the bottom ark] ON THE FLOOR.[line break]" in upper case;
			now the bottom ark is not notionally carried;
		if the fourth step is not in Storage East:
			if Storage East does not enclose the fourth step:
				if the player does not carry the fourth step:
					give directions from the walkthrough-loc to the location of the fourth step;
					now the walkthrough-loc is the location of the fourth step;
					clear and take the fourth step;
				give directions from the location of the fourth step to Storage East;
				now the walkthrough-loc is Storage East;
			otherwise if the player does not carry the fourth step and something is on the fourth step:
				clear the fourth step;
				say line break;
		if (the bottom ark is a thing and the bottom ark is not in Storage East and the player does not carry the bottom ark) or the fourth step is not enclosed by Storage East:
			say paragraph break;
	[remove any stuff that's sitting on the steppers]
	if the steppers need clearing:
		let l be the list of encumbering things;
		say "You'll need to move [if the number of entries in l is 1]something[otherwise]some things[end if] out of your way first.[paragraph break]";
		repeat with encumbrance running through l:
			if the encumbrance is the case labelled handbells:
				if an ark (called bother) is notionally carried:
					say "PUT [the bother] ON THE FLOOR. " in upper case;
					now the bother is not notionally carried;
				say "TAKE THE CASE LABELLED HANDBELLS. ";
			otherwise:
				say "TAKE [the encumbrance]. " in upper case;
		say paragraph break;
	if the case labelled handbells is encumbering:
		say "You can't climb while holding the case labelled handbells[em dash]it's too bulky[em dash]so you'll have to get it out of the way.[paragraph break]PUT THE CASE LABELLED HANDBELLS ON THE LOWEST SHELF.[paragraph break]";
	[put the steppers in place]
	if the steppers need moving:
		say "The way the stuff in this room is currently arranged, you can't get high enough to see the top shelf properly.[paragraph break]";
		say "PUT [the fourth step] ON [the bottom ark].[paragraph break]" in upper case;
	[climb the steppers]
	let putative height be 1;
	if the player is on a stepper and the location is Storage East and the steppers don't need moving:
		now the putative height is the total height of the holder of the player;
	if the putative height < 4:
		say "From here, you just have to [if the putative height < 4]climb up and [end if]see what that container is.[paragraph break]";
	if the putative height < 2:
		say "CLIMB [the second step]. " in upper case;
	if the putative height < 3:
		say "CLIMB THE CABINET. ";
	if the putative height < 4:
		say "CLIMB [the fourth step].[paragraph break]" in upper case;
	[take a photo of the can of goo]
	say "You still can't reach the container (and you won't be able to), but you can get a good look at it now.[paragraph break]LOOK AT THE GLISTENING CONTAINER. TAKE A PHOTO OF THE GLISTENING CONTAINER."

To decide which object is the height (h - a number) step:
	let chosen step be nothing;
	repeat with step running through steppers enclosed by Storage East:
		[say "Considering [the step]: its total height is [the total height of the step], and on it [is-are the list of things on the step].[line break]";]
		if the total height of the step is h and no stepper is on the step:
			now the chosen step is the step;
			break;
	decide on the chosen step.

A thing can be notionally cleared.
A thing is usually not notionally cleared.
A thing can be notionally carried.
A thing is usually not notionally carried.

To clear and take (step - a stepper):
	clear the step;
	say "TAKE [the step].[line break]" in upper case;
	now the step is notionally carried.

To clear (step - a stepper):
	repeat with item running through not notionally cleared things on the step:
		if the item is the case labelled handbells:
			say "TAKE THE CASE LABELLED HANDBELLS.[paragraph break]You can't climb or move stuff around while holding [the case labelled handbells][em dash]it's too bulky[em dash]so you'll have to get it out of the way.[paragraph break]";
			if the walkthrough-loc is not Storage East:
				give directions from the walkthrough-loc to Storage East;
				say line break;
			say "PUT THE CASE LABELLED HANDBELLS ON THE LOWEST SHELF.";
			if the walkthrough-loc is not Storage East:
				say line break;
				give directions from the Storage East to the walkthrough-loc;
				say line break;
		otherwise:
			say "TAKE [the item]. " in upper case;
		now the item is notionally cleared;
	now the step is notionally cleared.

Definition: a thing is non-stepper if it is not a stepper.
Definition: a thing is encumbering if it is not a stepper and it is not the can of goo and it is not the player and it is on a stepper and it is not notionally cleared and it is enclosed by Storage East.

To decide if the steppers need clearing:
	if something is encumbering:
		decide yes;
	decide no.

To decide if the steppers need moving:
	if the height 2 step is nothing or the height 4 step is nothing:
		decide yes;
	decide no.

To decide if the steppers don't need moving:
	if the steppers need moving:
		decide no;
	decide yes.
		
This is the Engelgurt walkthrough rule:
	say "Let's figure out what that heap of bars and cables[if the location is not the balcony] up in the balcony[end if] is.[paragraph break]";
	if the Engelgurt is not noted:
		say "The information you need [if the balcony encloses the crook]wasn't always[otherwise]isn't[end if] in the balcony, though[if the location does not enclose the crook]. You need to look elsewhere for that[end if].[paragraph break]";
		if the location is not the location of the crook:
			give directions from the location to the location of the crook;
			say line break;
		say "LOOK AT THE CROOK. LOOK UP THE CROOK IN THE NOTES.[paragraph break]There's more to that story, but we're only interested in that weird German-sounding word right now.[paragraph break]LOOK UP THE ENGELGURT IN THE NOTES.[paragraph break]";
		if the Engelgurt is seen:
			say "It's not [if the location is Storage West]here[otherwise]in the store room[end if], but you've seen something like that before...[paragraph break]";
		otherwise:
			say "It's not [if the location is Storage West]here[otherwise]in the store room[end if], though.[paragraph break]";
		if a photo shows the Engelgurt:
			say "LOOK AT THE PHOTO OF THE TANGLE OF BARS.";
		otherwise:
			if the location does not enclose the crook:
				give directions from the location of the crook to the Balcony;
				say line break;
			say "LOOK AT THE HEAP OF BARS AND CABLES. TAKE A PHOTO OF THE HEAP.";
	otherwise:
		say "Remember this?[paragraph break]LOOK UP THE ENGELGURT IN THE NOTES.[paragraph break]It isn't in the storage room where it's supposed to be, though. [run paragraph on]";
		if a photo shows the Engelgurt and the location is not the Balcony:
			say "It's actually back up in the Balcony.[paragraph break]LOOK AT THE PHOTO OF THE BARS AND CABLES.";
		otherwise:
			if the location is not the Balcony:
				say "It's actually in the Balcony.[paragraph break]";
				give directions from the location to the Balcony;
				say line break;
			otherwise:
				say "It's actually right here.[paragraph break]";
			say "LOOK AT THE HEAP OF BARS AND CABLES[if no photo shows the engelgurt]. TAKE A PHOTO OF THE HEAP[end if].";

This is the trapdoor walkthrough rule:
	say "It's time to sort out the trapdoor[if the location is not the Balcony] in the balcony[end if]. ";
	if the trapdoor is not open:
		if the player does not carry the crook or (the player does not carry an enterable supporter and no portable enterable supporter is in Balcony):
			say "The trapdoor's pretty high up, so you're going to need something to stand on [italic type]and[roman type] something to hook the pull-cord with. ";
		say paragraph break;
		now the walkthrough-loc is the location;
		let chosen supporter be nothing;
		if a portable enterable supporter (called item) is in Balcony:
			now the chosen supporter is the item;
		otherwise if the player carries an enterable supporter (called item):
			now the chosen supporter is the item;
		if the crook is in the location:
			explain getting the crook;
		if the player does not carry an enterable supporter and the chosen supporter is nothing:
			say "There are quite a few things in the church that you can stand on, but let's get one of the arks from the storage room.[paragraph break]";
			if an ark (called possibility) encloses no stepper:
				now the chosen supporter is the possibility;
			otherwise:
				now the chosen supporter is Noah's Ark;
			if the walkthrough-loc is not the location of the chosen supporter:
				give directions from the location to the location of the chosen supporter;
				now the walkthrough-loc is the location of the chosen supporter;
				say line break;
			if something is on the chosen supporter:
				repeat with widget running through the things on the chosen supporter:
					say "TAKE [the widget]. " in upper case;
				say line break;
			say "TAKE [the chosen supporter].[paragraph break]" in upper case;
		if the player does not carry the shepherd's crook and the crook is not in the location:
			explain getting the crook;
		if the trapdoor is closed:
			say "You've got what you need to open the trapdoor, so let's do it.[paragraph break]";
		if the walkthrough-loc is not the Balcony:
			give directions from the walkthrough-loc to the Balcony;
			say line break;
		if the chosen supporter is not in Balcony:
			say "DROP [the chosen supporter]. " in upper case;
		otherwise if the chosen supporter is a flippable supporter and the chosen supporter is right way up:
			say "TURN [the chosen supporter]. " in upper case;
		if the player is not on the chosen supporter or the posture of the player is not standing:
			say "STAND ON [the chosen supporter].[line break]" in upper case;
		say "OPEN THE TRAPDOOR WITH THE CROOK. ";
	otherwise:
		say "But the trapdoor is already open! All you have to do is go[paragraph break]";
	say "UP."

To explain getting the crook:
	say "For a hook, you'll need the shepherd's crook from the nativity play.[paragraph break]";
	if the walkthrough-loc is not the location of the crook:
		give directions from the walkthrough-loc to the location of the crook;
		now the walkthrough-loc is the location of the crook;
		say line break;
	say "TAKE THE CROOK.[paragraph break]".

Carry out requesting the walkthrough when Confronting Aiden is happening:
	say "Sharon needs to know what's been going on here.[paragraph break]";
	if the location is not the Nursery:
		give directions from the location to the nursery;
		say line break;
	say "WAKE SHARON.";
	stop the action.

Last carry out requesting the walkthrough:
	say "Sorry, there is no walkthrough available for this part of the game."
	[THIS SHOULD NEVER HAPPEN]

Section - Examining etc

Understand "examine [things]" as examining.

[Understand "examine/x/check/describe/look/read/watch" or "look at" as "[examine]".]

This is the reject examine commands with too many objects rule:
	if the player's command includes " all " or the player's command includes " everything ": 
		let comm be indexed text;
		let comm be the player's command;
		if comm matches the regular expression "^(examine|x|check|describe|look|read|watch|look at) ":
			say "Sorry, that command would include too many objects. Please be more specific about what you want to look at.";
			reject the player's command.

The reject examine commands with too many objects rule is listed before the generate action rule in the turn sequence rules.

Understand "look around" as looking.

Understand "examine in/inside [something]" as searching.

Understand "examine under [something]" as looking under.

[These two rules were removed to improve performance:]

[Understand "staring" as a person photo when the action taken of the item described is examining.]

[Understand "looking" and "looking through" as a person photo when the action taken of the item described is searching.]

Understand "look for [text]" and "search for [text]" and "find [text]" as a mistake ("Sorry, you'll have to be more specific about where you want to search.").

[Tasting]

Understand "lick [something]" as tasting.

Instead of tasting something, say "You'd prefer to keep your tongue away from [the noun]."

Section - Querying (to replace consulting)

Querying it about is an action applying to one thing and one visible thing and requiring light.

[We need to specify that the player can also query about rooms.]

Understand "look up [any room] in [something]" as querying it about (with nouns reversed).
Understand "look [any room] up in [something]" as querying it about (with nouns reversed).
Understand "consult [something] about [any room]" as querying it about.
Understand "read about [any room] in [something]" as querying it about (with nouns reversed).
Understand "read [something] about [any room]" as querying it about.
Understand "look at [something] about [any room]" as querying it about.

Understand "look up [any thing] in [something]" as querying it about (with nouns reversed).
Understand "look [any thing] up in [something]" as querying it about (with nouns reversed).
Understand "consult [something] about [any thing]" as querying it about.
Understand "read about [any thing] in [something]" as querying it about (with nouns reversed).
Understand "read [something] about [any thing]" as querying it about.
Understand "look at [something] about [any thing]" as querying it about.

[NB: the order of these lines matters: a choice of room vs. thing is decided by which understand line is matched first, not by "does the player mean" rules. If problems arise, reconsider removing the [any room] lines and giving each room a dummy object instead. See chat log 22/8/13 for more info.]

[Understand "look up [text] in [text]" or "look [text] up in [text]" or "read about [text] in [text]" as a mistake ("You can't see any such thing.").] [doesn't work, either because I7 is ordering the understand lines non-optimally or because it's got two text tokens in it, I dunno]
Understand "look up [any thing] in [text]" or "look [any thing] up in [text]" or "read about [any thing] in [text]" as a mistake ("You can't see any such thing.").

Querying vaguely about is an action applying to one visible thing and requiring light.

Understand "look up [any room]" as querying vaguely about.
Understand "look [any room] up" as querying vaguely about.
Understand "read about [any room]" as querying vaguely about.

Understand "look up [any thing]" as querying vaguely about.
Understand "look [any thing] up" as querying vaguely about.
Understand "read about [any thing]" as querying vaguely about.

Instead of querying vaguely about:
	say "(in [Casey's notes])[command clarification break]";
	try querying Casey's notes about the noun.

Understand "look [text] up in [something]" as consulting it about (with nouns reversed).
Understand "read [something] about [text]" as consulting it about.
Understand "look at [something] about [text]" as consulting it about.

Consulting vaguely about is an action applying to one topic.

Understand "look up [text]" as consulting vaguely about.
Understand "look [text] up" as consulting vaguely about.
Understand "read about [text]" as consulting vaguely about.

Instead of consulting vaguely about:
	say "(looking up '[the topic understood]' in [Casey's notes])[command clarification break]";
	try consulting Casey's notes about the topic understood.

A thing has some text called the note.
A room has some text called the note.

A thing can be noted.
A room can be noted.

A thing can be generically-noted or specially-noted. A thing is usually generically-noted.
A room can be generically-noted or specially-noted. A room is usually generically-noted.

Does the player mean querying the notes about:
	it is very likely.

Does the player mean querying about the location:
	it is likely.

Does the player mean querying the notes about the location:
	it is likely.

Does the player mean querying vaguely about the location:
	it is likely.

Does the player mean querying about a thing (called possibility) enclosed by the location:
	if the possibility is a photo:
		it is very unlikely;
	otherwise:
		it is likely.

Does the player mean querying vaguely about a thing (called possibility) enclosed by the location:
	if the possibility is a photo:
		it is very unlikely;
	otherwise:
		it is likely.

Does the player mean querying about an unknown thing:
	it is very unlikely.

Does the player mean querying vaguely about an unknown thing:
	it is very unlikely.

Does the player mean querying about an unvisited room:
	it is very unlikely.

Does the player mean querying vaguely about an unvisited room:
	it is very unlikely.

[First does the player mean querying about a photo:
	it is very unlikely.]

Check querying it about (this is the can only consult the notes rule):
	if the noun is not Casey's notes:
		say "You probably won't learn much that way." instead.
[Hm, I'm not sure about that response. You can change it if you have any better ideas.]

[We'll probably have to add some "Instead of querying" rules for any things like books that might conceivably be consultable.]

[Have CONSULT PERSON ABOUT THING remap to ASK PERSON ABOUT THING.]
Instead of consulting someone about:
	try asking the noun about the topic understood.

Instead of querying someone about something:
	try quizzing the noun about the second noun.

[If the player tries to look up something the parser doesn't understand in the notes, the consulting action takes care of it and this rule kicks in.]
Instead of consulting Casey's notes about:
	say "You riffle through the notes, but you can't find anything that seems relevant."
	[This response has to be vaguer than the one below, because we can't really tell what the player was trying to find out about.]

Check querying Casey's notes about (this is the can't find note to consult rule):
	if the note of the second noun is empty:
		say "You riffle through the notes, but it looks like Casey didn't have anything to say about [the second noun]." instead.
		[We could use the vague response here as well, but I think it's helpful for the player to know that the game understood what s/he was trying to do, it just couldn't do it.]
		
Carry out querying Casey's notes about (this is the print the appropriate note rule):
	now the second noun is noted;
	if the second noun is generically-noted:
		say "[first time][bracket]This action may cause some slight lag in the browser version of this game. We sincerely regret any inconvenience.[close bracket][paragraph break][only]You flick through the notes and read Casey's scrawly handwriting: [run paragraph on]";
	say the note of the second noun;
	say line break.

[If we want something else to happen when Robin reads a note - e.g. we want to record that she now knows a specific piece of info - we can add a "Carry out querying Casey's notes about the whatever" rule to handle that. As I am just about to do for the Black Light Make-Out Room.]

[Fix for problems parsing abbreviated version of LOOK WHATEVER [UP]

thanks to severedhand and zarf for their help http://www.intfiction.org/forum/viewtopic.php?f=7&t=8995]

Not-really-looking is an action applying to one visible thing.

Report not-really-looking:
	say "You can't see any such thing."

[*DEL]
Understand "look [any known thing]" as not-really-looking.
Understand "look at [any known thing]" as not-really-looking.
Understand "read [any known thing]" as not-really-looking.
Understand "look [any thing]" as not-really-looking.
Understand "look at [any thing]" as not-really-looking.
Understand "read [any thing]" as not-really-looking.

Does the player mean not-really-looking a faux thing:
	it is very likely.

Rule for printing a parser error when the latest parser error is the noun did not make sense in that context error:
	let the pc be indexed text;
	let the pc be the player's command;
	if the pc matches the regular expression "^(look|l) up":
		say "I only understood you as far as wanting to look up.";
	otherwise if the pc matches the regular expression "^(look|l) at":
		say "You can't see any such thing.";
	otherwise if the pc matches the regular expression "^(look|l) (in|into|inside|through|under)":
		say "You can't see any such thing.";
	otherwise if the pc matches the regular expression "^(look|l) ":
		say "I only understood you as far as wanting to look.";
	otherwise if the pc matches the regular expression "^read about":
		say "I only understood you as far as wanting to read about.";
	otherwise if the pc matches the regular expression "^read ":
		say "I only understood you as far as wanting to read.";
	otherwise:
		say "You aren't aware of any such thing."

Section - Talking To

Talking to is an action applying to one visible thing.
Understand the command "talk" as something new. [Because Conversation Framework defines TALK TO as a synonym for SAY HELLO TO by default.]
Understand "talk to/with [something]" and "speak to/with [something]" and "chat to/with [something]" as talking to.

Does the player mean talking to a person:
	it is very likely.

Check talking to when the noun is not a person:
	say "[The noun] wouldn't have much to say to you." instead.

Report talking to:
	say "[The noun] ignores you."
[This is a terribly dull default message, but ideally each NPC will have their own set of responses in all situations and this one will never show up. I'm putting it in as a sort of failsafe, though.]

Talking vaguely is an action applying to nothing.

[Removed to improve performance:]
[Understand "talking" as a person photo when the action taken of the item described is the action of talking vaguely.]

[Conversation Framework has a concept of "speaking" which is much the same thing as chatty behavior, but I'm nervous about meddling with it in case I break something.]
Talking to is chatty behavior.
Asking something about is chatty behavior.
Telling something about is chatty behavior.
Asking something for is chatty behavior.
Answering something that is chatty behavior.
Requesting something for is chatty behavior.
Imploring something for is chatty behavior.
Quizzing something about is chatty behavior.
Informing something about is chatty behavior.
Saying hello to is chatty behavior.
Saying goodbye to is chatty behavior.
Saying yes is chatty behavior.
Saying no is chatty behavior.
Saying sorry is chatty behavior.
Talking vaguely is chatty behavior.
Giving something to is chatty behavior.
Showing something to is chatty behavior.
Hailing is chatty behavior.

The block saying no rule is not listed in any rulebook.
The block saying yes rule is not listed in any rulebook.
The block saying sorry rule is not listed in any rulebook.

Report saying no:
	say "That was a rhetorical question."

Report saying yes:
	say "That was a rhetorical question."

Report saying sorry:
	if the current interlocutor is a person and the current interlocutor is awake:
		say "[The current interlocutor] just gives you a puzzled look, but you feel a little better.";
	otherwise:
		say "No one responds to your apology, but you feel a little better."

The say goodbye when moving rule is not listed in any rulebook.

Understand "say [any thing] to [someone]" as quizzing it about (with nouns reversed).
[Understand "say [any thing]" as quizzing it about (with nouns reversed).]

[Fixes to Conversation Framework, because its behaviour is a bit weird w/r/t the current interlocutor sometimes.]

Before implicit-conversing when the current interlocutor is nothing or the current interlocutor is not visible (this is the new implicit-conversing needs current interlocutor rule):
	let np be the number of visible people who are not the player;
	if np is 0,  say "There's no one here to talk to.";
	if np > 1, say "You need to specify who you want to speak to.";
	if np is not 1, stop the action;
	let the interlocutor be a random visible person who is not the player;
		 if the interlocutor is a person begin;
			   say "(addressing [the interlocutor])";
			   let sn be the second noun;
			   implicitly greet the interlocutor;
			now the current interlocutor is the interlocutor;
			   now the second noun is sn;
	 otherwise ;
		say "You're not talking to anyone.";
		stop the action;
	end if.

The new implicit-conversing needs current interlocutor rule is listed instead of the implicit-conversing needs current interlocutor rule in the before rules.

Before implicit-conversing when the current interlocutor is not visible and the current interlocutor is not nothing  (this is the new can't converse with absent interlocutor rule):
	let np be the number of visible people who are not the player;
	if np is 0:
		say "[The current interlocutor] isn't here.";
		reset the interlocutor instead.

The new can't converse with absent interlocutor rule is listed instead of the can't converse with absent interlocutor rule in the before rules.

Last after imploring for:
	say "[The noun] do[es]n't have any such thing."

Section - Entering/Sitting/Standing/Getting In/On/Out Of/Off Things

Posture is a kind of value. The postures are standing and sitting.

A person has a posture. The posture of a person is usually standing.

A container has a posture called the usual posture. The usual posture of a container is usually standing.
A supporter has a posture called the usual posture. The usual posture of a supporter is usually standing.

The entering action has a posture called the chosen posture.

Setting action variables for entering:
	if specifically sitting:
		now the chosen posture is sitting;
	otherwise if specifically standing:
		now the chosen posture is standing;
	otherwise if the noun is enterable:
		now the chosen posture is the usual posture of the noun;
	otherwise:
		now the chosen posture is standing.
	
[I'm making these text matches quite specific, in case we implement a sitar or a music stand or something that might give us false matches.]		
To decide if specifically sitting:
	if the player's command includes "sit in" or the player's command includes "sit on":
		decide yes;
	decide no.

To decide if specifically standing:
	if the player's command includes "stand on" or the player's command includes "step on":
		decide yes;
	decide no.

Understand "step on/onto/into [something]" as entering.
	
Check entering (this is the new can't enter what's already entered rule):
	if (the actor is on the noun or the actor is in the noun) and the posture of the actor is the chosen posture:
		say "You're already [chosen posture] [if the noun is a container]in[otherwise]on[end if] [the noun]." instead.

The new can't enter what's already entered rule is listed instead of the can't enter what's already entered rule in the check entering rules.

Instead of entering something held by the actor:
	say "You can't [if the chosen posture is sitting]sit[otherwise]stand[end if] on [the noun] while you're still holding it."

Check entering (this is the new implicitly pass through other barriers rule):
	if the holder of the actor is the noun: [this fixes a problem with the postures code]
		continue the action;
	if the noun is a stepper or the noun is on a stepper: [this bit is for the Towers of OCD code]
		continue the action;
	abide by the implicitly pass through other barriers rule.

Check entering when the location is not Storage East:
	if the noun is on something (called the lower level):
		say "It seems a bit dangerous to get on [the noun] while it's on [the lower level]." instead.

The new implicitly pass through other barriers rule is listed instead of the implicitly pass through other barriers rule in the check entering rules.

Carry out entering:
	now the posture of the actor is the chosen posture.

Report entering when the posture of the actor is sitting:
	say "You sit down [if the noun is a container]in[otherwise]on[end if] [the noun].";
	rule succeeds.

Report entering when the posture of the actor is standing:
	say "You [if the previous location is the noun]stand up on[otherwise]step up onto[end if] [the noun].";
	rule succeeds.

Carry out exiting:
	now the posture of the actor is standing.

Carry out getting off:
	now the posture of the actor is standing.
	
Understand "climb on [something]" and  "climb up on [something]" and "climb up onto [something]" and "climb onto [something]" as climbing.
Understand "go up [something]" as climbing. [mostly for stairs]

Instead of climbing an enterable thing:
	try entering the noun.

Instead of going down when the player is on something or the player is in something:
	try exiting.

Before climbing down:
	try going down instead.

Instead of going up when the room up from the location is nothing and the posture of the player is sitting:
	try exiting.

Exiting is leaving.
Going outside is leaving.

Understand the command "cross" as something new.

Section - Exit listing

[totally stole most of this code from the Inform manual's example Bumping Into Walls]

Exit listing is an action applying to nothing.
Understand "exits" and "list exits" as exit listing.

Definition: a direction (called thataway) is viable if the room thataway from the location is a room and
	(the door thataway from the location is nothing or the door thataway from the location is revealed and the door thataway from the location is comprehended). 

Check going nowhere:
	try exit listing instead.

Carry out exit listing:
	let count of exits be the number of viable directions; 
	if the count of exits is 0, say "There is nowhere to go." instead; 
	if the count of exits is 1, say "From here, the only way out is [list of viable directions]."; 
	otherwise say "From here, you could head [list of viable directions]." 

Instead of exit listing in Hall4 when Hall4 is dark:
	say "The only exit you can see is south, back down the hallway."

Section - Hiding

Hiding in is an action applying to one thing.
Understand "hide in/inside/under/underneath/behind [something]" as hiding in.

A thing has some text called the hide description.
The hide description of a thing is usually "".

Check hiding in:
	if the hide description of the noun is "":
		say "[The noun] wouldn't make much of a hiding spot." instead.

Report hiding in:
	say the hide description of the noun;
	say line break.

Section - Discarding

Dropping something is discarding.
Putting something on something is discarding.
Inserting something into something is discarding.
Giving something to someone is discarding.
Throwing something at something is discarding.

Understand "put [things] on floor" or "put [things] on the floor" or "place [things] on floor" or "place [things] on the floor" as dropping.
Understand "drop [things] on floor" or "drop [things] on the floor" as dropping.

Check putting something on (this is the restricted convert put to drop rule):
	if the second noun is down:
		try dropping the noun instead.

The restricted convert put to drop rule is listed instead of the convert put to drop where possible rule in the check putting it on rules.

[Last check dropping something when Robin is on something (called the drop zone):
	let comm be indexed text;
	let comm be the player's command;
	if comm matches the regular expression "floor$":
		continue the action;
	otherwise:
		try putting the noun on the drop zone instead.]
[Ryan wanted it to work this way but I (Emily) am OVERRULING HIM because mostly Robin isn't allowed to put things on things she's standing on anyway]

Carry out dropping (this is the nonstandard dropping rule):
	now the noun is in the location.
	
The nonstandard dropping rule is listed instead of the standard dropping rule in the carry out dropping rules.

Understand "place [things] on [something]" as putting it on.
Understand "place [things] in [something]" as inserting it into.

Section - Pushing It To

[Just changing the default messages. Why is this so hard??? I feel like I must be doing something wrong]

Check an actor pushing something to (this is the can't push things to nowhere rule):
	if the room the second noun of the location is nothing:
		say "You can't go that way, and neither can [the noun]." instead.

Check an actor pushing something to the up (this is the can't push things up rule):
	say "There's no way you could get [the noun] up there." instead.

Check an actor pushing something to the down (this is the can't push things down rule):
	say "You couldn't get [the noun] down there safely." instead.

The can't push vertically rule is not listed in the check pushing it to rulebook.

The can't push things to nowhere rule is listed after the can't push to non-directions rule in the check pushing it to rulebook.
The can't push things to nowhere rule is listed before the standard pushing in directions rule in the check pushing it to rulebook.
The can't push things up rule is listed before the standard pushing in directions rule in the check pushing it to rulebook.
The can't push things down rule is listed before the standard pushing in directions rule in the check pushing it to rulebook.
The can't push things up rule is listed after the can't push things to nowhere rule in the check pushing it to rulebook.
The can't push things down rule is listed after the can't push things to nowhere rule in the check pushing it to rulebook.

Section - Washing

Washing is an action applying to one thing.

Understand the commands "clean", "scrub", "wipe" as something new.
Understand "wash [something]" and "clean [something]" and "scrub [something]" and "wipe [something]" as washing.

Check washing something:
	if the noun is not the hands:
		say "You don't have anything to clean [the noun] with." instead.
	
Section - Following

[Stole most of this code from the Inform 7 Recipe Book because I'm too lazy to figure it out myself.]

A person has an object called last location.

Understand "follow [any person]" as following.
Understand "follow [something]" as following.
Understand the commands "chase" and "pursue" as "follow".
Following is an action applying to one visible thing.

Check following: 
	if the noun is not a person, say "[The noun] [is-are]n't going anywhere." instead;
	if the noun is the player, say "You can't seem to escape yourself." instead; 
	if the location of the noun is the location, say "[The noun] is right here." instead; 
	if the last location of the noun is nothing or the noun is off-stage, say "It's not clear where [the noun] has gone." instead.

Carry out following: 
	let the destination be the location of the noun; 
	if the destination is not a room, say "[The noun] isn't anywhere you can follow." instead; 
	let aim be the best route from the location to the destination through obvious rooms; 
	say "(heading [aim])[line break]"; 
	try going aim.

To move (pawn - a person) tidily to (target - a room): 
	now the last location of the pawn is the holder of the pawn; 
	move the pawn to the target.

First carry out an actor going:
	now the last location of the actor is the location of the actor.

Section - Listening

The block listening rule is not listed in the check listening to rulebook.

Report listening:
	say "The church is silent."

Section - Sleeping

Understand "go to bed/sleep" as sleeping.

Sleeping in is an action applying to one thing.
Understand "sleep in/on [something]" as sleeping in.

Check sleeping in:
	if the noun is the sleeping bag:
		try entering the sleeping bag instead;
	if the noun is not enterable:
		say "[The noun] would make a terrible bed." instead;
	if the noun is in the Nursery:
		say "You have a perfectly good sleeping bag right here." instead;
	try sleeping instead.

Does the player mean sleeping in the sleeping bag:
	it is very likely.

Definition: a person is asleep if the last action of it is the action of sleeping.

Definition: a person is awake if it is not asleep.

Section - Opening

Understand "unroll [something]" as opening.

Opening it with is an action applying to two things.

Understand the command "open" as something new.
Understand "open [something]" as opening.

Understand "open [something] with [something]" as opening it with.
Understand "pull [something] with [something]" and "pull [something] down with [something]" as opening it with.
Understand "pry [something] with [something]" and "pry [something] open/off/out with [something]" as opening it with.
Understand "prise [something] with [something]" and "prise [something] open/off/out with [something]" as opening it with.

A thing can be crowbarish.
A thing is usually not crowbarish.

Check opening it with:
	if the noun is lockable:
		try unlocking the noun with the second noun instead.

Check opening it with:
	if the noun is not openable:
		say "That's not something you can open." instead;
	if the noun is open:
		say "[The noun] is already open." instead;
	if the noun is not the trapdoor and the noun is not the can of tuna:
		say "You don't need anything except your hands to open [the noun]." instead;
	if the second noun is not crowbarish:
		say "[The second noun] really isn't a suitable tool for opening [the noun]." instead.

Check opening something with the hands:
	try opening the noun instead.

Check opening it with:
	if the second noun is not carried by the player and the second noun is not part of the player:
		say "(first taking [the second noun])[command clarification break]";
		silently try taking the second noun;
	if the second noun is not carried by the player and the second noun is not part of the player:
		say "You'll have to be holding [the second noun] before you can do that." instead.

Carry out opening it with:
	now the noun is open.

Report opening it with:
	say "You open [the noun] with [the second noun]."
	[this can be totally boring because we're going to override it for anything that needs to be opened with another thing. I just want to make sure there's a backup if we accidentally forget to override the message for something]
	
Section - Moving Things

Understand "steal [things]" and "collect [things]" and "lift [things]" and "raise [things]" as taking.

The can't exceed carrying capacity rule is not listed in the check taking rulebook.
[because if you take a hundred photos you couldn't pick anything else up!]

Understand "turn [something] over" or "turn over [something]" as turning.
[Understand "turn [something] upside-down" or "turn [something] upside down" as turning.
Understand "put [something] upside-down" or "put [something] upside down" as turning.]
[This works without being specified, because Inform treats the "upside down" as part of the object's name regardless.]
Understand the command "flip" as "turn".

To decide if specifically turning upside down:
	let comm be indexed text;
	let comm be the player's command;
	if comm matches the regular expression "upside< \->down$":
		decide yes;
	decide no.

Understand "slide [something]" and "lean on/against [something]" as pushing.
Understand "straighten [something]" and "tidy [something]" and "neaten [something]" and "organise [something]" and "organize [something]" as pushing.

After dropping something:
	say "You place [the noun] carefully on the floor."

Vaguely putting is an action applying to one thing.
Understand "put [something]" as vaguely putting.

Check vaguely putting:
	if specifically turning upside down:
		try turning the noun instead;
	say "You'll have to be more specific about where you want to put [the noun]." instead.

Section - Hooking Things

Vaguely hooking is an action applying to one thing.
Understand "hook [something]" as vaguely hooking.

First instead of vaguely hooking something:
	if the player carries the crook:
		try hooking the noun with the crook;
	otherwise:
		say "You don't really have anything to hook that with."

Hooking it with is an action applying to two things.
Understand "hook [something] with/using [something]" as hooking it with.

First instead of hooking something with:
	if the second noun is not the crook:
		say "[The second noun] isn't really suitable for hooking things, seeing as it lacks anything much of a hook." instead;
	if the noun is not the trapdoor and the noun is not the pull-cord:
		say "There doesn't seem much point in hooking that." instead;
	try opening the noun with the second noun instead.

Does the player mean hooking something held with:
	it is unlikely.

Taking it with is an action applying to two things.
Understand "take [something] with/using [something]" and "get [something] with/using [something]" and "reach [something] with/using [something]" as taking it with.

Check taking it with:
	if the second noun is the hands:
		try taking the noun instead;
	otherwise:
		say "It'd be more convenient to just grab [the noun] with your bare hands."

Does the player mean taking something carried with:
	it is very unlikely.

Section - Miscellaneous silly verbs

[Playing - for the SNES and ping pong table]

Playing is an action applying to one thing.
Understand "play [something]" as playing.

Check playing:
	say "[The noun] [is-are]n't something you can play." [LAZY]


[knocking on]

Knocking on is an action applying to one thing.
Understand "knock on/at [something]" and "knock [something]" and "tap [something]" and "tap on [something]" as knocking on.

Instead of knocking on something:
	try attacking the noun.
	
Instead of knocking on a door:
	say "No one answers."

[Insert more specific rules for knocking on other things elsewhere.]


[kissing]

Understand "kiss [something]" as kissing.

Check kissing when the noun is not a person:
	say "[The noun] do[es] not return your affection." instead.
	
	
[eating and drinking]

Understand the command "eat" as something new.
Understand "eat [something]" as eating. [so you don't have to be holding something to (try to) eat it]

Understand "drink from [something]" and "drink out of [something]" as drinking.

Instead of drinking something:
	say "That's not at all drinkable."


[freaking out - for the (optional) photo of Orchid freaking out in tutorial]

Freaking out is an action applying to nothing.
Understand "freak out" or "panic" as freaking out.

[revisit this response. We should be able to have some fun with it, as an Easter egg for creative players.]
Report freaking out:
	say "You fall to your knees. Your heart is racing. You grab your chest and try to control your breathing.[paragraph break]Eventually, you get yourself back together."

[Removed to improve performance:]
[Understand "freaking" as a person photo when the action taken of the item described is freaking out.]


[scowling - for photos of Orchid after you've caught Aiden]

Scowling is an action applying to nothing.
Understand "scowl" and "frown" and "glower" as scowling.

Report scowling:
	say "You twist your face into a hideous scowl, but you can only hold the expression for a few seconds";
	if an awake non-player person (called bro) is in the location:
		say ". [if Orchid is in the location]Orchid[otherwise][The bro][end if] fails to notice";
	say "."

[floating]

[Mainly here so that the ghostly figure can be photographed while floating.]

Floating is an action applying to nothing.
Understand "float" and "float in the air" and "float in air" as floating. Understand the command "levitate" as "float".

Check floating:
	say "You're a bit too corporeal for that." instead.

[Removed to improve performance:]
[Understand "floating" and as a person photo when the action taken of the item described is the action of floating.]


[screaming]

Screaming is an action applying to nothing.
Understand "scream" and "shriek" as screaming.

[Removed to improve performance:]
[Understand "screaming" and "at you/me" as a person photo when the action taken of the item described is screaming.]

Instead of screaming:
	say "You let out a panicky shriek and then clasp your hands over your mouth as the sound echoes around the empty church."

Instead of screaming in the presence of the cat when the cat is in the nursery window:
	say "You let out a panicky shriek. Sharon jumps and glares at you."

Instead of screaming in the presence of a non-player person (called witness):
	say "You let out a panicky shriek. [The witness] jumps and then snickers at you."

Instead of screaming in the presence of Sharon when Sharon is asleep:
	if Aiden is in the nursery:
		try waking Sharon;
	otherwise:
		say "If you wake Sharon you'd have something to scream about for real."

Instead of screaming in the presence of the ghostly figure:
	say "You try to scream, but your throat constricts and nothing comes out."


[exercising]

Exercising is an action applying to nothing.
Understand "exercise" as exercising.

Report exercising:
	say "You do a few jumping jacks, breathing deeply and evenly."

[shaking]

Shaking is an action applying to one thing.
Understand "shake [something]" as shaking.

Check shaking the player:
	say "You try your best to bring yourself back to your senses." instead.

Check shaking a person:
	say "You decide instead to respect [its-their of the noun] personal space." instead.

Check shaking an open tin can:
	say "Inadvisable. The contents of the can would get all over everything." instead.

Check shaking something:
	if the noun is not portable:
		say "Shaking [the noun] could easily have disastrous consequences." instead.

Report shaking a photo:
	say "You give the photo a stiff shake[if in darkness][otherwise if the subject of the noun is darkness or the noun is an overexposed photo], but don't notice much of a change[otherwise], and the image seems to become a little clearer[end if].";
	stop the action.

Report shaking:
	say "Shaking [the noun] produces no obvious effect."


Section - Minor Verb Default Replacement Texts

[Hi, I'm Ryan, I put these here. I don't know if Emily would want them to be in one of those previous sections, but TOO BAD]

Instead of singing:
	say "You mutter the West High fight song under your breath:[paragraph break][italic type]Fight, Fight, West High[line break]Stomp [']Em Out Like Ants[line break]Mortify The Other Guy[line break]And Make Him Pee His Pants[roman type][paragraph break]But it doesn't make you feel any better."

Understand "dance" as a mistake ("If you remember correctly, that's a sin.").

Instead of attacking something:
	say "You have to assume you'd get in huge trouble if you [if the noun is a person]attacked [the noun][em dash]like suspended-at-the-very-least levels of trouble[otherwise]broke anything[end if]."

Instead of attacking Robin:
	say "The worst abuse you see fit to dispense upon yourself is muttering 'Get it together, Robin!' under your breath."

Instead of thinking:
	say "You notice that you're thinking, and for a second you start thinking about thinking, but that doesn't get you anywhere."
	
Instead of waking up:
	say "You pinch yourself on the hand[unicode 8212]but it turns out that you were already awake, and now your hand hurts."

Instead of waking the player:
	try waking up.

Instead of burning:
	say "You don't have any matches."
	
Understand "pray" and "pray to [god]" and "pray to [jesus]" as a mistake ("You don't know how.").

Instead of swearing mildly:
	say "You mutter the word quietly, and wonder if the building will notice."
	
Instead of swearing obscenely:
	say "You can only utter the onset of a syllable before you stop yourself. You're in a church!"

Understand "plug [text]" as a mistake ("You don't know where any of the outlets are in this building.").

Understand "bite [something]" as eating.

Understand "xyzzy" as a mistake ("You sneeze.").

Check jumping when Robin is on something:
	say "Your current position is too precarious for that kind of thing." instead.
	
Check jumping:
	say "You jump up and down a few times and feel a bit warmer after the exercise." instead.

The block jumping rule is not listed in the check jumping rulebook.

Part - Debugging commands (not for release)

[When play begins:
	say "([bold type]Debugging commands:[roman type][line break]	
	GONEAR [bracket]ROOM or THING[close bracket]: instantly moves PC to the location in question[line break]
	PURLOIN [bracket]THING[close bracket]: instantly acquire any item in the game[line break]
	FROTZ: turn on all the lights (same as switching on the fuse box)[line break]
	DEFROTZ: return the lights to their state at the beginning of the game)[line break]".]

Section - Checking Evidence

Checking evidence is an action out of world.
Understand "evidence" as checking evidence.

Carry out checking evidence:
	let evidence-list be the list of every non-ectoplasm evidential thing;
	add a random ectoplasm to evidence-list;
	let clues be a list of things;
	repeat with item running through evidence-list:
		if the player has evidence of the item:
			add item to clues;
	if clues is empty:
		say "You haven't found any evidence yet.";
	otherwise:
		say "You quickly review the evidence you've collected so far. Let's see: there's [clues with definite articles][if the number of entries in clues is 1].[paragraph break]That's about it at the moment[end if]."

Section - Frotzing

[A testing command to turn the lights on in the basement.]

Frotzing is an action applying to nothing.
Understand "frotz" as frotzing.

Check frotzing:
	if the west basement switch is switched on:
		say "The lights are already on." instead.

Carry out frotzing:
	now Hall4 is lighted;
	now Hall3 is brightly lit;
	now Hall2 is brightly lit;
	now Storage East is lighted;
	now Storage West is lighted;
	now Sanctuary is brightly lit;
	now Altar is brightly lit;
	now the west basement switch is switched on.

Report frotzing:
	say "The lights are now on!"

Defrotzing is an action applying to nothing.
Understand "unfrotz" and "defrotz" as defrotzing.

Check defrotzing:
	if the west basement switch is switched off:
		say "The lights are already off." instead.

Carry out defrotzing:
	now Hall4 is dark;
	now Hall3 is dimly lit;
	now Hall2 is dimly lit;
	now Storage West is dark;
	now Storage East is dark;
	unless Ghostly Apparition has ended:
		now Sanctuary is dimly lit;
		now Altar is dimly lit;
	now the west basement switch is switched off.

Report defrotzing:
	say "The lights are now off again!"

Section - Resetting Towers of OCD

Resetting Towers of OCD is an action applying to nothing.
Understand "reset towers" and "reset ocd" and "reset towers of ocd" as resetting Towers of OCD.

Carry out resetting Towers of OCD:
	if the player is on a stepper:
		surreptitiously move the player to the location;
	now everything on a stepper is carried by the player;
	now the can of goo is on the wall shelves;
	now Noah's Ark is in Storage East;
	now the Ark of the Covenant is in Storage East;
	now the case labelled handbells is on the Ark of the Covenant.

Report resetting Towers of OCD:
	say "All the elements of the Towers of OCD magically spring back into their original places!";
	if the location is Storage East:
		try looking.

Volume - Photography

Part - Photo types

Section - Photos in general

A photo is a kind of thing.

A photo can be prototypical or non-prototypical.
A photo is usually prototypical.
[Actually the real photos are all non-prototypical but we'll explicitly say so when we create them.]

Understand "photo/photograph/picture/pic/snap/snapshot/print" as "[photo]".

Understand "[photo]" as a photo.
Understand "of" as a photo.

Understand "photos/photographs/pictures/pics/snaps/snapshots/prints" as "[photos]".
Understand "[photos]" as the plural of photo when the player does not carry the stack of photos.

The indefinite article of a photo is "a". [Inform was getting confused by photos whose subjects' names began with a vowel, resulting in "an photo of the ectoplasm" etc.]

[Removed to improve performance:]

[A photo can be subject-lit.
A photo is usually not subject-lit.

Understand "glowing" and "lit" as "[subject-lit]".
Understand "darkened" and "dark" as "[darkened]".

Understand "[subject-lit]" as a photo when the item described is subject-lit.
Understand "[darkened]" as a photo when the subject of the item described is the black sphere and the item described is not subject-lit.

A photo can be subject-empty or subject-full.
A photo is usually subject-empty.

Understand "empty" as "[subject-empty]".
Understand "[subject-empty]" as a photo when the subject of the item described is the jam jar and the item described is subject-empty.]

[This is so that when we print the name of a thing, we know whether we're just printing its name, or we're printing its name as part of the name of a photo.

THAT COMMENT DOESN'T EVEN MAKE SENSE. GO TO BED EMILY]
Photo-name-printing is a truth state that varies. Photo-name-printing is false.

First before printing the name of a photo:
	now photo-name-printing is true.
	
Last after printing the name of a photo:
	now photo-name-printing is false.

First before printing the plural name of a photo:
	now photo-name-printing is true.

Last after printing the plural name of a photo:
	now photo-name-printing is false.

Rule for printing the name of a photo (called pic):
	say "photo of [the random thing shown by the pic]".

Rule for printing the name of a photo (called pic) when the player is shown by the pic:
	say "photo of you".

Rule for printing the plural name of a photo (called pic):
	say "photos of [the random thing shown by the pic]".

Section - Printing the description of a photo

Printing the description of something is an activity on photos.

Carry out examining a photo:
	carry out the printing the description activity with the noun;
	[skip the rest of the examining rules]
	rule succeeds.

[TODO: Replace these placeholder rules with actual default description rules. Interestinger ones.]

Rule for printing the description of a photo (called pic):
	say "It's a photo you took of [the subject of the pic]."
	
Rule for printing the description of a person photo (called pic):
	say "It's a photo you took of [the subject of the pic] [action description of the pic]."

[Rule for printing the description of a container photo (called pic):
	say "It appears to be a photo of [the contents of the pic with indefinite articles] [if the subject of the pic is a container]in[otherwise]on[end if] [the subject of the pic]."
	[This sounds silly when Robin or one of the NPCs is among the contents of the container/supporter, so we'll have to write a special rule (or possibly one per enterable thing, depending on how fancy we want to get) to deal with this.]]

[Rather than having to write a rule for printing the description for every photographable object in the game, we'll add a photo description property and print it as the description of a photo of that object. For more complicated photo subjects, like people, we can use rules instead, but this is much easier in most cases.]  
A thing has some text called the photo description.
Definition: a thing is simply-photo-described if the photo description of it is not empty.
[I'm sorry, that's a terrible name. I couldn't come up with anything better.]

Rule for printing the description of a photo (called pic) when the subject of the pic is simply-photo-described:
	say the photo description of the subject of the pic;
	say line break.

Instead of discarding when the noun is a photo or the noun is the stack of photos:
	say "Better not. You don't want to lose any of your evidence."

Section - The standard photo

[This is the standard kind of photo, for taking a picture of something that has no important features to make note of.]

The standard photo is a photo.

Section - The person photo

A person photo is a kind of photo.

A person photo has indexed text called the action description.
Understand the action description property as describing a person photo.

A person photo has indexed text called the the-less action.
Understand the the-less action property as describing a person photo.

A person photo has a stored action called the action taken.

To make (pic - a person photo) show someone performing (act - a stored action):
	if the act is chatty behavior:
		now the act is the action of talking vaguely;
	now the action taken of the pic is the act;
	now the action description of the pic is the textual representation of the act;
	let desc2 be indexed text;
	let desc2 be the action description of the pic;
	replace the word "the" in desc2 with "";
	now the the-less action of the pic is desc2.

The standard person photo is a person photo.

After printing the name of a person photo (called pic):
	say " [action description of pic]".

After printing the plural name of a person photo (called pic):
	say " [action description of pic]".

[Removed to improve performance:]
[Understand "taking a [photo]" and "taking [photo]" and "taking a [photo] of myself" and "taking [photo] of myself" and "taking [photo] of yourself" as a person photo when the subject of the item described is Robin.]


[Removed to improve performance—Hey, Ryan here. Emily worked super hard to get photos of containers to contain information about their contents, but it ended up ruining performance or something. What you see below is all that remains of her efforts (I think?). A DANG SHAME:]
[Section - The container photo

[For taking a photo of something - a container OR a supporter - which has something in/on it.]

A container photo is a kind of photo.

A container photo has a list of things called the contents.

The standard container photo is a container photo.]

Section - The overexposed photo

[nothing to see here!!! move along!!!!!]

An overexposed photo is a kind of photo.
Understand "overexposed" and "over-exposed" and "over exposed" as an overexposed photo.
Understand "overexposed/over-exposed [photos]" or "over exposed [photos]" as the plural of an overexposed photo.

The blur of light is a thing.

Rule for printing the name of an overexposed photo:
	say "overexposed photo".

Rule for printing the plural name of an overexposed photo:
	say "overexposed photos".

The indefinite article of an overexposed photo is usually "an".

After photographing the blur of light with the Polaroid:
	say "You snap a picture of [the original subject]. The camera whirrs and vibrates and eventually spits out a print of the photo."

Rule for printing the description of an overexposed photo:
	say "For some reason, this photo came out really overexposed. You can't tell what it's supposed to be a picture of; it's just a blur of light."

The standard overexposed photo is an overexposed photo.

Section - Photos taken in the dark

[The darkness just hangs out off-stage. The player never encounters it in person, it's just a dummy object to use as the subject of photos that are taken without sufficient light.]

darkness is a thing.

Rule for printing the description of a photo (called pic) when the subject of the pic is darkness:
	say "The photo is just a black rectangle. There are maybe some lighter smudges of black here and there, but you can't tell what it's supposed to be."
	
Section - The Photo Stack

A stack of photos is a thing.
The stack of photos is ambiguously plural.
Understand "[photos]" as the stack of photos.

Instead of examining the stack of photos when the player carries no photos:
	say "You haven't taken any photos yet."

Instead of examining the stack of photos when the player carries exactly one photo (called pic):
	try examining the pic.

Instead of examining the stack of photos:
	say "You're holding [number of photos carried by the player in words] photos:";
	now all things enclosed by the player are unmarked for listing;
	now all photos carried by the player are marked for listing;
	list the contents of the player, with newlines, indented, giving inventory information, including contents, with extra indentation, listing marked items only;
	rule succeeds.

Instead of taking inventory when the player carries less than 5 photos:
	if the number of things enclosed by the player is 0:
		say "You are empty-handed.";
	otherwise: 
		say "You are carrying:[if the player carries less than 2 photos][line break][end if]";
		now all things enclosed by the player are marked for listing;
		now the stack of photos is unmarked for listing; 
		list the contents of the player, with newlines, indented, giving inventory information, including contents, with extra indentation, listing marked items only;
	rule succeeds.
	
Instead of taking inventory when the player carries at least 5 photos:
	if the number of things enclosed by the player is 0:
		say "You are empty-handed.";
	otherwise: 
		say "You are carrying: [line break]";
		now all things enclosed by the player are marked for listing;
		now all photos carried by the player are unmarked for listing; 
		list the contents of the player, with newlines, indented, giving inventory information, including contents, with extra indentation, listing marked items only;
	rule succeeds.

Part - The appearance relation

[This is stolen from the Claims Adjustment example in the I7 documentation.]

Appearance relates one thing (called the subject) to various photos.
The verb to be shown by implies the appearance relation.
The verb to show (he shows, they show, he showed, it is shown, it is showing) implies the reversed appearance relation.

Understand "of [something related by reversed appearance]" or "[something related by reversed appearance] [photo]" as a photo.
Understand "of [something related by reversed appearance]" or "[something related by reversed appearance] [photo]" as a person photo.

Understand "[photos] of [something related by reversed appearance]" or "[something related by reversed appearance] [photos]" as the plural of a photo.
Understand "[photos] of [something related by reversed appearance]" or "[something related by reversed appearance] [photos]" as the plural of a person photo.

[Removed to improve performance:]

[Understand "of [subject-lit] [something related by reversed appearance]" or "of a [subject-lit] [something related by reversed appearance]" as a photo when the item described is subject-lit.
Understand "of [darkened] [something related by reversed appearance]" or "of a [darkened] [something related by reversed appearance]" as a photo when the item described is not subject-lit.

Understand "of [subject-empty] [something related by reversed appearance]" or "of a/an [subject-empty] [something related by reversed appearance]" as a photo when the item described is subject-empty.]

[Does the player mean doing something with a photo:
	it is very unlikely.] 

Part - Taking a photo of something

Photographing it with is an action applying to one visible thing and one carried thing.

Understand "take a photo/photograph/picture/pic/snap/snapshot of [something] with [something preferably held]" as photographing it with.
Understand "take photo/photograph/picture/pic/snap/snapshot of [something] with [something preferably held]" as photographing it with.
Understand "photograph [something] with [something preferably held]" or "snap [something] with [something preferably held]" as photographing it with.

[Make the camera the most likely thing to take photos with.]
Understand "take a photo/photograph/picture/pic/snap/snapshot of [something] with [camera]" as photographing it with.
Understand "take photo/photograph/picture/pic/snap/snapshot of [something] with [camera]" as photographing it with.
Understand "photograph [something] with [camera]" or "snap [something] with [camera]" as photographing it with.

[Do something about how TAKE A PHOTO of sometimes gets parsed as GET PHOTO instead of PHOTOGRAPH.]
After reading a command:
	[say "(DEBUG: [the player's command])[command clarification break]";]
	if the player's command includes "take [photo] of" or the player's command includes "take a [photo] of":
		replace the matched text with "photograph";
	if the player's command matches "take [photo]" or the player's command matches "take a [photo]":
		replace the player's command with "photograph".

First before taking a photo:
	[say "(DEBUG: [the player's command])[command clarification break]";]
	if the player's command matches "take [photo]" or the player's command matches "take a [photo]":
		try photographing instead.

Section - Action variables

The photographing it with action has a thing called the prototype.
The photographing it with action has a thing called the new photo.
The photographing it with action has a thing called the original subject.

First setting action variables for photographing:
	if the noun is a thing:
		now the original subject is the noun.

[Ahahaha I made the stupidest mistake when I was writing this rule. It made a bug that stopped you from going places in the dark!]
Setting action variables for photographing when in darkness or the location is dimly lit (this is the photography in darkness results in photography of darkness rule):
	if the noun is not the flickering lights and the noun is not the ghostly figure and the noun is not enclosed by the ghostly figure and the noun is not Orchid and the noun is not the glowing tissue paper and the noun is not lit and the location does not enclose the lit black sphere:
		now the noun is darkness.

Setting action variables for photographing when in Hazy Place (this is the can't photograph the real ghost rule):
	now the noun is the blur of light;
	now the prototype is the standard overexposed photo;
	rule succeeds.
	
Setting action variables for photographing (this is the which prototype photo to use rule):
	if the noun is a person:
		now the prototype is the standard person photo;
[	otherwise if the noun is a supporter and something is on the noun:
		now the prototype is the standard container photo;
	otherwise if the noun is a container and the player can see a random thing which is in the noun:
		now the prototype is the standard container photo;]
	otherwise:
		now the prototype is the standard photo.

Section - Check rules

[This rule should never come into play, since there's a rule preventing the player from discarding the camera.]
Check photographing when the actor does not hold the Polaroid:
	say "You seem to have mislaid your camera. You won't be able to take any photos until you find it again." instead.

Check photographing when the second noun is not the Polaroid (this is the only cameras can take photos rule):
	say "[The second noun] isn't a camera, sorry." instead.

Check photographing the camera with (this is the camera can't photograph itself rule):
	say "You'll need a mirror if you're really serious about that." instead.

Check photographing a direction with:
	if the noun is inside:
		say "You'd be better off taking a photograph of something more specific." instead;
	otherwise:
		say "You decide that the [if the noun is up]ceiling[otherwise if the noun is down]floor[otherwise if the noun is outside]view outside[otherwise]view to the [noun][end if] isn't spectacular enough to be worth the photo paper." instead.

[This rule is negotiable. It would be kind of fun to let the player take photos of photos (and photos of photos of photos, and so on) but it would also be a disambiguation nightmare. I feel it's probably easier to just forbid it, but we can negotiate. Someone on intfiction.org suggested including Numbered Disambiguation Choices to deal with this sort of horror.]
[I say it's a waste of time! -RV]
Check photographing a photo with (this is the better not take photos of photos because it's confusing rule):
	say "A photo of a photo would be a waste of time. Useless clutter." instead.
	
Check photographing it with:
	if Robin is in the noun or Robin is on the noun:
		say "It's hard to do that while you're [if Robin is on the noun]on[otherwise]in[end if] [the noun]." instead.
		
Section - Carry out rules

First carry out photographing (this is the create a new photo rule):
	let the copy be a new object cloned from the prototype;
	[if we ran out of memory and couldn't create a new photo:]
	if the copy is nothing:
		say "The camera clicks and whirrs but nothing happens. It looks like you must have run out of photo paper.";
		stop the action;
	otherwise:
		now the new photo is the copy;
	now the noun is shown by the new photo;
	now the new photo is non-prototypical;
	move the new photo to the player.

[Make sure the action description of a person photo is correct. By default, all photos will link to the same action description, which obviously isn't what we want. See the Dynamic Objects documentation, section 2.1, for details.]
After cloning a new object from a person photo:
	fix the cloned action taken property;
	fix the cloned action description property;
	fix the cloned the-less action property.
	
[After cloning a new object from a container photo:
	fix the cloned contents property.]
	
Carry out photographing a person with (this is the capturing a person's last action in a photo rule):
	let the captured action be a stored action;
	if the noun is the player:
		now the captured action is the current action;
	otherwise:
		now the captured action is the last action of the noun;
	make the new photo show someone performing the captured action.

[Carry out photographing it with when the prototype is the standard container photo (this is the capturing the contents of a container or supporter rule):
	if the noun is a supporter:
		now the contents of the new photo is the list of things on the noun;
	otherwise:
		now the contents of the new photo is the list of things in the noun.]
	
Section - Report rules

Report photographing something with while in darkness (this is the report taking a photo in the dark rule):
	say "The camera clicks and vibrates. A moment later, you feel the photo fall into your hand.";
	rule succeeds.

[TODO: probably rewrite this default message, it was just supposed to be a placeholder while I was working on the code]
Report photographing (this is the default report taking a photo rule):
	say "You snap a picture of [the noun]. The camera whirrs and vibrates and eventually spits out a print of the photo."

Part - Bugfix Kindly Supplied by Melvin Rangasamy

[With some tweaks by Emily Boegheim.

This is to solve the bug whereby Inform 7 fails to realise that images showing different subjects are distinguishable objects.]

Include (-

Attribute photo;

-) after "Definitions.i6t".

Include (- has photo, -) when defining a photo.

Include (-

[ Identical o1 o2 p1 p2 n1 n2 d1 d2 i j flag;
	if (o1 == o2) rtrue;  ! This should never happen, but to be on the safe side
	if (o1 == 0 || o2 == 0) rfalse;  ! Similarly
	if (o1 ofclass K3_direction || o2 ofclass K3_direction) rfalse; ! Saves time

	!  What complicates things is that o1 or o2 might have a parsing routine,
	!  so the parser can't know from here whether they are or aren't the same.
	!  If they have different parsing routines, we simply assume they're
	!  different.  If they have the same routine (which they probably got from
	!  a class definition) then the decision process is as follows:
	!
	!     the routine is called (with self being o1, not that it matters)
	!       with noun and second being set to o1 and o2, and action being set
	!       to the fake action TheSame.  If it returns -1, they are found
	!       identical; if -2, different; and if >=0, then the usual method
	!       is used instead.

	if (o1 has photo && o2 has photo) {
		d1 = noun; d2 = second;
		noun = o1; second = o2;
		ProcessRulebook((+ the photograph comparison rule +));
		noun = d1; second = d2;
		if (RulebookSucceeded()) {
			!print "Identical!^";
			rtrue;
		}
		else if (RulebookFailed()) {
			!print "Different!^";
			rfalse;
		}
	}

	if (o1.parse_name ~= 0 || o2.parse_name ~= 0) {
	  if (o1.parse_name ~= o2.parse_name) rfalse;
	  parser_action = ##TheSame; parser_one = o1; parser_two = o2;
	  j = wn; i = RunRoutines(o1,parse_name); wn = j;
	  if (i == -1) rtrue;
	  if (i == -2) rfalse;
	}

	!  This is the default algorithm: do they have the same words in their
	!  "name" (i.e. property no. 1) properties.  (Note that the following allows
	!  for repeated words and words in different orders.)

	p1 = o1.&1; n1 = (o1.#1)/WORDSIZE;
	p2 = o2.&1; n2 = (o2.#1)/WORDSIZE;

	!  for (i=0 : i<n1 : i++) { print (address) p1-->i, " "; } new_line;
	!  for (i=0 : i<n2 : i++) { print (address) p2-->i, " "; } new_line;

	for (i=0 : i<n1 : i++) {
		flag = 0;
		for (j=0 : j<n2 : j++)
			if (p1-->i == p2-->j) flag = 1;
		if (flag == 0) rfalse;
	}

	for (j=0 : j<n2 : j++) {
		flag = 0;
		for (i=0 : i<n1 : i++)
			if (p1-->i == p2-->j) flag = 1;
		if (flag == 0) rfalse;
	}

	!  print "Which are identical!^";
	rtrue;
];

-) instead of "Identical" in "Parser.i6t".

[Ugh, this rule ended up being horribly complicated. Just look at all those confusing if/otherwise blocks. Gross.

To be clear, the grossness is my fault, not Melvin's.]
This is the photograph comparison rule:
	let image one be random thing which is shown by the noun;
	let image two be a random thing which is shown by the second noun;
	if image one is image two:
		[[check if one photo is subject-lit and the other isn't]
		if the noun is subject-lit and the second noun is not subject-lit:
			rule fails;
		otherwise if the noun is not subject-lit and the second noun is subject-lit:
			rule fails;
		[check if one photo is subject-empty and the other is subject-full]
		if the noun is subject-empty and the second noun is subject-full:
			rule fails;
		otherwise if the noun is subject-full and the second noun is subject-empty:
			rule fails;] [*DEL]
		[if both are person photos, check whether the action descriptions match]
		if the noun is a person photo and the second noun is a person photo:
			if the action description of the noun matches the text the action description of the second noun:
				rule succeeds;
			otherwise:
				rule fails;
		otherwise:
			rule succeeds;
	otherwise: [image one is not image two]
		rule fails.

Carry out taking inventory (this is the print advanced inventory rule):
	issue library message taking inventory action number 2;
	say ":[if the player holds less than two photos][line break][end if]";
	list the contents of the player, with newlines, indented, including contents, giving inventory information, with extra indentation.

The print advanced inventory rule is listed instead of the print standard inventory rule in the carry out taking inventory rulebook.

[Removed to improve performance:]
[Part - Disambiguating Photos

[TODO: possibly try to come up with some kind of non-horrible rule for printing the name of non-photo things while disambiguating against photos]

Understand "actual" and "real" as a thing when the item described is not a photo.]

Volume - The Way Things Work

[This is a place for new kinds and attributes, tweaks to existing ones, etc., if they don't fit better in some other part of the source, I guess.]

Part - Useful Odds and Ends

Section - Print and delete the first line of a table

[This is for conversation tables (or rather, monologue tables), which is why the column is called "remark".] 

To print and delete the first line of (the table in question - a table-name):
	repeat through the table in question:
		say the remark entry;
		say line break;
		blank out the whole row;
		break.

Section - Saying an em dash

[I do not know if this is a good idea. It might not work properly with interpreters that can't support Unicode properly.]

To say em dash:
	say "[unicode 8212]".
	
[Fun fact: Emily always types out "[em dash]" but I have the number memorized so I just type out "[unicode 8212]".]

Section - Printing the player's command

[Fix for the >TAKE PHOTO / What do you want to take of? bug, courtesy of gurok: http://www.intfiction.org/forum/viewtopic.php?f=7&t=8146]
To decide which snippet is the player's verb:
   (- verb_wordnum * 100 + 1 -).

Repeating the player's verb is an activity.

Rule for repeating the player's verb:
   say the player's verb.

Rule for repeating the player's verb when the player's verb matches "i" or the player's verb matches "inv" or the player's verb matches "inventory":
   say "take inventory";

Rule for repeating the player's verb when the player's verb matches "x":
   say "examine";

Rule for repeating the player's verb when the player's verb matches "l":
   say "look";

Rule for repeating the player's verb when the player's verb matches "z":
   say "wait";

[Additions for Conversation Framework]
Rule for repeating the player's verb when the player's verb matches "t":
	say "tell someone about";
	
Rule for repeating the player's verb when the player's verb matches "a":
	say "ask someone about";
	
[For photography (will only kick in if "after reading a command" fails to rewrite the command first, though)]
Rule for repeating the player's verb when the player's command includes "take [photo]":
	say "take a".

This is the language verb rule:
   carry out the repeating the player's verb activity.

Include (-
Replace LanguageVerb;
-) after "Definitions.i6t".

Include (-
[ LanguageVerb;
   (+ language verb rule+)();
   rtrue;
];

-) after "Language.i6t".



Part - Room Tweaks

Section - The name of a room

[If the name of a room is printed in some ordinary text, it should be printed as, e.g. "the nursery", not just "Nursery".]

A room has some text called the lower case name.
The lower case name of a room is usually "[printed name of the item described in lower case]".
[Some rooms, e.g. the Sunday School room, might need some upper case letters in their lower case name, or might need a more complicated lower case name, or something. We can fix them by giving them the appropriate lower case name.]

Rule for printing the name of a room (called the place):
	if the constructing the status line activity is going on or the current action is looking or the reading a command activity is going on: [the reading a command activity is what runs the UNDO code]
		say "[printed name of the place]";
	otherwise if the asking which do you mean activity is going on or photo-name-printing is true:
		say "[lower case name of the place]";
	otherwise:
		say "the [lower case name of the place]".

Section - Visited rooms

[According to the fiction of the game, Robin must have already visited some of the rooms in the church (and know what they are). We could just mark them as "visited", but we might want to know if it's the first time the *player* has visited the room.]

A room can be backstory-visited. A room is usually not backstory-visited.

Definition: a room is explored if it is visited or it is backstory-visited.

After printing the name of a direction (called way) when (the current action is looking or the current action is exit listing) and printing cat stuff is false:
	let destination be the room way of the location;
	if the destination is a room and the destination is explored:
		if the destination is a hallway and the destination is not Hall4 and the location is a hallway:
			say " to more hallway";
		otherwise:
			say " to the [lower case name of the destination]".

Section - Secret rooms

[This is to control the pathfinding; we don't want the cat to try to lead Robin through the tunnel or anything like that.]

A room can be hidden or obvious. A room is usually obvious.

Section - Secret doors

[This code is stolen from Andrew Owen's extension Secret Doors. The extension no longer works because it's got a silly typo in it (I don't understand how it worked in the first place?), so I've copied and edited the relevant code here.]

A secret door is a kind of door.
A door can be revealed or unrevealed.
A door is revealed.
A secret door is unrevealed.
A secret door is scenery.
A secret door is closed.

To print the you can't go message:
	(- L__M(##Go, 2, 0); -).

To print the you can't see message:
	(- L__M(##Miscellany, 30, 0); -).

Before going through a secret door which is unrevealed:
	print the you can't go message instead.

Before doing something to a secret door which is unrevealed:
	if the action requires a touchable noun:
		print the you can't see message instead;
	otherwise:
		say "You aren't aware of any such thing." instead.

Before doing something when the second noun is a secret door and the second noun is unrevealed:
	if the action requires a touchable second noun:
		print the you can't see message instead;
	otherwise:
		say "You aren't aware of any such thing." instead.

Section - Switching on the lights

[Robin automatically switches lights on when she enters a room and off when she leaves. This is a bit tricky because some rooms can't have their lights switched on at first, but they can later. Plus the hallway should probably be treated as a single room for lighting purposes? Good grief.]

A room can be light-switchable. A room is usually not light-switchable.

The lights-off count is a number that varies. The lights-off count is 0.

Definition: a room is light-switch-offable if it is light-switchable and the lights-off count is less than 6 and the number of non-player people in it is 0.

Definition: a room is light-switch-onable if it is light-switchable and it is not visited and the number of non-player people in it is 0.

Last check going when the location is light-switch-offable or the room gone to is light-switch-onable:
	if the room gone to is Hazy Place or the room gone from is Hazy Place:
		continue the action;
	say "You ";
	if the location is light-switch-offable:
		say "turn the lights off in [the location]";
		increment the lights-off count;
		if the room gone to is light-switch-onable:
			say " and ";
	if the room gone to is light-switch-onable:
		say "switch the lights on in the next room.";
	otherwise:
		say "."
[TODO: Actually, the more I mess about in the game, the less I like the way this currently works. Probably need to rethink it.

Changed the text a bit, so it doesn't trail off at the end. Still not really happy with the overall effect though.]

[Let's maybe just have this light-switching business happen in the basement, and let it be implied upstairs? It'll probably get a bit tedious after a while.

IT DOES GET A BIT TEDIOUS. Implementing this now.]

A light switch is a backdrop. It is in Steeple, Balcony, Back Room, Sanctuary, Narthex, Dining, Main Floor Stairway, Mens, Ladies, Kitchen, Stairs, Boiler, Basement Stairway, Nursery, Youth, Blacklight, Sunday, Hall1, Storage West, and Storage East.

Understand "lights" as the light switch.

The description of the light switch is "The light switch is unobtrusive and looks exactly like every other light switch in the church."

The note of the light switch is "'I don't have much to say about the lights. Sometimes there's a problem and you have to go mess with the fuse box. But in your case it doesn't matter, because you should be leaving the lights off anyway! You're hunting a ghost!'"

Instead of switching off the light switch:
	say "You can't take decent photos in the dark."
	
Instead of switching on the light switch:
	say "The lights are already on."

Instead of turning or pushing the light switch:
	if in darkness or the location is dimly lit:
		try switching on the light switch;
	otherwise:
		try switching off the light switch.

After deciding the scope of the player when (the location is Storage West or the location is Storage East) and the west basement switch is switched off:
	place the light switch in scope.

Instead of switching on the light switch when (the location is Storage West or the location is Storage East) and the west basement switch is switched off:
	say "Feeling along the wall near the door, you manage to find the light switch, but nothing happens when you turn it on."

Does the player mean switching on the light switch:
	it is very likely.

Does the player mean switching off the light switch:
	it is very likely.

Does the player mean querying about the light switch:
	it is very likely.

Does the player mean querying vaguely about the light switch:
	it is very likely.

Does the player mean doing something with the light switch when chatty behavior:
	it is very unlikely.


[Also, some rooms are only dimly lit, which restricts your activities there.]

A room can be dimly lit or brightly lit.
A room is usually brightly lit.

Instead of examining something when the location of the noun is dimly lit:
	if the noun is the flickering lights or the noun is the ghostly figure or the noun is the glowing tissue paper or the noun is Orchid or the noun is lit or the noun is Robin or the noun is the stack of photos:
		continue the action;
	otherwise if the location encloses the lit black sphere:
		continue the action;
	otherwise:
		say "It's too dark here to make out any details."

Instead of querying Casey's notes about something when the location is dimly lit:
	say "There isn't enough light for you to read Casey's notes here."

Instead of consulting Casey's notes about when the location is dimly lit:
	say "There isn't enough light for you to read Casey's notes here."

After photographing something with the Polaroid when the location is dimly lit:
	if the noun is the original subject:
		continue the action;
	[otherwise we've changed the subject to "darkness"]
	say "The camera hums and drops an indistinct photograph into your hand."

Part - People Tweaks

[That sounds kind of mean, but whatever.]

Section - Familiarity

A person is usually familiar.

Section - "Non-Player" People (and Things)

Definition: a thing is non-player if it is not the player.

Section - Monologuing

[Orchid and Sharon both have quite a bit to say to Robin in the tutorial. We might need it for Aiden's monologue at the end, too, though we'll probably have to change the definition of monologuing for him.]

A person has a table-name called the monologue table.

Definition: a person (called character) is monologuing if the monologue table of the character is not empty.

To have (NPC - a person) monologue:
	print and delete the first line of the monologue table of the NPC.

Section - Last Action

[Keep track of the last action a character performed.]

A person has a stored action called the last action.

The last action of a person is usually the action of waiting.

[[NPCs]
First after someone doing something:
	[don't bother with the cat, the only time you can take a photo of it is when it's in the window or waiting for the player]
	if the actor is not the cat:
		let the act be the current action;
		[Remove the actor from the stored action; we don't need to store that information and it messes up the way the action gets printed out. If the actor part is the player, Inform 7 won't mention the actor's name when the action is printed onscreen, which is what we want here.]
		change the actor part of the act to the player;
		now the last action of the actor is the act;
	continue the action.]
[I think this is all handled manually now?]

[the player]
First every turn:
	now the last action of the player is the current action.

[If we don't like the default description of an action we can add it as a special case here.]
To decide what indexed text is the textual representation of (act - a stored action):
	if the act is searching the nursery window:
		decide on "looking through the window";
	if the act is photographing the player with the Polaroid:
		decide on "taking a photo of yourself";
	if action name part of the act is the examining action:
		decide on "staring at something";
	if the act is floating:
		decide on "floating in the air";
	if the act is talking vaguely:
		decide on "talking about something";
	if the act is screaming:
		decide on "screaming at you";
	if the act is pushing:
		decide on "moving [the noun part of the act]";
	decide on "[act]".

Section - Actual Interlocutor

[Figuring out who the player is talking to.]

To decide which object is the actual interlocutor:
	if the current action is talking to or the current action is asking someone about or the current action is telling someone about or the current action is asking someone for or the current action is answering someone that:
		decide on the noun;
	if the current action is requesting someone for or the current action is imploring someone for or the current action is quizzing someone about or the current action is informing someone about or the current action is saying hello to or the current action is saying goodbye to:
		decide on the noun;
	if the current action is giving something to or the current action is showing something to:
		decide on the second noun;
	if exactly one non-player person (called de facto) is enclosed by the location:
		decide on the de facto;
	if no non-player person is enclosed by the location:
		decide on nothing;
	decide on the current interlocutor.

Part - Things

Section - Initial appearances for things on supporters

[This is mainly for Sharon's initial appearance when she's sleeping on the cots, but it might come in handy for other stuff. Anyway it's easier to find for future reference if it's up here.

Based on mattw's code at http://www.intfiction.org/forum/viewtopic.php?f=7&t=1331&view=previous ]

For printing a locale paragraph about a thing (called the item) (this is the complex describe what's on scenery supporters in room descriptions rule):
	if the item is [not undescribed and the item is] scenery and the item does not enclose the player:
		if a locale-supportable thing is on the item:
			[new code]
			repeat with possibility running through things on the item:
				if possibility is not mentioned and possibility provides the property initial appearance and initial appearance of the possibility is not "":
					say "[initial appearance of possibility][paragraph break]";
					now the possibility is mentioned;
			[end new code]
			set pronouns from the item;
			if an unmentioned locale-supportable thing is on the item: [this line is also new code]
				repeat with possibility running through things on the item:
					now the possibility is marked for listing;
					if the possibility is mentioned:
						now the possibility is not marked for listing;
				increase the locale paragraph count by 1;
				say "On [the item] ";
				list the contents of the item, as a sentence, including contents,
					giving brief inventory information, tersely, not listing
					concealed items, prefacing with is/are, listing marked items only;
				say ".[paragraph break]";
	continue the activity.

The describe what's on scenery supporters in room descriptions rule is not listed in any rulebook.

Section - Comprehending stuff

[Some things need to be understood before they can be used/classed as evidence.]

A thing can be comprehended.
A thing is usually comprehended.

Section - Evidence

A thing can be evidential. A thing is usually not evidential.

To decide if the player has evidence of (clue - a thing):
	if the player encloses the clue:
		decide yes;
	if a photo shows the clue:
		decide yes;
	decide no.

To decide if the player does not have evidence of (clue - a thing):
	if the player has evidence of the clue:
		decide no;
	decide yes.

Section - Touching Stuff

To decide whether the current action implies touching (feelie - a thing):
	if the action requires a touchable noun and the feelie is the noun:
		decide yes;
	if the action requires a touchable second noun and the feelie is the second noun:
		decide yes;
	decide no.

Understand "pet [something]" and "pat [something]" and "stroke [something]" as touching.

Section - Ectoplasm

[TODO: Do we actually want it to be called "ectoplasm"? "Goo" or whatever seems a bit too skeptical, though.]

ectoplasm is a kind of thing.
The indefinite article of ectoplasm is "some".

The description of an ectoplasm is usually "The ectoplasm is pale gray, thick and gooey."

The note of an ectoplasm is usually "'Robin. While you're looking for ghosts, if you see any ectoplasm, you should try to collect it and bring it back somehow. It would be important evidence for the existence of a ghost. And also I think it would just be cool to have.'"

The photo description of ectoplasm is usually "The ectoplasm looks thicker and grayer in the photograph."

Understand "goo" and "gooey" and "goop" as ectoplasm.
Understand "something" and "thing" and "wet" as ectoplasm.
Understand "cold" and "slimy" and "slime" as ectoplasm.
Understand "streak" and "grey" and "gray" and "grayish" and "greyish" and "gunk" as ectoplasm.
Understand "ecto" and "plasm" as ectoplasm.

Instead of smelling ectoplasm:
	say "The ectoplasm smells bitter and unnatural."

Instead of smelling the hands when the player carries ectoplasm (called goop):
	try smelling the goop.

Instead of smelling the jam jar when the player carries ectoplasm (called goop):
	try smelling the goop.

Instead of tasting ectoplasm:
	say "[one of]How do ghosts produce ectoplasm anyway? You're not sure it's something you want to put in your mouth[or]You touch the ectoplasm gingerly with the tip of your tongue. The bitter tang makes you gag[or]You tried it once. Science does not demand any further sacrifices of you[stopping]."

Before eating ectoplasm:
	try tasting the noun instead.

Ectoplasm is always evidential.

To decide if the player has evidence of (clue - ectoplasm):
	if the player carries a thing that contains ectoplasm:
		decide yes;
	if a photo shows ectoplasm:
		decide yes;
	decide no.
	
Definition: a thing is non-ectoplasm if it is not ectoplasm.

After printing the name of ectoplasm (called goo) while taking inventory:
	if the player carries the goo [directly, not inside something else] and photo-name-printing is false:
		say " (stuck to your fingers)".

There are two ectoplasm. [The two spare grammar-defying ectoplasm are off-stage.]

Instead of taking ectoplasm when the player holds the jam jar and the jam jar does not contain ectoplasm:
	try inserting the noun into the jar.

Instead of taking ectoplasm with the jam jar:
	try inserting the noun into the jam jar.

Instead of inserting ectoplasm into the jam jar:
	let goop be a random off-stage ectoplasm;
	now the goop is in the second noun;
	say "You carefully scoop a sample of the ectoplasm into the jar.";
	rule succeeds.

Before showing ectoplasm to someone when the noun is in the jam jar:
	try showing the jam jar to the second noun instead.

Before giving ectoplasm to someone when the noun is in the jam jar:
	try giving the jam jar to the second noun instead.

Instead of taking ectoplasm when the player carries ectoplasm:
	say "You've already got it all over your hand."

Instead of photographing the hands with the Polaroid when the player carries ectoplasm (called goop):
	try photographing the goop with the Polaroid instead.

Instead of inserting ectoplasm into the jam jar when the jam jar contains ectoplasm:
	say "You've already taken a sample of the ectoplasm; that should be sufficient."

Check inserting ectoplasm into a container that is not the jam jar:
	say "[The second noun] [is-are]n't really a suitable container for an ectoplasm sample." instead.

Instead of taking ectoplasm when the noun is in the jam jar:
	say "Best leave your ectoplasm sample in the jar. You don't need to get [if the player carries ectoplasm]even more of it on your hands[otherwise]it all over your hands again[end if]."

Rule for deciding whether all includes ectoplasm enclosed by the jam jar:
	it does not.

Instead of taking ectoplasm when the noun is not in the jam jar and the player encloses the jam jar and the jam jar encloses ectoplasm:
	say "You've already collected some ectoplasm."

Instead of taking ectoplasm:
	say "You don't really want to get it all over your hands again."

[We need to figure out exactly what Orchid is using for ectoplasm so we can get the descriptions right.]
After touching ectoplasm when the player does not carry ectoplasm:
	let goop be a random off-stage ectoplasm;
	now the player carries the goop;
	say "You prod the ectoplasm gingerly, then rub it between your fingers. It's thick and gooey, with a slightly oily texture. And now it's all over your hand again."

After touching ectoplasm when the player carries ectoplasm:
	say "You rub the ectoplasm between your fingers. It's thick and gooey with a slightly oily texture and sticks to your fingers stubbornly."
	
Instead of discarding when the noun is ectoplasm and the player carries ectoplasm [directly] (this is the ectoplasm is stuck to your fingers rule):
	say "It clings to your fingers stubbornly."

Instead of dropping ectoplasm when the player encloses ectoplasm and the player does not carry ectoplasm [the player carries ectoplasm inside something else]:
	let goop be a random ectoplasm enclosed by the player;
	let pot be a random container that contains the goop;
	try dropping the pot.

Instead of discarding when the noun encloses ectoplasm:
	say "You went to a lot of trouble to collect that ectoplasm. Better not leave it lying around."

Instead of washing ectoplasm when the player carries the noun: [i.e. it's stuck to her fingers]
	try washing the hands.

After photographing something with when the player carries ectoplasm and Ghostly Apparition is not happening and the player is not on a supporter:
	say "You hold the camera awkwardly in one hand, trying not to get any of the ectoplasm on it. The photo falls to the floor and[if in darkness], after feeling around for a moment,[end if] you pick it up."

The canonical ectoplasm is an ectoplasm that varies.
When play begins:
	now the canonical ectoplasm is a random ectoplasm.

Setting action variables for photographing:
	if the noun is an ectoplasm:
		now the noun is the canonical ectoplasm.

Section - Sinks and Taps

A sink is a kind of container.
A sink is usually scenery.
A sink can be switched on or switched off.
A sink is usually switched off.
Understand "sink" and "sinks" and "basin" and "basins" as a sink.
Understand "faucets" and "tap" and "taps" and "faucet" as "[faucets]".
Understand "[faucets]" as a sink.

Instead of inserting something into a sink:
	say "[The noun] might get wet."

Instead of putting something on a sink:
	try inserting the noun into the second noun.

[Removed to improve performance (CAN YOU BELIEVE THIS WAS ACTUALLY IN THE GAME THOUGH)]:
[faucets are a kind of thing. They are plural-named.
The printed name of faucets is usually "faucets". [To stop Inform from automatically calling them "the sink's faucets" or, even worse, "the sinks's faucets".]
Some faucets are part of every sink.

Locality relates a thing (called X) to a room (called Y) when Y is the location of X.
The verb to be located in implies the locality relation.

Before printing the name of a faucets (called tap) when the asking which do you mean activity is going on or photo-name-printing is true:
	say "[location of the tap] ";

Understand "[something related by locality] [faucets]" as a faucets.] 

Instead of switching on sink:
	try washing the hands.

Instead of switching off sink:
	say "The faucets are already turned off."

Instead of turning a sink:
	try switching on the noun.

[Instead of turning faucets:
	try switching on the noun.

Instead of switching off faucets:
	say "The faucets are already turned off."

Instead of switching on faucets:
	try washing the hands.

Instead of photographing faucets with:
	try photographing the holder of the noun with the second noun.

Instead of querying Casey's notes about a faucets:
	try querying Casey's notes about the holder of the second noun.
	
Instead of drinking a faucets:
	try drinking the holder of the noun.] [ALSO REMOVED TO IMPROVE PERFORMANCE]

Instead of drinking a sink:
	say "You're not thirsty enough to bother drinking straight from the faucet."

Section - Mirrors

A mirror is a kind of thing.
Understand "mirror" as a mirror.

The description of a mirror is usually "Your own face peers back at you."

Instead of searching a mirror:
	try examining the noun.

Instead of photographing the camera with the camera in the presence of a mirror (called the reflector):
	silently try photographing the reflector with the camera;
	if a photo shows the reflector:
		say "You aim the camera at [the reflector] and take a photo of the reflection."

Rule for printing the description of a photo (called pic) when the subject of the pic is a mirror:
	say "It's a photo of [the subject of the pic]. The reflection in [it-them of the subject of the pic] shows you holding the camera up to your face."

Instead of entering a mirror, say "You're in a weird enough situation already."

Section - General Supporter/Container Tweaks

Instead of putting something on a supporter when the player encloses the second noun:
	say "You lack the dexterity to balance [the noun] on [the second noun] while you're still holding [the second noun]."

Instead of putting something on something when the second noun encloses the player:
	say "There isn't really room for both you and [the noun] on [the second noun]."

[Robin is too tidy to put things on the floor that don't belong on the floor.]
A thing can be floor-standing or supporter-requiring. A thing is usually supporter-requiring.
Last check dropping a supporter-requiring thing:
	say "You can't just drop [the noun] on the floor. What if someone steps on [it-them of the noun] or trips over [it-them of the noun]?" instead.

A container can be contents-mentionable-in-description. A container is usually contents-mentionable-in-description.

This is the new examine containers rule:
	if the noun is a container and the noun is contents-mentionable-in-description:
		consider the examine containers rule.

The new examine containers rule is listed instead of the examine containers rule in the carry out examining rules.

A supporter can be contents-mentionable-in-description. A supporter is usually contents-mentionable-in-description.

This is the new examine supporters rule:
	if the noun is a supporter and the noun is contents-mentionable-in-description:
		consider the examine supporters rule.

The new examine supporters rule is listed instead of the examine supporters rule in the carry out examining rules.

Section - Flippable Container/Supporters

[these are things that are containers when upright and enterable supporters when upside down]

A stepper is a kind of supporter.
A stepper is usually enterable.
[The stepper code should really all be up here, not down in the storage room code, but oh well. Moving it is too much bother now.]

A flippable supporter is a kind of supporter.
A flippable supporter is usually enterable and portable.
A flippable supporter is usually floor-standing.

A flippable supporter can be upended or right way up.
A flippable supporter is usually right way up.

Before printing the name of an upended flippable supporter:
	say "upside-down ".

Understand "upside-down" and "upside down" as a flippable supporter [when the item described is upended]. [That part was removed to improve performance.]

An interior is a kind of container.
An interior is usually privately-named.
An interior is part of every flippable supporter.
Rule for printing the name of a container (called crate) when the crate is part of a flippable supporter (called main item):
	say "[main item]".

[DEBUG]
[After printing the name of a container incorporated by a flippable supporter:
	say " (inner space)".
Understand "inner" and "space" as a container when the item described is part of a flippable supporter.]

When play begins:
	repeat with item running through flippable supporters:
		now the inner space of the the item is privately-named.

To decide which container is the inner space of (main item - a flippable supporter):
	let the crate be a random container incorporated by the main item;
	decide on the crate.


[removing from]
Understand "[something related by reversed incorporation]" as a container. [CAN'T DELETE THIS]

Does the player mean removing from a flippable supporter:
	it is very unlikely.

Does the player mean removing from a container incorporated by a flippable supporter:
	it is very likely.

Check removing something from a flippable supporter:
	try taking the noun instead.

Rule for clarifying the parser's choice of a flippable supporter:
	do nothing.

Rule for clarifying the parser's choice of a container incorporated by a flippable supporter:
	do nothing.


Does the player mean doing something with a container incorporated by a flippable supporter:
	it is very unlikely.

Does the player mean quizzing about a container incorporated by a flippable supporter:
	it is very unlikely.
	
Does the player mean informing about a container incorporated by a flippable supporter:
	it is very unlikely.

Does the player mean querying about a container incorporated by a flippable supporter:
	it is very unlikely.

Does the player mean requesting for a container incorporated by a flippable supporter:
	it is very unlikely.

Does the player mean querying vaguely about a container incorporated by a flippable supporter:
	it is very unlikely.

	
Last report examining a flippable supporter:
	let crate be the inner space of the noun;
	if something is in the crate:
		say "In [the crate] [is-are a list of things in the crate]."
	
Instead of searching a right way up flippable supporter:
	try searching the inner space of the noun.
	
Instead of turning an upended flippable supporter:
	if specifically turning upside down:
		say "[The noun] is already upside down.";
	otherwise:
		continue the action.

Instead of turning a flippable supporter when the player is on the noun:
	say "You'll have to step off it first."

Instead of turning a flippable supporter:
	if the noun is upended:
		say "You [if something is on the noun]pick up the stuff on [the noun] and turn it[otherwise]turn [the noun][end if] over, so the open side is upwards.";
		if something is on the noun:
			now the player carries everything on the noun;
		now the noun is right way up;
	otherwise:
		let the crate be a random container incorporated by the noun;
		say "You [if something is in the crate]pick up the stuff in [the noun] and turn it[otherwise]turn [the noun][end if] upside down.";
		if something is in the crate:
			now the player carries everything in the crate;
		now the noun is upended.

Instead of inserting something into a flippable supporter:
	let the crate be a random container incorporated by the second noun;
	try inserting the noun into the crate.

Instead of inserting something into a container which is part of an upended flippable supporter:
	say "You can't put anything in [the second noun] while it's upside down."

After inserting something into a container which is part of a flippable supporter:
	say "You put [the noun] into [the second noun]."

Instead of putting something on a flippable supporter when the second noun is right way up:
	let the crate be a random container incorporated by the second noun;
	try inserting the noun into the crate.

Instead of entering a flippable supporter when the noun is right way up:
	say "There isn't much point in [if specifically sitting]sitting[otherwise]standing[end if] on [the noun] while it's the right way up."

After entering a flippable supporter when the chosen posture is sitting:
	say "You sit down on [the noun] and try not to knee yourself in the chin."
	
Section - Biblical Things

[This indicates whether they are to be found within the Bible in general, and the huge Bible in the narthex in particular.]

A thing can be Biblical. A thing is usually not Biblical.

Section - Fake Things To Solve Stupid Disambiguation Issues

An object can be faux.

A faux-thing is a kind of thing.
It is always faux.
It is usually privately-named and familiar.

First does the player mean querying about a faux object:
	it is very likely.

First does the player mean querying vaguely about a faux object:
	it is very likely.

Rule for clarifying the parser's choice of a faux object:
	do nothing.
	
Check querying Casey's notes about a faux object:
	say "You'll have to be more specific about what you want to look up." instead.


The faux-door is a faux-thing.
The printed name of the faux-door is "door".
Understand "door" and "doors" and "doorway" and "doorways" as the faux-door.


The faux-sink is a faux-thing.
The printed name of the faux-sink is "sink".
Understand "sink" and "sinks" and "[faucets]" as the faux-sink.

Check querying Casey's notes about the faux-sink:
	if the location encloses exactly one sink (called basin):
		try querying Casey's notes about the basin instead.


[The faux-faucet is a faux-thing.
Understand "[faucets]" as the faux-faucet.
The printed name of the faux-faucet is "faucets".
The faux-faucet is plural-named.

Check querying Casey's notes about the faux-faucet:
	if the location encloses exactly one faucets (called tap):
		try querying Casey's notes about the tap instead.] [*DEL]


The faux-stall is a faux-thing.
The printed name of the faux-stall is "stall".
Understand "stall" and "stalls" as the faux-stall.

Instead of querying Casey's notes about the faux-stall:
	if in Ladies:
		try querying Casey's notes about the ladies' room stalls;
	otherwise if in Mens:
		try querying Casey's notes about the men's room stalls;
	otherwise:
		continue the action.


The faux-toilet is a faux-thing.
The printed name of the faux-toilet is "toilet".
Understand "toilet" and "toilets" as the faux-toilet.


Instead of querying Casey's notes about the faux-toilet:
	if in Ladies:
		try querying Casey's notes about the ladies' room toilets;
	otherwise if in Mens:
		try querying Casey's notes about the men's room toilets;
	otherwise:
		continue the action.


The faux-garbage-can is a faux-thing.
The printed name of the faux-garbage-can is "garbage can".
Understand "garbage" and "garbage/rubbish/trash can" and "rubbish" and "trash" as the faux-garbage-can.

Instead of querying Casey's notes about the faux-garbage-can:
	if in Ladies:
		try querying Casey's notes about the ladies' room garbage can;
	otherwise if in Mens:
		try querying Casey's notes about the men's room garbage can;
	otherwise:
		continue the action.


The faux-mirror is a faux-thing.
Understand "mirror" and "mirrors" as the faux-mirror.
The printed name of the faux-mirror is "mirror".

Check querying Casey's notes about the faux-mirror:
	if the location encloses exactly one mirror (called reflector):
		try querying Casey's notes about the reflector instead.


The faux-couch is a faux-thing.
Understand "[couch]" as the faux-couch.
The printed name of the faux-couch is "couch".

Check querying Casey's notes about the faux-couch:
	try querying Casey's notes about a random couch instead.

	
The faux-can is a faux-thing.
The printed name of the faux-can is "can".
Understand "can" and "tin" and "of" as the faux-can.

Instead of querying Casey's notes about the faux-can:
	try querying Casey's notes about a random tin can.


Faux-room is a room.
It is faux.
Understand "room" and "location" and "here" and "place" as the faux-room.
The printed name of the faux-room is "Room".

Check querying Casey's notes about the faux-room:
	try querying Casey's notes about the location instead.

Volume - The Plot

Part - Tutorial/Intro

Tutorial is a scene.
Tutorial begins when play begins.
Tutorial ends when Sharon is asleep.

Section - Orchid's Instructions

[To begin with, Orchid gives Robin some instructions. Some of these, Robin is gonna ignore, though.]

Orchid's Instructions is a scene.
Orchid's Instructions begins when play begins.
Orchid's Instructions ends when Orchid is not monologuing.

Section - Finding the Blankets

[This is the first puzzle, which should introduce looking up things in Casey's notes. Sharon asks Robin to find her some blankets in the nursery. Robin looks in the notes, finds out their location. They're out of reach because she's short, so she has to climb onto the recliner to get at them.]

Blanket Quest is a scene.
Blanket Quest begins when Orchid's Instructions ends.
Blanket Quest ends when Sharon carries the blankets and Sharon is not monologuing.

Section - Photographing the Cat

[Second puzzle - if you can call it that - introducing the use of the Polaroid. Earlier on, Orchid has told Robin to take a photo of any ghostly manifestation she sees. When this scene begins, Orchid freaks out at a scary face in the window, hopefully prompting the player to take a photo of it. Turns out it was just the cat.]

Ghost Photography 101 is a scene.
Ghost Photography 101 begins when Blanket Quest ends.
Ghost Photography 101 ends when the cat is off-stage.

Part - Collecting Apparent Ghost Evidence

Ghost Hunt is a scene.
Ghost Hunt begins when Tutorial ends.
Ghost Hunt ends when Ghostly Apparition ends.

Section - The Flickering Lights

Investigating the Basement is a scene.
Investigating the Basement begins when Ghost Hunt begins.
Investigating the Basement ends when the flickering lights are off-stage.

Section - Following the Cat to the Boiler Room

A scene can be cat-following. A scene is usually not cat-following.

Distracted by a Cat is a cat-following scene.
Distracted by a Cat begins when Investigating the Basement ends.
Distracted by a Cat ends when the cat is off-stage. [I'm seeing a pattern begin to develop here.]

Section - Seeing the Ghostly Apparition

Ghostly Apparition is a scene.
Ghostly Apparition begins when Robin is in the Sanctuary for the first time.
Ghostly Apparition ends when the ghostly figure is off-stage and the ghostly figure is seen.

Part - Uncovering the Hoax

Uncovering a Hoax is a scene.
Uncovering a Hoax begins when Ghostly Apparition ends.
Uncovering a Hoax ends when Confronting Aiden begins.

Section - Getting a Cat Hint

The Cat Hints is a recurring cat-following scene.
The Cat Hints begins when Uncovering a Hoax is happening and the time since progress counter >= the time to hints and a puzzle is available and the cat is not in Hazy Place.
The Cat Hints ends when the cat is off-stage and the time since progress counter < the time to hints.

Section - Confronting Aiden

Confronting Aiden is a scene.
Confronting Aiden begins when the player is in Steeple.

Volume - Testing

Part - Test Scripts

Test me with "test tutorial / test ghost-hunt / test hoax-evidence / amusing".

Section - Tutorial

Test blankets with "x me / x orchid / x notes / search cupboards / look up blankets in notes / get on recliner / open cupboards / get blankets / x blankets / down / give blankets to sharon".

Test face with "x face / freak out / take photo of face / x photo of face / hit orchid".

Test tutorial with "test blankets / test face".

Section - Collecting Apparent Ghost Evidence

Test lights with "w / s / w / n / n / n / take photo of lights".

Test ectoplasm with "s / s / photograph ectoplasm / i / s / s / take photo of ectoplasm / x photo of ectoplasm".

Test follow-cat with "follow cat / touch cat / follow cat / g / g".

Test wash-hands with "s / u / se / x hands / wash hands / x hands / wash hands".

Test ghost-appearance with "nw / s / w / n / tell orchid about ectoplasm / z / x ghost / photograph ghost / follow ghost / g"

Test ghost-hunt with "test lights / test ectoplasm / test follow-cat / test wash-hands / test ghost-appearance".

Section - Finding Evidence of a Hoax

Test sliding-door-notes with "look up back room in notes / x sliding door / open sliding door / d".

Test sliding-door-search with "x back wall / x panel / open panel / push panel / pull panel / d".

Test lights-and-fuse-box with "x fuse box / x switch / turn switch on / x fuse box / close fuse box / x fuse box".

Test mysterious-lights-rig with "x black sphere / x mirror / x cloth / feel sphere / turn on sphere / photograph party sphere / turn off sphere / photograph mirror / photograph cloth"

Test ocd-ocdly with "x glistening / get glistening / take photo of glistening container / x wall shelves / x amplifier / x ark of covenant / x noah's ark / x cabinet / x handbells / stand on cabinet / stand on amplifier / stand on cabinet / stand on covenant / stand on handbells / get case / get on amp / put case on floor / put case on shelf / d / put case on shelf / l / get on case / get on covenant / get on amplifier / get on cabinet / take photo of glistening / d / put ark on ark / noah's / covenant / l / get on case / get on amplifier / get on cabinet / get on noah's / x glistening / take photo of glistening / get glistening / l / d".

Test ocd-quickly with "s / s / e / get handbells / put handbells on shelf / put noah's on covenant / get on amplifier / get on cabinet / get on noah's / take photo of glistening / take glistening / d".

Test find-engelgurt with "w / w / x manger / look up manger in notes / x chairs / look up chairs in notes / x crayons / look up crayons in notes / x robes / look up robes / x crook / look up crook / x engelgurt / look up engelgurt / e / s / s / e / n / u / s / w / n / e / u / x bars / take photo of bars".

Test mens-room with "d / w / s / e / n / ne / ne / ne / x urinals / x stalls / x sinks / x garbage / x glowing / take photo of glowing / take glowing".

Test real-ghost with "out / s / e / get can of tuna / get can opener / w / n / d / n / open tuna / drop can / z / z / z / z / z / z / z / z / z / z / z / z / z / z / z / z / z / z / z / z / z / z / z / z / n / x girl / take photo of girl / talk to girl / i / x photos / x overexposed photo".

Test open-trapdoor with "s / s / w / n / n / w / get crook / e / s / s / e / n / u / s / e / x sturdy box / get box / w / w / n / e / u / drop box / stand on it / turn it over / stand on it / open trapdoor / open trapdoor with crook".

Test confront-aiden with "u / ask aiden about orchid / d / ask aiden about orchid / d / w / n / s / ask orchid about aiden / ask aiden about orchid / e / n / d / e / i / x photos / wake sharon".

Test hoax-evidence with "get keys / test sliding-door-notes / test lights-and-fuse-box / test mysterious-lights-rig / test ocd-quickly / test find-engelgurt / test mens-room / test real-ghost / test open-trapdoor / test confront-aiden".

Volume - The Player

Part - Robin

Robin Monaghan is a woman. The player is Robin. [This is hardly necessary, it just feels tidy.]
Understand "yourself" and "you" as Robin.

The description of Robin is "You are Robin Monaghan, high school junior, associate reporter for the West High [italic type]Warbler[roman type]. You are not overly fond of churches, and you have never claimed to believe in ghosts, but here you are."

The note of Robin is "'Let's see. Robin is, I would say, the second-best writer on the newspaper staff. She is pretty fun a lot of the time but some of the time she is a stick in the mud. Mostly she's good though. She came to the church youth group one or two times but now she doesn't want to go back for a bunch of reasons. The reason I am writing about you in the third person is: it's a joke. It's supposed to be funny.'"

your hands are part of Robin. They are plural-named.
Understand "hand" and "finger" and "fingers" as your hands.
The description is "[if the player carries ectoplasm]Your hand is covered in some kind of sticky goo[otherwise]Your hands appear to be clean[end if]."

Instead of opening something with when the second noun is Robin or the second noun is the hands:
	try opening the noun.

Instead of taking something with when the second noun is Robin or the second noun is the hands:
	try taking the noun.

Instead of touching your hands when the player carries ectoplasm (called goop) [directly, not in something else]:
	try touching the goop instead.

Instead of washing the hands when the player cannot see a sink:
	say "There's nowhere to wash your hands here";
	if the player carries ectoplasm:
		say "[first time]. You're briefly tempted to wipe the goo off on your jeans, but catch yourself in time[only]";
	say "."

Instead of washing the hands:
	if the player carries ectoplasm (called goop):
		remove the goop from play;
		say "You scrub your hands until every trace of ectoplasm is gone.";
	otherwise:
		say "You can't see any dirt on your hands, but you wash them just in case."

Before tasting or eating the hands when Robin carries ectoplasm (called goop):
	now the noun is the goop.

Instead of searching the hands:
	try examining the hands.

Every turn when the player carries ectoplasm and Ghostly Apparition is not happening: [gonna assume that the ghost appearance is exciting enough to make Robin forget about the ectoplasm annoyance]
	if the west basement switch is switched off and (the location is in Basementhall or the location is Storage West or the location is Storage East):
		continue the action;
		[don't want these messages to show up when Robin is in the dark hallway and hasn't had a chance to look at her hands yet]
	if a random chance of 1 in 8 succeeds: [might need to adjust the likelihood here]
		say "[one of]A little ectoplasm drips off your fingers[or]Your fingers itch where the ectoplasm is clinging to them[or]You're about to adjust the camera strap around your neck, but catch yourself just before you smear the ectoplasm on it[or]You hold your gooey hand out, away from your body, so as not to get any ectoplasm on your clothes[at random]." [TODO: add a few more lines about Robin being annoyed by the ectoplasm on her hands; probably rewrite some of the existing ones]

[it would be nice if Robin refuses to take certain actions on the grounds that she'll make a mess?
OR that might be really annoying, DON'T DO THAT]

The photo description of Robin is "It turns out it's not that easy taking a photo of yourself with a Polaroid camera. The picture came out slightly blurry; you're not sure if it's because you weren't holding the camera still, or because it was too close to your face."

[Responses to talking to yourself.]
Instead of chatty behavior in the presence of a non-player person when the noun is the player:
	say "[The list of visible non-player people] would think you were crazy."

After chatty behavior when the noun is the player:
	say "Your voice echoes in the empty room."

The telling yourself rule is not listed in any rulebook.

[This stops Conversation Framework from jumping in with its own response to talking to yourself.]
Instead of saying hello to or saying goodbye to the player:
	try talking to the player instead.

Part - The Player's Stuff

The player carries Casey's notes.
The notes are plural-named.

Understand "notebook" and "caseys" as Casey's notes.

The description of Casey's notes is "The notebook is thick, and as you flick through the pages, it looks like it's nearly full. Surely Casey didn't make notes about [italic type]everything[roman type] in the church? How long did he spend on this anyway?[paragraph break]There's way too much information here to sit down and read it all at once, but you can look up anything you want to know about."

The note of Casey's notes is "'I know there's a lot of the church you haven't seen or that you don't remember, so I'm writing down everything I can think of. I feel like these notes will be an invaluable resource when I get older and feel the need to revisit the carefree days of my youth, so make sure you give them back to me.'"

Instead of discarding when the noun is Casey's notes:
	say "Casey told you not to let Orchid see them. You'd better keep them with you."

The player wears a Polaroid camera.
[See Photography volume above for info on its behaviour.]

The description of the Polaroid camera is "Something went wrong with the West High [italic type]Warbler[roman type]'s official camera, so Casey scrounged up this old Polaroid instead. He was very keen on you using a Polaroid camera for this story, for some reason[em dash]he gave you enough photo paper to document a whole congregation of ghosts."

The note of the Polaroid camera is "'I'm really glad you're using a Polaroid to get proof of the ghost, because that's what Winona Ryder used in [italic type]Beetlejuice[roman type] to get proof of Alec Baldwin and Geena Davis.[paragraph break]'Now that I think of it, in the movie, the ghosts didn't actually show up on the Polaroids. But that's probably not how it works in real life.'"

Understand "strap" as the Polaroid.

Instead of discarding when the noun is the Polaroid:
	say "You need to hang onto it in case you see evidence of a ghost."

Instead of taking off the Polaroid:
	try dropping the Polaroid instead.

Instead of wearing the Polaroid:
	say "You've already got the strap around your neck. After all, you wouldn't want to drop it."

Instead of querying Casey's notes about a photo:
	try querying Casey's notes about the Polaroid.

The player carries the stack of photos.

Volume - The World

Part - NPCs And How They Act

Section - Sharon

Sharon is a woman.

The description of Sharon is "Sharon is the student teacher assisting the staff advisor for the West High [italic type]Warbler[roman type]. In the past the staff has always managed quite well without a student teacher, but she does come in handy as a chaperone for situations like this[if Sharon is asleep].[paragraph break]Even in sleep, there's a certain sourness to her expression. She looks a bit like a dog dreaming about a rabbit it can't catch[end if]."

The note of Sharon is "'Why do people like Sharon want to become teachers? (By [apostrophe]people like Sharon[apostrophe] I mean [apostrophe]people who obviously hate everything about teaching, even and especially students[apostrophe].) If you're already completely jaded and cynical after your first semester of student teaching, isn't that a sign that you should give up on K-12 education and possibly on life in general?'"

Rule for printing the description of a photo (called pic) when the subject of the pic is Sharon:
	if the action taken of the pic is pushing the cots:
		say "The photo shows Sharon bent over the nursery cots, rearranging them into some kind of grown-up sized bed.";
	otherwise if the action taken of the pic is examining:
		say "Sharon is staring at something out of frame. She looks rattled, in spite of her claim not to believe in ghosts.";
	otherwise if the action taken of the pic is sleeping:
		say "Sharon is lying on the cots, sleeping, or trying to sleep. The photo was taken at such an angle that you can't see her face, but her hunched position suggests that her makeshift bed is less than comfortable.";
	otherwise:
		say "The photograph caught Sharon at a bad moment; she looks like she smelt something particularly nasty."

Instead of kissing sharon:
	if sharon is awake:
		say "Such a gesture could only serve to further sour her mood.";
	otherwise:
		say "There must be a better way to awaken her."

The initial appearance of Sharon is "[sharon-init-appear]."

To say sharon-init-appear:
	if the cat is in the nursery window:
		say "Sharon is staring at the face in the window, her eyes wider than usual";
	otherwise if Sharon is awake and the last action of Sharon is pushing the cots:
		say "Sharon is shuffling some tiny cots around, trying to to turn them into a grown-up-sized bed";
	otherwise if Sharon is awake:
		say "Sharon is keeping an eye on you";
	otherwise:
		say "Sharon's makeshift bed doesn't look comfortable, but apparently she's fast asleep anyway";
		now the blankets are mentioned.

[For some reason, which I think is an I7 bug, Inform won't let me just say "the last action of Sharon is [whatever]".]
When play begins:
	now the last action of Sharon is the action of pushing the cots.

The monologue table of Sharon is the Table of Sharon's Chatter.

Every turn when Blanket Quest is happening and Sharon is monologuing (this is the Sharon talks a lot rule):
	if Sharon is not in the nursery:
		move Sharon to the nursery;
		now the current interlocutor is Sharon;
	have Sharon monologue.

Instead of going west in the Nursery when Blanket Quest is happening and the time since Blanket Quest began is 0 minutes:
	say "You almost collide with Sharon in the doorway and stop short."
	
Table of Sharon's Chatter
remark
"[if going west]She[else]Sharon[end if] stomps into the nursery. 'There are supposed to be some blankets somewhere in this nursery,' she says. 'Robin, can you find them and grab them for me?' She looks thoughtfully at the child-sized cots scattered around the room."
"'Okay, girls,' [if the current action involves Sharon]she[otherwise]Sharon[end if] says. 'Here's how this is going to work.'"
"[if the noun is not Sharon]Sharon continues, [end if]'This is your story and your investigation, so don't expect me to be running around the church with you all night.' She pushes one of the cots up against the wall. "
"[if the current action involves Sharon]She[otherwise]Sharon[end if] pulls another of the cots toward her and lines it up with the first one. 'I don't care if you stay up all night, tomorrow isn't a school day so it's not my problem if you're exhausted. Personally, I want my eight hours, though.'"
"[if the noun is not Sharon]Sharon continues, [end if]'So I'm going to stay here and sleep, and you two are going to get on with your investigating, and not wake me up unless there's a life-threatening emergency or something.'"
"'Orchid, pass me that cot in the corner, will you?' [if the noun is Sharon]she[else]Sharon[end if] asks.[paragraph break]Orchid pulls a face but goes to get it."
"'Just don't complain to me when you've spent the whole night ghost-hunting and have nothing to show for it,' Sharon says. She pushes the last cot into place alongside the other two and frowns at the result."
"[if the current action involves Sharon]She[otherwise]Sharon[end if] unrolls a thin mattress on top of the cots. 'I expect the West High [italic type]Warbler[roman type] can find some other story to run if this one doesn't work out. It'll be a nuisance, though.'"
"[end of Sharon's monologue]"
[This sequence almost certainly needs to be longer.] [um, apparently not? -R]

To say end of Sharon's monologue:
	now the last action of Sharon is the action of waiting;
	if Sharon has the blankets:
		say "Sharon looks at the makeshift cot bed and purses her lips. 'I guess that'll do,' she says.[run paragraph on]";
	otherwise if the blankets are not in the nursery cupboards:
		say "Sharon [if Robin has the blankets]takes the blankets from you[otherwise]picks up the blankets[end if]. 'Thanks, Robin,' she says. She looks at the makeshift cot bed and purses her lips. 'I guess that'll do.'[run paragraph on]";
		now Sharon carries the blankets;
	otherwise:
		say "'Have you found the blankets yet?' [if the noun is Sharon]she[otherwise]Sharon[end if] asks.[paragraph break]";
		if the blankets are discovered or the blankets are seen:
			if the player is on the recliner and the posture of the player is standing:
				say "'I'm just getting them now,' you say.[paragraph break]Sharon humphs impatiently.[run paragraph on]";
			otherwise:
				say "'Yes, but I can't reach them.' You point out the location of the blankets.[paragraph break]Sharon tries to pull them down from the cupboard and fails. She's barely taller than you are. 'This room was designed for giantesses,' she grumbles.[run paragraph on]";
		otherwise:
			say "You shake your head.[paragraph break]'They must be in one of these cupboards. Somewhere.' She looks up at them and sighs. Sharon is barely taller than you, and she obviously doesn't want to try to search the cupboards herself. 'Someone from the church would know.'[paragraph break]'Don't look at me,' Orchid says.[run paragraph on]".

Instead of giving the blankets to Sharon (this is the give Sharon the blankets rule):
	say "Sharon takes the pile of blankets from you. 'Thanks, Robin,' she says.";
	now Sharon carries the blankets;
	rule succeeds.

Instead of showing the blankets to Sharon:
	try giving the blankets to Sharon.

Instead of informing Sharon about the blankets when Robin carries the blankets:
	try giving the blankets to Sharon.

Instead of talking to Sharon when Robin carries the blankets:
	try giving the blankets to Sharon.

[The "greeting type is implicit" bit is necessary to make the give Sharon the blankets rule work, because:
- with Conversation Framework included, giving something to Sharon sometimes triggers an implicit saying hello action
- which in turn triggers this rule
- which would stop the action before actually getting to the giving bit, if this rule didn't specifically ignore implicit greetings.]
Instead of chatty behavior when the actual interlocutor is Sharon and Sharon is monologuing (this is the can't get a word in edgewise with Sharon rule):
	if the current action is saying hello to Sharon and the greeting type is implicit:
		continue the action;
	say "Sharon ignores you and keeps talking."

Instead of talking to Sharon when Sharon does not carry the blankets and Robin does not carry the blankets: [this should only be possible after Sharon finishes her monologue but before she goes to sleep]
	try quizzing Sharon about the blankets.

Instead of chatty behavior when the actual interlocutor is Sharon and Ghost Photography 101 is happening:
	say "Sharon isn't paying attention to you. She's focused on the spectral face in the window."

After quizzing Sharon about the blankets:
	say "'What was that about the blankets again?'[paragraph break]'Can you find them for me,' Sharon says question-marklessly. 'They're supposed to be in the nursery somewhere[em dash]that's what I was told, anyway.'"

After quizzing Sharon about Robin:
	say "'What am I supposed to be doing again?'[paragraph break]";
	if Sharon carries the blankets:
		say "'You're the official photographer for the story, so try to get photos of anything spooky-looking that you manage to find.' Sharon's voice expresses strong skepticism about this possibility. 'You and Orchid will be writing the story together[em dash]but for tonight you're just doing research.'";
	otherwise:
		say "'Can you find me the blankets?' Sharon says. 'I was told they're in here somewhere.'".

After quizzing Sharon about Orchid:
	say "Sharon says, 'You're the official photographer for the story, but you and Orchid will both write it and share the credit. Do try to work together.'"

The West High Warbler is a thing.
The printed name of the Warbler is "West High [italic type]Warbler[roman type]".
Understand "paper" and "newspaper" and "story" as the Warbler.
The Warbler is familiar.

Instead of quizzing Sharon about when the second noun is the Polaroid or the second noun is the Warbler:
	try quizzing Sharon about Orchid.

After quizzing Sharon about Casey:
	say "'Thank goodness for the school doesn't allowed boy-girl sleepovers,' Sharon says. 'Two girls is quite enough bother for me.'"

After quizzing Sharon about Jennie:
	say "'I don't believe in ghosts, but I'm sure you and Orchid can find something spooky to write about. Goodness knows I wouldn't be spending a night in this church if I had a say in the matter.'"

After quizzing Sharon about Sharon:
	say "'I suppose you girls will be running around half the night,' Sharon says, 'but I plan to get some sleep. Don't wake me up unless it's really serious.'"

Last after quizzing Sharon about something:
	say "Apparently Sharon can't be bothered answering your question."

Last after asking Sharon about:
	say "Apparently Sharon can't be bothered answering your question."

Last after informing Sharon about something:
	say "Sharon listens to you with her eyebrows raised."
	
Last after telling Sharon about:
	say "Sharon listens to you with her eyebrows raised."

Last after requesting Sharon for something:
	say "Sharon quirks an eyebrow. 'What makes you think I'd be able to help you with that?'"

Instead of chatty behavior when the actual interlocutor is Sharon and Tutorial is happening and Ghost Photography 101 has ended:
	say "Sharon completely ignores you."

Does the player mean quizzing Sharon about the ghostly figure:
	it is very likely.

Does the player mean informing Sharon about the ghostly figure:
	it is very likely.

Does the player mean requesting Sharon for the ghostly figure:
	it is very likely.

Persuasion rule when asking Sharon to try doing something:
	say "Historically it has been [italic type]Sharon[roman type] who bosses [italic type]you[roman type] around.";
	persuasion fails.
	
Instead of chatty behavior when the actual interlocutor is Sharon and Sharon is asleep:
	if the current action is saying hello to Sharon and the greeting type is implicit:
		continue the action;
	try waking Sharon.

After listening when Sharon is monologuing:
	say "Sharon is still talking."

[TODO: Why doesn't Sharon just ask Orchid to look for the blankets, if Robin and Sharon are both too short? Maybe we need an excuse to get Orchid out of the room temporarily. Or, better, give her something to do that stops her from helping with the blankets.]

At the time when Sharon goes to bed:
	say "Sharon yawns. 'Right, I'm going to bed now. Keep out of trouble, don't break anything, and don't wake me up unless it's [italic type]really, really[roman type] serious, got it?' Without waiting for an answer, she lies down on the row of cots, facing away from you, and pulls the blankets over herself.";
	now Sharon is on the cots;
	now the blankets are on the cots;
	now the last action of Sharon is the action of sleeping.

Instead of shaking Sharon when Sharon is asleep:
	try waking Sharon instead.

[Waking Sharon ends the game]
Instead of waking Sharon when Sharon is asleep:
	if Aiden is in the Nursery:
		say "Sharon stirs, rubs her eyes, and looks at you, then Orchid, then Aiden. She says something that one does not typically say in a church.[paragraph break]***";
		wait for any key;
		say paragraph break;
		say “‘What did she say?’[paragraph break]’I won’t repeat it.'[paragraph break]Casey slumps back in his chair. ‘You would make a great Christian,’ he mutters.[paragraph break]’I’m sure I would.’ You try not to smile.[paragraph break][if the player does not have evidence of the black sphere]’So, how did they manage the spooky lights in the hallway?’[paragraph break]’I think they just used a cheap party ball from the mall. The atmosphere, more than anything, is what made it seem at all spooky.’[paragraph break][end if][if the player does not have evidence of the can of goo]’What about the ectoplasm?’[paragraph break]’That could have been any kind of non-paranormal gunk. Maybe it was car wax.’[paragraph break][end if][if the Engelgurt is not comprehended]’Well, how did Aiden fly around in the sanctuary?’[paragraph break]’I’m entertaining a hypothesis. I noticed components from an unusual mechanism lying around in the balcony.’[paragraph break]’The Engelgurt?’[paragraph break]’If you like. I assume that, while I was chasing after Aiden, Orchid was up in the balcony disassembling the rig so it would look ever so slightly less suspicious.’[paragraph break][end if]'Hey, did you hear what happened to Orchid?’[paragraph break]’I heard that she’s suspended from the next two issues of the paper.’[paragraph break]Casey pouts. ‘I wanted to be the one to tell you.’[paragraph break]’Sorry. Why don’t you tell me what happened to Aiden?’[paragraph break]’I’d love to! Except, I didn’t know anything happened to Aiden. I didn’t think they could do anything, since he’s not on the newspaper?’[paragraph break]’Unfortunately for Aiden, in this case the left and right hands are in close communication. The drama department is putting him on probation for an as-yet undetermined period.’[paragraph break]Casey perks right back up. ‘Robin! That is the best thing I’ve ever heard! Although the thing about Orchid is pretty great too.’[paragraph break]’Well, Orchid is getting what she wanted anyway, I think. The [italic type]Warbler[roman type] is going to run a story that people will actually read, for once[unicode 8212]except this one is true.’";
		end the story finally;
	otherwise:
		say "On second thoughts, maybe what you wanted to say isn't urgent enough to brave Sharon's wrath at being woken."

Section - Orchid

Orchid is a woman.

The description of Orchid is "Orchid, like most girls your age, is taller than you. Her cheeks are covered in freckles that you had never seen before tonight; she must put makeup on over them for school. You have the uneasy feeling that there are more freckles on her left cheek than her right.[paragraph break][if the cat is in the nursery window]She's staring at the face in the window and[em dash]has she turned pale? Her freckles look even more prominent[otherwise if Ghostly Apparition is happening]She catches your eye, scowls, and nods fiercely in the direction of the ghostly figure[otherwise]She notices you examining her, and you look away[end if]."

Understand "freckle" and "freckles" as Orchid.

The note of Orchid is "'Here's an anecdote about Orchid. This happened just recently. I didn't tell you, because I wanted to save it for here. Okay:[paragraph break]'I'm at school after hours because of rehearsals. But we're still in the fairly lazy stage of the production, so there's lots of time to just goof off around the school. So while the underclassmen are sitting around in the hall running lines, I head up to the drama office, because there are donuts there.[paragraph break]'But Orchid and Aiden Kingsley are already up in the office, and as soon as I poke my head in Orchid looks up and says [']Actors only, please!['] and I'm like [']Actually, I am an actor,['] and she says, [']Well, you know what I mean.['][paragraph break]'No, Orchid! I do not have the slightest idea what you mean. I would have just grabbed a donut and left, but you're not allowed to take the donuts out of the drama office, so I just left.[paragraph break]'I wouldn't say I hate Orchid. Hate is a very strong word. I completely hate Aiden, though.'"

Instead of showing something to Orchid:
	if Orchid is waiting for an answer:
		continue the action;
	say "Orchid apparently doesn't have time for that right now."

Instead of showing Casey's notes to Orchid, say "Casey said not to show them to Orchid[unicode 8212]and if Orchid saw anything in there to make her mad at Casey, she'd probably get really mad at you, too."

Instead of informing Orchid about Casey's notes:
	try showing Casey's notes to Orchid.

Rule for writing a paragraph about Orchid:
	if the location is Sanctuary and Sanctuary was not visited:
		now Orchid is mentioned;
	otherwise if Ghost Photography 101 is happening:
		say "Orchid is clutching your arm.";
	otherwise if the location is the Nursery:
		say "Orchid is lounging around, [if Orchid is monologuing]lecturing you[otherwise]yawning[end if].";
	otherwise if Ghostly Apparition is happening and the ghostly figure is visible:
		say "Orchid is watching the ghostly figure closely.";
	otherwise if Aiden is in the location:
		say "Orchid stands near Aiden, her arms crossed.";
	otherwise:
		say "Orchid is hanging around."

Rule for printing the description of a photo (called pic) when the subject of the pic is Orchid:
	if the action taken of the pic is examining:
		say "The photo shows Orchid staring intently at something.";
	otherwise if the action taken of the pic is talking vaguely:
		say "Orchid appears to be staring directly at the camera. Her mouth is open in a silent lecture.";
	otherwise if the action taken of the pic is freaking out:
		say "The photo is slightly out of focus[em dash]well, it's hard to take a good photo of someone when she's clinging to your arm. But the image is clear enough for you to see that Orchid's eyes are wide and her mouth is hanging open slightly.";
	otherwise if the action taken of the pic is screaming:
		say "Orchid's face is scrunched up and her mouth is wide open. Even her freckles look angry.";
	otherwise if the action taken of the pic is scowling:
		say "Orchid scowls out of the photo at you, her eyebrows scrunched and her mouth turned down at the corners.";
	otherwise: [waiting]
		say "The photo shows Orchid standing around aimlessly, a slight frown on her face."

The monologue table of Orchid is the Table of Orchid's Bossiness.

Table of Orchid's Bossiness
remark
"'You'd better hold onto this,' she says, handing you the camera, 'you're the official photographer. Just don't forget, if you see something that might be evidence of a ghost, make sure you take a photo of it.'[paragraph break]You slip the camera strap around your neck."
"[if the current action involves Orchid]She[otherwise]Orchid[end if] notices your notebook. 'What's all that?' she asks.[paragraph break]'Oh, Casey gave it to me. It's just some notes about things in the church.'[paragraph break]'Is there anything about me in there?'[paragraph break]'I don't think so,' you lie, and Orchid loses interest."
"'Oh, and,' Orchid adds, 'don't turn the lights on while you're looking around unless you have to. Some ghosts won't appear unless it's dark, and we've only got tonight to gather all our evidence.'"

After looking for the first time [when the first room description is printed, in the nursery]:
	now the last action of Orchid is the action of talking vaguely;
	now the current interlocutor is Orchid;
	have Orchid monologue.

Every turn while Orchid is monologuing:
	have Orchid monologue.

Instead of chatty behavior when Orchid is monologuing:
	say "Orchid says, 'Hang on,' and keeps talking."

Instead of chatty behavior when Orchid is not monologuing and Sharon is off-stage: [the SINGLE TURN between Orchid shutting up and Sharon arriving]
	say "Orchid yawns and misses whatever you were trying to say."

Instead of chatty behavior when the actual interlocutor is Orchid and Sharon is in Nursery and Sharon is monologuing:
	say "Sharon talks over you, drowning you out."

Instead of chatty behavior when the actual interlocutor is Orchid and Tutorial is happening and Ghost Photography 101 is not happening and Ghost Photography 101 has happened:
	say "Orchid brushes you off."

Instead of going somewhere when Orchid's Instructions is happening:
	say "Orchid catches your arm before you can leave. 'Hang on,' she says, 'we should finish figuring out our plans first.'"

When Orchid's Instructions ends:
	now the last action of Orchid is the action of waiting.

After quizzing Orchid about the Warbler for the first time:
	say "'Book reviews and stories about school sports!' Orchid says. 'That's not what people want to read. They want something [italic type]exciting[roman type], something they can talk about. And that's what this story is going to be, for once. If I get to be editor-in-chief...' She drones on for several more minutes about her plans for the [italic type]Warbler[roman type], but you've tuned out.";
	
After quizzing Orchid about the Warbler:
	say "You really don't need to hear Orchid's spiel about How To Run A School Newspaper again."

After quizzing Orchid about Orchid:
	say "Don't get her started."

After quizzing Orchid about Sharon:
	say "Orchid pulls a face[if Sharon is in the location], but is too circumspect to say anything in Sharon's presence[end if]."

After quizzing Orchid about Casey:
	say "'Yeah, it's too bad he couldn't come tonight,' Orchid says with perfect insincerity."

After quizzing Orchid about Aiden:
	say "'Aiden Kingsley? I didn't think you knew him. He's just a guy from drama.'"

After quizzing Orchid about Heather:
	say "Orchid just laughs at your mention of Heather."

After quizzing Orchid about Jennie:
	say "'Don't forget to take a photo of any ghost evidence you come across!' Orchid says and glares at you as if she already suspects you of having overlooked a vital clue."

Instead of quizzing Orchid about when the second noun is Robin or the second noun is the Polaroid:
	try quizzing Orchid about Jennie.

Last after quizzing Orchid about something when the location is the Nursery:
	say "Orchid shrugs."

Last after asking Orchid about when the location is the Nursery:
	say "Orchid shrugs."

Last after informing Orchid about something when the location is the Nursery:
	say "Orchid says, 'Really?' with a glazed expression in her eyes."

Last after telling Orchid about when the location is the Nursery:
	say "Orchid says, 'Really?' with a glazed expression in her eyes."

Last after answering Orchid that when the location is the Nursery:
	say "Orchid smiles wearily at you and doesn't answer."

Persuasion rule for asking Orchid to try doing something:
	say "Before you can finish asking, Orchid squints at you, and you shut up.";
	persuasion fails.

At the time when Orchid critiques your photography:
	now the last action of Orchid is the action of talking vaguely;
	now the current interlocutor is Orchid;
	if a photo shows the cat:
		say "[if the current action is chatty behavior and the actual interlocutor is Orchid]She[otherwise]Orchid[end if] peeks over your shoulder at the photo you took. 'Hey, that's good!' she says. 'I guess you can see how it's a cat now, but it looked really spooky, didn't it?'";
	otherwise if a photo shows Orchid freaking out:
		say "[if the current action is chatty behavior and the actual interlocutor is Orchid]She[otherwise]Orchid[end if] peeks over your shoulder at the photo of her and turns pink. 'What did you do that for? Why didn't you take a photo of that face? I mean, I know it wasn't actually a ghost, but it could've been! We have to make sure we've got photos of all the ghost evidence we find, if we're going to do this investigation properly.'";
	otherwise:
		say "'Why didn't you take a photo of the face?' Orchid asks. 'I mean, I know it wasn't actually a ghost, but it could've been! We have to make sure we've got photos of all the ghost evidence we find, if we're going to do this investigation properly.'"

To decide if a photo shows Orchid freaking out:
	repeat with pic running through photos that show Orchid:
		if the action taken of the pic is freaking out:
			decide yes;
	decide no.

Instead of saying yes when Ghost Photography 101 has ended and the time since Ghost Photography 101 ended is 2 minutes and a photo shows the cat:
	say "'Yeah, I almost thought we'd found our ghost already!'[paragraph break]Orchid laughs and says, 'We'll have to keep looking.'".

Instead of saying no when Ghost Photography 101 has ended and the time since Ghost Photography 101 ended is 2 minutes and a photo shows the cat:
	say "'I don't think so,' you say, 'it was just hard to tell what it was in the dark.'[paragraph break]'Oh,' says Orchid. 'I see. Well, I hope we can find a ghost that [italic type]will[roman type] meet your high standards.'".

When Ghost Hunt begins:
	move Orchid tidily to the Sanctuary;
	now the current interlocutor is nothing;
	say "Orchid says, 'We'd better get started. How about this: you check out the basement, and I'll go upstairs and look around there, and then we'll meet up and compare notes. Don't forget to take a photo if you find any evidence of a ghost!' She slips out of the room. A moment later, you hear her footsteps echoing in the stairwell as she heads back up to the main floor."

After quizzing Orchid about the blankets when the blankets are not seen:
	say "[one of]'Hey, Orchid? Do you know where the blankets are? You go to this church, right?'[paragraph break]Orchid shrugs. 'Yeah, but as a rule I don't hang around the nursery. I guess they're probably somewhere in[em dash]' She waves her hand vaguely at the cupboards on the wall[or]Orchid said she didn't know where the blankets are kept[stopping]."

Instead of requesting Orchid for the blankets:
	if the blankets are discovered:
		try asking Orchid to try taking the blankets;
	otherwise:
		try quizzing Orchid about the blankets.

Instead of talking to Orchid when the blankets are undiscovered:
	try quizzing Orchid about the blankets.

Instead of kissing Orchid, say "The reason your overnight investigatory team had to consist of two girls was to minimize the chances of any funny business."
[imagining an alternative universe game where we decided to rip off Gone Home and there is a whole other plotline where Robin and Orchid totally hook up]

Instead of chatty behavior when Ghost Photography 101 is happening and the actual interlocutor is Orchid:
	say "Orchid doesn't seem to hear you. She's just staring open-mouthed at the face in the window."

Instead of chatty behavior when Ghostly Apparition is happening and Ghostly Apparition has been happening for at least two turns and Orchid is visible:
	if the current action is saying hello to Orchid and the greeting type is implicit:
		continue the action;
	if Orchid is in the location:
		if the ghostly figure is visible:
			say "Orchid shushes you and gestures urgently at the camera.";
		otherwise:
			say "Orchid gives you a push and mutters, 'Never mind that now, follow the ghost!'";
	otherwise:
		say "You can't bring yourself to shout across the big empty church at Orchid."

Section - Aiden

Aiden is a man. [GROSS]
Aiden can be transparent.
Aiden is transparent.

When play begins:
	now the last action of Aiden is the action of waiting.

The photo description of Aiden is "Aiden smirks at the camera, apparently unconcerned at having been caught."

Does the player mean quizzing Aiden about the ghostly figure:
	it is very likely.

Does the player mean informing Aiden about the ghostly figure:
	it is very likely.

Does the player mean requesting Aiden for the ghostly figure:
	it is very likely.

The ghostly figure is a woman. [It's actually Aiden, but I think it's gonna be easier to treat the fake ghost as a separate NPC.]
The ghostly figure is neuter. [as well as female, thanks to Felix Larsson's Second Gender extension]

The ghostly figure is evidential.
The ghostly figure is not familiar.

Understand "pale" and "movement" as the ghostly figure.
Understand "ghost" and "jennie" and "jenny" and "bancroft" as the ghostly figure [when the ghostly figure is on-stage]. [Removed to improve performance.]

When play begins:
	now the last action of the ghostly figure is the action of floating.

Rule for writing a paragraph about the ghostly figure:
	if the ghostly figure is in the Sanctuary:
		say "A ghostly figure floats near the ceiling.";
	otherwise:
		now the ghostly figure is mentioned. [instead there'll be a paragraph about the figure leaving]

The description of the ghostly figure is "The glowing figure is wearing a pale dress and her hair is in pigtails[if Jennie is noted], just like Casey said[end if]. You can't make out the expression on her face, but she's holding her hands out as if she's reaching for something or asking for something."

Rule for printing the description of a photo (called pic) when the subject of the pic is the ghostly figure:
	if Aiden is seen:
		say "Now you know the ghostly figure was Aiden in glow-in-the-dark makeup and a pigtailed wig, it looks a lot less ghostly. You can almost make out a shadowy mustache under his makeup.";
	otherwise:
		say "In the photo, the ghostly figure stands out starkly against the darkness of the church. The glow emanating from her obscures her features, but you can just make out her pigtails and the shape of her dress."

Before photographing something enclosed by the ghostly figure with the Polaroid:
	try photographing the ghostly figure with the Polaroid instead.

[Rule for printing the name of the ghostly figure when Aiden is seen:
	say "fake ghost".

Understand "fake" as the ghostly figure when Aiden is seen.] [Removed to improve performance.]

The ghostly figure can be transparent.
The ghostly figure is transparent.

Does the player mean querying about the ghostly figure:
	if the ghostly figure is on-stage:
		it is very likely;
	otherwise:
		it is very unlikely.

Does the player mean querying vaguely about the ghostly figure:
	if the ghostly figure is on-stage:
		it is very likely;
	otherwise:
		it is very unlikely.

The ghostly figure's pigtails are part of the ghostly figure.
The ghostly figure's pigtails are plural-named and privately-named.
The printed name of the ghostly figure's pigtails is "pigtails".
Understand "pigtail" and "pigtails" as the ghostly figure's pigtails.
The description of the pigtails is "You can barely make out her pigtails in the darkness."

Does the player mean querying about the ghostly figure's pigtails:
	if Hazy Place is visited:
		it is very unlikely;
	otherwise:
		it is likely.

Does the player mean querying vaguely about the ghostly figure's pigtails:
	if Hazy Place is visited:
		it is very unlikely;
	otherwise:
		it is likely.

Rule for clarifying the parser's choice of the ghostly figure's pigtails:
	do nothing.
	
The ghostly figure wears a pale dress.
Understand "old-fashioned" and "clothes" and "wispy" as the pale dress.
The description of the pale dress is "Her dress is old-fashioned, and it glows eerily."

Rule for clarifying the parser's choice of the pale dress:
	do nothing.

Does the player mean querying about the pale dress:
	if the glowing dress is not seen:
		it is likely;
	otherwise if the player is in Hazy Place:
		it is very unlikely;
	otherwise:
		it is possible.

Does the player mean querying vaguely about the pale dress:
	if the glowing dress is not seen:
		it is likely;
	otherwise if the player is in Hazy Place:
		it is very unlikely;
	otherwise:
		it is possible.

Instead of querying Casey's notes about when the second noun is the ghostly figure's pigtails or the second noun is the pale dress:
	try querying Casey's notes about Jennie Bancroft.

Instead of doing something when the current action implies touching the ghostly figure or the current action implies touching the ghostly figure's pigtails or the current action implies touching the pale dress:
	say "The ghostly figure is too [if the ghostly figure is in the sanctuary]high up and too [end if]far away."

Does the player mean querying about the ghostly figure:
	it is very unlikely. [instead we want the player to find the Jennie Bancroft note]

Does the player mean querying vaguely about the ghostly figure:
	it is very unlikely.

Instead of querying Casey's notes about the ghostly figure:
	try querying the notes about Jennie Bancroft.

Does the player mean following the ghostly figure:
	it is very likely.

Does the player mean quizzing Orchid about the ghostly figure:
	it is very likely.

Does the player mean informing Orchid about the ghostly figure:
	it is very likely.

Does the player mean requesting Orchid for the ghostly figure:
	it is very likely.

Does the player mean implicit-quizzing the ghostly figure:
	it is very likely.

Does the player mean implicit-informing the ghostly figure:
	it is very likely.

Does the player mean implicit-requesting the ghostly figure:
	it is very likely.

Rule for clarifying the parser's choice of the ghostly figure:
	do nothing.

Persuasion rule for asking Aiden to try doing something:
	let aido be indexed text;
	now aido is "[the current action]";
	replace the text "asking Aiden to t" in aido with "T";
	say "'[aido] yourself,' Aiden grumbles.";
	persuasion fails.

Section - The Cat

The cat is a neuter animal.

The cat is not familiar.

Understand "face" and "pale" and "in window" and "in the window" as the cat.
Understand "gideon" as the cat when Hazy Place is visited and the location is not Hazy Place.

Rule for printing the name of the cat when the cat is in the nursery window:
	say "face in the window".

The description of the cat is "[if the cat is in the nursery window]The face almost seems to be floating in midair. You can't make out any details in the dim light[otherwise]The cat is almost completely white, except for a small black spot on the end of its tail. When it sees you looking at it, it narrows its pale blue eyes at you[end if]."

The initial appearance of the cat is "[if the location is Hazy Place]The cat is cradled in her arms[otherwise]The cat is sitting in the middle of the room, staring at you[end if]."

Rule for printing the description of a photo (called pic) that shows the cat:
	if the action taken of the pic is searching the window:
		say "It's the photo you took of the cat looking in through the window. How did you ever think it was a person? It's much too small and pale to be anything human. Although outside the window, obscured by the glass, it did look pretty ghostly.";
	otherwise:
		say "The cat was facing away from you but it must have noticed you taking a photo, because its head is turned back towards the camera. Its big blue eyes stare out of the photo at you."

Instead of chatty behavior when the actual interlocutor is the cat and the cat is in the nursery window:
	say "Your throat constricts and you can't get the words out."

Persuasion rule for asking the cat to try doing something:
	say "The cat listens thoughtfully to your instructions and does nothing of the sort.";
	[TODO: this response is probably a bad idea, because what if the player tries "CAT, GO [direction the cat was already headed]"?]
	persuasion fails.

First instead of showing something to the cat:
	say "The cat stares up into your face, apparently unaware of [the noun]."
	[this is what our cat does. every time. "kitty, look at this nice toy/yummy food/reflection of you/lolcat/box you can sit in!" kitty: ........]

Instead of chatty behavior when the noun is the cat:
	if the current action is saying hello to and the greeting type is implicit:
		continue the action;
	say "The cat waits patiently for you to finish your speech."

[The cat is going to rush off as soon as the player enters the room, so there isn't much point in including a paragraph about it being in the room as well as the one about it leaving.]
Rule for writing a paragraph about the cat when the cat is leading the player and the cat is not waiting for the player and the cat is not in Hazy Place:
	now the cat is mentioned.

The cat can be leading the player. The cat is not leading the player.
The cat can be waiting for the player. The cat is not waiting for the player.
The cat has a room called the destination.
The cat has a direction called the next move.

When Distracted by a Cat begins:
	now the cat is in Hall1;
	now the destination of the cat is Boiler.

Printing cat stuff is a truth state that varies.
Printing cat stuff is false.

Before reading a command:
	now printing cat stuff is false.

When the Cat Hints begins:
	let way be the best route from the location to Boiler through lit rooms, using even locked doors;
	if the way is nothing:
		now the time since progress counter is the time to hints - 1;
		[we can try to start the scene next turn]
		rule fails;
	let entry be the door way from the location;
	if the entry is a door and the entry is closed:
		now the time since progress counter is the time to hints - 1;
		[we can try to start the scene next turn, when maybe the door is open]
		rule fails;
	now the puzzle being hinted is a random available puzzle;
	now the destination of the cat is the home of the puzzle being hinted;
	now the cat is leading the player;
	[say "DEBUG: The puzzle being hinted is [the puzzle being hinted]. The cat is aiming for [the destination of the cat].";]
	move the cat to the location;
	now printing cat stuff is true;
	if the way is up or the way is down:
		say "The cat trots [if the way is up]down[otherwise]up[end if] the stairs and [one of]purrs[or]mews[at random] at you.";
	otherwise:
		say "The cat strolls in from [the way] and [one of]purrs[or]mews[at random] at you."

Every turn when a cat-following scene is happening and the cat is not in the destination of the cat and the cat is in the location:
	now printing cat stuff is true;
	if the cat is not leading the player:
		now the cat is leading the player;
	if the cat is waiting for the player and the cat is in Sunday and the player is in the location of the cat for the first turn:
		[have the cat wait around for the player for one turn]
		make no decision; [skip the rest of this rule, but keep going through any other every turn rules]
	otherwise if the cat is waiting for the player for the second turn:
		[the cat must be waiting for the player to open a door, UNLESS THERE'S A BUG OF COURSE]
		let way be the next move of the cat;
		let barrier be the door way from the location;
		if the barrier is a door and the barrier is closed:
			[The player didn't open the door for the cat. Naughty player! Cat gives up and goes home.]
			now the destination of the cat is Boiler;
			have the cat move on, silently;
			if the cat is not in the location:
				say "The cat whisks its tail at you, then turns and runs off [if the next move of the cat is down]down the stairs[otherwise if the next move of the cat is up]up the stairs[otherwise]to [the next move of the cat][end if].";
		otherwise:
			have the cat move on, silently;
			say "As soon as there's a cat-width between the door and the doorframe, the cat slips through it.";
		make no decision; [we're done with this rule]
	[say "DEBUG: the puzzle being hinted is [the puzzle being hinted]. The destination of the cat is [the destination of the cat].";]
	have the cat move on;
	if the cat is in the location:
		[the cat couldn't go any further because something (i.e. a door) blocked its path]
		let way be the next move of the cat;
		let barrier be the door way from the location;
		if the barrier is a door and the barrier is closed:
			say "[one of]The cat mews at you, then turns away and scratches at [the barrier][or]The cat rubs up against [the barrier] and mews[at random].";
			now the cat is waiting for the player;
		otherwise:
			say "The cat pauses in the middle of the room, looking confused."; [This should never happen, BUT.]
			[the cat gives up and goes home]
			now the destination of the cat is Boiler.

[A standard phrase to move the cat to its next location. Normally it uses "try the cat going..." to move the cat, but if we use the "silently" option it uses "silently try the cat going" instead.]
To have the cat move on, silently:
	if the destination of the cat is an obvious room and the location of the cat is an obvious room:
		now the next move of the cat is the best route from the location of the cat to the destination of the cat through lit obvious rooms, using even locked doors;
	otherwise:
		now the next move of the cat is the best route from the location of the cat to the destination of the cat through lit rooms, using even locked doors;
	if silently:
		silently try the cat going the next move of the cat;
	otherwise:
		try the cat going the next move of the cat.

[CATS CAN'T OPEN DOORS
or anything else for that matter]
Check the cat opening something:
	stop the action.

[The first time the cat appears, it waits in the Basement Stairway for a turn to let the player get a look at it.]
Last carry out the cat going:
	if Distracted by a Cat is happening and the cat is in Sunday:
		now the cat is waiting for the player;
	otherwise:
		now the cat is not waiting for the player.

[Report which way the cat went when it left the player's location.]
Report the cat going when the room gone from is the location:
	if the cat is not leading the player:
		say "The cat looks up at you for a moment and mews. Then it spins around and dashes off to [the next move of the cat].";
	otherwise:
		if the next move of the cat is up:
			say "The cat scampers up the stairs.";
		else if the next move of the cat is down:
			say "The cat bounds downstairs.";
		else:
			say "[one of]You're still on the other side of the room when the cat scampers off to [the next move of the cat][or]The cat wanders off to [the next move of the cat], glancing back at you as if to check whether you're following[or]The cat watches you for a moment, but runs off to [the next move of the cat] before you can get near it[at random].";
	rule succeeds.

Every turn when a cat-following scene is happening and the cat is in Boiler and the player is in Boiler:
	remove the cat from play;
	now the time since progress counter is 0;
	now the cat is not leading the player;
	say "[one of]The cat mews at you, slinks behind one of the room's many pipes and fixtures, and is gone[or]As you enter, the cat ducks behind one of the pipes[stopping]."

Every turn when the Cat Hints is happening and the cat is in the destination of the cat and the cat is in the location and the cat is not in Boiler:
	now the destination of the cat is Boiler;
	let the hintable be the linked evidence of the puzzle being hinted;
	say the cat hint of the hintable;
	say line break.

The time since cat visibility is a number that varies.
The time since cat visibility is 0.

To possibly end the cat chase:
	if Distracted by a Cat is happening and the cat is in Hall1:
		continue the action;
	if the cat is not in the location:
		now the cat is not leading the player;
		remove the cat from play;
		now the time since progress counter is 0.

[If the player goes in the wrong direction instead of following the cat, the cat just gives up and the cat-following scene ends.]
Last carry out going when a cat-following scene is happening:
	if the cat is not in the location:
		[check if the player went where the cat was going to go BEFORE the cat]
		let the provisional destination be nothing;
		if the destination of the cat is an obvious room and the location of the cat is an obvious room:
			now the provisional destination is the best route from the location of the cat to the destination of the cat through lit obvious rooms, using even locked doors;
		otherwise:
			now the provisional destination is the best route from the location of the cat to the destination of the cat through lit rooms, using even locked doors;
		[say "DEBUG: the provisional destination is [the provisional destination]. The noun is [the noun]. The cat is in [the location of the cat]. The cat is heading for [the destination of the cat].";]
		if the provisional destination is a direction:
			now the provisional destination is the room provisional destination from the location of the cat;
		if the provisional destination is a room and the provisional destination is the location:
			have the cat move on, silently;
			if the cat is in the provisional destination:
				say "The cat bounds [if the location is a hallway and the location of the cat is a hallway]down the hall[otherwise]into the [lower case name of the location][end if] ahead of you.";
	if the cat is in the location:
		now the time since cat visibility is 0;
	otherwise:
		possibly end the cat chase;
	continue the action.

First after doing something other than going when a cat-following scene is happening:
	if the cat is in the location:
		now the time since cat visibility is 0;
	otherwise:
		if Cat Hints is happening and the destination of the cat is Boiler:
			possibly end the cat chase;
		otherwise:
			increment the time since cat visibility;
			if the time since cat visibility >= 10:
				possibly end the cat chase;
	continue the action.

[the player isn't allowed to touch the cat]
Instead of doing something when the current action implies touching the cat and a cat-following scene is happening:
	if the cat is in the destination of the cat:
		say "The cat scampers away from you before you can touch it.";
	otherwise:
		[say "DEBUG: the puzzle being hinted is [the puzzle being hinted]. The destination of the cat is [the destination of the cat].";]
		have the cat move on, silently;
		say "The cat dodges your hand and bounds off [if the next move of the cat is up]upstairs[otherwise if the next move of the cat is down]downstairs[otherwise]to [the next move of the cat][end if]."

The time since progress counter is a number that varies.
The time since progress counter is 0.
The time to hints is a number that varies.
The time to hints is 50.

Every turn during Uncovering a Hoax:
	increment the time since progress counter.

First carry out photographing an evidential thing with (this is the photographic reset the time since progress counter rule):
	if the player does not have evidence of the noun:
		now the time since progress counter is 0.

First carry out taking an evidential thing (this is the grabby hands reset the time since progress counter rule):
	if the player does not have evidence of the noun:
		now the time since progress counter is 0.

A puzzle is a kind of object.
A puzzle has a thing called the linked evidence.
A puzzle has some text called the non-spoilery name.
A puzzle can be walkthroughable or non-walkthroughable. A puzzle is usually walkthroughable.

To decide what room is the home of (teaser - a puzzle):
	decide on the location of the linked evidence of the teaser.

Definition: a puzzle is available if it is not complete.
Definition: a puzzle is complete if the player has evidence of the linked evidence of it.

The puzzle being hinted is a puzzle that varies.

A thing has some text called the cat hint.
The cat hint of a thing is usually "[if the item described is evidential]Whoops, Emily and Ryan forgot to put in a hint for this thing. Silly them![otherwise]BUG: [the item described] isn't evidence, why is the cat hinting it???"

Towers of OCD is a puzzle.
The non-spoilery name of the Towers of OCD is "the glistening container".
Definition: Towers of OCD is available if it is not complete and the Dark Basement is complete.
The can of goo is a thing. [necessary for silly old Inform to not get confused]
The linked evidence of Towers of OCD is the can of goo.
Definition: Towers of OCD is complete if the player has evidence of the can of goo.
The cat hint of the can of goo is "The cat leaps up onto the amplifier, looks up at the highest shelf and mews."

The Secret Passageway is a puzzle.
The sliding door is a door.
The linked evidence of the Secret Passageway is the sliding door.
Definition: The Secret Passageway is complete if the sliding door is comprehended.
The cat hint of the sliding door is "The cat pads over to the back of the room and rubs up against the wall, purring."

The Dark Basement is a puzzle.
Definition: The Dark Basement is available if it is not complete and the Secret Passageway is complete.
The fuse box is a thing.
The linked evidence of the Dark Basement is the fuse box.
Definition: the Dark Basement is complete if the west basement switch is switched on.
The cat hint of the fuse box is "The cat sits down underneath the fuse box and starts licking its paw."

The Lighting Rig is a puzzle.
Definition: The Lighting Rig is available if it is not complete and the Secret Passageway is complete.
The black sphere is a thing.
The linked evidence of the Lighting Rig is the black sphere.
Definition: the Lighting Rig is complete if the black sphere is comprehended and the player has evidence of the black sphere.
The cat hint of the black sphere is "The cat [if the black cloth is in the Tunnel and the black sphere is in the Tunnel]steps delicately across the cloth and sniffs [otherwise if the black sphere is not in the Tunnel]stretches up to sniff [otherwise]sniffs [end if]at the black sphere."

The Scary Male Bathroom is a puzzle.
The non-spoilery name of the Scary Male Bathroom is "the men's restroom".
The glowing tissue paper is a thing.
The linked evidence of the Scary Male Bathroom is the glowing tissue paper.
[The tissue paper doesn't need a cat hint because the cat can't get into the men's bathroom. It'll just go up to the door and then the standard cat-wants-to-go-through-a-closed-door behaviour will handle that.] [NICE -rcv]

The Hooky Thing is a puzzle.
The non-spoilery name of the Hooky Thing is "finding a hook-shaped thing".
The Hooky Thing is non-walkthroughable.
Definition: The Hooky Thing is available if it is not complete and the Dark Basement is complete.
The shepherd's crook is a thing.
The linked evidence of the Hooky Thing is the crook.
Definition: The Hooky Thing is complete if the player carries the crook.
The cat hint of the crook is "The cat bats a paw at the end of the crook, then jumps back."

The Flying Rig is a puzzle.
The non-spoilery name of the Flying Rig is "the tangle of bars and cables".
Definition: The Flying Rig is available if it is not complete and the Dark Basement is complete.
The Engelgurt is a thing.
The linked evidence of the Flying Rig is the Engelgurt.
Definition: the Flying Rig is complete if the Engelgurt is comprehended and the player has evidence of the Engelgurt.
The cat hint of the Engelgurt is "The cat springs onto the pile of bars and things and from there up onto the railing of the balcony but[em dash]probably wisely[em dash]immediately jumps back down again."

The Final Confrontation is a puzzle.
The non-spoilery name of the Final Confrontation is "the trapdoor".
The pull-cord is a thing.
The linked evidence of the Final Confrontation is the pull-cord.
Definition: the Final Confrontation is available if it is not complete and the Scary Male Bathroom is complete and the Flying Rig is complete and the Lighting Rig is complete [and Towers of OCD is complete]. [we admit defeat, Towers of OCD has problems and you do not have to complete it to finish the game, even if you are relying on the walkthrough]
Definition: the Final Confrontation is complete if the Steeple is visited.
The cat hint of the pull-cord is "The cat puts its paws up on the wall below the trapdoor and stretches, yawning widely."

Section - Random Church People Mentioned in Notes

[Well why don't you put this in a better place, if you're so smart?]
[FINE I WILL, BECAUSE YOU'RE RIGHT, I AM THAT SMART]

The West High Warbler is a familiar thing. Understand "newspaper" and "journalism" and "school" as the West High Warbler.
The note of the West High Warbler is "'So, we understand that West High is a lousy school, but I think the [italic type]Warbler[roman type] is pretty good for a high school newspaper. We do a good job. I mean you and me specifically. It's too early to say who's going to be editor-in-chief next year but I'm pretty sure it's you.'"

Grace United Methodist Church is a familiar thing. Understand "methodists" as Grace United Methodist Church.
The note of Grace United Methodist Church is "'I have been going to Grace United Methodist since I was a little baby. I got baptized there, I got confirmed there, I met most of my friends there. I know you think churches are stupid, so you probably think my church is stupid, but I love my church, so shut up.'"

Does the player mean querying about Grace United Methodist when the location is not Dining:
	it is likely.

Does the player mean querying vaguely about Grace United Methodist when the location is not Dining:
	it is likely.

Casey is a man.
The note of Casey is "'Dear Robin. I am Casey, your friend. I am writing to you from the past. Back here in the past it is very dark, but we have hope that you, in the future, will be able to rekindle the flame of enlightenment and lead us to wisdom. Have you seen a ghost yet? If you haven't seen a ghost, keep looking around until you do. Thanks. Sincerely, Casey.'"

Patrick the youth leader is a man.
The note of Patrick the youth leader is "'Patrick the youth leader is Grace's youth leader currently. In the distant past there was a different, previous youth leader, but we don't talk about her.[paragraph break]'Actually, I'm looking over the other notes I've written and it looks like I've probably said too much about Patrick the youth leader too.'" 

Gwen is a woman. Understand "homeschooled" and "home schooled" and "home-schooled" as Gwen. [I believe that Robin would consider it disrespectful to refer to Gwen as "Homeschooled Gwen" when she doesn't know her, even if Casey does]
Instead of querying the notes about Gwen: try querying the notes about the abstract painting.

Heather is a woman.
The note of Heather is "'Remember Heather? She's on the newspaper, but she never says or does anything? I don't really care for her, but if you like her I'm willing to reconsider.'"

Tammy is a woman.
The note of Tammy is "'If you don't know Tammy, Tammy is this girl who goes to church but kind of hates it and always brings some non-church-related reading material to pass the time. She describes herself as a [apostrophe]hobby reader[apostrophe], which is a really awful term, but it's Tammy, so who cares.'"

Minnie is a woman.
The note of Minnie is "'Minnie was this hardcore college girl who used to play music here. She had tattoos. Sometimes she played acoustic guitar as accompaniment on Sunday mornings, but she brought her electric guitar to youth group and played Christian rock and everybody loved her.[paragraph break]'But every youth group night, she'd have to lug her amp in and out. It wasn't a big amp, but it was heavy enough to be a pain to carry around. Plus it didn't sound very good.[paragraph break]'But then, on Minnie's birthday, Patrick the youth leader somehow caused this gigantic halfway-decent amplifier to appear. And it had to stay at the church, that was the whole point of it, but it was understood that this was Minnie's amplifier.[paragraph break]The last paragraph has been erased and rewritten a few times. 'The details of the reason that Minnie stopped coming to church aren't my business to tell you about. She had to leave, and her amp stayed behind, and now it's in the basement where nobody will ever use it.'"

Marcie Kempulfers is a woman.
The note of Marcie Kempulfers is "'Marcie Kempulfers is in charge of the bell choir and that kind of thing and she's boring.'"

Pastor Ivan Shearer is a man.
The note of Ivan Shearer is "'Good old Pastor Ivan! What can you say about someone like Pastor Ivan? Not much!'"

Chad is a man. [I want to leave him unnoted so bad, but being unnoted is basically a clue that you're important, and Chad is not.]
The note of Chad is "'Chad's an all right guy. Or, he used to be. I haven't heard from him in a while.'"

The lock-in is a familiar thing. Understand "lockin" and "lock in" and "sleepover" and "slumber party" and "karaoke" as the lock-in.
The note of the lock-in is "'I just realized that you never got around to asking me how the lock-in went. Let me give you a taste of what you missed: Patrick the youth leader brought a two-foot cube of popcorn, and over the course of the night we chiseled pieces off and ate them. They were so crusty, and so nasty.[paragraph break]'We did karaoke in the sanctuary, and Orchid was hogging the machine. Naturally. So in an effort to restore balance, Patrick the youth leader started bugging Heather, who has never expressed any interest in singing in front of people, to get up there. She whimpered about it a lot, but peer pressure is a powerful force[unicode 8212]actually I guess Patrick the youth leader isn't really Heather's peer?[unicode 8212]anyway, she did get up, and she sang the quietest, creakiest, most pathetic version of [']Take On Me['] that has ever been sung on this earth. It was horrible! And it was everyone's fault![paragraph break]'What else? There was a lot of praying. And obviously we drank too much pop and we played Romans and Christians for like three minutes and then we played Sardines, and it was great. The whole thing was great. I'm intentionally omitting the greatest parts here, to make you feel bad about not coming.'"

The presbyterian denomination is a familiar thing. Understand "presbyterians" as the presbyterian denomination.
The note of the presbyterian denomination is "'You probably think it's pretty racist of me to rag on Presbyterians as much as I do. Well, Robin, there's a lot you don't know about me. It's very possible that I have very good reasons for saying all the terrible things I've said, and you just don't know what they are, because I'm a complicated person with a lot of secrets. So don't judge.'"

The youth group is a familiar thing.
The note of the youth group is "'Currently the youth group has about a dozen members. It depends on whether you count tagalong junior high students, or the one or two people who are supposedly banned but show up anyway, or nominal members who never show up because they're too cool for us.[paragraph break]'Ours is a diverse youth group, although I guess it's mostly white Christians. Teondis was black, but he left the group when Orchid broke up with him. We also had an atheist member for a while, but he quit showing up for some reason. We're diverse in other ways, though.[paragraph break]'I keep suggesting cool names for the youth group, so we don't have to keep calling it just [']the youth group['] anymore, but none of them catch on. I don't have the clout with these people that I should. Patrick the youth leader has a lot of influence, obviously, and he makes a lot of suggestions, but his are all super hokey. We could probably get somewhere if we ever teamed up, but that seems unlikely.'"

Teondis is a man.
The note of Teondis is "'Fun fact about Teondis: I am pretty sure that is the correct spelling of his name.'"

Theater is a familiar thing. Understand "drama" and "plays" as theater.
The note of theater is "'Orchid and I are both in plays and stuff at school, but for very different reasons. I want to act[unicode 8212]or do props or lights, if that's what's demanded of me[unicode 8212]so I can show you, the audience, a different world, where people other than you have problems other than yours, and by distracting you with those fake people's fake problems I can hopefully relieve the incredible stress of your actual problems, which if you never took a break from worrying about them would definitely reduce you to a crumbled angry skeleton of a person. I want to save you from your self-inflicted torture of taking everything so dang seriously all the time.[paragraph break]'Orchid is in theater because she likes the attention.'"






Part - Things About Rooms And The Things In Them
	
Chapter - The Basement

The EntireBasement is a region.
Storage West, Storage East, Sunday, Youth, Blacklight, Nursery, Basement Stairway, and Boiler are in the EntireBasement.
[Basementhall is in the EntireBasement.]

Section - Nursery

Nursery is a room.
"[if unvisited]Sharon, in her wisdom, decided that everybody should sleep in this cramped corner of the church basement.[paragraph break][end if][if the toys are not in the toybox]Battered toys spill out of the toybox and across the floor, piling up in a corner of the room.[otherwise]The nursery looks much homier now you've tidied all the toys into the toybox.[end if] Cupboards are mounted high on the walls, out of reach of tiny fingers, and an enormous blue recliner sits underneath them. It's already dark outside, and the doors [west] and [south] are dark as well.[paragraph break]Your sleeping bag is lying on [if the toys are in Nursery]a tidy patch of[otherwise]the[end if] floor, ready for when you finish your investigation."

The note of Nursery is "'What I heard is that you guys are going to sleep in the nursery, which doesn't make sense because there are couches in the youth rooms, but I guess those couches are pretty grody actually. I see your point. They keep some blankets up in the cupboards, but somebody as short as you is probably going to have to climb up on top of the recliner to reach them. No offense, Robin. I'm just saying. You're really short.'"

Carry out querying the notes about Nursery:
	now the blankets are discovered;
	now the nursery cupboards are transparent.

Instead of going from the Nursery during Blanket Quest:
	if Sharon carries the blankets:
		say "Sharon hasn't finished her lecture yet.";
	otherwise:
		say "Sharon said the blankets were in here somewhere. You'd better find them for her before you start exploring."

Instead of going from the Nursery during Ghost Photography 101:
	say "Orchid is still clinging to your arm."
	
Instead of going from Nursery when Tutorial is happening and Ghost Photography 101 has happened and Ghost Photography 101 is not happening:
	say "Maybe you should see if there's anything else Sharon needs first."

After going from the Nursery:
	now the current interlocutor is nothing;
	continue the action.

Robin Monaghan is here. [Not necessary, I just like to make it explicit.]



[Sharon isn't here. Yet. She enters after Orchid finishes her monologue.]

Orchid is here.

The recliner is an enterable supporter in the Nursery. It is scenery.
Understand "enormous" and "blue" and "corduroy" as the recliner.
Understand "chair" and "armchair" as the recliner.

The description of the recliner is "The armchair is covered in lumpy blue corduroy. At least it's clean."

The note of the recliner is "'The nursery's recliner is one of the most important pieces of furniture in the church, because anybody who has to babysit rowdy preschoolers needs somewhere comfortable where they can collapse in exhaustion. It's also an important strategic position whenever the floor is lava.'"

Before entering the recliner when not specifically sitting and not specifically standing:
	if the blankets are in the nursery cupboards:
		now the chosen posture is standing;
	otherwise:
		now the chosen posture is sitting.

After entering the recliner when the chosen posture is standing:
	say "You step up onto the seat of the recliner, adding at least a foot to your height.".

After entering the recliner when the chosen posture is sitting:
	say "You sit down on the recliner and almost disappear into its cushiony depths."

Instead of pushing or pulling or turning the recliner:
	say "The recliner is bogged in the carpet."

Instead of pushing the recliner to:
	try pushing the recliner.

The nursery cupboards are openable closed containers in the Nursery. They are plural-named and scenery.
Understand "cupboard" and "top" and "shelf" and "shelves" as the nursery cupboards.

The description of the nursery cupboards is "The nursery cupboards are numerous and mounted high up on the walls, presumably so the kids can't get into them[if something is in the nursery cupboards and the nursery cupboards are open].[paragraph break]In the nursery cupboards [is-are a list of things in the nursery cupboards][end if]."

The nursery cupboards are not contents-mentionable-in-description.

Before doing something with something (called untouchable) that is in the closed nursery cupboards:
	if the current action implies touching the untouchable or the current action is examining the untouchable:
		say "(first opening the nursery cupboards)[command clarification break]";
		silently try opening the nursery cupboards;
		if the nursery cupboards are closed:
			stop the action.

Instead of opening or searching the nursery cupboards when the player is not on the recliner or the posture of the player is not standing:
	if the blankets are seen or the blankets are discovered:
		say "You can barely reach the cupboards from down here.";
	otherwise:
		say "There are way too many cupboards, and you can't even see what's on the top shelves from here, much less reach them. Surely there's a better way to find out where the blankets are kept than just hunting through everything."

Instead of searching the closed nursery cupboards:
	say "(first opening [the nursery cupboards])[command clarification break]";
	try opening the nursery cupboards.

Instead of searching the nursery cupboards when the nursery cupboards contain nothing:
	say "There's nothing else of interest in the cupboards."

After opening the cupboards when the blankets are in the nursery cupboards:
	if the blankets are discovered:
		say "Yes, there are the blankets. Right where Casey said they'd be.";
	otherwise:
		say "It takes some hunting to find them, but there are the blankets, up on the top shelf."

The blankets are in the nursery cupboards. They are plural-named.
The blankets can be discovered or undiscovered. They are undiscovered.
The blankets are familiar.
Understand "blanket" and "sheet" and "sheets" as the blankets.

The description of the blankets is "The blankets are small, thin, and colorful. Probably they're designed for making kids[apostrophe] afternoon naps fun, not for actually sleeping under." 

Instead of querying Casey's notes about when the second noun is the blankets or the second noun is the nursery cupboards:
	try querying Casey's notes about Nursery.

Instead of taking the blankets when the blankets are in the nursery cupboards and the player is not on the recliner:
	say "You can just see the blankets over the edge of the shelf, but you can't quite reach them."

Instead of taking the blankets when the blankets are in the nursery cupboards and the player is on the recliner and the posture of the player is sitting:
	say "Sitting on the recliner is comfortable, but not a practical position from which to reach the blankets."

Instead of wearing the blankets:
	say "You throw a light-colored blanket over your head and moan, 'Woooo! I'm a spooky ghost!' No one laughs, so you take it off again."

Instead of taking or pulling the blankets when Sharon is asleep:
	say "You give the blankets a gentle tug, but Sharon stirs and pulls them closer around herself."

Instead of jumping when Robin is in the Nursery and the blankets are in the nursery cupboards:
	say "Jumping briefly puts you within reach of the cupboards, but the effect doesn't last long enough to be a solution to your problem."

The nursery window is a closed transparent scenery container in the Nursery.
Understand "reflection" as the nursery window.

The note of the nursery window is "'The nursery window doesn't open at all, which is a fire hazard if you ask me. I'm no fire marshal, but I've got some opinions.'"

Rule for printing the name of the nursery window when the asking which do you mean activity is going on or photo-name-printing is true:
	say "nursery window".

The description of the nursery window is "You have a foggy idea that the window probably overlooks the church parking lot, but it's too dark to see anything except your own reflection."

Instead of examining the nursery window when the cat is in the nursery window:
	try examining the cat.

Instead of searching the nursery window: try examining the nursery window.

Instead of opening the nursery window:
	say "It seems to be stuck[if the cat is not in the window]. Anyway, it's really too cold to have the windows open[end if]."
	
When Ghost Photography 101 begins:
	now the cat is in the window;
	now the cat is seen;
	now the last action of the cat is the action of searching the nursery window;
	now the last action of Orchid is the action of freaking out;
	now the last action of Sharon is the action of examining the cat;
	say "Suddenly Orchid gasps and grabs your arm. 'Someone's outside!' she squeaks, pointing at the window.[paragraph break]Behind the glass, you see a pale face, blurry in the dim light."

Every turn when the cat is in the window and the time since Ghost Photography 101 began is 3 minutes:
	say "Sharon says sharply, 'Well, aren't you going to take a photo?' but her voice sounds shakier than usual."

Instead of photographing the window with something when the cat is in the window:
	try photographing the cat with the Polaroid.

After photographing something with something when the cat is in the window:
	remove the cat from play;
	now the last action of the cat is the action of waiting;
	now the last action of Orchid is the action of waiting;
	now the last action of Sharon is the action of waiting;
	Orchid critiques your photography in one turn from now;
	Sharon goes to bed in two turns from now;
	say "The click of the shutter sounds loud in the silence and seems to startle the creature outside the window. It turns and vanishes out of the light, its tail flicking past the glass. Orchid gives a high-pitched giggle.[paragraph break]'Scared of a cat!' Sharon says. 'You better not freak out and wake me up every time you mistake a shadow for a ghost tonight.'"
	
The cots are an enterable supporter in the Nursery. They are plural-named and scenery. They are not contents-mentionable-in-description. The usual posture of the cots is sitting.
Understand "tiny" and "bed" and "beds" and "cot" as the cots.

The description of the cots is "Each cot is composed of scratchy fabric stretched over an aluminum frame, and there is no way any of them could be very comfortable[if Sharon is on the cots].[paragraph break]Sharon is sleeping on the cots, hunched up awkwardly underneath the blankets[end if]."

The note of the cots is "'The cots in the nursery are for little pre-school babies. Nobody else in their right mind would sleep on those cots. Any couch in the church is more comfortable. You can even sleep in the pews. Don't be a schmuck.'"

Instead of taking or pushing or pulling or turning the cots:
	if Sharon is in Nursery and Sharon is awake:
		say "Sharon shoos you away from the cots.";
	otherwise if Sharon is asleep:
		say "You can't mess with the cots without waking Sharon.";
	otherwise:
		say "You adjust the alignment of the cots slightly."

After entering the cots when the chosen posture is sitting:
	say "You perch on the edge of one of the cots and focus on not sliding down into the sagging center of the mattress."

Instead of entering the cots when the chosen posture is standing and Sharon is not in the Nursery:
	say "The frame of the cot creaks and bends as soon as you put your weight on it, so you quickly step back down."

Instead of entering the cots when Sharon is awake and Sharon is in the Nursery:
	say "Sharon whisks the cot out from under you[if the chosen posture is standing]r foot[end if] before you can [if the chosen posture is sitting]sit down[otherwise]step up onto it[end if]."

Instead of entering the cots when Sharon is asleep:
	say "There's no room for you on the cots while Sharon is sleeping on them. You'd be [chosen posture] on her instead."

Instead of sleeping in the cots:
	say "Sharon is [if Sharon is asleep]sleeping[otherwise]going to sleep[end if] there."

Instead of putting something on the cots while Sharon is awake and Sharon is in the nursery:
	if the noun is the blankets, try giving the noun to Sharon instead;
	say "Sharon sweeps [the noun] off the cot onto the floor.";
	now the noun is in the location;
	if the noun is the toys, now the noun is undescribed.

Instead of putting something on the cots while Sharon is asleep:
	say "There's hardly room for [the noun] on the cots while Sharon's sleeping on them."

The toys are a thing in the Nursery. They are plural-named and undescribed.
Understand "battered" and "toy" as the toys.
Understand "cars" and "wooden" and "blocks" and "tea" and "set" and "teaset" and "block" as the toys.

The description of the toys is "Toy cars, a lot of wooden blocks, a tea set, that kind of thing. It looks like someone tried to take a bite out of one of the blocks."

Instead of eating the toys, say "The prospect raises a sick feeling in your stomach. All these toys are probably covered in germs."

Instead of putting the toys on something:
	say "The toys belong in the toybox."

Instead of inserting the toys into something when the second noun is not the toybox:
	say "The toys belong in the toybox."

After taking the toys when the toys were in Nursery:
	say "You gather up an armful of toys."

After inserting the toys into the toybox:
	say "You sit down for a moment and arrange the toys neatly in the box. A bit of time spent organizing is always very relaxing.[paragraph break]There. Much better."
	
Instead of taking the toys when the toys are in the toybox:
	say "You just put the toys away. Don't start making a mess again."

Instead of pushing the toys:
	if the toys are in the toybox:
		try taking the toys;
	otherwise:
		try inserting the toys into the toybox.

Instead of inserting something into the toys:
	say "The toys are too small and probably too unhygienic to be a suitable container for anything."

The toybox is a scenery container in the Nursery.
Understand "toy box" and "box" as the toybox.

The hide description of the toybox is "[if the toybox contains the toys]You won't fit in the toybox now that all those toys are inside[otherwise if sharon is asleep]As you consider your whim to jump inside of the toybox, you look over at Sharon. She might wake up, and wonder what you're doing, at any moment[otherwise]It's too late for you to disappear unnoticed now.[end if]."

The description of the toybox is "It's just a bright red box made of lightweight plastic. You imagine that the toddlers in the church probably knock it over a lot. At least it's not heavy enough to do them any damage."

The note of the toybox is "'The toybox is one really bad hiding place for games like Sardines, because to get inside you have to dump out all of the toys. It's not because the pile of toys lying on the floor looks suspicious or out of place at all; it's because Patrick the youth leader will insist that you put all the toys away afterward.'"

The note of the toys is "'When church people need to get rid of their kids['] old toys, they always think of the church before they think of the thrift store. So there are way too many old beat-up toys in the nursery, and it's a huge pain to put them all away, and once in a while you have to disinfect all of them, which is also a huge pain. This is an example of purely altruistic actions leading to terrible negative consequences.'"

Instead of searching the toybox when nothing is in the toybox:
	say "The toybox is almost empty."

Instead of entering the toybox:
	say "Even the weight of the toys has left stress marks in the plastic. It definitely would not bear your weight."

Instead of taking the toybox:
	if the toys are in the toybox:
		say "You imagine the sorrow of the small children running eagerly into the nursery only to discover that their precious toys are missing, and decide to leave the toybox where it is.";
	otherwise:
		say "The toybox is light enough, but it's too large to carry around comfortably. You decide to leave it where it is."

Instead of turning the toybox:
	if the toys are in the toybox:
		say "You can't bear the thought of spilling the toys everywhere again after you've just tidied them up.";
	otherwise:
		say "You upend the toybox and discover a crack in the bottom. Doesn't seem like it'll be much use for anything except containing toys, so you turn it up the right way again."

Instead of pushing or pulling the toybox:
	say "You shift the toybox slightly, so that it's sitting exactly parallel to the wall."

The sleeping bag is an enterable container in the Nursery. It is scenery.
The indefinite article of the sleeping bag is "your".
Understand "magenta" as the sleeping bag.

The description of the sleeping bag is "This is the sleeping bag you got in fifth grade. You still fit in it just fine, so you can't replace it, even though it's in an unbearable shade of magenta."

Instead of taking the sleeping bag:
	say "It'll be fine staying here while you investigate."

Instead of sleeping when the location is the nursery:
	try entering the sleeping bag.

Instead of sleeping when the location is not the nursery:
	say "When you feel you've got all the evidence you need, your sleeping bag is waiting for you in the nursery."
	
Instead of entering something during Ghost Photography 101: [mainly to prevent Robin from going to bed at an inopportune moment]
	say "Orchid's grasp on your arm is too tight for you to go anywhere."

Instead of entering the sleeping bag when Orchid is in the nursery and Aiden is not in the nursery and Ghost Photography 101 is not happening:
	say "Orchid says, 'You can't go to bed yet, we haven't even started our investigation!'"

Instead of entering the sleeping bag when Aiden is in the nursery:
	say "It's too late for that now. You have to do something about Orchid and Aiden."

Check entering the sleeping bag:
	[We might be able to help the player a bit at this point by actually listing the evidence they've found so far.]
	say "Before you get into your sleeping bag, you quickly review the evidence you've collected. Are you sure you have everything you need for the story?";
	say ">> [run paragraph on]";
	if the player consents:
		continue the action;
	otherwise:
		say "After a moment's thought, you change your mind about going to bed. Maybe there's still something you missed." instead.

To decide whether no evidence was encountered:
	if the cat is not seen:
		decide yes;
	decide no.

To decide whether only some fake ghost evidence was encountered:
	if the cat is seen and the ghostly figure is not seen:
		decide yes;
	decide no.

To decide whether all the fake ghost evidence was encountered:
	if the ghostly figure is seen:
		decide yes;
	decide no.

To decide whether hoax evidence was found:
	if the player has evidence of the black sphere or the player has evidence of the cheap-looking mirror or the player has evidence of the black cloth:
		decide yes;
	if the player has evidence of the can of goo:
		decide yes;
	if the player has evidence of the Engelgurt:
		decide yes;
	if the player has evidence of the glowing tissue:
		decide yes;
	decide no.

To decide whether no hoax evidence was found:
	if hoax evidence was found:
		decide no;
	decide yes.

After entering the sleeping bag:
	say "[line break]You put your belongings aside and snuggle down in the sleeping bag.[paragraph break]";
	say "***";
	wait for any key;
	say paragraph break;
	if no evidence was encountered: [this should not be possible, as Orchid should stop you from going to bed at this point]
		say “‘So, you didn’t find any indication that there was a ghost. At all.’[paragraph break]’Well, what did you expect, Casey? Ghosts aren’t real.’[paragraph break]’But, I mean, the church is still pretty spooky at night. You could have gotten at least a [‘]what a spooky church[‘] story out of it. How much did you look around?’[paragraph break]’I’m sorry you’re disappointed,’ you say, ‘but I wrote what I wrote, and they’ll just have to print it.’”;
		end the story saying “They ended up running Heather’s book review instead.”;
	otherwise if only some fake ghost evidence was encountered:
		say “‘And this is all you have?’[paragraph break]Casey [if the player carries exactly one photo]considers your single photo[otherwise]spreads your photos out on his desk[end if].[paragraph break][if a photo shows the flickering lights]’Some spooky lights. Okay.’ He furrows his brow.[paragraph break][otherwise if the flickering lights are seen]’Well, I saw some spooky lights,’ you offer.[paragraph break]’Did you take a picture?’[paragraph break]’I did not.’[paragraph break][otherwise if a photo shows the cat]'It's a cat.'[paragraph break]You nod.[paragraph break][otherwise]’Well, I also saw a spooky cat.’[paragraph break]’A cat?’[paragraph break]You nod.[paragraph break][end if][if the player carries something that contains ectoplasm]You pull out your jar of ectoplasm. ‘I also found this!’[paragraph break]He squints at the slimy stuff in the jar. It looks rather more mundane in the light of day. You start stuffing it back in your bag.[paragraph break][otherwise if a photo shows an ectoplasm]He squints at the image of the ectoplasm. ‘Sure, sure. Fairly legit, I guess’ he says.[paragraph break][otherwise if an ectoplasm is seen]’I also encountered some ectoplasm.’[paragraph break]’So what happened to it?’[paragraph break]’I washed it off. It was all over my hand, Casey. It was disgusting.’[paragraph break][end if]He tries to smile. ‘You’re going to write a whole article just based on this?’[paragraph break]’Well,’ you say, ‘I’m going to try.’”;
		end the story saying “They ended up running Heather’s dressage article instead.”;
	otherwise if all the fake ghost evidence was encountered and no hoax evidence was found:
		say “‘It sounds like you had quite the spooky evening!’[paragraph break]’It certainly seemed that way at the time, but now I’m not so sure. Take a look at [if the player carries exactly one photo]this[otherwise]these[end if],’ you say, handing Casey your photo[if the player carries more than one photo]s[end if].[paragraph break][if a photo shows the flickering lights]He points at the image of the flickering lights. ‘I imagine these were more impressive in motion.’[paragraph break]’How did you know they were moving?’[paragraph break]’Well, they look kinda like lights from a party ball.’[paragraph break]You look at the picture again. He has a point.[paragraph break][otherwise]’You saw some spooky lights. But you didn’t get a picture.’[paragraph break]’I was very frightened,’ you say.[paragraph break]’Well, what did they look like?’[paragraph break]You describe the lights to Casey, and he frowns. ‘They sound like the kind of lights you get from one of those party balls.’[paragraph break][end if][if the player carries something that contains an ectoplasm]’And you collected some ectoplasm?’[paragraph break]'Well, I collected this.' You hand him the jar.[paragraph break]'Looks like ectoplasm to me!'[paragraph break]'Does it look like it could be anything else?'[paragraph break]'Yeah, I guess. It could be...'[paragraph break]'It could be anything.'[paragraph break][otherwise if a photo shows the ectoplasm]He turns his attention to the image of the weird slime. 'If this isn't ectoplasm, then what is it?'[paragraph break]'It could be any kind of non-paranormal gunk,' you say. 'It could be car wax, for all we know.'[paragraph break][otherwise]'You also saw some ectoplasm.’[paragraph break]’Yes, I did.’[paragraph break]’Well, what happened to it?’[paragraph break]’I washed it off. It was all over my hand, Casey. It was disgusting.’[paragraph break][end if][if a photo shows the ghostly figure]He lifts the photo of the floating girl. ’Now, this photo looks like a ghost,’ he says, ‘but I mean, it also kind of looks like any number of fake ghost photos you see all over the place. Not that I don’t believe it was a real ghost, but other people might not find it so convincing.’ He tries to smile.[paragraph break][otherwise]’But you did see a real ghost?’[paragraph break]’Yes.’[paragraph break]’You saw a free-floating, full-torso, vaporous apparition. But you didn’t get a picture.’[paragraph break]’It was a very stressful situation.’[paragraph break][end if]";
		if the player has evidence of the flickering lights or the player has evidence of a random ectoplasm or the player has evidence of the ghostly figure:
			say "He sighs. ’So, what are you thinking?’[paragraph break]’I think I was duped.’”;
			end the story saying “They ended up running the story anyway.”;
		otherwise:
			say "He tries to smile. ‘You’re going to write a whole article just based on this?’[paragraph break]’Well,’ you say, ‘I’m going to try.’";
			end the story saying "They ended up running Heather's travelogue from Peoria instead";
	otherwise if hoax evidence was found:
		say “‘Okay, what have you got?’[paragraph break]’Well.’ You take a bit longer than necessary to pull the photos out of your backpack. It’s nice to have Casey at your mercy, for once. ‘For a while, I almost thought there was really a ghost.’[paragraph break]’Surely not!’ Casey gasps.[paragraph break]";
		if the player has evidence of the black sphere and the black sphere is comprehended:
			say "’For example, I saw some [italic type]spoooooky[roman type] lights in the basement. But then I found this.’ ";
			if the player encloses the black sphere:
				say "You reveal the party light sphere";
			otherwise: [the proof is a photo]
				say "You reveal your photo[if more than one photo shows a lighting-rig-componentish thing]s[end if] of [list of lighting-rig-componentish things shown by a photo with definite articles]";
			say ".[paragraph break]Casey chortles. ’What a cheap effect!’[paragraph break]";
		otherwise:
			say "’For one thing, I saw these lights in the basement.’ [if a photo shows the flickering lights]You pass Casey the photo you took.[paragraph break]’Well, those look fake[otherwise]You quickly describe the flickering lights.[paragraph break]'Well, that sounds fake[end if]. Did you find out how they did it?’[paragraph break]’I’m entertaining a hypothesis,’ you say, and you quickly move on to your next exhibit.[paragraph break]";
		if a photo shows the can of goo:
			say "’I also found this [‘]ectoplasm[‘]. Which turned out to be this:’ You show Casey the photo you took of the can of goo.[paragraph break]’I know that shelf! How the heck did you get up there?’[paragraph break]’Don’t ask.’[paragraph break]";
		otherwise:
			say "’And I found this clearly fake ectoplasm.’[paragraph break]’How do you know it was fake?’[paragraph break]’It’s obviously not real ectoplasm, Casey.’[paragraph break]’Oh, the word [‘]obviously[‘]. That’s real journalism.’[paragraph break]";
		say "Then Casey leans in. ‘But I heard you saw a real ghost. She flew around and everything.’[paragraph break]’Well, I did see something like that.’[paragraph break]’But?’[paragraph break]";
		if the player has evidence of the glowing tissue:
			say "’I found this piece of tissue paper. The substance on the tissue was glowing just like the ghost.’[paragraph break]’And where did you find this?’[paragraph break]’I found it in the men’s room,’ you say, and Casey’s eyes go wide, but you go on before he has a chance to say anything.[paragraph break]";
		if the Engelgurt is comprehended:
			say "’I also found the [‘]Engelgurt[‘].’[paragraph break]’From the Christmas play, yeah!’ Casey nods.[paragraph break]’Only it wasn’t in the storage room. It was up on the balcony.’[paragraph break]’So...’[paragraph break]’So the [‘]ghost[‘] was not flying unassisted.’[paragraph break]";
		otherwise if a photo shows the Engelgurt and the Engelgurt is not comprehended:
			say "’I also found this.’ You hand him the picture of the strange device you found in the balcony.[paragraph break]He looks it over. ‘This is the Engelgurt! The flying machine from the Christmas play!’ He grimaces. ‘That ghost was no ghost, Robin.’[paragraph break]";
		otherwise:
			say "’I think something fishy was going on with that ghost.’[paragraph break]’Sounds like it.’[paragraph break]";
		say "Casey leans back in his chair. ‘So, do you think it was Orchid?’[paragraph break]’It couldn’t have been. She was right there in the room with me. And the only other person in the church was Sharon.’[paragraph break]’Well, can’t you write some kind of exposé? ‘Church Ghost Obviously Completely Fake[quotation mark]?’[paragraph break]’I don’t think I can make it stick, but I’ll try. I definitely won’t let them print Orchid’s ghost story with my name on it, though. It’s a bunch of lies.’”;
		end the story saying “They ended up running the story without your name on it.”;
	otherwise:
		say "BUG: Emily screwed up some code here, whoopsies!".



The walls are a backdrop.
Understand "wall" as the walls.
The walls are in Balcony, Stairs, Altar, Sanctuary, Narthex, Dining, Kitchen, Main Floor Stairway, Mens, Ladies, Tunnel, Storage East, Storage West, Boiler, Basement Stairway, Nursery, Sunday, and Youth.

The description of the walls is "You don't notice anything notable about the walls."

Section - Youth Room

Youth is south of Nursery. The printed name of Youth is "Youth Room".
Youth is light-switchable.

Understand "youth room" as Youth.

The description of Youth is "A large part of this room is taken up by a rickety ping pong table. Couches line the edges of the room, some of them close enough to the table that they're probably not safe to sit on if someone is actually playing ping pong. On the other hand, you can't see any ping pong paddles anywhere, so maybe that's not an issue. In between the couches, doors lead [north], [west], and [south]."

The note of Youth is "'So, this is where us High School Christians hang out and experience fellowship. There are also junior high kids.[paragraph break]'Actually, what happened was, the junior high and high school groups used to be consolidated, but then Orchid and some other high schoolers thought they needed a separate group for mature people. The criterion was supposed to be [']maturity['], but I mean you can't tell people that they can't join your group because they're too immature, that's elitist, so instead the criterion for the new group was, whether you're in high school or not.[paragraph break]'And in the big kids group we just play tag and gossip anyway, so, so much for maturity! Orchid just didn't want to have to put up with junior high kids; that's what was going on.'"

The ping pong table is a scenery supporter in Youth. The description of the ping pong table is "This scratched, peeling ping pong table has seen better days."

Understand "ping-pong" as the ping pong table.

The note of the ping pong table is "'The ping pong table actually belongs to Patrick the youth leader, which originally meant that we had to take extra special care not to sit on it or scratch it up or anything, but eventually we realized that if it belongs to the [italic type]church[roman type] then we need to be careful with it but if it belongs to [italic type]Patrick the youth leader[roman type] then we can abuse it however we want.[paragraph break]'I think Heather or somebody wanted to repaint the whole thing and put a Jesus fish on there or something but that plan has yet to pan out.'"

Instead of playing the ping pong table:
	say "The complete lack of ping pong paddles, a ping pong ball and an opponent makes that impractical."

Instead of pushing or pulling or turning when the noun is the ping pong table or the noun is the youth room couches:
	say "There's very little room left in the youth room to rearrange the furniture."

A couch is a kind of supporter.
A couch is usually enterable, scenery, and plural-named.
Understand "grody" and "couch" and "couches" as "[couch]".
Understand "[couch]" as a couch.

[Before printing the name of a couch (called sofa) when the asking which do you mean activity is going on or photo-name-printing is true:
	say "[location of the sofa] ";

Understand "[something related by locality] [couch]" as a couch.] [Removed to improve performance.]

Instead of querying Casey's notes about a couch:
	say "'An interesting tidbit! Over the years since Patrick the youth leader introduced these couches that he got for free into our basement, the couches in the youth room eventually got so grody that we had to switch them with the couches in the blacklight room, where it's slightly harder to tell how grody a couch is. Imagine Patrick the youth leader's chagrin when he found out that the couches he took out of the blacklight room were super grody anyway!'"

Instead of doing something when the current action implies touching the youth room couches or the current action implies touching the blacklight room couches:
	say "You'd rather not touch the couches. In fact, you'd rather not even get too close to them."

The youth room couches are a couch in Youth.

The description of the youth room couches is "The couches appear to be haunted by the ghosts of years of snacks and soft drinks. Casey wasn't kidding when he said they were grody."

The garish golden quiz tournament trophy is in Youth.
The printed name of the quiz trophy is "trophy".
The initial appearance of the quiz trophy is "A garish golden trophy catches your eye."
The description of the quiz trophy is "The figurine on top is a generic gold-painted plastic person raising its arms in victory. The text at the bottom reads: 'BIBLE QUIZ TOURNAMENT CHAMPIONS[quotation mark]."
Understand "bible quiz" and "bible tournament" and "bible trophy" and "figurine" and "person" and "plastic" and "plastic person" and "text" as the quiz trophy.

The note of the quiz tournament trophy is "'If you come across a trophy in the youth room: that trophy, we won in a Bible Quiz Tournament, like three years ago(?). We were competing with youth groups from the Lutheran church and the Presbyterian church up the street, and we did not take these Bible Quiz Tournaments seriously [italic type]at all,[roman type] but apparently the other groups took them even less seriously, and we took home the trophy five out of six times. I'm pretty sure the Presbyterians never won once.[paragraph break]'After that, we stopped having Bible Quiz Tournaments, and since we won the last one the trophy just stayed in the church, even though it was actually originally provided by one of the other churches.[paragraph break]'Robin, I'm not saying Presbyterians are dumb. But man, Robin: their kids sure are.'"

After doing something when the current action is querying Casey's notes about the quiz trophy or the current action is examining the quiz trophy:
	now the printed name of the quiz trophy is "Bible Quiz Tournament trophy";
	continue the action.

Instead of inserting something into the quiz trophy:
	say "This isn't a cup sort of trophy, it's the kind with a figurine of a little victorious person on it."

Instead of putting something on the quiz trophy:
	say "The trophy doesn't have enough of a flat surface to be putting things on it."
	
The trolley is a supporter in Youth. The printed name of the trolley is "TV trolley".
The description of the trolley is "A cube of black metal bars, mounted on casters."
The trolley is pushable between rooms.
Understand "tv trolley" as the trolley.

Rule for writing a paragraph about the trolley:
	if the trolley is in Youth:
		say "A trolley with an ancient TV [if the super nintendo is on the trolley]and a Super Nintendo [end if]on it is standing in front of one of the couches.";
	otherwise:
		say "The TV trolley is standing in the middle of the room, looking lonely."

Instead of taking the trolley:
	say "The trolley would be too big and awkard to pick up even if it didn't have a TV on it."

Instead of putting something on the trolley when the noun is not the Super Nintendo:
	say "It looks like the trolley was specially designed for the TV and SNES. There isn't really room for anything else on it."

Some casters are part of the trolley. The description of the casters is "The casters are small wheels stuck to the bottom of the TV trolley. They have hair and lint stuck in them."

The note of the trolley is "'Yeah, we have video games at church. I think the SNES belongs to Chad. Does Chad even come to church anymore? Well, he's gotta come back someday. To get his SNES.'"

Instead of querying Casey's notes about the casters:
	try querying the noun about the trolley.

The television set is a device. It is on the trolley. Understand "TV" and "telly" and "faux" and "wood" and "paneling" as the television set.

The description of the television set is "It must be a pretty old set, based on the faux wood paneling on the sides."

The note of the television set is "'For some reason, we never actually watch TV on the TV in the youth room. We just play video games. I guess it's because on TV you never know if you're gonna see something sinful, like a lady in a bikini, or a tattoo.'[paragraph break]Underneath this, Casey wrote 'Speaking of bikinis:' and then erased it."

Instead of taking the television set,
	say "Far too heavy."

The television set is switched off.
Instead of switching on the television set,
	say "You have better things to do than watching television right now."

The Super Nintendo Entertainment System is on the trolley.
Understand "snes" as the Super Nintendo Entertainment System.
The printed name of the Super Nintendo Entertainment System is "SNES".
The Super Nintendo is not contents-mentionable-in-description.

The description of the Super Nintendo Entertainment System is "A thin layer of dust on the power switch[if Bubsy is in the Super Nintendo] and the Bubsy cartridge in the slot[end if] suggests that the SNES hasn't been used for a while."

After taking the Super Nintendo when the Super Nintendo was on the trolley:
	say "You unplug the SNES from the TV and pick it up."

After putting the Super Nintendo on the trolley:
	say "You plug the SNES back into the TV."

Instead of switching on the Super Nintendo Entertainment System, say "This is no time for video games[if the Super Nintendo Entertainment System contains the Bubsy cartridge]. Especially not Bubsy[end if]."

Instead of playing the Super Nintendo:
	try switching on the Super Nintendo.

Instead of playing Bubsy:
	say "This is no time for video games. Especially not Bubsy."

Instead of switching off the Super Nintendo:
	say "The SNES is already turned off."

Check inserting something into the Super Nintendo Entertainment System:
	if the noun is not the Bubsy cartridge:
		say "The slot of the SNES won't contain [the noun]." instead.

The note of the super nintendo entertainment system is "'We used to play nothing but Tetris Attack, all the time. But eventually it started to cause some strife among the youth group. So Patrick the youth leader took Tetris Attack away.'"

The Super Nintendo Entertainment System contains a Bubsy cartridge. Understand "busby" as Bubsy. The description of the Bubsy cartridge is "The front of the cartridge features a picture of a grinning, winking cat and the subtitle 'Claws Encounters of the Furred Kind'."

The note of the Bubsy cartridge is "'I don't even know what Bubsy is about. A squirrel? It doesn't even have a proper two-player mode. You have to wait and watch the other guy play. It's bad for the group. I miss Tetris Attack.'"

Tetris Attack is a thing.
Understand "panel de pon" as Tetris Attack.
It is familiar.
The note of Tetris Attack is "'Tetris Attack is this really great game that we used to play all the time in youth group. It isn't really anything like Tetris; it's actually a reskin of a Japanese game called Panel De Pon. How it works is, there are all these different colored squares, and you have to switch them around two at a time, and...'[paragraph break]Casey's notes on Tetris Attack continue for several pages. He's even included some diagrams. This is not information pertinent to your investigation."

Section - Black Light Make-Out Room

Blacklight is south of Youth. The printed name of Blacklight is "Black Light Room".

Blacklight is not light-switchable. [Because we have the lights-on message in the room description.]

Understand "black light" and "black-light" and "blacklight" and "make-out" and "make" and "out" and "makeout" as "[blacklight-room]".
Understand "[blacklight-room]" and "[blacklight-room] room" as Blacklight.

The description of Blacklight is "[if unvisited]You flip the light switch, but the only things that light up are your shirt and your socks.[paragraph break][end if]The black walls in here have been painted with Bible verses that glow underneath the UV lights. The couches are glowing, too; unsettlingly green. The door to the north, on the other hand, is barely visible.[paragraph break]Propped up in one corner is a rough wooden cross."

After looking in Blacklight when Blacklight encloses the black sphere and the black sphere is lit:
	say "The party light sphere casts flashing blue lights across the walls and ceilings, giving the room an irreligious resemblence to a nightclub.";
	continue the action.

After switching on the small button in Blacklight when the black sphere was comprehended:
	say "The party sphere lights up, casting flickering blue lights across the walls and ceilings. Now all you need are some glow sticks."

After switching off the small button in Blacklight:
	say "The party lights die out and the room returns to its usual eerie glow."

The note of Blacklight is "'Yes! This room! So, Patrick the youth leader got us together a few years ago and had us paint the walls of this room black, and then paint Bible verses on it, and then install black lights instead of normal lights, so it would look like a Christian rave or whatever. And I mean it looks fairly cool, I guess.[paragraph break]'But when you have a dark room, with purple lights, and a bunch of couches, and then you invite teenagers into the room and leave them in that room unattended, what happens is, the teenagers will use that room for what Patrick the youth leader calls [']heavy petting['].[paragraph break]'So that's why this room is called the Black Light Make-Out Room.'"

Carry out querying Casey's notes about Blacklight:
	now the printed name of Blacklight is "Black Light Make-Out Room".

Instead of exiting when the player is in Blacklight:
	try going north instead.

Some blacklights are scenery in Blacklight. Understand "uv" and "lights" and "light" and "black" and "black-light" and "black lights" and "black light" and "bulb" and "bulbs" as the blacklights.

The description of the blacklights is "Long fluorescent bulbs radiate a dim purple light. If you remember correctly, you shouldn't look directly at them."

Instead of querying Casey's notes about the blacklights:
	try querying Casey's notes about Blacklight.

Does the player mean doing something with the blacklights:
	it is likely.

Does the player mean doing something with the light switch when the location is Blacklight:
	it is unlikely.

Instead of switching off the blacklights:
	try switching off the light switch.

Instead of switching on the blacklights:
	try switching on the light switch.

The blacklight room couches are a couch in Blacklight.
Understand "stains" and "stain" and "glowing" as the blacklight room couches.

The description of the blacklight room couches is "The black lights reveal several glowing stains on the couches."

Some verses are scenery in Blacklight. The printed name of the verses is "verses painted on the walls". The verses are plural-named. Understand "bible" and "verse" or "verses" or "paint" or or "word" or "words" or "painted" or "on" or "walls" or "wall" as the verses.

The description of the verses is "There are maybe a dozen different verses, painted in different UV-sensitive colors, but one in particular demands your attention:[paragraph break][italic type]For the wages of sin is death, but the free gift of God is eternal life.[roman type][paragraph break]It should be 'The wages of sin [italic type]are[roman type] death.'"

The note of the verses is "'I had nothing to do with the verses in the black light room. After we painted the walls black, I wanted to reserve one wall for a big mural of a dolphin, and that idea got voted down, so I got mad and went home.'"

The rough wooden cross is scenery in Blacklight. The description of the wooden cross is "You have to guess that whoever constructed this cross purposefully chose the roughest, most splintery lumber available.[paragraph break]Around the center plank (which is twice as tall as you are), on top of the crossbar, is a 'crown of thorns' made from rusty barbed wire."

Instead of taking the rough wooden cross, say "The wooden cross is far too unwieldy, and splintery."

The note of the rough wooden cross is "'If you ask me I think that big gnarly cross in the black light room is in kind of poor taste, mostly because of the barbed wire crown on top. It doesn't belong in a church, it belongs in a grungy music video, with some rotting steak or something.[paragraph break]'I dunno, I feel like the torture of Christ isn't exactly radical? But I guess Patrick the youth leader likes it.'"

The rusty barbed wire crown of thorns is part of the rough wooden cross. The description of the crown of thorns is "The wire was probably stolen from the fence around a junk yard. Your jaw clenches involuntarily."

Instead of taking the crown of thorns, say "Even if you were tall enough to pull it over the top of the cross, you wouldn't want to carry that crown around with you."

Instead of querying casey's notes about the crown of thorns:
	try querying casey's notes about the rough wooden cross.

Section - Basement Stairway

Basement Stairway is a privately-named room.
Understand "stairway" and "basement stairway" as Basement Stairway.
Basement Stairway is west of Nursery and south of boiler and north of Sunday. [I don't want to call it a 'hallway' but this room basically only exists to connect rooms. maybe Basement Stairway is actually a good name for it]

Basement Stairway is backstory-visited and light-switchable.

The description of Basement Stairway is "This room is apparently just for show; there are no tables or chairs. A wide staircase runs up to the main floor of the church, and doorways lead [east], [south] and [north]. Mounted on the wall is a gigantic abstract painting."

The note of Basement Stairway is "'Man, I already wrote something about Gwen's painting. There's nothing else to say about the bottom of the stairs.'"

Instead of going up from Basement Stairway during Investigating the Basement:
	if the flickering lights are seen: [that means the player went into the hallway but not right to the end for some reason]
		say "You know, maybe you should take a closer look at those weird lights you saw before you go upstairs.";
	otherwise:
		say "Orchid said she'd investigate upstairs. You'd better finish checking out the basement before you go up and join her."

The gigantic abstract painting is scenery in Basement Stairway. Understand "Homeschooled Gwen" or "Gwen" or "Gwen's" or "Gwen's painting" as the gigantic abstract painting.

The description of the gigantic abstract painting is "The piece is very pleasant, if not particularly interesting: the inoffensive greens and pinks could suggest river waters, or branches waving in the wind. Its size is probably its most impressive quality."

The note of the gigantic abstract painting is "'I'll tell you about the big painting at the bottom of the stairs, but first I have to tell you about Homeschooled Gwen.[paragraph break]'Homeschooled Gwen, so called because she was totally homeschooled, went to our high school. Yes! She was not in fact [italic type]completely[roman type] homeschooled, because her parents sent her to West for seventh-period geometry, because they did not remember the quadratic formula. It is: x equals negative b plus or minus the square root of b squared minus 4ac, over 2a.[paragraph break]'But Gwen's parents couldn't remember that, even though I, Casey, memorized it so easily. So Gwen went to West High. And she was in my Geometry class.'"

go-on is a truth state that varies.

[I rewrote this code so that you wouldn't end up with a bunch of "if the player consents" indented halfway across the screen. I admit there are probably better things I could've been doing with my time. Anyway, you can now just stick the rest of the story in the Table of Gwen's Story and the code should deal with it. I included the thing where the last want-to-continue ignores the player's input, by the way.]
After querying Casey's notes about the gigantic abstract painting:
	say "You pause before flipping the page. This story barely seems relevant to the church, much less your investigation. Are you sure you want to keep reading? ";
	repeat with count running from 1 to the number of rows in the Table of Gwen's Story:
		say "[line break]>> [run paragraph on]";
		now go-on is whether or not the player consents;
		if go-on is true or count is the number of rows in the Table of Gwen's Story:
			say the instalment in row count of the Table of Gwen's Story;
			say line break;
		otherwise:
			say "You close the notebook to save the rest of the story of Homeschooled Gwen for another time.";
			rule succeeds.

Table of Gwen's Story
instalment
"You turn the page:[paragraph break]'So, due to being homeschooled, Gwen was not at all socialized for interacting with us teens of the modern era. What I am saying is that she was super weird. I was friends with her mostly out of morbid curiosity. Here are some of the weird stunts she pulled in Geometry: Told us all individually what breed of dog we would be if we were dogs. Read us stories, apparently like Wendy read to the Lost Boys. Gave us a survey about what we found attractive in girls.[paragraph break]'When I say [']us['] I mean just the boys in class. Gwen was super boy-crazy, because, due to being homeschooled all of her life, Gwen had never seen a boy before. Yes.'[paragraph break]This looks like it might be turning into an extended rant against the institution of homeschooling (Casey can be something of a jerk when it comes to things he knows nothing about). Do you want to continue?"
"Casey goes on:[paragraph break]'I mean she had probably seen a male human before. She had a dad; we found that out eventually. But you know what I mean.[paragraph break]'Anyway one day after school, after seventh-period geometry, I was hanging out in the mud room waiting for my ride home, and Gwen was there too, waiting for her own ride back to her own horrible school-home hybrid, and then [italic type]Patrick the youth leader[roman type] showed up, because he's still allowed on school grounds. And he asked me about the lock-in.'[paragraph break]It is increasingly clear to you that this story has no bearing on your investigation. Do you want to keep reading it?"
"You turn another page.[paragraph break]'Or he asked me about [italic type]something.[roman type] Probably the lock-in. But we were talking, and Gwen was there, and Patrick the youth leader WITHOUT CONSULTING ME BEFOREHAND looks over at Gwen and asks her if she wants to come to the lock-in. And of course Gwen says yes, because there will be boys there.[paragraph break]'Gwen came to the lock-in, and after that she started coming to the high school group meetings. And an unfortunate percentage of people in the group did not seem to catch on right away that Gwen was a nutbar, so she felt very welcome and emotionally supported by us, and when Patrick the youth leader opened the floor for [']discussion['] after his sermons we all got to listen to her insane opinions about Thanksgiving, and family togetherness, and sex, etc., etc.'[paragraph break]You look up from the notes. Maybe there's something else you should be doing. Do you want to continue reading?"
"'Then, this was around the end of last semester, Patrick the youth leader gave all of us in the group one of his CHALLENGES. His CHALLENGE to us this particular week was for us to think about our relationship to the church. I mean, like, [']spend the next seven days thinking about this, and come back next week with something really profound.['] A lot of Patrick the youth leader's CHALLENGES are basically attempts to get us to say something profound.[paragraph break]'And of course we all forgot about this CHALLENGE immediately, because come on. He never follows up on any of them anyway. He just read in some youth leader magazine that you should give your youth group CHALLENGES.[paragraph break]'And when we came back the next week, of course, Patrick the youth leader doesn't bother asking anyone what brilliant answers we came up with to his incredibly vague CHALLENGE. We had a sermon and then we played Sardines or whatever.[paragraph break]'But before we could leave, Patrick the youth leader called us all together, because Gwen had something to share with us.'[paragraph break]Keep reading?"
"'Now that I think about it, she must have brought the painting in with her, left it behind a couch or something, and waited for three hours or however long for Patrick the youth leader to ask for people's answers to his CHALLENGE of the previous week. She was just too freaked out and embarrassed to bring it up herself, until the very last possible moment.[paragraph break]'So yeah it was this painting. I'm not gonna bother describing this painting, because you can see it yourself. But if you think about it, all those tiny brushstrokes on a canvas that huge must have taken a zillion hours to do, and she supposedly only had a week to paint it. I guess that's what the church meant to Gwen.'[paragraph break]It goes on a little longer, but that feels like an end to the 'substance' of the story, such as it is. Should you bother reading the rest?"
"[if go-on is false]You are unable to master your curiosity and continue reading anyway.[paragraph break][end if]'Well, sometime during the summer, Gwen quit coming to the youth group. It took me a while to figure out what happened but it turns out she got [italic type]grounded[roman type] from [italic type]church[roman type] because her parents found out she had a [italic type]crush[roman type] on a [italic type]boy[roman type] who went there.[paragraph break]'So, that is the story on Homeschooled Gwen. You will never meet her, because her parents will never let her out of the house again.'"

Section - Boiler Room

Boiler is a privately-named room. The printed name of Boiler is "Boiler Room". Understand "boiler room" as Boiler.

Boiler is light-switchable.

The description of Boiler is "[if unvisited]All of a sudden you feel very claustrophobic. [end if]This must be a fairly large room, but most of the space is given over to snaking pipes, anonymous machinery, and the church's giant boiler."

The note of Boiler is "'I've only been inside of the boiler room very briefly, but I have to assume it's a really cool place to hang out. Make sure you tell me all about it when you get the chance.'"

Some pipes are scenery in boiler.
Understand "snaking" as the pipes.
The description of the pipes is "Many of the pipes run all the way down to the floor from their tangled maze in the ceiling, making some corners of the room impossible to reach."

Instead of searching the pipes, say "There are few gaps wide enough to stick your head through, and they only reveal more of the same scenery."

Instead of searching the pipes when the cat is in Hazy Place:
	say "A space leads north through the pipes, but you can't tell where it goes."

Instead of looking under the pipes:
	try searching the pipes.

Instead of entering the pipes:
	if the cat is in Hazy Place:
		try going north;
	otherwise:
		say "You can't find a gap in the pipes that's large enough to fit a human."

The note of the pipes is "'I believe that the boiler room has a lot of pipes. It stands to reason.'"

The anonymous machinery is scenery in boiler.
The anonymous machinery is ambiguously plural.
The printed name of the anonymous machinery is "machinery".
Understand "gauge" and "pressure gauge" and "valve" and "valves" and "gauges" and "fixture" and "fixtures" as the machinery.
The description of the machinery is "A menagerie of valves and pressure gauges stick out from the pipes at awkward angles, some dangerously close to your eye level."

Instead of turning or switching on or switching off the machinery:
	say "You don't know what any of this stuff does, and you probably shouldn't fiddle with it."

Instead of querying Casey's notes about the anonymous machinery:
	try querying the notes about the trueboiler.

The trueboiler is privately-named scenery in boiler.
The printed name of the trueboiler is "boiler".
Understand "boiler" and "giant boiler" as the trueboiler.
The description of the trueboiler is "A massive faceless hulk of an appliance. You can hear it murmuring quietly to itself[unicode 8212]or maybe you're hearing something else."

The note of the trueboiler is "'One thing I do know is that the church's boiler is incredibly inefficient. That's what certain adults in the congregation keep whining about, anyway.'"

Instead of touching something that is scenery in boiler, say "Your guess is that anything in here that doesn't burn your hand when you touch it will probably break immediately instead."

Instead of switching on the trueboiler, say "It's on right now, as far as you can tell."

Instead of switching off the trueboiler, say "If there is indeed a switch somewhere that deactivates the boiler, you have no idea where it is."

Instead of listening to boiler, say "Now you can only hear air currents: the whisper of an empty building at night."

Instead of listening to the trueboiler, say "As you perk up your ears to listen closer, the noise stops."

Instead of inserting something into the trueboiler, say "The boiler has no obvious orifices that could contain [the noun]."

Instead of opening the trueboiler: say "You can't find anything that looks like it would open."

Instead of searching the trueboiler: say "Even after a thorough looking-over, you have no idea how this thing works."

Instead of leaving when the player is in Boiler: try going south.


Section - Sunday School Room

Sunday is a room. The printed name of Sunday is "Sunday School Room". The lower case name of Sunday is "Sunday School room". Sunday is west of Youth.
Sunday is light-switchable.

Understand "sunday" and "sunday room" and "sunday school room" and "school room" and "school" and "schoolroom" as Sunday.

The description of Sunday is "This is a room designed for small people. Low benches are arranged like pews in two rows, facing toward a small table that serves as an altar. A poster explaining the Ten Commandments hangs on one wall, and doors lead [north], [east] and [west]."

The note of Sunday is "'The Sunday School room is a lot of fun for a variety of reasons, some of which I have to leave for other sections. But one very important thing to know is that the Sunday School room is a choke point between the storage hallway and the rest of the basement.[paragraph break]'This is very important to know for fire safety reasons, but also for when you're playing Romans and Christians, because the Roman can camp at that door and just pick off Christians as they come around the corner. This is one reason we've switched to playing Sardines instead, in recent years.'"

The small table is a scenery supporter in Sunday. Understand "doily" as the small table. Understand "altar" as the small table [while the location is Sunday]. [removed to improve performance]

The description of the small table is "As altars go, it is fairly tacky: sponge-painted to simulate age, draped with an excessively ornate doily."

The note of the small table is "'The little altar thing in the Sunday School room is a really horrible hiding place when you're playing Sardines. Even with the lights off, it's totally obvious if someone is under there. But, even though it's such a dumb place to hide, everyone always looks there first, which is important for certain meta-Sardines reasons that I won't get into here.'"

The hide description of the small table is "You crouch under the table for a second, feel ridiculous, and decide to exit."

The small white cross is on the small table.
The small white cross is crowbarish.

The description of the small white cross is "This is a nice little cross for a table. Nothing fancy."

The note of the small white cross is "'Man, when I was a little kid in Sunday School, and we had those little kid Bible lessons, I always used to stare at that little white cross, to pass the time or something. I'm pretty sure I was too smart for Sunday School. Sunday School is a weird thing to be too smart for, I guess.[paragraph break]'Although I guess when the high schoolers broke off to be their own mature group, that was basically the same thing. Kind of. In that situation it was because we were too cool for the junior high kids. But that's still really weird.[paragraph break]'What I mean is, it's really weird for people who identify as Christians to also identify as being too cool for something.'"

The bird cage is on the small table. Understand "birdcage" and "perch" as the bird cage.

The description of the bird cage is "The curving faux-wrought-iron bars and caked-on 'rust' identify this as one of the non-functioning, overpriced decorations which are so popular in craft stores. What does a Sunday School need with a bird cage?"

The note of the bird cage is "'Can't really tell you what the Sunday School bird cage is for; it showed up after I graduated. I think it might have something to do with like, Old Testament animal sacrifices? Back when you had to give the priest two of your doves instead of just praying for forgiveness?'"

Instead of searching the bird cage:
	say "The bird cage contains a convenient perch for a bird, but no bird."

Instead of opening or closing the bird cage:
	say "The bird cage seems to be welded closed. It must be purely decorative."

Instead of inserting something into the bird cage:
	try opening the bird cage.

Rule for printing room description details of the bird cage: do nothing.

Some low benches are scenery in Sunday. The low benches are an enterable supporter. The low benches are plural-named. Understand "bench" and "low bench" as the low benches. Understand "pew" or "pews" as the low benches [while the location is Sunday]. [removed to improve performance.]

The description of the low benches is "The benches only come up to about a foot off the ground, but they're longer than you are tall, and built from heavy unfinished pine."

The note of the low benches is "'You can use the benches in the Sunday School room for one of two things. Well, you can also sit on them. But you can use them for two other things:[paragraph break]'For one, there's just barely almost enough space to hide under one, if you're skinny, so they make a great Sardines hiding place because each next person who finds you can hide underneath a different pew and it technically counts as one [']spot['].[paragraph break]'For the other: You can't tell anybody this, but one time Gwen and I snuck in big branches from one of the trees outside and we jumped around on those benches and had an awesome swordfight. It was intense. When it was over with we were both panting and sweaty and there were pieces of bark all over the floor that we had to sweep up before Patrick the youth leader saw it.[paragraph break]'I guess I basically liked Gwen, even though she was super weird.'"

After entering the benches:
	say "You [if the chosen posture is sitting]sit down uncomfortably on[otherwise]step up onto[end if] one of the tiny benches."

After getting off the benches:
	say "You get off the low bench.";
	try looking.

The hide description of the low benches is "You roll underneath one of the benches. The sight of the unfinished wood underneath, as you lie with your arms at your sides, reminds you of the interior of a coffin. You crawl back out."

Instead of taking the low benches:
	say "The benches are surprisingly heavy for their size. You start to pick one up, almost drop it on your foot, and decide to leave it be."
	
Instead of pushing or pulling or turning the low benches:
	say "You straighten the rows of benches a little." 

The Ten Commandments poster is scenery in Sunday. The description of the Ten Commandments poster is "The Ten Commandments are retold here by little cartoon children, in language appropriate to the target demographic: 'Thou shalt not covet thy neighbour's house, thou shalt not covet thy neighbour's wife,' etc., becomes 'Don't be jealous of what other people have.'[paragraph break]As rusty as you are in this area, you can't remember which commandment is the one about adultery. It must be the seventh one, which is translated here as 'Be faithful and loyal.'"

The note of the Ten Commandments poster is "'Man, I know you're gonna have some issue with how that poster misconstrues the original meaning of the scripture or something. I am gonna tell you in advance to just calm down. Don't worry about it. Look for a ghost.'"

Instead of taking the Ten Commandments poster, say "The poster should probably stay up on the wall."


Section - Hallway

[Personally I like these all having the same name and being distinguished by different landmarks?]

Basementhall is a region. Hall1 is in basementhall. Hall2 is in basementhall. Hall3 is in basementhall. Hall4 is in basementhall.
Basementhall is in the EntireBasement.

A hallway is a kind of room. The printed name of a hallway is usually "Hallway".
A hallway is usually not light-switchable.

The faux-hall is a privately-named thing.
The printed name of the faux-hall is "basement hallway".
Understand "hall" and "hallway" and "basement" as the faux-hall.

The note of the faux-hall is "'The basement hallway, as you will notice, is super creepy, and this is because it's really long. There's a lot of room for somebody to be sneaking up behind you. Actually, make sure you don't read this particular note while you're standing in the hallway, because it'll freak you out if you do.'"

Carry out looking in a hallway when the flickering lights are unseen:
	now the flickering lights are seen.
	[I don't know why the lights don't get marked as seen automatically, but they don't. Sigh.]

Instead of querying Casey's notes about a hallway:
	try querying Casey's notes about the faux-hall.

A mural is a kind of thing.
A mural is usually scenery.
A mural is usually Biblical.
Understand "mural" and "mural of" and "wall" as a mural.
Understand "murals" and "walls" as the plural of a mural.

Before printing the name of a proper-named mural when photo-name-printing is true:
	say "the ".

After printing the name of a mural when photo-name-printing is true:
	say " mural".

The note of a mural is usually "'I volunteered to paint a mural in the basement, and Patrick the youth leader gave me permission, so I painted a mural of the Four Beasts (the man and the lion and the ox and the eagle) and it was a GREAT MURAL but once they saw it they went ahead and covered it up with JONAH.'"

The faux-mural is a mural. It is plural-named.
Understand "murals" as the faux-mural.
The printed name of the faux-mural is "murals".

First does the player mean querying about a mural (called art):
	if the art is the faux-mural:
		if the player is in Back Room:
			it is likely;
		otherwise:
			it is very likely;
	otherwise:
		it is very unlikely.
		
First does the player mean querying vaguely about a mural (called art):
	if the art is the faux-mural:
		if the player is in Back Room:
			it is likely;
		otherwise:
			it is very likely;
	otherwise:
		it is very unlikely.

Rule for clarifying the parser's choice of the faux-mural:
	do nothing.

Instead of taking or turning or pushing or pulling a mural:
	say "[The noun] [is-are] painted onto the wall."

Hall1 is a hallway. Hall1 is west of Sunday.
The description of Hall1 is "[hall1-desc]."
To say hall1-desc:
	say "The hallway runs north";
	if the west basement switch is switched off:
		say " into darkness. It's hard to tell how long it might be from here, though the Sunday School room to the east provides some light at this end. [one of]A huge figure looms out of the shadows[em dash]no, wait, it's just a plywood image of a lighthouse[or]The plywood lighthouse, on the other hand, peacefully fails to provide any light[stopping]. Behind the lighthouse and partially obscured by it is a mural of Jesus blessing the little children";
		if the flickering lights are on-stage:
			say ".[paragraph break]A movement catches your eye. Some tiny lights seem to be floating at the far end of the hall, flickering occasionally";
	otherwise:
		say ", past the plywood lighthouse, all the way up to the door to the tunnel. A doorway to the east leads into the Sunday School room, and on the wall opposite it is the mural of Jesus blessing the little children".
		
The plywood lighthouse is scenery in Hall1. Understand "light house" and "house" as the lighthouse. The description of the plywood lighthouse is "The piece of plywood is about eight feet tall and an inch thick. A minimum of detail has been painted on to convey that this tapered rectangle is supposed to be a lighthouse. There are no supports; it's just propped up against the wall."

Instead of switching on the plywood lighthouse, say "The plywood image is merely a symbol of a lighthouse, and can't shed any light of its own."

Instead of switching off the plywood lighthouse, say "The plywood lighthouse can't be turned off or on."

Instead of pulling or pushing or taking the plywood lighthouse, say "You get the feeling that if you tried to move this thing anywhere, it would fall on top of you and snap in half."

The note of the plywood lighthouse is "'The lighthouse symbolizes how the church is a beacon of guidance to the rest of the world. I don't mean my church specifically, I mean all of Christianity. Or at least Methodists.'"

Jesus is a mural in Hall1.
Jesus is male and neuter.
The description of Jesus is "As is traditional, Jesus is depicted with very long hair, a white robe and a beatific smile."

Instead of querying Casey's notes about Jesus when Hall1 is not visited:
	try consulting Casey's notes about "Jesus" instead.
	[this is a long-winded way of getting the "you can't find that in the notes" response, because looking up Jesus and getting the thing about the murals doesn't make sense before you've seen the Jesus mural]

Instead of kissing Jesus:
	say "You're pretty sure that kissing images of Jesus is not a United Methodist tradition."

The little children are a mural in Hall1.
The little children are plural-named and ambiguously plural.
Understand "blessing" as the little children.
The description of the little children is "Happy little children of various ethnic backgrounds are crowded around Jesus."

Last check going to Hall1 from Sunday when the west basement switch is switched off and Sunday is light-switchable:
	say "You flick the hallway light switch on, but nothing happens. In the end, you turn the Sunday School room lights back on so you can see what you're doing.";
	now Sunday is not light-switchable.
	
Instead of switching on the light switch when the location is a hallway and the west basement switch is switched off:
	say "You flick the light switch on and off a few more times. Nope, still nothing."

Instead of switching off the light switch when location is a hallway and the west basement switch is switched off:
	say "The lights are already off." 

Hall2 is a hallway. Hall2 is north of Hall1.
Hall2 is dimly lit.

The description of Hall2 is "[hall2-desc]."

To say hall2-desc:
	if the west basement switch is switched off and the location does not enclose the lit black sphere:
		if the flickering lights are on-stage:
			say "The tiny lights to the north continue to drift aimlessly. "; 
		say "Thanks to the light filtering in from the south end of the hall, you can make out some kind of mural painted on the wall, but you can't tell what it's supposed to represent in the gloom";
	otherwise:
		say "The hallway continues to run north and south. The wall here is taken up by a mural of a beached whale and a man lying on the sand".

The whale is a mural in Hall2.
Understand "beached" as the whale.
The description of the whale is "The plump blue whale is flicking its tail in the air in a cheerful sort of fashion. It doesn't seem at all bothered by having beached itself and vomited up a human."

Rule for printing the name of the whale when photo-name-printing is false and the west basement switch is switched off and the location does not enclose the lit black sphere:
	say "bluish blob".
Understand "bluish" and "blob" as the whale.

Jonah is a mural in Hall2.
Jonah is male and neuter.
Understand "man" and "lying" and "human" as Jonah.
The description of Jonah is "It looks like Jonah hasn't recovered from his ordeal in the belly of the whale yet."

Rule for printing the name of Jonah when photo-name-printing is false and the west basement switch is switched off and the location does not enclose the lit black sphere:
	say "the indistinct smear".
Understand "indistinct" and "smear" as Jonah.

The beach is a mural in Hall2.
Understand "sand" and "sea" as the beach.
Understand "yellow" and "blue" as the beach.
The description of the beach is "Bright yellow sand, bright blue sea. Clearly a tropical paradise."

Rule for printing the name of the beach when photo-name-printing is false and the west basement switch is switched off and the location does not enclose the lit black sphere:
	say "swathe of yellow".
Understand "swathe" and "of" and "yellow" as the beach.

Hall3 is a hallway. Hall3 is north of Hall2.
Hall3 is dimly lit.

The description of Hall3 is "[hall3-desc]."

To say hall3-desc:
	if the west basement switch is switched off and the location does not enclose the lit black sphere:
		if the flickering lights are on-stage:
			say "The flickering lights are only a little way to the north now. Otherwise, it's";
		otherwise:
			say "It's";
		say " almost completely dark. At least you can still see the light at the south end of the hallway. The rectangles to the east and west must be doorways to somewhere, but they're barely distinguishable from the walls";
	otherwise:
		say "A huge rainbow arches across the wall in this part of the hallway. Underneath it, a door leads west, with an identical door opposite on the east wall".

The huge rainbow is a mural in Hall3.
Understand "arch" as the rainbow.
The description of the huge rainbow is "The rainbow is painted in a bright, cartoony style, with thin black lines between each of the colors. Unfortunately whoever painted it put the indigo and violet stripes in the wrong order." [The rainbow on the window at our local Christian bookshop has this problem. I try not to look at it.]

Rule for printing the name of the huge rainbow when the west basement switch is switched off and photo-name-printing is false:
	say "wall".

Index map with Hall1 mapped west of Sunday.

Section - End of Hallway

[There's kind of a lot of stuff going on here so it seems neater for it to have its own section.]

Hall4 is a dark hallway. The printed name of hall4 is "End of Hallway". The lower case name of hall4 is "the end of the hallway". Hall4 is north of Hall3.

Understand "end of hall/hallway" and "end of the hall/hallway" and "hall/hallway end" as Hall4.

The description of Hall4 is "The hall ends here at a heavy door to the north. A huge mural of David fighting Goliath adorns the west wall, but it's marred by a streak of gray gunk clinging to the wall."

Rule for printing the description of a dark room when the location is Hall4 and the flickering lights are in Hall4:
	say "It is pitch dark, and you can't see a thing except for the flickering lights floating across the ceiling."

Rule for printing the description of a dark room when the location is Hall4:
	say "It is pitch dark, and you can't see a thing except for the dim light trickling in from the south end of the hallway."
	
Carry out going to Hall4:
	now Hall4 is visited. [even though it's still dark and entering a dark room usually doesn't count as visiting it]

Check going a direction in Hall4 when Hall4 is dark and the noun is not south:
	unless the room gone to is Tunnel and the Tunnel is visited:
		say "It's too dark to see where you're going. A dim light from the south end of the hallway shows you the way back, but if you wander around at random you're probably just going to walk into walls." instead.

The flickering lights are a backdrop. The flickering lights are plural-named and evidential. The flickering lights are in Basementhall.
Understand "tiny" and "floating" and "light" and "blue" as the flickering lights.

The description of the flickering lights is "The lights float across the ceiling like rings of blue smoke, blinking in and out to a frantic, inaudible rhythm."

The photo description of the flickering lights is "Blue lights trail across the photograph, bright against the darkness of the hallway."

Does the player mean doing something with the flickering lights: [as opposed to the light switches]
	it is very likely.

[The lights need to be visible in the darkness.]
After deciding the scope of the player when the location is Hall4 and Hall4 is dark:
	if the flickering lights are in Hall4:
		place the flickering lights in scope.

[And you need to actually be able to look at them and stuff, even though it's dark.]
Visibility rule:
	if the noun is the flickering lights:
		there is sufficient light.

Instead of doing something when the current action implies touching the flickering lights:
	say "The lights are insubstantial (you don't want to say 'ghostly[quotation mark]) [if the location is Hall4]and you can't reach them[otherwise]and they're floating further down the hall[end if]."

Check going north from Hall3 when the flickering lights are on-stage:
	if a photo shows the flickering lights:
		say "As you approach the lights, they flicker one more time and vanish, leaving you in complete darkness.";
		remove the flickering lights from play;
		continue the action.

After photographing the flickering lights with when Hall4 is visited:
	remove the flickering lights from play;
	say "A moment after the shutter clicks, the lights flicker one more time and vanish, leaving you in [if the location is not Hall4]almost [end if]complete darkness. The photo slides out of the camera and falls into your hand."

Instead of following the flickering lights:
	if the location is Hall4:
		say "The flickering lights are right here.";
	otherwise if the location is a hallway:
		try going north;
	otherwise:
		say "You are not aware of any such thing."

David is a mural in Hall4.
David is male and neuter.
Understand "west" as David.
The description of David is "David stands poised, ready to sling a stone at Goliath. The mural makes him look like a ten-year-old. You can't really remember the the details of the story now, but surely he'd have to be a bit older than that before he was allowed to do battle with giants."

The giant is a mural in Hall4.
The giant is male and neuter.
Understand "Goliath" and "west" as the giant.
The description of the giant is "Goliath is stooping slightly to avoid hitting his head on the ceiling of the hallway."

Rule for printing the name of the giant when the location is Hall4 and in darkness:
	say "wall".

Instead of touching the giant:
	let goop be a random ectoplasm in Hall4;
	try touching the goop.

One ectoplasm is in Hall4. [I love how ectoplasm is countable in this world]

After deciding the scope of the player when the location is Hall4 and Hall4 is dark:
	place the giant in scope;
	let goo be a random ectoplasm in Hall4;
	place the goo in scope.

[so the player can feel the wall in the dark. yes, this is weird]
[Understand "wall" and "walls" as an ectoplasm when the location is Hall4 and the item described is in Hall4 and in darkness.] [removed to improve performance]

Robin can be grossed out. Robin is not grossed out.
[She becomes grossed out on her first encounter with ectoplasm.]

Instead of touching an ectoplasm when the noun is in Hall4 and Hall4 is dark:
	if the player is grossed out:
		say "You run your hand across the wall until you find the patch of ectoplasm again, then rub it between your fingers. It's thick and gooey, with a slightly oily texture[if the player does not carry ectoplasm]. And now it's all over your hand again[end if].";
		if the player does not carry ectoplasm:
			let goop be a random off-stage ectoplasm;
			now the player carries the goop;
	otherwise:
		discover the ectoplasm at the end of the hall.

Rule for writing a paragraph about ectoplasm (called goo) when the goo is in Hall4:
	now the goo is mentioned.

Instead of going south from Hall4 for the first time:
	if the flickering lights are in Hall4:
		remove the flickering lights from play;
		say "As you turn to leave, the lights flicker one more time and vanish, leaving you in complete darkness. ";
	if the player is not grossed out:
		discover the ectoplasm at the end of the hall;
	otherwise:
		continue the action.

To discover the ectoplasm at the end of the hall:
	say "You run a hand along the wall to help you find your way, and your fingers trail through something cold and slimy. Ugh, now it's on your hand.";
	now the player is grossed out;
	let goo be random off-stage ectoplasm;
	now the player carries the goo.

Orchid can be glimpsed. Orchid is not glimpsed.
[Or it might be Aiden. Or Sharon sleepwalking. Or it might be a GHOST but codewise let's say it's Orchid.]

Every turn when in Hall4 and Orchid is not glimpsed and Hall4 is lit and Robin was in Tunnel:
	say "Echoing footsteps draw your attention to a figure walking down the south end of the hallway, but whoever it is goes into the Sunday School room before you can see their face.";
	now Orchid is glimpsed.

Section - Storage West

Storage West is a dark room. [Storage West is southwest of Hall3 and northwest of Hall2.]
The lower case name of Storage West is "the western storage room".
Understand "western" and "west" and "storage room" and "store room" and "storeroom" as Storage West.

The note of Storage West is "'There are two storage rooms, one on each side of the basement hallway. There is some neat stuff in those rooms, let me tell you.'"

[a manger, extra chairs, dozens of tiny hempen robes, big cloth signs for parade floats, tricycles, a 50s-era office desk—I will have to go back and check for more. Probably the reason these are locked is because they were such good hiding places for Sardines]
[shepherd's crook, notable absence of angel rig]

[The description of Storage West is an automagically generated list of all this stuff...]
A manger, a huge stack of plastic chairs, a tin of half-used crayons, a pile of tiny hempen robes, a shepherd's crook, some big cloth signs, a tricycle, an unopened packet of marshmallows, an exercise ball, and some paper streamer chains are in Storage West.
[I left the office desk out because I like the "Robin can only put things down in the manger" joke. This is probably not a good reason.]

The description of the manger is "This is clearly a manger from a Nativity scene[unicode 8212]some wisps of hay are still caught in the corners. But baby Jesus is nowhere to be seen."
The manger is a floor-standing container.
Understand "wisps" and "of" and "hay" as the manger.
Instead of taking the manger, say "It's not a huge manger, but carrying it around in both hands would be a pain."

Instead of entering the manger, say "As small as you are, the manger is smaller."

The manger is Biblical.

The note of the manger is "'So last year, Orchid said she wanted to run the Christmas play. And Marcie Kempulfers, who always runs the Christmas play every year, thought that was a great idea. She said it would be really cute to have a little girl be in charge of the play. This was last year, so Orchid was either fifteen or sixteen years old, I think.[paragraph break]'The first thing Orchid suggested was that they use a real baby to play the baby Jesus. Some church person had a baby, and Orchid, because she's completely a girl, was totally nuts for this baby. But Marcie says oh no, we always use a doll for the baby Jesus, real babies cause so many headaches.[paragraph break]'You can see where this is going. But I'll write more about it in some other entries. This is Entry One of Four.'"

The hempen robes are ambiguously plural.
Understand "robe" and "hemp" and "hempen robe" and "stack" and "stack of robes" as the hempen robes.
The hempen robes can be tidy. The hempen robes are not tidy.
Understand "tidy" as the hempen robes [when the hempen robes are tidy]. [removed to improve performance]
The description of the hempen robes is "The robes could only be worn by children, but they could represent any number of Bible characters. [if the hempen robes are tidy]They have been folded and stacked up neatly[otherwise]They've been tossed on the floor in a pitiable heap[end if]."

Rule for printing the name of the hempen robes:
	if the hempen robes are tidy:
		say "neat stack of hempen robes";
	otherwise:
		say the printed name of the hempen robes.

Instead of taking or pushing the hempen robes when the hempen robes are not tidy:
	say "You pick up the robes, but only to fold them neatly and put them back in a tidier pile.";
	now the hempen robes are tidy.

Instead of taking or pushing the tidy hempen robes:
	say "You can't bring yourself to disturb them again."	
	
The note of the hempen robes is "'So next Orchid says she wants the shepherds to sing a song. You have to understand that the shepherds are played by the littler kids, and the older kids play angels or Mary and Joseph or the wise men, and while every older kid ends up having to sing a song at some point, the shepherds do not. I think Orchid wanted them to sing [']Silent Night['], which is a pretty good choice for little kids to sing. I will admit that Orchid made a good call here.[paragraph break]'But Marcie did not agree. I'm pretty sure Marcie thinks that because it's impossible to get little kids to carry a tune or understand Italian music vocabulary, nobody wants to hear them sing in church, which is a critical misconception and I think indicates some sort of psychological defect in Marcie Kempulfers's brain. Anyway.[paragraph break]'Marcie made up this insane justification to have the bell choir play [']Silent Night['] instead. She said the bell choir had already practiced it, which was technically true, although specifically they had practiced it during the previous calendar year. But Marcie got her way.[paragraph break]'I hope you're not reading this as me defending Orchid, because I would never do that. Don't worry; we will get to the part where Orchid is horrible in the next entry. This is Entry Two of Four.'"

The description of the shepherd's crook is "A wooden pole with a large hook at one end. Presumably this is for the Christmas play. Judging by its size, the shepherds are played by children."
Understand "pole" and "wooden pole" and "hook" as the crook.
The shepherd's crook is crowbarish.
Instead of waving the crook, say "No matter how much you wave it, it's still just a crook."

The note of the Engelgurt is "'Well, Marcie shut down a couple more of Orchid's ideas, and Orchid eventually figured out her real problem, which was: she kept telling Marcie her ideas. If she was going to change anything about the Christmas program, she was going to have to do it behind Marcie's back.[paragraph break]'So she got her dad to build this crazy device, the Engelgurt, which used a system of rods and pulleys and winches to make someone glide across a room, like an angel.[paragraph break]'A couple nights before the Christmas program, Orchid and her dad came in and set up the Engelgurt while nobody else was around. I mean, I have to assume her dad was involved, although I don't remember him ever getting in trouble over it. Whatever.[paragraph break]'The night of the program came around and for a while everything went exactly as Marcie planned. Jesus was a doll, and the bell choir played [']Silent Night['], and so on. But in the middle of the angels singing [']Angels We Have Heard On High['], Orchid appears up in the balcony, in her non-regulation homemade angel robe, and starts floating down to Earth, and oh no I've run out of space! Cliffhanger! This is Entry Three of Four!'"

The note of the shepherd's crook is "'Okay. Just as she's about to land, Orchid's robe gets caught on one of the shepherd's crooks, and the Engelgurt suffers a critical failure and Orchid falls to the floor and according to some witnesses almost dies. The whole service gets shut down so that Orchid can go to the emergency room, although I think all that happened was she sprained her ankle.[paragraph break]'Of course, Orchid is in huge trouble with Marcie Kempulfers and with the church in general, but what does being in trouble with a church even mean? It's not like they can expel you. We're Methodists. They didn't even get rid of the Engelgurt; it's still in the storage room.[paragraph break]'And anyway Orchid wasn't sorry at all. She kept saying, [']At least it was new!['] [']At least it was exciting!['] Because that's how she operates.[paragraph break]'That's it. This is Entry Four of Four.'"








A storage door is a kind of door. A storage door is usually privately-named and scenery.
Understand "store" and "storage" and "store room" and "storage room" and "storeroom" and "storeroom's" and "room's" and "door" as a storage door.
[Understand "to" and "to the" as a door when the item described is in the location.] [removed to improve performance.]

Understand "doors" as the plural of a door.

[The last door is a storage door that varies.

Carry out going when the door gone through is a storage door:
	now the last door is the door gone through.] [removed to improve performance.]

Instead of querying Casey's notes about a storage door, try querying Casey's notes about Storage West.

The west storage room door is a storage door. It is west of Hall3 and east of Storage West.

Rule for printing the name of the west storage room door when the location is Storage West and the current action is looking:
	say "door leading out to the hall".
Understand "leading" and "out" and "to" and "the" and "hall" as the west storage room door.

[Rule for printing the name of the northwest storage door:
	if the location is Hall3 or the location is Storage West:
		say "door to the [if the location is Hall3]west[otherwise]northeast";
	otherwise:
		say "west storage room's northern door".
Understand "to west/w" and "to the west/w" as the northwest storage door when the location is Hall3.
Understand "northeast" and "ne" as the northwest storage door when the location is Storage West.
Understand "west" and "northern" as the northwest storage door when the location is not Hall3 and the location is not Storage West.
Instead of going west in Hall3: try going southwest.

The southwest storage door is a storage door. It is northwest of Hall2 and southeast of Storage West.
Rule for printing the name of the southwest storage door:
	if the location is Hall2 or the location is Storage West:
		say "door to the [if the location is Hall2]west[otherwise]southeast";
	otherwise:
		say "west storage room's southern door".
Understand "west" and "w" as the southwest storage door when the location is Hall2.
Understand "southeast" and "se" as the southwest storage door when the location is Storage West.
Understand "west" and "southern" as the southwest storage door when the location is not Hall2 and the location is not Storage West.
Instead of going west in Hall2: try going northwest.

Instead of going east in Storage West: try entering the last door.] [REMOVED TO IMPROVE PERFORMANCE]

Instead of leaving when the player is [directly] in Storage West: try going east.

Instead of going nowhere when in darkness and (the location is Storage West or the location is Storage East):
	say "You stumble about in the dark and [one of]stub your toe on[or]trip over[or]stumble into[or]bump into[or]bruise your hand on[at random] something."

Before going when in darkness and (the room gone to is Hall2 or the room gone to is Hall3) and (the room gone from is Storage East or the room gone from is Storage West):
	say "Your flailing about in the dark eventually discovers a door."

[move the doors to the end of the list of junk in Storage West]
Rule for writing a paragraph about something in Storage West:
	let junk be the list of things in Storage West;
	remove {walls, Robin, light switch, west storage room door} from the junk, if present;
	add {west storage room door} to the junk;
	say "You can see [junk with indefinite articles] here."

After printing the name of a container (called the item) in Storage West while looking:
	if something is in the item:
		say " (in which [is-are a list of things in the item])";
	otherwise:
		say " (empty)".



The description of the huge stack of plastic chairs is "Taller than you are. Looks wobbly."
The plastic chairs are ambiguously plural. Understand "chair" or "plastic chair" as the plastic chairs.
Instead of taking or pulling or pushing or turning or climbing or entering the plastic chairs, say "You'd probably knock the whole stack over if you tried that."
The note of the plastic chairs is "'The plastic chairs in the storage room are the flimsiest dang chairs you'll ever come across. I won't name any names, but I know somebody who stood on top of one of them and it flattened out like Bambi underneath him. They had to throw the chair away, but the guy I'm talking about refuses to feel guilty, because there's no way it was his fault.'"

The tin of half-used crayons is ambiguously plural.
The description of the crayons is "Some of the crayons are half-used. Some of them are more like three-quarters used, or even four-fifths used. The rest are broken."
Instead of inserting something into the crayons, say "The tin is already pretty much full."
The note of the crayons is "'Before a certain unnamed individual left the church, we used to have art activities in Sunday School all the time. It was awesome. But things change, and people change, and on and on. I think there are some crayons left in the storage room.'"


The indefinite article of the big cloth signs is "several".
Understand "several" as the big cloth signs.
The description of the big cloth signs is "You can see that there's text printed on them, but as they're rolled up, you can't read them."
Instead of opening the big cloth signs: say "There's no room to unroll them in here, and anyway you'd never be able to roll them up again."
Instead of taking the big cloth signs: say "You start to pick up one of the signs, but it slips through your hands and starts to unroll, so you quickly push it back into place."
The note of the big cloth signs is "We hang up the cloth signs in the sanctuary for Advent. I think they say [']Hope['], [']Wait['], [']Expect['], and [']Await['].'"

The description of the tricycle is "A pale blue toddler's tricycle, not unlike the one you had when you were a kid. Yours had streamers on the handles, but if this one ever did, they're long gone."
Instead of entering the tricycle: say "The tricycle creaks under your weight, so you quickly get off it again."
Instead of taking the tricycle: say "The tricycle is wedged in among all the other junk and you can't pull it free."
Instead of pushing or pulling or turning the tricycle: say "The tricycle is wedged in among all the other junk, and there's no room for it to move."
Instead of pushing the tricycle to: try pushing the tricycle.

Understand "ride [something]" as entering.

The note of the tricycle is "'When I was in preschool here, things were laid out differently, and the youth room was just this big empty room. And there were two tricycles. I don't know what happened to the other one.[paragraph break]'Anyway, me and another kid would ride the tricycles around the room in a circle, and we'd sing the theme song from COPS, which goes: [apostrophe]Bad boy, bad boy. Whatcha gonna do? Whatcha gonna do when they come for you?[apostrophe][paragraph break]'There are more words to the song, but that was the only part we knew, and we sang it over and over and over again. Actually, it's still the only part I know.'"

The marshmallows are ambiguously plural.
The marshmallows can be open.
The marshmallows can be openable.
The marshmallows are closed and openable.
The marshmallows can be tasted. The marshmallows are not tasted.
The description of the marshmallows is "The best before date is smudged, but judging by the [if the marshmallows are handled]hardness of the marshmallows[otherwise]layer of dust on the packet[end if] you suspect they're well past it by now."
The note of the marshmallows is "'I think I left some marshmallows in one of the storage rooms a while back. They're yours if you can find them.'"

After opening the marshmallows:
	now the printed name of the marshmallows is "packet of marshmallows";
	say "You tear the packet open. The marshmallows rattle against each other in a way that marshmallows never should."
	
Instead of closing the open marshmallows: 
	say "You fold down the top of the marshmallow packet and it promptly springs open again."

Instead of smelling the marshmallows:
	if the noun is open:
		say "Well, you can't [italic type]smell[roman type] anything wrong with them.";
	otherwise:
		say "You can't smell anything through the packet."

Instead of touching the marshmallows: say "The marshmallows are as hard as rock candy."

Instead of tasting or eating the marshmallows:
	if the marshmallows are not open:
		say "(first opening [the marshmallows])[command clarification break]";
		silently try opening the marshmallows;
	if the marshmallows are tasted:
		say "A second experiment hardly seems necessary.";
	otherwise:
		say "You pick one of the elderly marshmallows out of the bag and eye it suspiciously. It looks okay, but it feels more like a cookie than a marshmallow. Are you sure you actually want to eat it?[line break]";
		say ">> ";
		if the player consents:
			say "You pop the marshmallow into your mouth and crunch down on it.[paragraph break]Okay, that is definitely not how eating a marshmallow is supposed to go.";
			now the marshmallows are tasted;
		otherwise:
			say "Probably wisely, you return the marshmallow to the bag."

The exercise ball is floor-standing.
The description of the exercise ball is "A huge neon pink exercise ball. Some church member is into yoga?"
Instead of taking the exercise ball: say "It's too big, you'd drop everything else you're carrying."
Instead of pulling or pushing or turning the exercise ball: say "The ball doesn't roll very far before bumping up against some of the other junk in the room."
Instead of pushing the exercise ball to: try pushing the exercise ball.
The note of the exercise ball is "'When I was a little kid, that exercise ball was a serious object. Shoving that thing around could have some pretty drastic effects on the world around me. Like, I could knock over some things I could never have knocked over on my own.[paragraph break]'But now I'm a teen, and I'm way taller than that exercise ball, and it's about as inconsequential to me as any other object. I have no idea what I'm going to think about exercise balls when I'm an actual adult.'"
Instead of exercising in the presence of the exercise ball:
	say "You sit on the exercise ball and do a few crunches while breathing deeply and rhythmically. Feeling refreshed and energized, you stand up again."
Instead of entering the exercise ball when the chosen posture is sitting:
	try exercising.


The description of the paper streamer chains is "Colorful chains made out of paper streamers, like you might use as decorations for a kid's birthday party. The links are all different sizes, and there doesn't seem to be any pattern to the choice of colors. At a guess, you'd say the Sunday School children made these."
Understand "streamers" as the paper streamer chains.
Instead of taking the paper streamer chains, say "The chains are on the other side of a mountain of junk, far out of your reach."
The note of the paper streamer chains is "'If you're in the storage room and you see some paper chains, DON'T TOUCH THOSE CHAINS. Those chains are way too old to still hold together, and yet they do still hold together, because nobody has moved them or paid any attention to them in about fifteen years. Take nothing but pictures, Robin.'"




Section - Storage East

Storage East is a dark room. [Storage East is southeast of Hall3 and northeast of Hall2.]
The lower case name of Storage East is "the eastern storage room".
Understand "eastern" and "east" and "storage room" and "store room" and "storeroom" as Storage East.

Instead of querying Casey's notes about Storage East, try querying Casey's notes about Storage West.

The description of Storage East is "This place is a mess.[paragraph break][towers of ocd puzzle status]."

The east storage room door is a storage door. It is east of Hall3 and west of Storage East.

[Rule for printing the name of the northeast storage door:
	if the location is Hall3 or the location is Storage East:
		say "door to the [if the location is Hall3]east[otherwise]northwest";
	otherwise:
		say "east storage room's northern door".
Understand "east" and "e" as the northeast storage door when the location is Hall3.
Understand "northwest" and "nw" as the northeast storage door when the location is Storage East.
Understand "east" and "northern" as the northeast storage door when the location is not Hall3 and the location is not Storage East.
Instead of going east in Hall3: try going southeast.

The southeast storage door is a storage door. It is northeast of Hall2 and southwest of Storage East.
Rule for printing the name of the southeast storage door:
	if the location is Hall2 or the location is Storage East:
		say "door to the [if location is Hall2]east[otherwise]southwest";
	otherwise:
		say "east storage room's southern door".
Understand "east" and "e" as the southeast storage door when the location is Hall2.
Understand "southwest" and "sw" as the southeast storage door when the location is Storage East.
Understand "east" and "southern" as the southeast storage door when the location is not Hall2 and the location is not Storage East.
Instead of going east in Hall2: try going northeast.

Instead of going west in Storage East: try entering the last door.] [Removed to improve performance. LOOK AT THIS STUFF
NO DON'T LOOK AT THIS STUFF, JUST SKIP RIGHT OVER IT LIKE THE COMPILER, THANKS]

Instead of leaving when the player is [directly] in Storage East: try going west.

[this code is complete spaghetti, argh]
To say towers of ocd puzzle status:
	say "A series of rickety shelves mounted to the north wall have failed to put the room into any kind of order. ";
	[lowest shelf status]
	if nothing is on the lowest shelf:
		say "For some reason, there's nothing on the lowest shelf, even though there's stuff everywhere else. ";
	otherwise:
		let item be a random thing on the lowest shelf;
		say "On the lowest shelf is [an item]";
		say ". ";
	[cabinet status]
	say "Near the shelves is [a sturdy cabinet], maybe [base height of the sturdy cabinet in words] feet tall";
	if a non-player thing (called item) is on the cabinet:
		say ", on which is [an item]";
	say ". ";
	[amplifier status]
	say "In front of the cabinet and blocking the lower two-thirds of its door stands a huge amplifier";
	if a non-player thing (called item) is on the amplifier:
		say " with [an item] on it";
	[ark statuses]
	if an ark (called top ark) is on an ark (called bottom ark) and the bottom ark is in Storage East:
		say ". [A bottom ark] is sitting on the floor by the cabinet, with [a top ark] on top of it";
	otherwise if an ark is in Storage East:
		say ". [A list of arks in Storage East] [if two arks are in Storage East]are[otherwise]is[end if] sitting on the floor by the cabinet";
		if a non-player thing (called item1) is on Noah's Ark and a non-player thing (called item2) is on the Ark of the Covenant:
			say ". There's [an item1] on Noah's Ark and [an item2] on the Ark of the Covenant";
		otherwise if a non-player thing (called item) is on Noah's Ark:
			say ". There's [an item] on Noah's Ark";
		otherwise if a non-player thing (called item) is on the Ark of the Covenant:
			say ". There's [an item] on the Ark of the Covenant";
	[goo status]
	if the can of goo is on the shelves:
		say ". [paragraph break]You can just make out a container of some kind on the very top shelf, glistening strangely".

Before listing nondescript items of Storage East:
	now all steppers are not marked for listing;
	now all bulky things are not marked for listing.

Before printing a locale paragraph about the lowest shelf:
	now everything on the lowest shelf is mentioned.

[To create this puzzle, we will need to create a world in which supporters have relative height and can stack on top of each other and you can only climb a tall one if you're on a shorter one already etc. etc.

I am out of my depth] [-I wrote this comment next to my original worthless attempt at programming the Towers of OCD; obviously Emily figured it out just fine. The concept of the puzzle is still mostly my fault though.]

A thing can be bulky. A thing is usually not bulky.

After taking a bulky thing:
	say "You heft up [the noun]."

Check inserting a bulky thing into:
	if the second noun is the lowest shelf or the second noun is the wall shelves:
		try putting the noun on the lowest shelf instead;
	otherwise:
		say "[The noun] is too bulky to fit in [the second noun]." instead.

A stepper is a kind of supporter. A stepper is usually enterable. A stepper is usually portable. A stepper is usually bulky.

A stepper has a number called base height.

[I feel like this is drawing too much attention to itself at the moment. It might work better if we stopped Inform from putting it in a separate paragraph. Or it might be better to just write the info into the descriptions manually, like you were going to originally.]
After examining a stepper when the location is Storage East and the noun is not an ark:
	say "[The noun] [is-are] about [if base height of the noun is 1]a foot[otherwise][base height of the noun in words] feet[end if] tall";
	if the noun is on something (called the platform):
		say " on its own, but sitting on top of [the platform], it's more like [total height of the noun in words] feet";
	say "."

To decide what number is the total height of (platform - a supporter):
	if the platform is not a stepper:
		decide on 0;
	let h be the base height of the platform;
	if the platform is on a stepper (called the underthing):
		now h is h plus the total height of the underthing;
	[say "Considering [the platform]: its base height is [base height of the platform], its total height is [h], and it is in/on [the holder of the platform].[line break]";] [DEBUG]
	decide on h.
	[maths, yesssss. not to mention RECURSION. I LOVE RECURSION]

To decide what number is the total height of (platform - a room):
	decide on 0.

To decide what number is the reach:
	let r be 1;
	if the player is on a stepper (called platform):
		now r is r plus the total height of the platform;
	decide on r.

The entering action has an object called the previous location.

Setting action variables for entering:
	now the previous location is the holder of the player.

After entering a stepper when the location is Storage East:
	if the previous location is the noun:
		say "You [if the chosen posture is sitting]sit down to consider your options[otherwise]stand up, ready for action[end if]";
	otherwise:
		say "You climb [if the holder of the player is not a stepper or the total height of the noun is greater than the total height of the previous location]up[otherwise if the total height of the noun is less than the total height of the previous location]down[otherwise]across[end if] onto [the noun]";
		let n be the total height of the holder of the can of goo minus 1;
		unless n is greater than the reach:
			say ". You now have a clear view of the glistening container";
	say "."

Check putting it on:
	if the noun is a stepper and something is supported by the second noun:
		say "[The noun] is too large to fit on [the second noun] while [the list of things supported by the second noun] [if the number of things supported by the second noun is greater than 1]are[otherwise]is[end if] still on it." instead;
	if the second noun is a stepper and the second noun supports a stepper (called the encumbrance):
		say "There's no room for [the noun] on [the second noun] while [the encumbrance] is on [the second noun]." instead;
	if the second noun is a stepper and the second noun is on a stepper:
		say "Stacking [the noun] on [the second noun] on [the holder of the second noun]? On second thoughts, piling things that high seems a bit dangerous." instead.

Check entering a stepper:
	if the player is on the noun:
		if the posture of the player is the chosen posture:
			say "You're already [chosen posture] on [the noun]." instead;
		otherwise:
			continue the action;
	unless the total height of the noun <= 2 and (the player is in the location or the player is on something that is not a stepper):
		if the total height of the noun is greater than the reach:
			say "[The noun] [is-are] too [if the base height of the noun is 1]high up[otherwise]tall[end if] for you to climb up on top of [it-them] safely";
			if the player is on a stepper (called platform):
				say ", even though you're on [the platform]";
			otherwise:
				say " from your current position";
			say "." instead;
	if something is supported by the noun:
		say "There's [a list of things supported by the noun] on top of [the noun]; you'll have to clear it off before you climb up." instead.

Check entering a stepper:
	if the player carries a bulky thing (called the encumbrance) and the total height of the noun is greater than the total height of the holder of the player:
		say "You can't climb up onto [the noun] while you're holding [the encumbrance], it's too big and awkward." instead.

Check entering an enterable thing when the noun is not a stepper and the noun is on a stepper:
	say "You're not sure that it's safe to climb onto [the noun] while [it-they][if the noun is plural-named][']re[otherwise][']s[end if] on [the holder of the noun]." instead.

[The default handling of getting off is buggy w/r/t steppers - it moves you down onto the thing that the thing you're on is on. Can't be bothered making it move you down to the previous stepper instead, and anyway it would be kind of annoying for the player to have to climb all the way down all the steps. So I'm just making it so that GET OFF or DOWN or whatever just gets you straight down to the floor.]
Carry out an actor getting off a stepper:
	surreptitiously move the actor to the location;
	rule succeeds.

Report an actor getting off a stepper:
	say "You clamber down again.";
	try looking;
	stop the action.

Instead of going up while the location is Storage East:
	say "You'll have to be more specific about what you want to climb up onto."

Check examining the can of goo:
	if the noun is supported by a stepper (called the current-stepper):
		let n be the total height of the current-stepper minus 1;
		if n is greater than the reach:
			say "[The noun] is up on top of [the current-stepper]; you can't get a look at it from your current height." instead.

[Check photographing it with:
	if the noun is supported by a stepper (called the current-stepper):
		let n be the total height of the current-stepper minus 1;
		if n is greater than the reach:
			say "[The noun] is up on top of [the current-stepper]; you can't take a proper photo of it from your current height." instead.]
			
Last instead of doing something when the current action implies touching the can of goo:
	let the current-stepper be the holder of the noun;
	let n be the total height of the current-stepper minus 1;
	[say "The current-stepper is [the current-stepper]. Its base height is [the base height of the current-stepper], its total height is [the total height of the current-stepper], and its reachability is [n]. The reach is [the reach].";]
	if n is greater than the reach:
		say "[The noun] is up on top of [the current-stepper]; you can't reach it from your current [one of]position[or]height[or]altitude[then at random]." instead;
	otherwise:
		say "[The can of goo] is still far out of reach, but at least from here you can take a clear picture of it." instead.

[Check putting it on:
	if the second noun is a stepper:
		let n be the total height of the second noun minus 1;
		if n is greater than the reach:
			say "You can't reach up to [the second noun] from your current [one of]position[or]height[or]altitude[then at random]." instead.]

Check putting it on:
	if the second noun is the lowest shelf and the player is on a stepper (called platform):
		say "You can't reach the lowest shelf while you're on things." instead.

Check taking:
	if the noun is bulky and the player carries a bulky thing (called the encumbrance):
		say "It's too awkward to pick up [the noun] while you're still holding [the encumbrance]." instead.

Check taking:
	if the noun is a stepper and the player is on a stepper (called the platform):
		say "It's too awkward to pick up [the noun] while you're on [the platform]." instead.

Check taking:
	if something (called the encumbrance) is on the noun:
		say "It's too awkward to pick up [the noun] while [the encumbrance] is on it." instead.

Instead of pushing or pulling a portable stepper:
	say "If you need to move [the noun], you'd be better off picking [it-them of the noun] up and putting [it-them of the noun] where you want [it-them of the noun]."

Instead of jumping in Storage East:
	if the player is on a stepper:
		say "You feel that would be dangerous and probably fruitless.";
	otherwise:
		say "You can't jump high enough to see the glistening container properly."



An ark is a kind of stepper. The base height of an ark is 2. An ark is floor-standing.
An ark is usually Biblical.

Does the player mean entering an ark that supports an ark:
	it is very unlikely.

Does the player mean climbing an ark that supports an ark:
	it is very unlikely.

Does the player mean putting a held ark on an ark that is in the location:
	it is very likely.

Does the player mean putting an ark that is in the location on a held ark:
	it is very unlikely.

Does the player mean putting on a held ark:
	it is very unlikely.

Does the player mean taking an ark that supports something:
	it is very unlikely.

Instead of opening an ark: say "The top of the ark is screwed down tightly."

Instead of turning an ark: say "The ark wouldn't be stable if you set it on end."

The plywood Noah's Ark is an ark in Storage East. The description of the plywood Noah's Ark is "The whole thing is made out of plywood, but smaller planks of wood have been painted on the sides to give the vague impression of a giant, rectangular seafaring vessel. It's two feet tall, two feet deep, and maybe four feet long."
Understand "noah" and "noahs" as Noah's Ark.

The note of the plywood Noah's Ark is "'Patrick the youth leader isn't much of a carpenter. The extent of his abilities is building sturdy wooden cubes. Which is why Noah's Ark doesn't look anything like Noah's Ark.'"

The plywood Ark of the Covenant is an ark in Storage East. The description of the plywood Ark of the Covenant is "This wooden box has been spraypainted gold and detailed with glitter markers in an attempt to convey that this is an object of great value. It's two feet tall, two feet deep, and maybe four feet long."

The note of the plywood Ark of the Covenant is "'When we put together the Ark of the Covenant for that play, we were all excited about opening it up and pretending that our faces were melting off. It was hilarious. Patrick the youth leader told us to stop, because it was sacrilegious. But we didn't stop. So he screwed the lid shut. So the part of the play where they put the Ten Commandments inside the Ark had to be cut. But every other part of that play ended up getting butchered just as much, so.'"

The case labelled handbells is an openable closed container. It is bulky.
The case labelled handbells is on the plywood Ark of the Covenant.

The printed name of the case labelled handbells is "case labelled 'HANDBELLS'".

After reading a command:
	remove quotes;
	[if the player's command ends in a full stop, delete the full stop]
	let comm be indexed text;
	let comm be the player's command;
	replace the regular expression "\.$" in comm with "";
	change the text of the player's command to comm.

Understand "labeled" and "label" as the case labelled handbells.

The description of the case labelled handbells is "The case is made of tough black plastic."

Rule for printing room description details of the case labelled handbells:
	do nothing.

The note of the case labelled handbells is "'Here is the deal with the handbells. Marcie Kempulfers is in charge of the bell choir. She has a very strict handbell protocol. The handbells are church property, and she says so frequently as people try to goof off in bell choir practice.[paragraph break]'But Patrick the youth leader is not so uptight, and for this youth service he thought we should do a bell choir number, without any oversight by or permission from Marcie Kempulfers. He decided we should play [apostrophe]Seek Ye First[apostrophe], because he could only force an octave's worth of kids to volunteer, and he thought it was an easy song. And it is an easy song. But we were poorly motivated.[paragraph break]'So the youth service rolls around, and the extremely unofficial youth bell choir has yet to master our song. But we're listed in the program. We have to go out and perform, and when we perform this song we are going to butcher it.[paragraph break]'Then, right before the service starts, Marcie Kempulfers shows up to have words with Patrick the youth leader. The effects of these words were, first of all, we were definitely not going to be performing a non-Kempulfers-supervised handbell piece, ever, and secondly, now Marcie takes the handbells home with her whenever they aren't being used at church.[paragraph break]'I don't know why she doesn't take the case too.'"

After dropping the case labelled handbells:
	say "You gingerly set down the handbell case."

Instead of opening or searching the case labelled handbells, say "You open the case, revealing numerous handbell-shaped depressions in the velvet lining. No handbells here.[paragraph break]You close the case."

Instead of entering or climbing the case labelled handbells:
	say "The plastic case flexes dangerously under your weight and you think better of climbing on it."

Instead of pushing or pulling or turning the case labelled handbells:
	say "If you want to move the handbells case, you'd be better off picking it up and putting it where you want it."

The sturdy cabinet is a stepper in Storage East. The base height of the sturdy cabinet is 3.

The description of the sturdy cabinet is "A bulky, utilitarian cabinet, trapped and inaccessible behind the mammoth amplifier."

The note of the sturdy cabinet is "'The cabinet in the storage room has a bunch of really cheap art supplies inside. Or at least it used to. I haven't seen the inside of it recently. It has too many drawers to make a decent hiding place.'"

Instead of opening the sturdy cabinet, say "The amplifier is blocking the cabinet door, and you've got no hope of moving it."
Instead of closing the sturdy cabinet, say "It's already closed, and it looks like it's going to stay that way."
Instead of inserting something into the sturdy cabinet: try opening the sturdy cabinet.

Instead of taking or pushing or pulling or turning the sturdy cabinet, say "The cabinet is too big to carry and too heavy to push."

The amplifier is a stepper in Storage East. The base height of the amplifier is 2.
Understand "amp" as the amplifier.
The description of the amplifier is "This thing is much too big, and probably much too loud, for this church. That's probably why it's down here collecting dust."

The note of the amplifier is "'There's an amplifier in the east storage room from back when Minnie used to come here.[paragraph break]'I guess I'll put in a separate entry about Minnie, but as far as the amp, all you need to know is, if you break it, nobody will notice or care.'"

Instead of switching on the amplifier, say "It's not plugged in."

Instead of switching off the amplifier, say "The amplifier is already turned off, and it's not plugged in anyway."

Instead of taking or pushing or pulling or turning the amplifier, say "You can barely budge the amp[if taking], much less carry it around[otherwise]; it's much too heavy[end if]."

The wall shelves are a scenery stepper in Storage East. The wall shelves are plural-named. The base height of the wall shelves is 6.
Understand "top" and "top shelf" as the wall shelves.

The description of the wall shelves is "A series of flimsy shelves have been mounted into the wall here. For the most part, their role as an organizational aid has been ignored."

The note of the wall shelves is "'Once, when I was a stupid young thing, I tried to climb up the shelves in the east storage room.[paragraph break]'Actually, I don't think I can write down exactly what happened. Don't try to climb up those shelves, Robin.'"

The note of the lowest shelf is "'Once, when I was a stupid young thing, I tried to climb up the shelves in the east storage room.[paragraph break]'Actually, I don't think I can write down exactly what happened. Don't try to climb up those shelves, Robin.'"

Instead of climbing the wall shelves, say "The shelves wouldn't support your weight."

Instead of climbing the lowest shelf, say "The shelves wouldn't support your weight."

Instead of putting something on the wall shelves:
	try putting the noun on the lowest shelf.

Instead of entering when the noun is the wall shelves or the noun is the lowest shelf:
	say "The shelves wouldn't support your weight."

The can of goo is an undescribed evidential thing on the wall shelves. Understand "glistening" and "container" as the can of goo.
The description of the can of goo is "It looks a lot like a paint can, but trails of viscous grayish goop are running down the sides. What printing you can see on the label is in the 'avoid contact with eyes, harmful if swallowed' vein."

The photo description of the can of goo is "Trails of glistening grayish goop run down the sides of the can and dribble onto the shelf beneath it."

Rule for printing the name of the can of goo when the player does not have evidence of the can of goo:
	let n be the total height of the wall shelves minus 1;
	if n is greater than the reach:
		say "glistening container";
	otherwise:
		say "[printed name of the can of goo]".

Instead of smelling the can of goo:
	say "You can't smell anything at this distance."

Instead of opening the can of goo:
	try touching the can of goo. [to get the "you can't reach" message]

Check photographing the can of goo with:
	let n be the total height of the wall shelves minus 1;
	if n is greater than the reach:
		say "You can't get a decent shot of whatever it is from down here. You'll need to get a bit closer." instead.
	
Does the player mean photographing the can of goo with:
	let n be the total height of the wall shelves minus 1;
	if n is greater than the reach:
		it is possible;
	otherwise:
		it is very likely.

Instead of taking the can of goo with the crook:
	let the current-stepper be the holder of the noun;
	let n be the total height of the current-stepper minus 1;
	[say "The current-stepper is [the current-stepper]. Its base height is [the base height of the current-stepper], its total height is [the total height of the current-stepper], and its reachability is [n]. The reach is [the reach].";]
	if n is greater than the reach:
		say "[The noun] is up on top of [the current-stepper]; even if you could reach it with the crook, the most you could do is knock it off onto the floor and make a huge mess." instead;
	otherwise:
		say "You can reach the can with the crook, sure, but the most you could do is knock it off onto the floor and make a huge mess. Maybe you should just leave it where it is."

First instead of hooking the can of goo with the crook:
	try taking the can of goo with the crook.

Instead of opening the can of goo with the crook:
	try taking the can of goo with the crook.

Instead of throwing something at the can of goo:
	say "All you could achieve by that is knocking [the can of goo] onto the floor and making a mess."

The lowest shelf is a scenery supporter in Storage East.
Understand "lower" and "low" and "bottom" as the lowest shelf.

The description of the lowest shelf is "It's weird that this shelf [if something is on the lowest shelf]had[otherwise]has[end if] been left empty, considering how much stuff is piled up everywhere else in the room."

Instead of putting a bulky thing on the lowest shelf when a bulky thing (called base) is on the lowest shelf:
	say "There's no room for [the noun] on the shelf while [the base] is there."

Instead of putting a bulky thing on something that is on the lowest shelf:
	say "The wall shelves don't leave enough space for you to put [the noun] on [the second noun]."

Instead of entering something when the noun is on the lowest shelf:
	say "[The noun] is on the shelf, and the shelf would hardly support your weight alone, never mind [the noun] as well."

Chapter - Upstairs

Section - Main Floor Stairway

Main Floor Stairway is a room. Main Floor Stairway is above Basement Stairway.

Main Floor Stairway is backstory-visited.

The description of Main Floor Stairway is "This room forms a sort of nook off the fellowship hall to the south. A wide flight of stairs leads down to the basement. There are doors to the north, northeast and southeast; according to the plaques on them, they open onto the pastor's office, the men's restroom and the ladies['] restroom respectively.[paragraph break]A vast framed poster is hanging on the wall in between the restroom doors, and in the middle of the room, directly facing the poster, is a wooden bench."

The note of Main Floor Stairway is "'There's an important philosophical difference between the top of the main stairway and the bottom of the main stairway. At the top, there's a poster of an eagle, but at the bottom, there's a painting that Gwen made. They're like opposites of each other. Kind of?'"

[north of here is the unopenable door to the pastor's offices. or, I want it to be unopenable (so we don't have to IMPLEMENT A BORING OFFICE) but maybe we need to make it openable just so people won't get mad at us after spending two hours trying to get inside. I DON'T KNOW

K I have an idea: attempting to open the office door gives you a series of any-key-to-continue messages (MY FAVORITE JOKE, ALWAYS FUNNY):

you open the door/the alarm goes off/police come/you are arrested/you are suspended/you don't get into college

then it finally turns out that this is just what went through your mind as you considered breaking into the office.]

The pastor's door is scenery in the Main Floor Stairway.
Understand "pastor" and "office" and "handle" and "plaque" as the pastor's door.
Understand "plaques" and "doors" as the plural of the pastor's door [when the location is Main Floor Stairway]. [*DEL]

The description of the pastor's door is "The door to the north is no more impressive than any other you've seen, but the plaque on it reads, 'Pastor Ivan Shearer.'". [Last name via http://www.namegenerator.biz/last-name-generator.php. I Googled "random surname" and Google obliged. Which is to say, there is nothing meaningful about the name whatsoever.][OH BUT IT'S SO GOOD: 'Shearer' -> shearing sheep -> shepherd -> pastor. IT'S SO GOOD.]

The pastor's door is not comprehended.
The pastor's door has a number called the fear.
The fear of the pastor's door is 0.

Understand "unlock [something]" as opening.

Instead of opening or entering or attacking or pushing or pulling the pastor's door:
	if the pastor's door is not comprehended:
		try going north;
	otherwise if the fear of the pastor's door is 0:
		say "You rattle the door handle, gently at first and then harder. Nothing happens.[paragraph break]";
		wait for any key;
		say "...and then the alarm starts screaming at you.[paragraph break]";
		wait for any key;
		say "Minutes later, flashing lights and sirens herald the arrival of the police.[paragraph break]";
		wait for any key;
		say "You are taken away in handcuffs.[paragraph break]";
		wait for any key;
		say "After Sharon's report to the principal, the school suspends you. The West High [italic type]Warbler[roman type] stands you down from its staff.[paragraph break]";
		wait for any key;
		say "No college will accept a delinquent like you. You end up finding work as janitor at West High. The teachers tell new students your story as a warning.[paragraph break]";
		wait for any key;
		say "...and then you are back in the present. You let go of the door handle and back away hastily.";
		increment the fear of the pastor's door;
	otherwise:
		say "No way. Nuh uh. Not happening."

Instead of going north in Main Floor Stairway:
	say "The door to the pastor's office is locked.";
	now the pastor's door is comprehended.

Instead of knocking on the pastor's door:
	say "No one answers. The pastor would be at home or something at this hour."

The note of the pastor's door is "'You used to be able to get into the pastor's office all the time, back when Patrick the youth leader had his own sub-office at one end of the room where he had free candy. But that was a security risk apparently, so they locked up the pastor's office and Patrick the youth leader had to move his office to his truck.'"

The eagle poster is scenery in the Main Floor Stairway.
Understand "vast" and "framed" and "frame" and "print" and "bible" and "verse" as the eagle poster.
The description of the eagle poster is "The poster is a vast glossy print of a bald eagle gliding through a bright blue sky. In a corner of the sky, a Bible verse is printed: 'But they that wait upon the LORD shall renew their strength; they shall mount up with wings as eagles. Isaiah 40:31.'".

The note of the eagle poster is "'The fact that it's a bald eagle in that poster makes it pretty clear that whoever designed that poster wanted that Bible verse to apply to the people of the United States of America somehow. Which makes me wonder, are there any other countries that insist God loves them the most? Because God can only love one country the most. That's what [']the most['] means.'"

Instead of taking the eagle poster:
	say "It's really too big to carry around with you."

The wooden bench is an enterable scenery supporter in the Main Floor Stairway.
Understand "heavy" and "timber" and "rustic" and "beams" as the wooden bench.
The description of the wooden bench is "The bench seems to have been made in a deliberately rustic style, with heavy timber beams. It doesn't look very comfortable."

The note of the wooden bench is "'If you really hate yourself, you can sleep on the bench at the top of the stairs, which I think we inherited from some other church that used it as a torture device during the Inquisition.'"

Instead of taking or pushing or pulling or turning the wooden bench:
	say "The bench is just as heavy as it looks."

The usual posture of the wooden bench is sitting.
After entering the wooden bench when the chosen posture is sitting:
	say "You sit down on the bench and regard the eagle poster thoughtfully."
	
The wide flight of stairs is a backdrop.
It is in Main Floor Stairway and Basement Stairway.
Understand "stairway" and "main stairway" and "staircase" as the wide flight of stairs.
The description of the wide flight of stairs is "The flight of stairs is wide enough for three or four people to walk side by side on it. The basement must get a lot of traffic sometimes."

The note of the wide flight of stairs is "'The main stairway is also the only stairway in the church that isn't completely awkward or hazardous.'"

Instead of entering or climbing the wide flight of stairs:
	if the location is Basement Stairway:
		try going up;
	otherwise:
		try going down.

Section - Restrooms

A restroom door is a kind of door. A restroom door is usually scenery.
Understand "door" and "restroom" and "rest" and "room" and "bathroom" as a restroom door.
Understand "plaques" as the plural of a restroom door [when the location is Main Floor Stairway].

First carry out going when the door gone through is a restroom door:
	say "The door swings shut behind you."

Check an actor going (this is the new can't go through closed doors rule):
	if the door gone through is not nothing and the door gone through is closed:
		if the actor is the player and the door gone through is a restroom door:
			continue the action;
		issue library message going action number 28 for the door gone through;
		silently try the actor opening the door gone through;
		if the door gone through is open, continue the action;
		stop the action.
	
The new can't go through closed doors rule is listed instead of the can't go through closed doors rule in the check going rules.

The mandoor is a restroom door.
It is southwest of Mens and northeast of Main Floor Stairway.
The printed name of the mandoor is "men's room door".
Understand "men's" and "man's" and "mens" as the mandoor.

The description of the mandoor is "[if the location is Main Floor Stairway]The plaque on the northeast door says 'MEN[quotation mark], and you can guess where it leads[else]From this side the door looks unremarkable[end if]."

The note of the mandoor is "'If you have time, you should go ahead and take a look in the men's room, since who knows how many opportunities like this you'll have in the future. I won't spoil anything by describing it here.'"

Steeling is a number that varies. Steeling is 0.

Check going in Main Floor Stairway when the door gone through is the mandoor:
	if Mens is visited:
		continue the action;
	otherwise:
		if steeling is 2:
			say "That's it. Enough with this cowardice.";
			continue the action;
		otherwise if steeling is 1:
			increase steeling by 1;
			say "You can put your hand on the handle, but that's as far as you get. You chide yourself for your indecision." instead;
		otherwise:
			increase steeling by 1;
			say "As you approach the door to the men's room, you stop yourself. Isn't this something that a creep would do?" instead.
			
Instead of opening the mandoor:
	if Mens is visited or steeling is greater than 1:
		say "[if the location is Main Floor Stairway]You push the door ajar[otherwise]You pull the door open[end if], and it swings closed." instead;
	otherwise:
		try going northeast instead.

Before saying no or saying yes when the location is Main Floor Stairway and steeling is 1:
	[say "The last thing you did was [the last action of the player]!";]
	if the last action of the player is going northeast or the last action of the player is opening the mandoor or the last action of the player is entering the mandoor:
		if saying no:
			say "Okay, maybe not, but it still feels pretty weird." instead;
		otherwise if saying yes:
			say "It's totally creepy. How could you bring yourself to sneak into a men's bathroom? Even if no one will ever know?" instead.



Mens is a room. The printed name of mens is "Men's Restroom".

Understand "men's" and "man's" and "male" as "[men's]".

Understand "[men's]" and "mensroom" and "restroom" and "bathroom" and "rest" and "rest room" and "[men's] room" as Mens.

The note of Mens is "'If you have time, you should go ahead and take a look in the men's room, since who knows how many opportunities like this you'll have in the future. I won't spoil anything by describing it here.'"

The description of Mens is "[if unvisited]Yes, this is more or less what you expected.[paragraph break][end if]There are some stalls, and some mirrors, and some sinks[if the garbage can is in Mens], and next to those is a garbage can[end if]. The only things you haven't seen before are the urinals[if unvisited]. Hardly worth having an ethical dilemma over[end if]."

Instead of smelling Mens, say "The atmosphere in here is thick with air freshener."

Instead of leaving when the player is in Mens: try going southwest.

The men's room stalls are scenery in Mens. Understand "stall" as the men's room stalls. They are plural-named. The description of the men's room stalls is "They look like restroom stalls you'd see anywhere else."

The note of the men's room stalls is "'I was volunteering at Vacation Bible School,' he begins, but that's as far as you'll let yourself read."

Instead of searching the men's room stalls, say "You give each stall a very quick once-over. Nothing to see here."

Instead of entering the men's room stalls, say "You give each stall a very quick once-over. Nothing to see here."

Instead of opening the men's room stalls, try searching the noun.

The men's room toilets are scenery in Mens. They are plural-named. Understand "toilet" as the men's room toilets. The description of the men's room toilets is "These are standard toilets. No ghosts."

The note of the men's room toilets is "'I was volunteering at Vacation Bible School,' he begins, but that's as far as you'll let yourself read."

Instead of searching the men's room toilets, say "You decide a close examination is unnecessary. Everything is probably just fine in there."

The urinals are scenery in Mens. They are plural-named. Understand "urinal" as the urinals. The description of the urinals is "It's not a concept that you would ever have come up with yourself, but the general principle seems sound."

The note of the urinals is "'The thing about the urinals in the men's room is, there are no divider walls in between them. I don't like it, Robin. I don't like it at all.'"

The men's room sinks are a sink in Mens. They are plural-named. The description of the men's room sinks is "The sinks are typical sinks."

The note of the men's room sinks is "'One thing you won't be able to notice about the men's room: They used to keep cleaning supplies under the sinks. They don't anymore, because little kids go in there and it was a huge health hazard. That's all I've got.'"

The men's room mirrors are a mirror in Mens.
The men's room mirrors are scenery and plural-named.

The note of the men's room mirrors is "'If you have time, you should go ahead and take a look in the men's room, since who knows how many opportunities like this you'll have in the future. I won't spoil anything by describing it here.'"

The men's room garbage can is an undescribed flippable supporter in Mens. Understand "trash" and "bin" and "wastebasket" as the men's room garbage can.

Instead of examining the men's room garbage can, say "It looks like a normal garbage can[if the inner space of the men's room garbage can contains the glowing tissue paper], except something inside is glowing softly[end if]."

The note of the men's room garbage can is "'If you have time, you should go ahead and take a look in the men's room, since who knows how many opportunities like this you'll have in the future. I won't spoil anything by describing it here.'"

Carry out dropping the men's room garbage can in Mens:
	now the noun is undescribed.

A glowing tissue paper is a thing.
Understand "crumpled" and "crumpled tissue" and "kleenex" as the glowing tissue paper.
Understand "something" and "thing" and "glow" as the glowing tissue paper.
The description of the glowing tissue paper is "This tissue was used to wipe up an eerie glowing substance, then thrown in a garbage can."
The glowing tissue paper is evidential.
The indefinite article of the glowing tissue paper is "some".

Visibility rule:
	if the noun is the glowing tissue paper:
		there is sufficient light.

When play begins:
	now the glowing tissue paper is in the inner space of the men's room garbage can. 

Does the player mean doing something with the glowing tissue paper:
	it is likely.

After taking the glowing tissue paper:
	say "You pick up the tissue by one corner, using only the very tips of your fingers."

Instead of smelling the glowing tissue paper:
	say "The tissue has a flat, stuffy scent that makes you want to sneeze."

The photo description of the glowing tissue paper is "Something about the lighting in the photo makes it hard to tell that the tissue paper was glowing, but the tissue has an unnatural greenish tinge to it."




Ladies is a room. The printed name of ladies is "Ladies['] Restroom".
Understand "ladies'" and "ladies" and "ladie's" and "lady's" and "women's" and "woman's" as "[women's]".
Understand "[women's]" and "restroom" and "rest" and "rest room" and "[women's] room" and "bathroom" as Ladies.

The womandoor is a restroom door. The womandoor is southeast of Main Floor Stairway and northwest of Ladies. The printed name of the womandoor is "ladies['] room door". Understand "ladies" and "ladies'" and "ladie's" and "women's" and "woman's" as the womandoor.

The description of the womandoor is "[if the location is Main Floor Stairway]The plaque on the northeast door says 'LADIES[quotation mark], and you can guess where it leads[else]From this side the door looks unremarkable[end if]."

The note of the womandoor is "'Well, Robin. I actually explored the ladies['] restroom briefly, during a game of Sardines. But then, because of events that transpired, the ladies['] restroom was declared to be off-limits. And shortly after that, because of fairness, the men's restroom was declared to be off-limits as well. For Sardines purposes at least.'"

Instead of querying Casey's notes about the womandoor, try querying the notes about Ladies.

Instead of opening the womandoor, say "[if the location is Main Floor Stairway]You push the door ajar[otherwise]You pull the door open[end if], and it swings closed."

The note of Ladies is "'Well, Robin. I actually explored the ladies['] restroom briefly, during a game of Sardines. But then, because of events that transpired, the ladies['] restroom was declared to be off-limits. And shortly after that, because of fairness, the men's restroom was declared to be off-limits as well. For Sardines purposes at least.'"

Sardines is a familiar thing. Understand "sardine" as sardines. The note of Sardines is "'Sardines is this awesome game we Christian Youth play instead of Romans and Christians now. In Sardines, a person hides somewhere and then everyone else looks for that person and when you find them you have to get in the hiding place and hide with them. It is fantastic. It's a lot less hectic than Romans and Christians.'"

Romans is a familiar thing. Understand "Romans and Christians" and "Christians" as Romans. The note of Romans is "'Romans and Christians is the game we used to play at church. The rules were, you had two Romans, and everyone else was the Christians, and the Christians just ran around the church in the dark while the Romans tried to catch them and take them to the Jail, which was a couch. If there are any other rules then I'm forgetting them.[paragraph break]'The excitement of being actively chased around the church caused a lot of Christians to kinda lose their heads and occasionally break stuff, so we started playing Sardines instead.'"

The description of Ladies is "A restroom in a church is about the same as any other restroom. You have your stalls, your sinks, your mirrors[if the ladies' room garbage can is in Ladies], your garbage can[end if]."

Instead of smelling Ladies, say "The atmosphere in here is thick with air freshener."

Instead of leaving when the player is in Ladies: try going northwest.

The ladies' room stalls are scenery in Ladies. The printed name of the ladies' room stalls is "ladies['] room stalls". Understand "stall" as the ladies' room stalls. They are plural-named. The description of the ladies' room stalls is "They look like restroom stalls you'd see anywhere else."

The note of the ladies' room stalls is "'To be honest Robin I did not get a good look at much of anything in the ladies['] room.'"

Instead of searching the ladies' room stalls, say "You give each stall a very quick once-over. Nothing to see here."

Instead of entering the ladies' room stalls, say "You give each stall a very quick once-over. Nothing to see here."

Instead of opening the ladies' room stalls, try searching the noun.

Instead of querying Casey's notes about the ladies' room stalls, try querying the notes about the ladies' room toilets.

The ladies' room toilets are scenery in Ladies. The printed name of the ladies' room toilets is "ladies['] room toilets". They are plural-named. Understand "toilet" as the ladies' room toilets. The description of the ladies' room toilets is "These are standard toilets. No ghosts."

The note of the ladies' room toilets is "'To be honest Robin I did not get a good look at much of anything in the ladies['] room.'"

Instead of searching the ladies' room toilets, say "You decide a close examination is unnecessary. Everything is probably just fine in there."

The ladies' room mirrors are a mirror in Ladies.
The ladies' room mirrors are scenery and plural-named.

The note of the ladies' room mirrors is "'To be honest Robin I did not get a good look at much of anything in the ladies['] room.'"

The ladies' room garbage can is an undescribed flippable supporter in Ladies. The printed name of the ladies' room garbage can is "ladies['] room garbage can". Understand "trash" and "bin" and "wastebasket" as the ladies' room garbage can.

The note of the ladies' room garbage can is "'To be honest Robin I did not get a good look at much of anything in the ladies['] room.'"

Instead of examining the ladies' room garbage can:
	say "The cleaner must have been in recently, because the garbage can is [if something is in inner space of the ladies' room garbage can]almost [end if]empty[if something is in the inner space of the ladies' room garbage can]. All that's in it is [a list of things in the inner space of the ladies' room garbage can][end if]."

Carry out dropping the ladies' room garbage can in Ladies:
	now the noun is undescribed.

The ladies' room sinks are a sink in Ladies. They are plural-named. The description of the ladies' room sinks is "The sinks are typical sinks."

The note of the ladies' room sinks is "'To be honest Robin I did not get a good look at much of anything in the ladies['] room.'"

Instead of querying Casey's notes about the ladies' room sinks, try querying the notes about the ladies' room toilets.


Section - Dining Room

Dining is south of Main Floor Stairway. The printed name of Dining is "Fellowship Hall".
Understand "dining room" as Dining. Understand "fellowship hall" and "fellowship" and "hall" as Dining.

Dining is backstory-visited.

The description of Dining is "This room is a bit crowded for your tastes: between the many round tables, each surrounded by a complement of folding chairs, there isn't much space to get from one end of the room to the other. The main stairway is north, the kitchen is east, and the narthex is west."

The note of Dining is "'The fellowship hall is where we eat dinner at youth group, and it is also where the younger kids sit around and talk about their junior high problems while the older kids play video games downstairs. The church also uses it for other purposes but I do not know or care about that stuff.'"

The round tables are plural-named scenery in dining. The round tables are supporters. Understand "tabletop" or "table" or "tabletops" as the round tables. The description of the round tables is "The tabletops look tolerably clean, if chipped in places."

The hide description of the round tables is "Hiding under a table would require moving some chairs around, which would entail reorganizing every chair in the room, which would take time that you don't have."

The note of the round tables is "'The fellowship hall tables are a lot like the cafeteria tables at school, in that where you sit during dinner at youth group is determined by your clique and your clique's social standing and that kind of thing. I will get around to drawing up a chart someday.'"

The folding chairs are plural-named scenery in dining.
Understand "chair" or "folding chair" as the folding chairs.
The folding chairs are enterable supporters. [this is asking for trouble isn't it?]
The usual posture of the folding chairs is sitting.
The description of the folding chairs is "The chairs are clearly ancient, and some of them look like safety hazards. Even West High can afford nicer chairs than these."

The note of the folding chairs is "'Nobody likes the chairs in the fellowship hall. A bunch of us at youth group have started just sitting on the floor while we eat. Patrick the youth leader has condemned us as [']disrespectful['] but he hasn't forced us to stop yet.'"

Instead of taking the folding chairs:
	say "You feel the folding chairs are of little use to you in their advanced state of decay."

Instead of entering the folding chairs when the chosen posture is standing:
	say "The chair wobbles treacherously as soon as you put any weight on it, so you think better of actually standing on it."

Instead of entering the folding chairs when the player is on the folding chairs:
	say "You switch to sitting on a different chair, but it doesn't seem much safer than the previous one you tried."

After entering the folding chairs:
	say "You sit down cautiously on one of the chairs. It wobbles but does not collapse."

The glass case is a transparent closed container in dining. The initial appearance of the glass case is "One corner is given over to a glass case containing a model church." Understand "pedestal" and "cube" as the glass case.

The description of the glass case is "The case itself is mounted in a pedestal about three feet high. You notice that there are numerous fingerprints caked onto the glass."

Instead of photographing something with when the noun is the glass case or the noun is part of the glass case:
	try photographing the model church with the second noun.

Some fingerprints are part of the glass case.
The description of the fingerprints is "They're mostly children's fingerprints, but some of these were left by adults."

Instead of washing the fingerprints, try washing the glass case.
Instead of washing the glass case, say "Wiping the fingerprints around would only result in some even more unsightly smears."

Instead of taking the glass case, say "The case is far too heavy to move around, and the model looks like it could be very fragile."

Instead of pushing or pulling or turning the glass case, try taking the noun.

Instead of opening the glass case, say "It looks like the case wasn't designed to be opened."

Instead of querying casey's notes about the glass case, try querying casey's notes about the model church. Instead of querying casey's notes about the fingerprints, try querying casey's notes about the model church. Instead of querying casey's notes about something that is part of the model church, try querying casey's notes about the model church.

The glass case contains a model church. The description of the model church is "It can't be a model of this church; it's the wrong shape. There's an incredible level of detail, though, from the tiles in the roof to the tiny people on the sidewalk. The double doors in front are open, and you can barely make out the shapes of little pews inside.".

The note of the model church is "'The model church is a model of some other church. I think it used to be inside of that church, but it got torn down, or they went bankrupt, and the model ended up in our church. It's kind of a creepy object, though. The guy who made it was clearly way too obsessed with his church.'".

Does the player mean querying about the model church when the location is not Dining:
	it is unlikely.

Some roof tiles are part of the model church. Understand "tile" as the roof tiles. The description of the roof tiles is "The tiles are miniscule. You can only imagine someone gluing them in place with a pair of tweezers."

Some tiny people are part of the model church. Understand "person" or "tiny person" or "minister" as the tiny people. The description of the tiny people is "The people look like models from a train set. They are predominantly Caucasian."

The sidewalk is part of the model church. The description of the sidewalk is "The texture of the sidewalk might really just be paint sponged onto paper."

The double doors are part of the model church. They are plural-named. The description of the double doors is "The doors are held open by a tiny person in a smock. He must be the minister!"

Some little pews are part of the model church. The description of the little pews is "There really are tiny pews in there. How much more furniture is under that roof?"

Before searching the model church:
	try examining the little pews instead.

Before taking the model church, say "You can't get your hands on the model, since there's a glass cube between it and you." instead.

Section - Kitchen

Kitchen is east of Dining.
The description of Kitchen is "The church kitchen contains much the same kind of thing as any kitchen, only more so. Long countertops line the walls, with cupboards and drawers underneath them. A break in the counters allows you to leave the kitchen via the door to the west, and on the far side of the room a sink is set into the counter."

The note of Kitchen is "'If you've seen [italic type]Jurassic Park[roman type] then the kitchen is really scary in the dark. If you haven't seen [italic type]Jurassic Park[roman type] then you should get around to that.'"

Instead of leaving when the player is [directly] in the Kitchen:
	try going west.

The kitchen counters are a scenery supporter in the Kitchen. They are plural-named.
Understand "counter" and "countertop" and "countertops" and "bench" and "top" and "tops" and "benchtop" and "benchtops" and "bench-tops" and "bench-top" as the kitchen counters.
The description of the kitchen counters is "The pattern on the kitchen countertops is a sensible gray-brown swirly design which hides almost any stain, at the expense of looking slightly grubby anyway."

The note of the kitchen counters is "'The counters in the kitchen are really cheap and obviously not intended to look really nice, so you'd assume that nobody cares if you chop vegetables on the counters instead of on a cutting board. But! You'd be wrong.'"

The kitchen cupboards are a scenery container in the Kitchen. They are plural-named. They are not contents-mentionable-in-description.
Understand "cupboard" and "cabinet" and "cabinets" as the kitchen cupboards.
The description of the kitchen cupboards is "You peek inside a random cupboard and find a large number of [one of]plates[or]glasses[or]plastic cups[or]mugs[or]bowls[or]tea cups[or]serving trays[at random] piled inside."

The note of the kitchen cupboards is "'The dishes in the kitchen were donated by generations of well-meaning people, and in a sense they belong to everybody, but actually they belong to nobody, which is why nobody feels obligated to keep them organized at all.'"

Instead of opening or searching the kitchen cupboards: try examining the kitchen cupboards.
Instead of closing the kitchen cupboards:
	say "The cupboards are already closed." 

The dishes are a thing in the kitchen cupboards. They are plural-named.
Understand "plates" and "glasses" and "plastic" and "cups" and "mugs" and "bowls" and "tea" and "cups" and "serving" and "trays" as the dishes.
The description is "There seem to be three different sets of dishes here and they're all mixed up together, even though the white bowls are noticeably shallower than the green bowls and they don't stack properly. Better not look too closely."

Instead of querying Casey's notes about the dishes, try querying Casey's notes about the kitchen cupboards.

Instead of taking or pushing or pulling the dishes:
	say "You restack a few of the dishes, but the cupboards don't look any more organized."

Instead of washing the dishes:
	say "The dishes are already clean. Someone did a good job of washing them, just not of stacking them afterwards."
	
The drawers are a scenery container in the Kitchen.
They are plural-named.
They are not contents-mentionable-in-description.
Understand "drawer" as the drawers.
The description of the drawers is "You pull open a random drawer for a moment, revealing an assortment of [one of]cutlery[or]kitchen utensils[or]dish towels[as decreasingly likely outcomes]."

The note of the drawers is "'One kitchen utensil you will never find in the church kitchen (or anywhere else in the church, probably) is a zester. Maybe I should explain: a zester is a tool like a cheese grater with really tiny blades to shave the outer rind, or [']zest['], off of a citrus fruit. But to indulge such luxuries is un-Christian.'"

Instead of opening or searching the drawers: try examining the drawers.
Instead of closing the drawers:
	say "The drawers are already closed." 
	
The cutlery is a thing in the drawers.
Understand "kitchen" and "utensils" as the cutlery.
The description of the cutlery is "The kitchen drawers contain more cutlery and utensils than you could imagine ever needing."

Instead of querying Casey's notes about the cutlery, try querying Casey's notes about the drawers.

Instead of taking the cutlery:
	say "The sheer number and variety of utensils in the drawers paralyzes your ability to choose any individual item."

Some dish towels are a thing in the drawers.
Understand "towel" and "tea towel" and "tea towels" as the dish towels.
The description of the dish towels is "There are four piles of dish towels in the drawer, and the towels are perfectly folded so that each pile takes up exactly a quarter of the space in the drawer."

The note of the dish towels is "'Drying dishes with a towel is not the best way to get dishes to dry, because you end up picking up germs from one thing and rubbing them all over another thing. However, according to Patrick the youth leader, making someone dry the dishes is good teamwork and contributes to fellowship etc. so long story short I'm not allowed to let the dishes dry on their own and leave before everyone else.'"

Instead of doing something when the current action implies touching the dish towels:
	say "You can't bring yourself to disturb the perfectly folded dish towels."

A tin can is a kind of container. ["tin can" instead of just "can" to avoid conflicts with all the other things with "can" in their names]
A tin can is usually closed and openable.
A tin can is usually floor-standing. [hm]
Understand "can" and "tin" and "aluminum" and "aluminium" as a tin can.
Understand "cans" and "tins" as the plural of a tin can.
Understand "open" as a tin can [when the item described is open and something is in the item described].
Understand "empty" as a tin can [when nothing is in the item described]. [removed to improve performance.]

Before printing the name of a tin can (called pot) when not searching:
	omit contents in listing;
	if nothing is in the pot:
		say "empty ";
	otherwise if the pot is open:
		say "open ".

The taking action has an object called the previous container.

Setting action variables for taking:
	now the previous container is the holder of the noun.

After taking a tin can when the previous container is the inner space of the orange box:
	say "Stolen."

After inserting a tin can into the inner space of the orange box:
	if the noun is open:
		say "You put [the noun] back into the Food Bank box. Maybe nobody will notice it's been opened.";
	otherwise:
		say "Your conscience gets the better of you, and you return [the noun] to its original location."

When play begins:
	now every tin can is in the inner space of the orange box.

Instead of querying Casey's notes about a tin can:
	say "On the subject of canned food Casey writes, 'I'm pretty sure the people who are supposed to be in charge of donating stuff to the Food Bank are really disorganized, because you always see spare cans of stuff lying around. Or maybe some of the cans were rejected by the Food Bank. I don't know how it works.'".

Instead of querying Casey's notes about the inner space of the orange box, try querying Casey's notes about the orange box.

Instead of querying Casey's notes about something enclosed by a tin can:
	try querying Casey's notes about the holder of the second noun.
	
Check opening a tin can:
	if the noun is open:
		say "[The noun] is already open." instead;
	otherwise if the player does not carry the can opener:
		say "You don't have a utensil capable of opening the can." instead.

After opening a tin can:
	say "After a few moments['] work with the can opener, the can is open, revealing [if the noun is the can of tuna]a pungent mass of tuna[otherwise][list of things in the noun with indefinite articles][end if]."
		
Check closing a tin can:
	if the noun is open:
		say "Nothing short of a soldering iron will get this can closed again." instead;
	otherwise:
		say "The can is already closed." instead.

Instead of opening a tin can with the can opener:
	try opening the noun.

Instead of smelling a closed tin can:
	say "The can just smells like metal."
		
Before eating a tin can:
	if the noun is closed:
		say "The aluminum can is not edible." instead;
	otherwise:
		try eating a random thing contained by the noun instead.

Instead of inserting something into a tin can:
	say "[if the noun is open]There's no room in the can for anything other than [the random thing enclosed by the noun][otherwise]The can is sealed shut[end if]."

Rule for printing room description details of a tin can:
	stop.

Instead of smelling something enclosed by a tin can:
	try smelling the holder of the noun.

Instead of touching or taking something enclosed by a tin can:
	if the noun is not the peach slices:
		say "You'd only make a mess."


Instead of eating or drinking a tin can:
	if the can is open:
		let the treat be a random thing contained by the noun;
		try eating the treat instead;
	otherwise:
		say "The can is sealed."


An orange box is a flippable supporter in the Kitchen.
The orange box is floor-standing.
Understand "sturdy" and "crate" and "food" and "bank" and "foodbank" as the orange box.
The description is "An old label on the box says 'Navel Oranges[quotation mark], but someone has written 'FOOD BANK' over that. Apparently they thought they'd need a good sturdy crate for all the Food Bank donations they'd get[if the orange box is upended]. At the moment the box is upside down[otherwise if nothing is in the inner space of the orange box], but it's completely empty[end if]."

Rule for writing a paragraph about the orange box:
	if the orange box is in the Kitchen:
		say "A sturdy box is sitting on the floor in the corner";
		if something is enclosed by the inner space of the orange box:
			say ", with ";
			if more than one tin can is enclosed by the orange box:
				say "some cans";
			otherwise if exactly one thing is in the inner space of the orange box and the orange box encloses a tin can:
				say "a single can";
			otherwise:
				say "[list of things in the inner space of the orange box with indefinite articles]";
			 say " in it";
		 say ".";
	otherwise if the orange box is upended:
		say "The orange box is sitting upside down on the floor.";
	otherwise:
		say "The orange box is sitting in the middle of the floor."

Instead of querying Casey's notes about the orange box:
	try querying Casey's notes about a random tin can.

Does the player mean closing the orange box:
	it is very unlikely.

Does the player mean opening the orange box:
	it is very unlikely.



The can of peaches is a tin can.
It is ambiguously plural.
[Understand "peach" and "slices" and "syrup" as the can of peaches when the can of peaches is closed or nothing is in the can of peaches.] [removed to improve performance.]

The description of the can of peaches is "[if nothing is in the can of peaches]There are no peaches left[otherwise]Peach slices in syrup. Mmmmm[end if]."

Instead of smelling the open can of peaches:
	say "The peach syrup smells delicious. Your stomach rumbles softly."

Some peach slices are in the can of peaches.
Understand "syrup" as the peach slices.

[this is kind of out of character for Robin, but it's the player's fault for having her open the can in the first place]
The description of the peach slices is "There seems to be a lot more syrup than peach slices in the can. It's just as well you checked the can before it got given to some unfortunate hungry family."

The peach slices can be half-eaten. The peach slices are not half-eaten.

Before eating or tasting or drinking the peach slices:
	try taking the peach slices instead.

Before eating or tasting the open can of peaches:
	if the peach slices are in the can of peaches:
		try taking the peach slices instead;
	otherwise:
		try examining the can of peaches instead.

Instead of taking the peach slices:
	if the peach slices are half-eaten:
		say "Well, maybe one more...[paragraph break]***[paragraph break]";
		wait for any key;
		say "The can is empty? How did that happen?[paragraph break]You lick the last traces of syrup off your fingers regretfully.";
		remove the peach slices from play;
	otherwise:
		say "You really shouldn't be wasting time eating the Food Bank peaches. On the other hand, you've already opened the can, and it'd be a pity to let them go to waste. Okay, just one slice.";
		now the peach slices are half-eaten.


The can of chicken soup is a tin can.
[Understand "noodle" as the can of chicken soup when the can of chicken soup is closed.] [removed to improve performance.]

The description of the chicken soup is "Chicken noodle soup, suitable sustenance for invalids."

Instead of smelling the open chicken soup:
	say "You experience a sudden, vivid flashback to that flu you had in seventh grade."

A noodly yellow soup is in the can of chicken soup.
Understand "chicken" and "noodle" and "noodles" as the yellow soup.

The description of the yellow soup is "Thick mushy noodles float in the soup."

Before eating or tasting or drinking the yellow soup:
	say "You haven't been able to stomach chicken soup since that awful flu you had once." instead. 

Instead of drinking the can of chicken soup:
	try eating the yellow soup.


The can of tuna is a tin can.

The description of the can of tuna is "The label says this is twelve ounces of chunk tuna, in water."

Instead of smelling the open can of tuna, say "You take a whiff and quickly pull [if the player carries the noun]the can away from your nose[otherwise]your nose away[end if]. That is strong stuff."

Instead of removing something from the can of tuna, say "[if the can of tuna is open]You can't bring yourself to pull the tuna out of the can. Its odor is offensive enough already[otherwise]The can is sealed shut[end if]."

The can of tuna contains a pungent mass of tuna. The printed name of the mass of tuna is "mass of tuna". The description of the mass of tuna is "Wet, white muscle tissue has been packed into this can in a very unappetizing manner."

Before eating the mass of tuna, say "You definitely aren't desperate enough to start eating tuna straight from the can." instead.

Instead of taking or touching the mass of tuna:
	say "You've already had one gross substance all over your hands tonight."


The can of cherry pie filling is a tin can.
[Ryan what kind of degenerate country sells premade pie filling and cookie dough? Beware, citizens of the United States of America, the fall of your once-mighty nation approaches]
[UPDATE 24 May 2014: it turns out you can buy ready-to-bake cookie dough in Australia; I've still never encountered tinned pie filling though]

The description of the can of cherry pie filling is "Unfortunately nobody donated any pastry to the Food Bank to go with this pie filling."

Instead of smelling the open can of cherry pie filling:
	say "Sweet[em dash]very sweet."

Some mushy cherries are in the can of cherry pie filling.
Understand "cherry" and "pie" and "filling" as the mushy cherries.

The description of the mushy cherries is "No doubt these cherries would have made a delicious pie for somebody if you hadn't opened the can prematurely."

Before eating or taking or tasting the mushy cherries:
	say "On the whole, you prefer your pie filling inside of a pie." instead.




A can of Sugarman's Potted Meat Food Product is a tin can. The printed name of the can of sugarman's is "can of 'Sugarman's Potted Meat Food Product'".

The description of the can of Sugarman's Potted Meat Food Product is "'Potted Meat Food Product' are the exact words used."

Instead of smelling the open can of Sugarman's Potted Meat Food Product: say "[one of]A decision you immediately regret[or]You don't think you could handle another whiff[stopping]."

A pink substance is in the can of Sugarman's Potted Meat Food Product.

The description of the pink substance is "It reminds you of that stuff your orthodontist used to make a cast of your teeth."

Before eating or tasting or taking the pink substance:
	say "The words 'food product' are not enough to convince you." instead.




The can of pears is a tin can.
It is ambiguously plural.
[Understand "sliced" and "slices" and "pear" and "in juice" and "in natural" and "natural" and "juice" as the can of pears when the can of pears is closed.] [removed to improve performance]

The description of the can of pears is "Sliced pears in natural juice. As opposed to supernatural juice." [Robin's sense of humour, ohhhh nooooo]

Instead of smelling the open can of pears:
	say "You detect an aroma of pears, with a hint of aluminum."

Some pear slices are in the can of pears.
Understand "pears" and "slices" and "sliced" and "in" and "natural" and "juice" as the pear slices.

The description of the pear slices is "The pear slices have that slightly grayish tinge that they get after having been stewed and canned."

Before eating or tasting or taking the pear slices:
	say "Pears are okay, but definitely better before the canning process." instead.


A can of tomato soup is a tin can.

The description of the can of tomato soup is "Of course there are cans of soup. Soup is the most important staple food for invalids and poor people."

Instead of smelling the open tomato soup:
	say "The soup smells... not much like tomatoes."

A thick red soup is in the can of tomato soup.
The indefinite article of the thick red soup is "some".

The description of the thick red soup is "It's just a thick, uniformly red liquid. It looks more like paint than food."

Before eating the thick red soup:
	say "Tomato soup is hardly your favorite kind of soup, and cold condensed tomato soup even less so." instead. [because Robin is not me, I LOVE tomato soup - though, okay, not cold and condensed]

Instead of drinking the thick red soup:
	try eating the thick red soup.

Instead of drinking the can of tomato soup:
	try eating the thick red soup.


	


[it was really bothering me that the jar was officially called an "empty jar" in the source even when it totally wasn't empty, so I renamed it.]
A jam jar is a container in Kitchen.
The description of the jam jar is "The jar has lost its lid[if ectoplasm is in the jar]. It used to be nice and clean, but now it contains a sample of ectoplasm[otherwise], but it appears to be clean and sterile anyway[end if]."

The initial appearance of the jam jar is "Somebody forgot to put away an empty jam jar."

The jam jar is not contents-mentionable-in-description.

Understand "empty" as the jam jar [when nothing is in the jam jar]. [removed to improve performance]

Before printing the name of the jam jar when nothing is in the jar and photo-name-printing is false:
	say "empty ".

The note of the jam jar is "You can't find a note for any specific jar, but Casey did write down something related.[paragraph break]'In like, I think freshman year, Patrick the youth leader had this great idea to try canning preserves, to raise money for some kind of thing. And he gave each of us a job: you get jars, you get strawberries, you get peaches, we'll all come together and can stuff.[paragraph break]'And, because we were freshmen and not yet completely jaded by the high school experience, we all brought the stuff we were supposed to. But on the day of the big event it turned out, in a revelation that completely shocked the socks off of Patrick the youth leader, that canning preserves is a pretty complicated process with steps beyond just mashing up fruit and putting it into jars. And if you mess it up then your jam will give people salmonella and they'll die.[paragraph break]'So we just sat around and ate fruit, and now there are a bunch of glass jars hanging around the church for no good reason.'"
The jam jar is specially-noted.

Rule for printing room description details of the jar when nothing is in the jar:
	stop.

Instead of inserting something that is not ectoplasm into the jam jar:
	if nothing is in the jam jar:
		say "Maybe you should keep the jar sterile for now.";
	otherwise:
		say "If you did that, you'd get ectoplasm all over [the noun], [italic type]and[roman type] you'd contaminate your sample of ectoplasm."

Instead of opening or closing the jam jar:
	say "The jam jar is missing its lid."

[Carry out photographing the jam jar with the Polaroid when ectoplasm is in the jam jar:
	now the new photo is subject-full.

Rule for printing the name of a photo (called pic) when the subject of the pic is the jam jar:
	say "photo of the [if the pic is subject-empty]empty [end if]jam jar[if the pic is subject-full] containing some ectoplasm[end if]".

Understand "containing ectoplasm" and "containing some ectoplasm" and "jar of ectoplasm" as a photo when the subject of the item described is the jam jar and the item described is subject-full.] [removed to improve performance]

Rule for printing the description of a photo (called pic) when the subject of the pic is the jam jar:
	say "It's a photo of the empty jam jar you found in the kitchen[if the jam jar contains ectoplasm], back when it was nice and clean and non-ectoplasm-containing[end if]."

Instead of photographing the jam jar with when the jar jam contains ectoplasm (called goop):
	try photographing the goop with the second noun.




The kitchen sink is a sink in the Kitchen. The description of the kitchen sink is "It's a big sink, capable of handling the influx of dirty dishes that church functions undoubtedly generate."

The can opener is a thing in the kitchen sink.
The can opener is privately-named.
The can opener is crowbarish.
Understand "can opener" and "tin opener" and "opener" as the can opener. 

The description of the can opener is "[if the can of tuna is closed]It looks to be tolerably clean[otherwise]You notice a small amount of tuna residue on the can opener[end if]."

The note of the can opener is "'There are probably eight different can openers in this church. I guess it's the kind of thing that everybody thinks the church couldn't possibly already have, so they leave their spare behind.'"

Section - Narthex

Narthex is west of Dining. [plot-unopenable main entrance]
Narthex is backstory-visited.

The note of Narthex is "'The narthex is the first room most people see in the church. I don't know whether it's the one you're going to see first.[paragraph break]'One time there was a bat in the narthex and it bit Tammy. I thought this was hilarious, but she did not.'"

The description of Narthex is "The main entryway to the church, this room has been set up to be as welcoming as possible. A rack of pamphlets sits in the center of a large table, flanked by pots of fake flowers. Lying open on a podium is a huge Bible. Pinned up on one wall is a board full of name tags.[paragraph break]To the east is the fellowship hall, and the large passage to the north obviously leads to the main sanctuary. The way out of the church is south of here, but that's none of your concern currently."

Instead of going south in the Narthex:
	say "Hopefully you can complete your investigation without having to leave the church tonight. It must be freezing outside."

The large table is scenery in Narthex. The description of the large table is "You would have expected a big doily, or at least a tablecloth, but this is a plain wooden table."

The note of the large table is "'The table in the narthex is useless when it comes to Sardines. You can see underneath it from virtually any angle.'"

The hide description of the large table is "[if Aiden is in the location]No point in hiding now[otherwise]There's plenty of space under the table, and although you're hardly 'hidden[quotation mark], you feel comfortable in a strange way, until you climb out from underneath."

A rack of pamphlets is scenery. It is on the large table.
It is ambiguously plural.
The description of the rack of pamphlets is "'Please Take One!' reads the cursive text at the bottom of the rack, but none of the pamphlets succeeds in engaging your curiosity. You've never been afflicted with any of the problems they purport to solve."

The note of the rack of pamphlets is "'I actually paid pretty close attention to those pamphlets in the narthex for a while, and I learned something interesting. People really do pick up the pamphlets and read through them, but they always put them back. Nobody ever takes one home, because subconsciously it feels like stealing from a church, which you're not supposed to do. Not that I've never stolen anything from church.'"

Instead of taking the rack of pamphlets, say "The rack is too flimsy to be of any use."

The pamphlet is part of the rack of pamphlets. The description of the pamphlet is "One pamphlet is titled '[one of]Meaningful Relationships?[or]Dealing With Loss[or]To Be A Wife[or]How To Tithe[or]To Be A Husband[then at random]'."

Understand "Meaningful Relationships" or "Dealing With Loss" or "To Be A Wife" or "How To Tithe" or "To Be A Husband" as the pamphlet.

The note of the pamphlet is "'I actually paid pretty close attention to those pamphlets in the narthex for a while, and I learned something interesting. People really do pick up the pamphlets and read through them, but they always put them back. Nobody ever takes one home, because subconsciously it feels like stealing from a church, which you're not supposed to do. Not that I've never stolen anything from church.'"

Instead of querying Casey's notes about the pamphlet, try querying Casey's notes about the rack of pamphlets.

Instead of taking the pamphlet, say "You won't let yourself give any of this material a closer look."

Some pots of fake flowers are an undescribed thing on the large table. They are plural-named. Understand "pot" or "pot of fake flowers" or "pots of flowers" or "pot of flowers" or "flower" as the pots of fake flowers. The description of the pots of fake flowers is "The plastic stems and silk blossoms are obviously artificial, but that hasn't stopped someone from trying to water them recently."

Instead of smelling the fake flowers, say "The flowers smell like wet plastic."

The note of the pots of fake flowers is "'It says a lot about us Christians that we won't even put real flowers in the narthex, right? It does. It doesn't necessarily say something bad, though. It could say that we have better things to do than worry about live flowers, for example. But you can interpret it in your own judgmental way, if you want.' Wow."

Instead of taking the pots of fake flowers, say "There's still a little water in the bottom of the pots[unicode 8212]no roots to absorb it[unicode 8212]and if you moved them around you might spill it all over."




The board full of name tags is a scenery supporter in Narthex.
Understand "nametags" as the board full of name tags.
The board full of name tags is ambiguously plural.
The description of the board full of name tags is "There must be a name tag on here for everybody who's ever set foot in this church[if the name tag is on the board full of name tags]. There's Casey's, and there's Orchid's, and this one says 'Robin[quotation mark][unicode 8212]but they must mean some other Robin[end if]."

The note of the board full of name tags is "'When the new pastor came, he insisted that everybody wear name tags all the time, but that didn't last long. Name tags are a pain.'"

The name tag is on the board full of name tags.
The name tag is undescribed.
Understand "robin's" and "robin" and "robin name tag" and "robin nametag" and "robin tag" and "nametag" as the name tag.
The description of the name tag is "'Robin' is written in red marker. Who would have thought to make a tag for you? When did anyone think you would really attend this church?"

The note of the name tag is "'When the new pastor came, he insisted that everybody wear name tags all the time, but that didn't last long. Name tags are a pain.'"

Instead of taking the board full of name tags, try taking the name tag.
	
After taking the name tag the first time:
	say "You pull the name tag labeled 'Robin' off of the board, even though it couldn't possibly be for you."

Check putting something on the board full of name tags:
	if the noun is not the name tag:
		say "That doesn't belong there." instead.

After putting the name tag on the board full of name tags:
	now the noun is undescribed;
	say "You attach the name tag to the board again."

The name tag is wearable.

After wearing the name tag:
	say "You pin the name tag on."


Section - The Huge Bible

The podium is scenery in Narthex. The description of the podium is "It resembles a miniature Roman column, although you think it would probably fall over in a stiff breeze."

The note of the podium is "'You want to be careful if you're sneaking around the narthex in the dark, because there is a podium in there that is really, really easy to not notice, and the corners of that podium are right about at crotch level.'"

The huge Bible is an undescribed thing on the podium. Understand "book" and "holy bible" and "good book" as the huge Bible. The description of the huge Bible is "The Bible is obviously a well-respected object around here, but it's not a mere display piece; the pages have been creased and scuffed by continuous use."

Instead of closing the huge Bible, say "It's probably supposed to remain open."

Instead of opening the huge Bible, say "It's already open."

The note of the huge Bible is "'But if you turn the lights on, you'll see there's a Bible on that podium. I'm pretty sure that Bible is worth hundreds of dollars. It has a lot of really nice pictures in it, at least. Don't mess with it.'"

After consulting the huge Bible about a topic listed in the Table of Biblical Quotations, say "[answer entry][paragraph break]".

Instead of querying the huge Bible about the huge Bible:
	say "It's all there in front of you."

Table of Biblical Quotations
Topic	Answer
"[church]"	"After a bit of flipping pages back and forth, this is the best you can come up with: 'For just as the body is one and has many members, and all the members of the body, though many, are one body, so it is with Christ. For in the one Spirit we were all baptized into one body[unicode 8212]Jews or Greeks, slaves or free[unicode 8212]and we were all made to drink of one Spirit.'"
"adam"	"Adam shows up near the beginning. It says here that 'all the days that Adam lived were nine hundred thirty years; and he died.'"
"eve"	"With some effort you find the passage you were thinking of: 'And the rib that the LORD God had taken from the man he made into a woman and brought her to the man.'"
"[abel]"	"It says here that Abel was murdered by Cain, who you seem to remember was the first vampire."
"[angel]"	"You assumed there should be angels appearing on every other page, but apparently they're rarer than you thought. Here's one: 'Then there appeared to him an angel of the Lord, standing at the right side of the altar of incense. When Zechariah saw him, he was terrified; and fear overwhelmed him.'"
"[apostle]"	"There were twelve disciples. You know that much. But you can't find a list of them anywhere. You do manage to find this: 'As he walked by the Sea of Galilee, he saw two brothers, Simon, who called Peter, and Andrew his brother, casting a net into the sea[unicode 8212]for they were fishermen. And he said to them, [']Follow me, and I will make you fish for people.['] Immediately they left their nets and followed him.' That's two of them, at least."
"[ark]"	"The Bible confirms that one ark was a big boat, and the other ark was a chest that they kept the Ten Commandments in. Confusing."
"[babel]"	"Near the beginning of the Bible, in the earliest periods of human history, there's a story where the people try to build a tower that reaches Heaven, which troubles God: 'So the LORD scattered them abroad from there all over the face of the earth, and they left off building the city.'"
"[baptism]"	"Here it says that Jesus wanted John the Baptist to baptize him, which seems reasonable, and yet 'John would have prevented him, saying, [']I need to be baptized by you, and do you come to me?['] But Jesus answered him, [']Let it be so now; for it is proper for us in this way to fulfill all righteousness.['] Then he consented.' You must have missed something."
"[christmas]"	"It takes a bit of searching, but you think you've found the Christmas story. It goes on for a while: an angel appears, another angel appears, Mary is there, Joseph is there, they go to Bethlehem, the angels and the shepherds are there[unicode 8212]it goes on for a really long time."
"circumcision"	"The Bible doesn't have anything to say about circumcision that you weren't already aware of."
"[cross]"	"While turning pages in the vicinity of the Crucifixion, you notice a passage in red text: 'Do you think that I cannot appeal to my Father, and he will at once send me more than twelve legions of angels? But how then would the scriptures be fulfilled, which say it must happen in this way?'"
"[david]"	"Apparently Goliath was 'six cubits and a span' tall, which is good to know. He sounds like quite the warrior. But, of course, tiny little David kills him with one little rock."
"[daniel]"	"Daniel has a whole book about him. He sees visions, and he talks to kings, and doesn't he[unicode 8212]here it is! He ends up in a den of lions. But it looks like he survives."
"[devil]"	"The devil does not show up nearly as often as you would have thought. Eventually, though, you find this: 'Everyone who commits sin is a child of the devil; for the devil has been sinning from the beginning. The Son of God was revealed for this purpose, to destroy the works of the devil.'"
"eden"	"This is odd: 'Then the LORD God said, [']See, the man has become like one of us, knowing good and evil; and now, he might reach out his hand and take also from the tree of life, and eat, and live forever['][unicode 8212]therefore the LORD God sent him forth from the garden of Eden, to till the ground from which he was taken.' What is 'like one of us' supposed to mean?"
"[esther]"	"Based on a short glance at the book of Esther, it looks like a very interesting story, but you don't have time to read through the whole thing right now. You make a mental note to check it out later."
"[flood]"	"The story of Noah and the flood, which always sounded so cute before, apparently is pretty grisly: 'And all flesh died that moved on the earth, birds, domestic creatures, wild animals, all swarming creatures that swarm on the ground, and all human beings; everything on dry land in whose nostrils was the breath of life died."
"[ghost]"	"To your surprise, you're able to find a ghost in the Bible, or at least something that sounds a lot like a ghost: 'The king said to her, [']Have no fear; what do you see?['] The woman said to Saul, [']I see a divine being coming up out of the ground.['] He said to her, [']What is his appearance?['] She said, [']An old man is coming up; he is wrapped in a robe.[']' Probably a ghost."
"[god]"	"You flip back and forth through the pages of the Bible. God features prominently throughout."
"[isaac]"	"The story of Abraham and his son Isaac says that God told Abraham to kill his son, and then at the last moment said that he shouldn't kill his son and that it was all a test. Abraham seems to take the experience in stride."
"[israel]"	"It says here that Israel was 'the name of Jacob[quotation mark], which doesn't make much sense."
"[jacob]"	"Jacob and Esau were the sons of Isaac. Their story is long and confusing."
"[jesus]"	"Quite a lot of the Bible is about Jesus.[paragraph break]You already knew that; it's not something you had to discover by examining this particular Bible."
"john"	"A cursory examination reveals that there must be at least fifty people named John in this book."
"[Jonah]"	"Jonah is easy to find; he has his own little book. There's not a lot of space for anything to happen, but he does hang out inside a whale for a while: 'But the LORD provided a large fish to swallow up Jonah; and Jonah was in the belly of the fish three days and three nights.' So, then, was it a whale, or was it a fish?"
"judas"	"Judas of course is the evil disciple who betrayed Jesus. You can't find the verse, but it should say somewhere in here that Judas ended up being eaten by Satan forever in Hell."
"lazarus"	"You're able to find the beginning of the story of Lazarus: 'After saying this, he told them, [']Our friend Lazarus has fallen asleep, but I am going there to awaken him.['] The disciples said to him, [']Lord, if he has fallen asleep, he will be all right.['] Jesus, however, had been speaking about his death, but they thought that he was referring merely to sleep. Then Jesus told them plainly, [']Lazarus is dead.[']'"
"[leper]"	"You flip through page after page, but you can't seem to find a single leper. You know they're in there somewhere, though."
"[moses]"	"Moses goes through a lot in the space allotted to him. Where's the part where he kills the Pharaoh? There's too much text to search through."
"[parable]"	"In the back end of the Bible, Jesus tells numerous parables, but you find one short one: 'The kingdom of heaven is like yeast that a woman took and mixed in with three measures of flour until all of it was leavened.' Why does it have to be a woman?"
"[sin]"	"There's a nice poem about sin about halfway through the Bible: 'There is no soundness in my flesh because of your indignation; there is no health in my bones because of my sin. For my iniquities have gone over my head; they weigh like a burden too heavy for me.'"
"[solomon]"	"King Solomon supposedly is the author of the book of Ecclesiastes, which looks extremely boring, and the Song of Songs, which makes you blush and quickly turn to another page."



Understand "ark" and "arks" and "ark of the covenant" and "noah's ark" as "[ark]".

Understand "church" and "churches" and "congregation" as "[church]".

Understand "cain" and "abel" and "cain and abel" as "[abel]".

Understand "angel" and "angels" and "cherub" and "cherubim" and "seraph" and "seraphim" as "[angel]".

Understand "apostle" and "apostles" and "disciples" and "disciple" as "[apostle]".

Understand "babel" and "tower of babel" as "[babel]".

Understand "john the baptist" and "baptist" and "baptism" and "baptize" and "baptizing" as "[baptism]".

Understand "baby Jesus" and "christmas" and "nativity" and "mary" and "joseph" and "shepherds" and "manger" and "bethlehem" and "noel" as "[christmas]".

Understand "cross" and "crucifixion" and "easter" and "crucifix" as "[cross]".

Understand "david" and "goliath" and "david and goliath" as "[david]".

Understand "dream" and "daniel" and "lions" and "lion" and "dreams" as "[daniel]".

Understand "devil" and "satan" and "beelzebub" and "demon" and "lucifer" as "[devil]".

Understand "purim" and "esther" and "haman" as "[esther]".

Understand "flood" and "noah" and "rainbow" as "[flood]".

Understand "samuel" and "witch" and "witch of endor" and "endor" and "ghost" and "ghosts" as "[ghost]"

Understand "god" and and "lord" and "father" and "trinity" and "holy spirit" and "spirit" and "holy ghost" as "[god]".

Understand "abraham" and "isaac" and "abraham and isaac" as "[isaac]".

Understand "israel" and "israelites" as "[israel]".

Understand "jacob" and "esau" and "jacob and esau" as "[jacob]".

Understand "jesus" and "christ" and "jesus christ" and "emmanuel" and "messiah" and "savior" as "[jesus]".

Understand "jonah" and "whale" and "leviathan" and "jonah and the whale" and "jonah and whale" as "[jonah]".

Understand "leper" and "lepers" as "[leper]".

Understand "moses" and "pharaoh" and "passover" and "exodus" as "[moses]".

Understand "parable" and "parables" as "[parable]".

Understand "sin" and "sinning" and "sins" and "evil" and "wickedness" and "transgression" and "iniquity" as "[sin]".

Understand "solomon" and "suleiman" and "king solomon" as "[solomon]".


Instead of querying the huge Bible about Jesus:
	try consulting the huge Bible about "christ".

Instead of querying the huge Bible about the little children:
	say "The story just says that Jesus was okay with blessing some children. It's not nearly as cute a story as it could be."

Instead of querying the huge Bible about an ark:
	try consulting the huge Bible about "arks".

Instead of querying the huge Bible about when the second noun is David or the second noun is the giant:
	try consulting the huge Bible about "david".

Instead of querying the huge Bible about the rainbow:
	try consulting the huge Bible about "rainbow".

Instead of querying the huge Bible about when the second noun is Jonah or the second noun is the whale or the second noun is the beach:
	try consulting the huge Bible about "jonah".

Instead of querying the huge Bible about when the second noun is Jennie or the second noun is the ghostly figure:
	try consulting the huge Bible about "ghost".

Instead of querying the huge Bible about when the second noun is the model church or the second noun is Grace United Methodist:
	try consulting the huge Bible about "churches".

Instead of querying the huge Bible about when the second noun is the manger or the second noun is the crook:
	try consulting the huge Bible about "nativity".

Rule for clarifying the parser's choice of something when the current action is querying the huge Bible about:
	do nothing.

After consulting the huge Bible about something:
	say "You can't seem to find exactly what you're looking for, but you do find this: '[random biblical excerpt]'[paragraph break]"
	
Instead of searching the huge Bible:
	say "You flip aimlessly through the book, and eventually your gaze lands on a passage: '[random biblical excerpt]'[paragraph break]"

To say random biblical excerpt:
	choose a random row in the Table of Random Excerpts;
	say "[verse entry]".

Check examining the huge Bible:
	if the player's command includes "read ":
		try searching the huge Bible instead.
	


Table of Random Excerpts
Verse
"But truly it is the spirit in a mortal, the breath of the Almighty, that makes for understanding. It is not the old that are wise, nor the aged that understand what is right."
"The LORD by wisdom founded the earth; by understanding he established the heavens; by his knowledge the deeps broke open, and the clouds drop down the dew."
"Do not say, [']Why were the former days better than these?['] For it is not from wisdom that you ask this."
"My eyes are spent with weeping; my stomach churns; my bile is poured out on the ground because of the destruction of my people, becaues infants and babes faint in the streets of the city."
"The weight of the golden earrings that he requested was one thousand seven hundred shekels of gold (apart from the crescents and the pendants and the purple garments worn by the kings of Midian, and the collars that were on the necks of their camels)."
"Therefore, one who speaks in a tongue should pray for the power to interpret. For if I pray in a tongue, my spirit prays but my mind is unproductive."
"But there are also many other things that Jesus did; if every one of them were written down, I suppose that the world itself could not contain the books that would be written."
"You must understand this, my beloved: let everyone be quick to listen, slow to speak, slow to anger; for your anger does not produce God's righteousness."
"I consider that the sufferings of this present time are not worth comparing with the glory about to be revealed to us."
"Not all flesh is alike, but there is one flesh for human beings, another for animals, another for birds, and another for fish."
"Even Satan disguises himself as an angel of light. So it is not strange if his ministers also disguise themselves as ministers of righteousness. Their end will match their deeds."
"Then I saw a new heaven and a new earth, for the first heaven and the first earth had passed away, and the sea was no more."
"See, the day is coming, burning like an oven, when all the arrogant and all evildoers will be stubble; the day that comes shall burn them up, says the LORD of hosts, so that it will leave them neither root nor branch."
"When words are many, transgression is not lacking, but the prudent are restrained in speech."



Check querying the huge Bible about something: say "You're not familiar with most of the Bible, but you suspect it has very little to say about [the second noun] anywhere." instead.

Last instead of querying the huge Bible about when the second noun is Biblical: say "You're not really sure where in the Bible to find that."

Instead of taking the huge Bible, say "You feel it would be disrespectful to remove the Bible from its podium."

Some pages are part of the huge Bible.
Understand "page" as the pages.
The description of the pages is "The pages are each very thin, but the book is still very thick."

Instead of querying Casey's notes about the pages, try querying Casey's notes about the huge Bible.

Instead of searching the pages, try searching the huge Bible.

Instead of turning the pages, try searching the huge Bible.


Section - Sanctuary

Sanctuary is north of Narthex.
The sanctuary is not light-switchable.
The sanctuary is dimly lit.

The description of Sanctuary is "This room can't be as large as it seems. It certainly didn't look so huge from outside. It must be some kind of optical illusion, created by the angles of the vaulted ceiling or the long rows of pews.[paragraph break]Near the southern exit is a rolltop desk. Up at the north end of the room is the altar, and the east stairway must lead up to the balcony.[paragraph break]It is dark outside, and [the stained glass windows] are dull and lifeless[if the sanctuary is dimly lit]. Orchid hasn't turned the lights on either; you can barely make out anything in the gloom[end if]."

The note of Sanctuary is "'The sanctuary obviously is the room where we all worship and hear sermons and that kind of thing. It is a very solemn room, as you can probably see. It feels that way for a [']pew['] different reasons.' Oh, Casey. Come on."

The vaulted ceiling is scenery in sanctuary. The description of the vaulted ceiling is "The bars of the ceiling recall the ribs of a ship."

The note of the vaulted ceiling is "'Heather told me she had a dream where there were dead bodies walled up in the ceiling of the sanctuary. I don't remember why she told me this.'"

Some long rows of pews are scenery in sanctuary. They are plural-named. Understand "pew" and "bench" and "benches" as the long rows of pews. 

The long rows of pews are enterable supporters. The usual posture of the long rows of pews is sitting.

After entering the long rows of pews when the chosen posture is sitting:
	say "You sit down in one of the pews and fold your hands in your lap demurely."

After entering the long rows of pews when the chosen posture is standing:
	say "You step up onto one of the pews."

After getting off the long rows of pews:
	say "You get off the pew.";
	try looking.

The description of the long rows of pews is "The benches are like dense underbrush: something could be hiding anywhere."

Instead of searching the long rows of pews:
	say "You look in between a few of the pews and find nothing to either interest or worry you."

The hide description of the long rows of pews is "[pews-hide-desc]."
To say pews-hide-desc:
	if the ghostly figure is in the location:
		say "You try to jump behind a pew, but end up plowing into Orchid, who shoves you back angrily";
	otherwise if Orchid is in the location:
		say "It wouldn't be much use hiding now that Orchid has already seen you";
	otherwise:
		say "You disappear yourself behind a pew, and the feeling of being so well-hidden turns out to be just as unnerving as the feeling you had previously, that somebody was watching you".

The note of the long rows of pews is "'Seriously, if you have to sleep in the church, a pew is your best bet.'"

The rolltop desk is scenery in sanctuary. Understand "roll top" or "roll" or "top" or "roll-top" as the rolltop desk.

The description of the rolltop desk is "You saw one of these in a movie once. The top recedes into the back of the desk, and you can hide a convict inside."

The note of the rolltop desk is "'The sound guy, whose name I don't remember, keeps all the controls for the sound and lights in that rolltop desk. He takes the key to the desk home with him, because the stuff inside is super expensive. I'm just fine with not breaking into it and messing around because I would probably break everything.'"

Instead of hiding in the rolltop desk, try opening the noun.

Instead of opening the rolltop desk, say "You try to pull back the top of the desk, but it's locked down."

Some stained glass windows are scenery in sanctuary. They are plural-named. Understand "stained-glass window" or "stained-glass windows" or "stained-glass" or "stained glass window" or "window" as the stained glass windows.

The description of the stained glass windows is "Some of the designs are purely abstract, but you notice a dove, a lion, a lamb, and what looks like an elk.".

The dove is scenery in sanctuary. Understand "dove window" as the dove. The description of the dove is "The dove is flying downward, its wings outstretched."
The dove is biblical.

The lion is scenery in sanctuary. Understand "lion window" as the lion. The description of the lion is "The lion is looking out into space with one paw raised."
The lion is Biblical.

The lamb is scenery in sanctuary. Understand "lamb window" as the dove. The description of the lamb is "The lamb is sitting down, its eyes closed."
The lamb is biblical.

The elk is scenery in sanctuary. Understand "elk window" as the elk. The description of the elk is "The elk is looking to the side, as if ready to take on some unseen assailant."

The note of the stained glass windows is "'I guess I've seen better stained glass in other churches, but ours is pretty nice. We've got the bases covered.'".

Instead of searching the stained glass windows, say "It's hard to make out any outside details through the texture of the glass.".

Instead of searching something when the noun is the dove or the noun is the lion or the noun is the elk or the noun is the lamb:
	try searching the stained glass windows.

Instead of querying casey's notes about the lion, try querying casey's notes about the stained glass windows.

Instead of querying casey's notes about the dove, try querying casey's notes about the stained glass windows.

Instead of querying casey's notes about the elk, try querying casey's notes about the stained glass windows.

Instead of querying casey's notes about the lamb, try querying casey's notes about the stained glass windows.

After deciding the scope of the player when the location is the Sanctuary:
	place the contents of Altar in scope.

Rule for reaching inside a room:
	say "You can't do that from this distance.";
	deny access.

When Ghostly Apparition begins:
	the fake ghost appears in one turn from now;
	say "Orchid comes over to meet you. 'I didn't find anything up here,' she says, 'how about you?'".
	
Orchid can be answered.
Orchid is not answered.

To decide if Orchid is waiting for an answer:
	if Ghostly Apparition is happening and the time since Ghostly Apparition began is 1 minute:
		decide yes;
	decide no.

Instead of informing Orchid about Jennie when Orchid is waiting for an answer:
	now Orchid is answered;
	say "You detail the evidence you've seen. Orchid listens, wide-eyed. 'That's great! You took photos of all this, right?' she says."

Instead of saying yes when Orchid is waiting for an answer:
	try informing Orchid about Jennie.

Instead of talking to Orchid when Orchid is waiting for an answer:
	try informing Orchid about Jennie.

Instead of saying no when Orchid is waiting for an answer:
	now Orchid is answered;
	say "Orchid pouts. 'Dang. I really, really wanted to see a ghost tonight.'".

Definition: a photo is evidence-showing if it shows an evidential thing.

Instead of showing the stack of photos to Orchid when Orchid is waiting for an answer:
	say "You fan out your stack of photos and hold them out to Orchid. [run paragraph on]";
	if the player carries an evidence-showing photo (called the item of interest):
		try showing the item of interest to Orchid;
	otherwise:
		let whichever be a random photo carried by the player;
		try showing whichever to Orchid.

Instead of showing an evidence-showing photo to Orchid when Orchid is waiting for an answer:
	now Orchid is answered;
	say "Her eyes widen at [the noun]. 'Wow, Robin,' she says, 'that's great, good work! That definitely looks paranormal to me.'"

Instead of showing a photo showing the cat to Orchid when Orchid is waiting for an answer:
	now Orchid is answered;
	say "Orchid stares at you for a moment and then says, 'We figured out that the cat [italic type]wasn't[roman type] a ghost, remember? Didn't you find anything else?'".

Instead of showing a photo to Orchid when Orchid is waiting for an answer:
	now Orchid is answered;
	say "She looks puzzled. 'Sorry, I don't see what I'm supposed to be looking at. This doesn't really look ghostly to me. You mean to say you didn't find anything?'".

Instead of informing Orchid about a photo when Orchid is waiting for an answer:
	try showing the second noun to Orchid instead.

Instead of informing Orchid about the stack of photos when Orchid is waiting for an answer:
	try showing the stack of photos to Orchid.

Instead of giving something to Orchid when Orchid is waiting for an answer:
	try showing the noun to Orchid.

Instead of quizzing Orchid about an evidential thing when Orchid is waiting for an answer:
	try informing Orchid about the second noun.

Instead of quizzing Orchid about a photo when Orchid is waiting for an answer:
	try informing Orchid about the second noun.

Instead of quizzing Orchid about the jam jar when Orchid is waiting for an answer and the jam jar contains ectoplasm:
	try informing Orchid about the second noun.

Instead of showing ectoplasm to Orchid when Orchid is waiting for an answer:
	now Orchid is answered;
	say "[ectoplasm-approval]".

Instead of showing the jam jar to Orchid when the jam jar contains ectoplasm and Orchid is waiting for an answer:
	now Orchid is answered;
	say "[ectoplasm-approval]".

To say ectoplasm-approval:
	say "Orchid inspects the goo. 'Actual ectoplasm! This is great! Make sure you've got a photo of it as well, though[em dash]we'll need it for the newspaper.'".

Instead of informing Orchid about the jam jar when the jam jar contains ectoplasm and Orchid is waiting for an answer:
	try showing the jam jar to Orchid.

Instead of informing Orchid about ectoplasm when Orchid is waiting for an answer:
	now Orchid is answered;
	say "'You found some ectoplasm?' Orchid says. 'That's great! You took a photo, right?'".

Instead of informing Orchid about the flickering lights when Orchid is waiting for an answer:
	now Orchid is answered;
	say "You describe the flickering lights to Orchid. 'Wow, that sounds really spooky!' she says. 'Did you get a photo?'"

Last instead of chatty behavior when Orchid is waiting for an answer:
	if the current action is saying hello to and greeting type is implicit:
		continue the action;
	say "Orchid brushes your [if asking about or quizzing about]inquiry[otherwise]remark[end if] aside. 'Never mind that right now, did you find any evidence of a ghost?'"

At the time when the fake ghost appears:
	move the ghostly figure to the sanctuary;
	now the ghostly figure is seen;
	now everything enclosed by the ghostly figure is seen;
	now the last action of Orchid is the action of examining the ghostly figure;
	say "[if Orchid is not answered]Before you can answer her question[otherwise]Before you can say any more[end if], Orchid gasps and points upwards. Something is floating near the ceiling, glowing faintly[em dash]a pale figure in a wispy dress."

Instead of going from the sanctuary when Ghostly Apparition is happening and the ghostly figure is off-stage:
	say "Your eyes haven't adjusted to the dark yet, and you can't see where you're going."

Every turn when the ghostly figure is on-stage:
	if the ghostly figure is in the Sanctuary for the second turn and not looking:
		say "The ghostly figure hovers silently in the air.";
	otherwise if the ghostly figure is in the Sanctuary for the third turn:
		say "The figure drifts towards the ground and glides northward towards the altar[if the player does not have evidence of the ghostly figure]. Orchid grabs your arm and whispers, 'Quick, the camera!'[otherwise].[end if]";
		move the ghostly figure tidily to the altar;
	otherwise if the ghostly figure is in the altar:
		say "The figure drifts northwards through a small door behind the altar.";
		now the last action of Orchid is the action of waiting;
		move the ghostly figure tidily to the back room.

Check going when Ghostly Apparition is happening:
	[say "DEBUG: the room gone to is [the room gone to]. The ghost is in [the location of the ghostly figure].";]
	if the ghostly figure is in Back Room and (the room gone to is the Altar or the room gone to is the Back Room):
		continue the action;
	otherwise if the room gone to is the location of the ghostly figure:
		continue the action;
	otherwise if the player is in Sanctuary and the ghostly figure is in Sanctuary:
		say "Orchid catches your arm before you can leave and points at the ghostly figure." instead;
	otherwise if the player is in Sanctuary:
		say "Orchid catches your arm and pushes you towards the altar. 'Get [if a photo shows the ghostly figure]some more photos[otherwise]a photo[end if]!' she hisses in your ear." instead;
	otherwise if the player is in Altar:
		say "Orchid meets you at the bottom of the steps and pushes you back up onto the stage. 'Follow it, see if you can get some more photos!' she whispers." instead.

Last carry out photographing Orchid with the Polaroid in Sanctuary:
	make the new photo show someone performing the action of screaming.

After photographing Orchid with the Polaroid in the Sanctuary:
	say "Orchid sees you pointing the camera at her and yells, 'Don't take pictures of me! Take a photo of the ghost!' Too late now, though; the photo is already sliding out of the camera."

Examining the ghostly figure is acting acceptably to Orchid.
Photographing the ghostly figure with the Polaroid is acting acceptably to Orchid.
Photographing Orchid with the Polaroid is acting acceptably to Orchid. [not really, but it has its own response]
Going is acting acceptably to Orchid.
Following the ghostly figure is acting acceptably to Orchid.

Orchid has a number called the impatience.
The impatience of Orchid is 0.

Every turn when (the location is Sanctuary or the location is Altar) and the ghostly figure is on-stage and the ghostly figure is not in Sanctuary:
	if the current action is not acting acceptably to Orchid and the current action is not chatty behavior:
		if the impatience of Orchid is 0:
			say "Orchid hisses, 'What are you waiting around for? Get [if a photo shows the ghostly figure]some more photos[otherwise]a photo[end if] of the ghost!'";
		otherwise if the impatience of Orchid is 1:
			say "'Stop wasting time, follow the ghost!' Orchid snaps at you.";
		otherwise if the location is Sanctuary:
			say "Orchid grits her teeth. 'Why do you keep messing around? Go, go, go!' She pushes you bodily up onto the stage.";
			move the player to Altar;
		otherwise if the location is Altar:
			say "Orchid clenches her fists. 'Keep moving! The ghost will get away!' She runs up onto the stage and shoves you headlong through the tiny door.";
			try going north;
		increment the impatience of Orchid.

Instead of switching on the light switch when the location is Sanctuary and Sanctuary is dimly lit:
	say "You can't find the switch in the darkness."

Instead of switching off the light switch when the location is Sanctuary and Sanctuary is dimly lit:
	say "The lights are already off."

Section - Altar

Altar is north of Sanctuary. ["raised area with pulpit and altar", some people call this the altar, I think that's a bad name for it because it CONTAINS an altar, whatever. this might be an issue of contention between casey and robin.]
Altar is dimly lit.

[BEWARE: here follows a stupid solution for a fairly stupid problem
LOOK UP ALTAR always wants to choose a room before an object, which means you can't LOOK UP [SUNDAY SCHOOL ROOM] ALTAR. So I am going to make ALTAR not refer to this room when the player is in the Sunday School room. I know: what even.]
[Altar is privately-named.
Understand "altar" as Altar when the location is not Sunday.] [removed to improve performance.]

The description of Altar is "A few steps up from the pews to the south are the preacher's pulpit, the altar, and the American and Christian flags. Behind them, to the north, is a tiny doorway."

The note of Altar is "'Many church enthusiasts use the word 'altar' to refer not only to the little table with the candles, but also to the raised area at the front of the sanctuary where the little table with the candles is located. This practice is completely incorrect and extremely stupid. The raised area is called a [apostrophe]raised area[apostrophe] or I guess maybe a [apostrophe]stage[apostrophe].'" Understand "stage" and "raised area" as Altar.

After deciding the scope of the player when the location is the Altar:
	place the contents of the Sanctuary in scope.

The preacher's pulpit is here. Understand "lectern" as the pulpit. The pulpit is scenery.

The description of the pulpit is "The lectern stands on the stage-left end of the platform. The austerity of its design is diminished somewhat by the microphone sticking out of the top."

The note of the pulpit is "'Patrick the youth leader never uses the pulpit. He always stands in the middle of the stage, and over the course of his sermon he slowly walks down the stairs until he's standing in the middle of the pews. So, if you're trying to be enthusiastic by sitting in the front, you'll end up having to crane your neck or sit sideways in the pew for the whole back half of the sermon.'"

The microphone is part of the pulpit. Understand "mic" as the microphone.

The description of the microphone is "The controls for the mic must be somewhere else. You don't see an on/off switch."

The note of the microphone is "'But Patrick the youth leader did use the microphone, when he sang. Luckily he doesn't do a lot of singing anymore.'"

Instead of touching the microphone, say "Nothing. It must not be turned on."
Instead of knocking on the microphone:
	try touching the microphone.

Instead of switching on or switching off the microphone, try examining the noun.


The truealtar is here. The truealtar is a privately-named scenery supporter. The printed name of the truealtar is "altar". Understand "altar" as the truealtar.

The hide description of the truealtar is "You duck behind the altar and discover that the side not facing the congregation is a little less austere and impressive. There is dust back here. You walk back out to the front."

The description of the truealtar is "A simple beige table. On the side facing the congregation is an insignia with the letters 'IHS'."

Instead of querying Casey's notes about the truealtar, try querying casey's notes about Altar.

Instead of putting something on the truealtar:
	say "You feel that it would be uncommonly disrespectful for you to put something on this sacred table. That honor is probably reserved for ministers and priests."


some candles are on the truealtar.
The indefinite article of the candles is "two".
Understand "two" and "candle" as the candles.
The description of the candles is "These aren't real candles[unicode 8212]they're just white plastic cylinders with gold covers on the tops. They don't even have wicks."

Instead of taking the candles, say "These imitation candles will never provide any light. Better to leave them here."

Instead of burning the candles:
	say "Even if you had a match, you couldn't light these candles. They're fake candles."

The note of the candles is "'Churches are all about candles. But candles can also be very dangerous. But can you imagine a church without candles? No, you can't. So we have completely fake, non-lightable candles, which are only dangerous if you wield them as bludgeoning weapons.'"



The insignia is part of the truealtar. Understand "IHS" and "knot" and "initials" as the insignia. The description of the insignia is "The initials 'IHS' are wreathed in a simple knot design."

The note of the insignia is "'I think the [']IHS['] on the altar stands for Jesus, somehow, but I haven't bothered to look it up.'"



The large cross is in the altar. "A large cross hangs high on the wall above the altar." Understand "crucifix" as the large cross.

The description of the large cross is "It must be fifteen feet from top to bottom. The wood is smooth, and the proportions are graceful. Aesthetically speaking, it is a very pleasant object."

The note of the large cross is "'A cross is not technically a crucifix unless there's a Jesus on it. We are not Catholics, so we have a plain old cross, and it suits us just fine.'"

Instead of taking the large cross, say "Even if you could ever pull it off the wall, it's far too large for you to carry."


A flag is a kind of thing.
Understand "flags" as the plural of a flag.

The American flag is here.
The American flag is a scenery flag.
The description of the American flag is "Although it's lying furled against its post, you can easily tell that this is an American flag. This one has tassels around the edges, and an eagle figurine is perched at the top."
Understand "post" and "tassel" and "tassels" and "eagle" and "figurine" and "eagle figurine" as the American flag.

The note of the American flag is "'The American flag! The home of the brave! Freedom and liberty! How respectful!'"

Instead of taking the American flag, say "You have no use for a big heavy flag."

The Christian flag is here.
The Christian flag is a scenery flag.
The description of the Christian flag is "You assume it's the Christian flag, because there's a red cross near the top, but you've never seen such a flag anywhere else."

First for deciding whether all includes a flag when taking:
	it does.

The note of the Christian flag is "'I have a couple beefs with the Christian flag's design. First, it cribs far too heavily from the American flag, which probably alienates all the Canadian Christians and Moroccan Christians who use it, if they even bother. Second, the cross is red on top of blue, which violates the rule of tincture.'"

Instead of taking the Christian flag, say "You have no use for a big heavy flag."

The rule of tincture is a thing. The note of the rule of tincture is "'The rule of tincture says that you don't put white and yellow next to each other, and you don't put colors other than white and yellow next to each other. It's an extremely well-established and basic rule for flags and coats of arms and that kind of thing.'"



Section - Back Room

Back Room is north of Altar. The description of Back Room is "This is a far cry from the grandeur of the sanctuary. The walls are covered in some kind of dark wood panels, but they're shabby and windowless, and the floor is bare concrete. A huge wooden chest and a closet full of robes take up one wall. You can head back out to the altar to the south[if the sliding door is comprehended], or try the sliding door to the north[end if]."

The note of the Back Room is "'The back room is kind of a secret, I guess, although we use it as the [']backstage['] whenever we do plays and that kind of thing. Nobody ever uses it as a hiding place when we're playing games, though. I guess it might be too obvious? Or maybe it's just too boring.[paragraph break]'You can get to the basement from the back room, too: there's a sliding panel in the back wall, but to get it open, you have to apply some upward pressure to get it out of its groove a little bit. That's the real secret, I guess.'"

Back Room is privately-named.
Understand "back" and "back room" as the back room.

Instead of leaving when the player is in the Back Room:
	try going south.

Carry out querying Casey's notes about the Back Room:
	now the sliding door is revealed;
	now the sliding door is comprehended;
	now the time since progress counter is 0.

Instead of querying Casey's notes about when the second noun is the sliding door or the second noun is the back room walls:
	try querying Casey's notes about the Back Room.



The huge wooden chest is scenery in back room. The huge wooden chest is a closed openable container. Understand "trunk" and "box" and "lid" as the huge wooden chest. The description of the huge wooden chest is "When you think about it, you realize that the chest is of the same dimensions as the adjacent closet. They're the same basic object, rotated in space. The chest also has a lid.".

The note of the huge wooden chest is "'I don't know if we've ever used the chest in the back room for anything. I think Patrick the youth leader said they could keep sheet music in it, but fat chance of Marcie leaving her sheet music at church.[paragraph break]'But it doesn't bother me if it's always empty. Sometimes I take naps in there.'".

Instead of pushing or pulling or turning or taking the huge wooden chest, say "The chest won't budge. It must be bolted to the floor."

Instead of entering or sleeping in the huge wooden chest, try hiding in the noun.

Check hiding in the huge wooden chest:
	if the huge wooden chest is closed:
		try opening the noun;
		continue the action.
		
Instead of hiding in the chest:
	say "You lie down inside the chest, and notice that there's plenty of space inside. It's not a bad spot.[paragraph break]";
	wait for any key;
	say "But there's no time for napping now.[paragraph break]You pull yourself out of the box."


The closet is scenery in back room.
Understand "robe closet" as the closet.

Instead of examining the closet, say "Hanging in the barely-finished plywood closet are maybe two dozen robes."

The note of the closet is "'The robe closet and the big chest thing in the back room were both built by Patrick the youth leader. Can you tell?'"

Instead of searching the closet, say "You paw hesitantly between the robes in a few places, but you can't find anything else in the closet."

Instead of inserting something into the closet, say "That doesn't really belong there."

The hide description of the closet is "You can only bear to hide yourself among the robes for a moment, as the fabric pressing against you on all sides very quickly feels uncomfortable."

Instead of entering the closet:
	try hiding in the noun.


The closet contains some choir robes. Understand "robe" and "choir robe" as the robes.

The description of the robes is "The robes are cut from a green satin fabric; you can't imagine one looking very flattering on anybody."

The note of the robes is "'I have never had to wear the church robes for anything, thank goodness. I did have to wear a robe for choir, school choir, freshman year, which is one reason I didn't take choir sophomore year.'"

Instead of hiding in the robes, try hiding in the closet.

Instead of wearing the robes, say "You would prefer not to."

Instead of taking the robes, say "You pull out one robe, straighten its sleeves, and put it back."

Instead of searching the robes, try searching the closet.



The stack of hymnals is here. "A stack of hymnals is crammed into one corner."
Understand "books" as the hymnals.
The stack of hymnals is a portable enterable supporter.
The stack of hymnals is floor-standing and ambiguously plural.

The description of the stack of hymnals is "The books have been stacked very tidily into an almost perfect one-foot cube. It's actually very pleasant to see such an orderly display."

The hymnal is part of the stack of hymnals. Understand "book" as the hymnal. The description of the hymnal is "[if the stack of hymnals supports something]Each hymnal is fairly perfect-looking on its own[otherwise]You flip one hymnal open to a random song. This one's called '[one of]Great Is Thy Faithfulness[or]O Come, O Come, Emmanuel[or]This Is My Song[or]Be Still, My Soul[or]Cuando El Pobre[then at random][quotation mark][end if]."

The note of the stack of hymnals is "'There are so many hymnals in this church. Nobody would notice if one went missing. I happen to know this for an established fact.'"

Instead of entering the stack of hymnals when the stack of hymnals is on something:
	say "[if the chosen posture is standing]Standing[otherwise]sitting[end if] on the hymnals is far too dangerous while they're on top of [the holder of the noun]. What if the stack collapsed underneath you?"

After entering the stack of hymnals:
	say "You [if the chosen posture is sitting]sit down on[otherwise]step up onto[end if] the stack of hymnals, careful not to knock any of them out of place."

Instead of querying Casey's notes about the hymnal, try querying Casey's notes about the stack of hymnals.

Instead of opening the hymnal, try examining the noun.

Instead of taking the hymnal, say "It's such a perfect stack of books, though. Taking one away would ruin it."


One ectoplasm is in the Back Room.
"Some more ectoplasm is smeared on the floor."

After going to the Back Room when the ghostly figure is in Back Room:
	say "You catch just a glimpse of the ghost in the darkness before it floats towards the back wall and vanishes. When you switch the light on, the room is empty.";
	remove the ghostly figure from play;
	continue the action.

When Ghostly Apparition ends:
	remove Orchid from play;
	now the last location of Orchid is nothing;
	now the last action of Orchid is the action of waiting;
	now the Sanctuary is brightly lit;
	now the Altar is brightly lit.

After listening to the Back Room:
	say "Everything is silent."

The back room walls are scenery in Back Room.
The back room walls are plural-named.
Understand "wall" and "shabby" and "dark" and "wood" and "panels" as the back room walls.

The description of the back room walls is "[if the sliding door is open]A sliding door at the back of the room stands open, leading down into a narrow tunnel[otherwise if the sliding door is comprehended]A sliding door is set into the wall at the back of the room[otherwise]Looking closely at the walls, you realise that one of the panels on the wall is sitting slightly further forward than the others[end if]."

Last carry out examining the back room walls:
	now the sliding door is revealed.

Does the player mean querying about the back room walls:
	if the player is in Back Room:
		it is very likely;
	otherwise:
		it is very unlikely.

Does the player mean querying vaguely about the back room walls:
	if the player is in Back Room:
		it is very likely;
	otherwise:
		it is very unlikely.

[I don't know why LOOK UP WALLS is falling through to "consult the notes about "walls"" but I can't be bothered figuring it out and fixing it properly.]
Instead of consulting Casey's notes about "walls" when the location is Back Room:
	try querying Casey's notes about the back room walls.

Instead of knocking on the back room walls:
	say "Most of the panels produce only a dull thud, but [if the sliding door is revealed]the jutting-out panel[otherwise]one panel, sitting slightly further forward than the others,[end if] echoes hollowly.";
	now the sliding door is revealed.

Instead of opening the back room walls:
	try opening the sliding door.

Instead of pushing the back room walls:
	try pushing the sliding door.

Instead of pulling the back room walls:
	try pulling the sliding door.

Instead of turning the back room walls:
	try turning the sliding door.

Instead of searching the back room walls:
	if the sliding door is revealed:
		try searching the sliding door;
	otherwise:
		try examining the back room walls.

Instead of touching the back room walls:
	if the sliding door is revealed and the sliding door is not comprehended:
		try touching the sliding door;
	otherwise:
		say "[if the sliding door is comprehended]The sliding door[otherwise]One of the panels[end if] juts out further than the others.";
		now the sliding door is revealed.

Instead of taking the back room walls:
	try taking the sliding door.

Instead of attacking the back room walls:
	try attacking the sliding door.

The sliding door is a secret door.
The sliding door is below Back Room and above Tunnel.
The sliding door is not comprehended.
The sliding door has a number called comprehension. The comprehension of the sliding door is 0.
The sliding door has a list of stored actions called the attempts.
Understand "odd" and "panel" and "loose" and "jutting" and "jutting-out" and "jutting out" and "groove" as the sliding door.

The description of the sliding door is "[if the sliding door is comprehended]There's no handle, but you know how to open the door now[otherwise]The panel is smooth and more or less identical to the other panels. There isn't any handle or anything like that on it. But it's definitely jutting out further than the other panels[end if]."

Instead of going north in Back Room when the sliding door is comprehended:
	try going down.

Instead of going down in Back Room when the sliding door is not comprehended:
	try going north.

Rule for printing the name of the sliding door when the sliding door is not comprehended:
	say "odd panel".

Opening the sliding door is attempting to comprehend the sliding door.
Pushing the sliding door is attempting to comprehend the sliding door.
Pulling the sliding door is attempting to comprehend the sliding door.
Turning the sliding door is attempting to comprehend the sliding door.
Searching the sliding door is attempting to comprehend the sliding door.
Touching the sliding door is attempting to comprehend the sliding door.
Taking the sliding door is attempting to comprehend the sliding door.
Knocking on the sliding door is attempting to comprehend the sliding door.
Attacking the sliding door is attempting to comprehend the sliding door.
Opening the sliding door with something crowbarish is attempting to comprehend the sliding door.
Taking off the sliding door is attempting to comprehend the sliding door.

Opening the back room walls is attempting to comprehend the sliding door.
Pushing the back room walls is attempting to comprehend the sliding door. 
Pulling the back room walls is attempting to comprehend the sliding door.
Turning the back room walls is attempting to comprehend the sliding door.
Searching the back room walls is attempting to comprehend the sliding door.
Touching the back room walls is attempting to comprehend the sliding door.
Taking the back room walls is attempting to comprehend the sliding door.
Knocking on the back room walls is attempting to comprehend the sliding door.
Attacking the back room walls is attempting to comprehend the sliding door.
Opening the back room walls with something crowbarish is attempting to comprehend the sliding door.
Taking off the back room walls is attempting to comprehend the sliding door.

First every turn when attempting to comprehend the sliding door when the sliding door is not comprehended:
	let new act be the current action;
	if the action name part of the new act is the taking off action:
		now new act is the action of taking the sliding door;
	repeat with act running through the attempts of the sliding door:
		if the action name part of the new act is the action name part of the act:
			continue the action;
	[otherwise, we haven't tried this action yet]
	increment the comprehension of the sliding door;
	if the comprehension of the sliding door is 3:
		now the sliding door is comprehended;
		now the sliding door is open;
		now the time since progress counter is 0;
		say "Nothing much seems to happen at first, but then the panel shifts upward and slides to the side. Behind it a narrow tunnel leads downwards. You open and close the panel a few more times to make sure you know how it works, and then leave it open.";
	otherwise:
		add the new act to the attempts of the sliding door.

To decide if the current attempt on the sliding door will fail:
	if the comprehension of the sliding door is less than 2:
		decide yes;
	repeat with act running through the attempts of the sliding door:
		if the action name part of the current action is the action name part of the act:
			decide yes;
	decide no.

Before taking off the sliding door:
	try taking the sliding door instead.

Before taking off the back room walls:
	try taking off the sliding door instead.

Instead of opening the sliding door when the sliding door is not comprehended:
	say "You're not quite sure how to open the panel, or even if it can be opened. You jiggle it experimentally and it shifts a little."

Instead of pushing the sliding door when the sliding door is not comprehended:
	say "You press your palms against the panel[if the current attempt on the sliding door will fail], but it doesn't seem to move[end if]."

Instead of pushing the sliding door to the up:
	try pushing the sliding door.

Instead of pulling the sliding door when the sliding door is not comprehended:
	say "You grab the edges of the panel and try to pull it towards you[if the current attempt on the sliding door will fail], but your fingers slip off. There isn't enough room for you to get a grip on it[end if]."

Instead of turning the sliding door when the sliding door is not comprehended:
	say "The panel doesn't really turn, but you jiggle it in a few different directions to see if it will move."
	
Instead of searching the sliding door when the sliding door is not comprehended:
	say "Looking more closely at the panel, you find a small groove in the floor next to it. That seems to indicate that you can slide it to the side[if the current attempt on the sliding door will fail], but you can't seem to make it move[otherwise], so you give it a tug[end if]." 

Instead of touching the sliding door when the sliding door is not comprehended:
	say "You run your hands across the panel, pushing gently on any irregularities you find in the wood[if the current attempt on the sliding door will fail]. There doesn't seem to be any secret button or anything, though[end if]."

Instead of taking the sliding door when the sliding door is not comprehended:
	say "The panel won't come away from the wall. You can only manage to raise it off the floor a little."

Instead of knocking on the sliding door:
	say "You rap on [the sliding door], producing a hollow echo[unless the sliding door is comprehended or the current attempt on the sliding door will fail], and then press on it gently[end if]."
	
Instead of attacking the sliding door when the sliding door is not comprehended:
	say "You hammer on the panel a few times, but your blows echo loudly in the tiny room and also hurt your hand so you soon quit[unless the sliding door is comprehended or the current attempt on the sliding door will fail] that and push the panel hard instead[end if]."

Instead of opening the sliding door with something when the sliding door is not comprehended:
	if the second noun is crowbarish:
		say "You try to slide [the second noun] between the panel and the wall to use it as a lever[if the current attempt on the sliding door will fail], but it doesn't really fit[otherwise]. It's pretty awkward[end if].";
	otherwise:
		say "[The second noun] [is-are of the second noun] not really suitable for that purpose."


After opening or closing the sliding door:
	say "Now you know how it works, the door slides [if the sliding door is open]open[else]closed[end if] easily."

Instead of pushing or pulling or taking the comprehended sliding door:
	if the sliding door is closed:
		try opening the sliding door;
	otherwise:
		try closing the sliding door.

Instead of searching the open sliding door when the location is Back Room:
	say "Behind the sliding door, a narrow staircase leads downwards[if Tunnel is visited] to the tunnel[otherwise]. You can't see from here what's at the bottom[end if]."

Section - Tunnel

Tunnel is a hidden room. [THERE HAS TO BE A TUNNEL]

The note of Tunnel is "'You might want to know where the tunnel is, because that's where the fuse box is. If I remember to draw a map for you then I'll make sure to point out the tunnel and stick it in here.'[paragraph break]There is no map."

The map is a thing.
Instead of querying the notes about the map: say "Casey said something about maybe including a map in his notes, but it looks like he didn't get around to it. Guess you'll have to rely on your memory."

First carry out going to Tunnel when the room gone from is Back Room:
	if Tunnel is not visited:
		say "Behind the sliding door you find a steep, narrow flight of stairs with no handrail. You walk down them slowly, not wanting to miss your footing in the dim light."

The description of Tunnel is "You could hardly call this a room. It's just a long, musty, narrow space behind a wall. At one end, a flight of stairs runs up to the room behind the altar; at the other end a heavy door leads out to the south. In between them, a fuse box is mounted on the wall[if the fuse box is open], its metal door hanging open to reveal a row of switches[end if]."

The heavy door is a scenery door.
It is unrevealed.
It is south of Tunnel and north of Hall4.
The description of the heavy door is "There's no writing on the door, but it could hardly say KEEP OUT more clearly if there were."

Instead of querying Casey's notes about the heavy door, try querying Casey's notes about Tunnel.

Instead of going down in Tunnel: try going south.

After going when the west basement switch is switched off and the room gone from is the Tunnel and the door gone through is the heavy door:
	say "The heavy door slips out of your fingers and slams shut behind you[if in darkness], leaving you in darkness[end if].";
	now the heavy door is closed;
	continue the action.

First carry out going north from Hall4 when Hall4 is dark:
	say "You fumble around in the dark, eventually finding the door again."

The tunnel stairs are plural-named scenery in Tunnel.
Understand "flight" and "of" and "stairs" as the tunnel stairs.
The description of the tunnel stairs is "The stairs are so steep and narrow that they slightly resemble a ladder. There isn't even a handrailing. That's got to be dangerous."

Instead of querying Casey's notes about the tunnel stairs, try querying Casey's notes about Tunnel.

Instead of entering or climbing the tunnel stairs: try going up.

The fuse box is a privately-named container in Tunnel.
It is scenery and openable and open and transparent.
Understand "fuse box" and "box" and "fusebox" and "metal" and "door" as the fuse box.
[Understand "fuse" and "switch" and "switches" and "west" and "basement" and "red" as the fuse box when the fuse box is closed.] [removed to improve performance.]

The note of the fuse box is "'The fuse box is in the tunnel. I don't know how it works. Ohms.'"

The description of the fuse box is "[fuse-box-desc]."

To say fuse-box-desc:
	if the fuse box is closed:
		say "The metal door of the fuse box is closed";
	otherwise:
		say "Someone left the fuse box door hanging open, showing [fuse-box-contents]".

To say fuse-box-contents:
	say "a neat row of red switches";
	if the west basement switch is switched off:
		say ". Most are in the on position, but one, marked 'WEST BASEMENT', is off";
	otherwise:
		say ", all switched on".

Instead of searching the fuse box:
	if the fuse box is closed:
		try opening the fuse box;
	otherwise:
		try examining the fuse box.

The examine devices rule is not listed in any rulebook.

After opening the fuse box:
	say "You open the fuse box, revealing [fuse-box-contents]."

Does the player mean closing the fuse box:
	it is likely.

Does the player mean opening the fuse box:
	it is likely.

The WEST BASEMENT switch is a scenery device. It is in the fuse box.
Understand "red" and "switches" and "fuse" and "label" as the west basement switch.

Instead of examining the west basement switch when the fuse box is closed:
	try examining the fuse box.

The description of the west basement switch is "Unlike some of the other switches, the 'West Basement' switch actually has a legible label[if the west basement switch is switched off]. It's also the only switch that's not turned on right now[end if]."

Instead of querying Casey's notes about the west basement switch, try querying Casey's notes about the fuse box.

Before switching on or switching off the west basement switch when the west basement switch is switched off and the fuse box is closed:
	say "(first opening the fuse box)[command clarification break]";
	silently try opening the fuse box;
	[failsafe in case opening somehow doesn't work, should never kick in though]
	if the fuse box is closed:
		say "You'll have to open the fuse box first." instead.

Check switching on the fuse box:
	try switching on the west basement switch instead.

Instead of switching off the fuse box:
	try switching off the west basement switch.

Instead of switching off or switching on the fuse box when the west basement switch is switched on:
	say "Better not mess with the fuses any more than you have to."

Carry out switching on the west basement switch:
	now all rooms are lighted;
	now all rooms are brightly lit.

After switching on the west basement switch:
	say "You flick the last switch into the ON position."
	[do we need to add something to make it clearer that this did something useful? probably not?]

Before pushing or turning the west basement switch:
	if the west basement switch is switched off:
		try switching on the west basement switch instead;
	otherwise:
		try switching off the west basement switch instead.

Before switching off or switching on the west basement switch when the west basement switch is switched on:
	say "Better not mess with the fuses any more than you have to." instead.

Before closing the west basement switch:
	try closing the fuse box instead.

Before opening the west basement switch:
	try opening the fuse box instead.

[mysterious lights rig]
A cheap-looking mirror, a black sphere, and a black cloth are evidential things in the Tunnel.

A thing can be lighting-rig-componentish.
A thing is usually not lighting-rig-componentish.
The cheap-looking mirror, the black sphere and the black cloth are lighting-rig-componentish.

Rule for writing a paragraph about a lighting-rig-componentish thing when all lighting-rig-componentish things are in Tunnel:
	say "[A cheap-looking mirror], [a black sphere] and [a black cloth] are lying in a heap on the floor."
	[man, this rule is so clever. I am so clever. also, Inform 7 is so clever to enable this rule]

The cheap-looking mirror is a mirror.
Understand "cheap" and "looking" and "glass" as the cheap-looking mirror.

The description of the cheap-looking mirror is "The mirror is large-ish, but the frame is just cheap fake-wood-grain plastic and the glass (is it actually glass?) is slightly warped. Judging by the Goodwill price sticker on the frame, its previous owner didn't think much of it either."

Instead of searching the cheap-looking mirror, say "Your slightly warped reflection gazes back at you."

The photo description of the cheap-looking mirror is "The frame of the mirror almost looks like real wood in this photo. The glass shows a warped reflection of you holding the camera up to your face."


The black sphere is not comprehended. 

Rule for printing the name of the black sphere:
	omit contents in listing;
	if the black sphere is comprehended, say "party light sphere";
	otherwise say the printed name of the black sphere.

After printing the name of the lit black sphere while taking inventory:
	say " (glowing)".

Understand "ball" as the black sphere.
Understand "party" and "light" and "lights" as the black sphere when the black sphere is comprehended.

The description of the black sphere is "[if the black sphere is comprehended]It's one of those party light sphere things,[otherwise]It's[end if] a black metal ball with [if the black sphere is lit]bright blue light glowing out of the glass panels[otherwise]numerous round glass panels[end if] set into it. [touch description of the black sphere]".

The black sphere has some text called the touch description.
The touch description of the black sphere is "Turning it over[if the current action is touching] in your hands[end if], you notice that it's heavyish and slightly warm to the touch. Your fingers also find a small button on the top? base? side? of the sphere."

Instead of touching the black sphere:
	say the touch description of the noun;
	say line break;
	rule succeeds.

Instead of turning the black sphere:
	try touching the black sphere.

Instead of searching the black sphere:
	if the black sphere is lit:
		say "You can't bear to look into the light for more than a moment.";
	otherwise:
		say "The interior of the sphere is dark, and you can't make out anything through the glass panels."

After printing the name of a room when (the location is dark or the location is dimly lit) and the location encloses the black sphere and the black sphere is lit (this is the mention room illumination rule):
	say "[roman type] (weirdly illuminated by [the black sphere]) ".

The small button is part of the black sphere.
The small button is a device.
The description is "The black rubber button is barely visible on the surface of the sphere."
Understand "rubber" as the small button.

Carry out switching on the small button:
	now the black sphere is lit.

After switching on the small button:
	say "The button clicks, and blue light streams from the glass panels on the sphere";
	if a mirror (called reflector) is enclosed by the location:
		say ", reflecting off [the reflector] and scattering across the walls and ceiling";
	if the black sphere is not comprehended:
		say ". This must be one of those party light sphere things that are supposed to make your house look like a nightclub or something";
		now the black sphere is comprehended;
	say ".";
	if the location is dimly lit:
		try looking.

Carry out switching off the small button:
	now the black sphere is not lit.

After switching off the small button:
	say "The button clicks again and the light dies out."

Instead of pushing the small button:
	if the noun is switched on:
		try switching off the noun;
	otherwise:
		try switching on the noun.

Instead of pushing the black sphere:
	try pushing the small button.

Instead of switching on the black sphere:
	try switching on the small button.

Instead of switching off the black sphere:
	try switching off the small button.

Instead of photographing the small button with:
	try photographing the black sphere with the second noun.

[Carry out photographing the lit black sphere with the Polaroid:
	now the new photo is subject-lit.

Rule for printing the name of a photo (called pic) when the subject of the pic is the black sphere and the black sphere is comprehended:
	say "photo of the [if the pic is subject-lit]glowing[otherwise]dark[end if] [black sphere]".] [removed to improve performance.]

Rule for printing the description of a photo (called pic) when the subject of the pic is the black sphere:
	if the black sphere is comprehended:
		say "The photo shows the party light sphere you found, although it's hard to make out much detail in the picture."; [TODO: come back to this]
	otherwise:
		say "The photo shows the black sphere you found. Faint glints of light reflect off the glass panels set into it."


The description of the black cloth is "Thick, heavy black cloth of some kind[em dash]a curtain or something, maybe."

Understand "thick" and "heavy" as the black cloth.

The indefinite article of the black cloth is "some".

The photo description of the black cloth is "It's hard to make out any details in the photograph, almost as if the black cloth had absorbed the light."

Instead of wearing the black cloth:
	if the player does not carry the noun:
		say "(first taking [the noun])[command clarification break]";
		silently try taking the noun;
		if the player does not carry the noun:
			stop the action;
	say "You drape the cloth around your shoulders like a cloak, but it's much too long. It trails across the floor and starts to collect dust, so you take it off again and fold it up."

Chapter - Balcony And Higher Rooms

Section - Balcony Stairs

Stairs is east of Sanctuary. [pacing.] The printed name of Stairs is "Balcony Stairs".
Understand "balcony stairs/stairwell" as Stairs.
The lower case name of Stairs is "the balcony stairwell".

The description of the Stairs is "There isn't really any room in here for anything except the stairs up to the balcony[if the potted plant is in the Stairs], but that hasn't stopped someone from putting a potted plant in the corner. If you can avoid tripping over the plant, an[otherwise]. An[end if] archway to the west leads back out into the sanctuary." 

The note of Stairs is "'One thing you will notice about the stairs to the balcony is that they are too narrow for a person headed upstairs to pass a person headed downstairs, and vice versa. This has given rise to many awkward situations over the years.[paragraph break]'I considered recording some of these situations here for your edification, but I can't think of any.'"

The potted plant is an undescribed thing in Stairs.
Understand "fake" and "tiny" and "pot" and "palm" and "tree" as the potted plant.

The description of the potted plant is "The plant is a tiny potted palm. It's fake, but that's fair enough. It's not like there's enough light in [if the location is Stairs]here[otherwise]the balcony stairway[end if] for a real palm tree to flourish."

The note of the potted plant is "'The plant in the balcony stairs is supposed to distract you from how cramped the stairs are. By making them even more cramped! I know, right?'"

After taking the potted plant when the potted plant was in Stairs:
	say "You remove the tripping hazard from the bottom of the balcony stairs."

The narrow balcony stairs are a backdrop.
They are plural-named.
They are in Stairs and Balcony.

The description of the narrow balcony stairs is "A narrow flight of stairs leads [if the location is Stairs]up to the balcony[otherwise]down to the sanctuary[end if]."

Instead of querying the notes about the narrow balcony stairs:
	try querying the notes about Stairs.

Instead of climbing or entering the narrow balcony stairs:
	if the location is Stairs:
		try going up;
	otherwise:
		try going down.

Section - Balcony

Balcony is above Stairs.

The description of Balcony is "Standing here, you have a good view of the altar and most of the sanctuary. A low wall with a railing along the top prevents accidents, though it probably also obscures the view for anyone sitting on the balcony pews. A projector is mounted at one end of the pews, pointing down towards the front of the sanctuary. [if the trapdoor is closed]On the ceiling behind the pews, you can see the square outline of what must be a trapdoor of some kind[otherwise]Behind the pews you can see the ladder up to the steeple[end if], and a narrow staircase leads back down to the sanctuary."

The note of Balcony is "'The balcony is a good place to sit if you're antisocial, or if you're like Tammy and you want to read a novel while you're in church. It gets really hot up there in the summer, though.'"

After deciding the scope of the player when the location is Balcony:
	place the Altar in scope;
	unless the player is on the balcony pews and the posture of the player is sitting:
		place the Sanctuary in scope.

The balcony pews are an enterable scenery supporter in the Balcony.
They are plural-named.
The usual posture of the balcony pews is sitting.
Understand "pew" as the balcony pews.

The description of the pews is "There are only a few rows of pews up here, mounted on a series of steps so the people at the back can still see down to the altar."

The note of the balcony pews is "'Hey, think about this: how the heck did they get those big long pews up into the balcony? Yeah. Spooky.'"

The hide description of the balcony pews is "You lie down on the floor under one of the pews, but after finding some ancient chewing gum stuck to the bottom of the pew, you soon wriggle out again."

Instead of taking or pushing or pulling or turning the balcony pews:
	say "The balcony pews are screwed to the floor, probably for safety reasons." [yeah, the safety of our puzzle design. muahaha]

After entering the balcony pews when the chosen posture is sitting:
	say "Sitting on the pew, you can still see the altar clearly, but not the sanctuary."

After entering the balcony pews when the chosen posture is standing:
	say "You step up onto one of the pews."

After getting off the balcony pews:
	say "You get off the pew.";
	try looking.

Instead of entering something which is on the balcony pews:
	say "Given the narrowness of the seats, climbing on [the noun] while [it-they][if the noun is plural-named][']re[otherwise][']s[end if] on the pews seems pretty dangerous."

The ancient chewing gum is part of the balcony pews.
Understand "chewinggum" as the chewing gum.

The description of the chewing gum is "It's so old it might have started to fossilize."

The note of the chewing gum is "'The gum under the pews in the balcony is probably Tammy's. I don't have any proof for this.'"

Instead of doing something when the current action implies touching the chewing gum:
	say "You can't bring yourself to touch the chewing gum. It might be old, but it's still been in someone's mouth."

The railing is scenery in the Balcony.
Understand "low" and "wall" and "thick" and "wooden" as the railing.
The description of the railing is "A low wall runs along the edge of the balcony, with a thick wooden railing set on top of it. It's hard to tell if the railing is just meant to be decorative, or if it was added as an afterthought to make the wall a bit higher."

The note of the railing is "'Every year for Christmas, we have to hang this stupid fancy garland on the balcony railing, and it's a huge pain.'"

The stupid fancy garland is a familiar thing. The note of the garland is "'I don't know where they keep the garland and that kind of thing in the non-Christmas season. Weird.'"

Instead of climbing or entering the railing:
	say "Just leaning over the railing makes you feel dizzy."

The projector is scenery in the Balcony.
Understand "strong" and "metal" and "cage" as the projector.
The description of the projector is "A strong metal cage protects the projector from careless and/or malevolent handling."

The note of the projector is "'Someone donated money for a projector to the church, and it caused this big kerfuffle in the Board of Trustees because they didn't really want a projector. But they couldn't spend the money on anything else, so now we have a projector that we never use!'"

Instead of switching on the projector:
	say "Judging by the cage it's in, the church really doesn't want people messing around with the projector."

Instead of switching off the projector:
	say "It's already switched off."

Instead of opening the projector:
	say "The cage is locked, presumably to stop people like you from messing with the projector."

[trapdoor]
The trapdoor is a door. It is above Balcony and below Steeple. It is scenery.
Understand "trap" and "door" and "trap-door" as the trapdoor.
Understand "square" and "outline" as the trapdoor.
Understand "ladder" as the trapdoor [when the trapdoor is open]. [removed to improve performance]

The description of the trapdoor is "[trapdoor description]."
To say trapdoor description:
	if the trapdoor is closed:
		say "The trapdoor is set into the ceiling, well above your head. The cord to pull it down seems to have got caught in the door, and only a small loop is visible on this side";
	otherwise:
		say "A short ladder leads [if the player is enclosed by Balcony]up[otherwise]down[end if] through the trapdoor".

The note of the trapdoor is "'The trapdoor goes up into the steeple. Nobody uses it, because like I said, the steeple is super dangerous.'"

Instead of querying Casey's notes about the pull-cord, try querying Casey's notes about the trapdoor.

First instead of doing something when (the current action implies touching the trapdoor or the current action implies touching the pull-cord) and the player is on the balcony pews:
	say "The pews are too far forward; there's no way you can reach the trapdoor from here."

First instead of doing something when (the current action implies touching the trapdoor or the current action implies touching the pull-cord) and the player is on something that is not the balcony pews and the posture of the player is sitting:
	say "You've got no hope of reaching the trapdoor while you're sitting down."

Instead of doing something other than opening something with when the current action implies touching the trapdoor and the trapdoor is closed:
	if the player is on something (called the platform):
		say "Even standing on [the platform], your fingers are still a long way from reaching the loop of cord.";
	otherwise:
		say "The pull-cord is caught up in the trapdoor, and you're much too short to reach it."

Instead of opening the trapdoor with the shepherd's crook when the trapdoor is closed and the player is not on something:
	say "Standing on tiptoe, you flourish the hooked end of the crook at the loop of cord, but you still can't [italic type]quite[roman type] reach."

After opening the trapdoor with the shepherd's crook:
	say "You catch the loop of cord with the end of the crook and pull. The trapdoor opens and a small ladder slides down, offering access to the steeple above."

Instead of opening the trapdoor with the can opener:
	say "If the can opener were a foot or so longer[if the player is not on something] and you were a foot or so taller[end if], you might be able to hook the cord with it, but it isn't, [if the player is not on something]and you aren't, [end if]and you can't."

Instead of opening the trapdoor with the small white cross:
	say "The cross just isn't big enough to reach the loop of cord[if the player is on something and the posture of the player is standing], even while you're standing on [the holder of the player][end if]."

Instead of jumping in the Balcony when the trapdoor is closed:
	if the player is on the balcony pews:
		say "You jump off the pew, miss the pull-cord by miles, and almost fall on your face."; 
		now the player is in Balcony;
	otherwise if the player is on something (called the base):
		say "You jump, miss the pull-cord, and almost fall off [the base]. You know, maybe there's a safer way to reach the cord.";
	otherwise:
		say "No matter how high you jump, the pull-cord is still well out of your reach."

Instead of climbing the trapdoor:
	try entering the noun.

Check an actor going when the actor is on a supporter (this is the new stand up before going rule):
	if the door gone through is not the trapdoor:
		abide by the stand up before going rule.
		
Check an actor going (this is the new can't travel in what's not a vehicle rule):
	if the door gone through is not the trapdoor:
		abide by the can't travel in what's not a vehicle rule.

The new stand up before going rule is listed instead of the stand up before going rule in the check going rules.
The new can't travel in what's not a vehicle rule is listed instead of the can't travel in what's not a vehicle rule in the check going rules.

Instead of closing the trapdoor:
	say "You've gone to so much trouble opening it, you'd better leave it alone now."

Instead of throwing something at the trapdoor:
	try throwing the noun at the pull-cord.

First instead of taking the trapdoor with something:
	try opening the trapdoor with the second noun.

The pull-cord is scenery in Balcony.
Understand "pull" and "cord" and "pullcord" and "pull-cord" and "loop" and "of" as the pull-cord.
The description of the pull-cord is "[if the trapdoor is closed]The cord to open the trapdoor is caught up in the door. You can just see a small loop sticking out between the ceiling and the edge of the trapdoor, well out of reach[otherwise]The pull-cord dangles off the end of the ladder[end if]."

First instead of hooking with the can opener when the noun is the trapdoor or the noun is the pull-cord:
	try opening the trapdoor with the second noun.

Instead of doing something when the current action implies touching the pull-cord and the trapdoor is closed:
	try opening the trapdoor. 

First instead of opening the pull-cord with something:
	try opening the trapdoor with the second noun.

First instead of taking the pull-cord with something:
	try opening the pull-cord with the second noun.

Last instead of throwing something at the pull-cord:
	say "[The noun] miss[es] the pull-cord, by a lot. Throwing has never been one of your strengths.";
	now the noun is in the location.

[engelgurt]
The Engelgurt is a thing in the Balcony.
The Engelgurt is not comprehended.
The Engelgurt is evidential.
The Engelgurt can be examined. It is not examined.

Instead of touching the Engelgurt, say "The bars feel cold. Cold like a broken promise."

Rule for printing the name of the Engelgurt when the Engelgurt is not comprehended:
	say "tangle of bars and cables".

Understand "tangle" and "tangled" and "heap" and "pile" and "of" and "bars" and "and" and "cables" as the Engelgurt.
Understand "metal" and "pulleys" as the Engelgurt.
Understand "engelgurt" as the Engelgurt [when the Engelgurt is comprehended]. [Removed to improve performance. This is the paradigm case, really: all these understand rules will tend to BREAK MIMESIS by letting you refer to things by names that don't make sense in-game, like calling the Engelgurt the Engelgurt before Robin knows it's called the Engelgurt. But all these realism-preserving rules in concert caused the game to run suuuuuuper sloooooow. So, I'm not sorry. I'm Ryan, by the way. Emily might be sorry.]

The initial appearance of the Engelgurt is "A tangled heap of bars and cables is lying near the edge of the balcony."

The description of the Engelgurt is "It's hard to tell [if the engelgurt is comprehended]how the Engelgurt works[otherwise]what this stuff is for[end if] with it all tumbled into a heap like that. There are some metal bars, and some pulleys and things, and a lot of tangled cables connecting things together."

Instead of entering the Engelgurt:
	say "All those bars and cables tumbled into a pile don't really make a good place to [if the chosen posture is sitting]sit[otherwise]stand[end if]."

Rule for printing the description of a photo (called pic) when the subject of the pic is the Engelgurt:
	if the Engelgurt is comprehended:
		say "You still can't figure out exactly how the Engelgurt is supposed to work; it's all just too tangled up. But the bars, cables and pulleys match the description in Casey's notes.";
	otherwise:
		say "The heap of bars, cables and pulleys looks even more confusingly tangled in the photo than it does in real life."

[The note of the Engelgurt is down in Storage West, with the rest of the Christmas Play Story.]

[The faux-Engelgurt is a thing.
It is privately-named.
It is familiar.
The printed name of the faux-Engelgurt is "Engelgurt".
Understand "Engelgurt" as the faux-Engelgurt when the Engelgurt is not comprehended.

When play begins:
	now the note of the faux-Engelgurt is the note of the Engelgurt.] [removed to improve performance]

Carry out examining the Engelgurt:
	now the Engelgurt is examined.

[Carry out querying Casey's notes about the faux-Engelgurt:
	now the Engelgurt is noted.]
	
After examining something when (the noun is the Engelgurt or the noun is a photo that shows the Engelgurt) and the Engelgurt is not comprehended and the Engelgurt is noted:
	now the Engelgurt is comprehended;
	say "Something about it seems familiar though. Wait[em dash]this must be the 'Engelgurt' Casey mentioned in his notes."

After querying Casey's notes about the Engelgurt when the Engelgurt is examined and the Engelgurt is not comprehended:
	say "Something about the description of the Engelgurt sounds vaguely familiar."

Instead of taking or pushing or pulling or searching the Engelgurt:
	say "You try to disentangle some of the cables but it's all too mixed up. You sort out one bit and the rest just gets knottier."

Check querying Casey's notes about the Engelgurt when the Engelgurt is not comprehended:
	unless the player's command includes "engelgurt":
		say "But what is it? You're not sure what to look for in the notes." instead.

First before doing something when (the noun is the Engelgurt or the second noun is the Engelgurt) and the Engelgurt is not comprehended:
	if querying about or querying vaguely about:
		continue the action;
	if quizzing about the engelgurt or informing about the engelgurt or requesting for the engelgurt:
		try asking the noun about "nonsense" instead;
	if the player's command includes "engelgurt":
		say "You can't see any such thing." instead.

Section - Steeple

Steeple is a room. Understand "church steeple" as the steeple. The description of Steeple is "The inside of the church steeple is a cold concrete cube with cinderblock walls. There's one window, but it's been boarded up."

The note of Steeple is "'One story says Jennie Bancroft threw herself off the church steeple. She did this because she loved some boy and her father forbade her to see him. Or, because she loved some boy and the boy didn't love her back. I think the second one is more likely; usually when a girl is forbidden to see a boy they're able to figure something out. They'll meet up in a secret location, like an old barn. Or just out in the woods somewhere.[paragraph break]'I've never been up inside the steeple, because they made it impossible to get in there, because it's super dangerous.'"

The cinderblock walls are scenery in steeple.
The cinderblock walls are plural-named.
The description of the cinderblock walls is "The walls and floor are same shade of rough concrete."
Understand "concrete" and "floor" as the cinderblock walls.

Instead of querying Casey's notes about the cinderblock walls:
	try querying casey's notes about Steeple.
	
The boarded up window is scenery in Steeple. Understand "board" or "boards" or "boarded-up" as the boarded up window. The description of the boarded up window is "This window has been boarded up for a very long time."

Instead of searching the boarded up window, say "All you can see in the window are the boards."

Instead of entering the boarded up window, say "Impossible, with all those boards in the way."

Instead of querying Casey's notes about the boarded up window:
	try querying casey's notes about Steeple.

Aiden is here. Understand "Kingsley" or "Aiden Kingsley" as Aiden. The initial appearance of Aiden is "[Aiden status]."

The note of Aiden is "'I guess you've probably seen Aiden in plays, but I've actually worked with him, so I know what I'm talking about. He is unbearable. You should be able to guess from the mustache.[paragraph break]'Aiden goes to parties, gets girls drunk, makes out with them, and then convinces them that his number is [']123-4567[']. And then he tells this story to everyone at rehearsal and expects us to laugh.[paragraph break]'Speaking of which. This is the worst thing Aiden does: He laughs at every single one of his own jokes[unicode 8212]sometimes just at sentences he says that can't even be considered jokes[unicode 8212]and it's always the same single-syllable laugh, always the same pitch, pronounced exactly the same way. It's not really a laugh, it's more of a signal that you're supposed to think he's funny.[paragraph break]'Aiden doesn't even go to my church, but he's the worst, and I think you should know that.'"

To say Aiden status:
	If the current action is looking:
		if the location is Steeple:
			say "[one of]You jump. There's a boy sitting on the floor, smirking at you. It's Aiden Kingsley[or]Aiden looks you over[stopping]";
		otherwise:
			say "[one of]Aiden is here, rubbing his arm and pouting[or]Aiden looks you over[at random]";
	Otherwise if the current action is going:
		if the location of Orchid is the location of Aiden:
			say "[one of]Aiden and Orchid come in after you[or]Orchid and Aiden follow along[or]Orchid and Aiden come down the stairs after you[or]You usher Aiden and Orchid in ahead of you[stopping]";
		otherwise:
			say "[one of]You look back to make sure Aiden has followed you[or]Aiden mopes along after you[or]Aiden follows you in[stopping]".
		
Instead of kissing aiden the first time, say "Driven by some insane whim, you walk up close to Aiden[unicode 8212]but once he lays a hand on you, you jump back. [if Orchid is in the location]Orchid[otherwise]He[end if] snickers."
		
Instead of kissing aiden, say "You can't. He's too gross."

The description of Aiden is "Aiden is an extremely gawky senior, a full head taller than you. He's still carrying his pigtailed wig, and he still has glowing makeup covering most of his face[unicode 8212]including his mustache. He carries himself with far too much confidence for how ridiculous he looks."

Aiden carries the pigtailed wig.
Understand "pigtail" and "pigtails" as the pigtailed wig.
The description of the pigtailed wig is "The wig is glowing faintly around the edges, where it rubbed up against Aiden's makeup."

Aiden wears the glowing makeup. The description of the glowing makeup is "In this light, Aiden looks less like a glimmering specter and more like a cheap special effect[if the glowing tissue paper is handled]. The makeup glows just like the stuff on the tissue paper you found in the men's room[end if]."

The mustache is part of Aiden. The description of the mustache is "Aiden's mustache has never been as impressive as he thinks, but it looks even stupider underneath all that makeup."
Understand "moustache" and "stache" as the mustache.

The glowing dress is here. "The ghost's glowing dress lies in a heap nearby. It doesn't look very ghostly now."
Does the player mean examining the glowing dress: it is very likely.
The description of the glowing dress is "It's been cut up and painted, but you're pretty sure you recognize the dress as the one Orchid wore last Halloween, when she was Dorothy Gale.".

Instead of wearing the glowing dress, say "You'd rather not get glowing paint all over yourself, if you can help it. Besides, it's incriminating evidence."

Does the player mean querying about the glowing dress:
	it is very unlikely.

Does the player mean querying vaguely about the glowing dress:
	it is very unlikely.

Aiden can be cold. Aiden is not cold.

Every turn while in Steeple (this is the Aiden gets cold rule):
	If Aiden is not cold:
		now Aiden is cold;
		say "Aiden stands up. 'Hey, hey, Robin! Are you enjoying your evening?' he asks, smiling.[paragraph break]You are not smiling. 'So you're the ghost.'[paragraph break]'Well, yes. Well, no. I am not a dead person. Heh. But I portrayed the ghost you saw, yes.'[paragraph break]'Does Orchid know?'[paragraph break]'Does Orchid know. Does Orchid know? What a well-chosen detective question.'[paragraph break]Aiden looks down the trapdoor behind you. 'Do you think we could continue this conversation downstairs? It's really freaking cold up here.'";
		now the current interlocutor is Aiden. [circumvent a zillion problems related to the current interlocutor]

Table of Bringing Aiden Before Sharon
next room	done	remark
Balcony	false	"'Yes. Now I'll tell you the answer to your question. Of course Orchid knows. This was all Orchid's idea. She was very naughty. And I'm not even supposed to be here, so I should probably get going.'[paragraph break]He tries to slip past you, but you grab his arm.[paragraph break]Aiden yelps like a dog. 'What's your problem?'[paragraph break]'You're not going anywhere. I've got a few more questions.'[paragraph break]'Then can you get your hands off me first? You're gonna break my arm.'[paragraph break]'Sure.' You let go of his arm. 'Let's get going.' You know exactly where the two of you are going, but you should probably keep that a surprise."
Stairs	false	"'Yeah, Orchid brought the idea to me. She said if she could write a story about a real ghost, then maybe people would actually read the newspaper. Heh. But I told her I'd help her.'[paragraph break]'Why?'[paragraph break]'For the heck of it, primarily.'"
Sanctuary	false	"'Originally, she was just going to let me in the church during the sleepover, and we'd just spend the night taking a bunch of ghost pictures. However. Then we found out that you were going to be the other newspaper girl staying the night. And Orchid said you'd never go along with it. So we had to change the plan.'"
Narthex	false	"As you enter the narthex, Orchid strolls in through the front door of the church. When she sees Aiden, her eyes go wide.[paragraph break]'Aiden! What are you doing here?'[paragraph break]'Hey. Don't bother,' Aiden snaps. 'I've already told her everything. Or almost everything. Heh.'[paragraph break]Orchid rushes up to Aiden with her fists clenched. 'You're lying. There's no proof. All you can prove is that you broke into the church and tried to scare us.'[paragraph break]'Well, no,' says Aiden. 'I can prove some other things. I have the receipts. For the makeup and everything. Because you had to use a credit card, idiot.'[paragraph break]Orchid goes quiet.[paragraph break]'Well, we can talk about this in the morning,' Aiden says, and he makes for the southern exit[unicode 8212]but you grab his arm, and he cries out softly. 'Yeah. Your way is probably better.'"
Dining	false	"'Now, where were we? Orchid didn't trust you to help her pull off this hoax. She said you're too much of a straight arrow. Those weren't her exact words. What were your exact words, Orchid?'[paragraph break]Orchid is looking down at her shoes.[paragraph break]Aiden shrugs. 'She said something really nasty.'"
Main Floor Stairway	false	"'But Orchid had this other great idea. She figured if we went a little further with the hoax, we could get you to believe the ghost was real. Heh.'[paragraph break]'I wasn't trying to trick you!' Orchid pleads. 'I just wanted it to be authentic. I wanted it to be a great story.'[paragraph break]'Sure, and then the great ghost story would be in the newspaper with the name of the most trustworthy reporter on staff on it, and Orchid's name too.' Aiden chuckles. 'But, looks like that didn't work out.'"
Basement Stairway	false	"Aiden looks past you, into the nursery, and you can see something click in his brain.[paragraph break]'Although, you know, we could still do it. You guys can write the story, I won't say anything, Orchid won't say anything, everyone will love it, you'll probably be co-editors-in-chief next year. Let's go back upstairs, we'll get some better pictures of the ghost.'[paragraph break]'No thanks,' you say."
Nursery	true	"BUG: Aiden doesn't actually have anything to say at this point and this text should not be printing, goshdangit."

The progress towards Sharon is a number that varies. The progress towards Sharon is 1.

Every turn during Confronting Aiden (this is the Aiden follows rule):
	if the location of Aiden is not the location of the player:
		[have Aiden follow the player]
		let the way be the best route from the location of Aiden to the location of the player, using doors;
		if the location of Aiden is the location of Orchid:
			silently try Orchid going the way;
		silently try Aiden going the way;
		if Aiden is in the location:
			say "[Aiden status].[paragraph break]";
		[Orchid is in the narthex]
		if the location is the Narthex:
			move Orchid to the location;
			now the last action of Orchid is the action of scowling;
		[have Aiden explain the plan]
		choose row progress towards Sharon in the Table of Bringing Aiden Before Sharon;
		if the done entry is false:
			say the remark entry;
			say line break;
			now the done entry is true;
			increment the progress towards Sharon.

Instead of saying yes when the player is in Steeple:
	say "'I think that's a very good idea,' you say in your most ominous voice.[paragraph break]Aiden beams at you obliviously. 'Great! Let's get going.'"

Instead of chatty behavior when the player is in Steeple:
	if the current action is saying hello to and the greeting type is implicit:
		continue the action;
	say "Aiden grimaces. 'Seriously, can we talk downstairs? I'm about to freeze to death. And then I'll be a real ghost! Heh.'"

Instead of chatty behavior when Confronting Aiden is happening and the location is not the Steeple:
	if the actual interlocutor is Sharon:
		try waking Sharon;
	otherwise if the actual interlocutor is Orchid:
		say "Orchid declines to comment.";
	otherwise: [presumably talking to Aiden]
		say "Aiden looks around nervously. 'Why don't we keep moving, huh?'";

[We need some machinery to prevent the player from taking any path except the one that leads back to the nursery.]
Check going during Confronting Aiden:
	choose row progress towards Sharon in the Table of Bringing Aiden Before Sharon;
	if the room gone to is not nothing and the room gone to is not the next room entry:
		say "No time to explore now. Sharon needs to know what's been going on." instead. 

Volume - The Ghost

Jennie Bancroft is a woman. Understand "ghost" or "ghosts" or "jenny" as Jennie Bancroft.

The note of Jennie Bancroft is "'I don't know how much you already know about the ghost in the church so I'll give you the whole story. Jennie Bancroft is the name of the ghost that haunts Grace United Methodist. She used to be alive, but then she died, in or around the church, sometime in the distant past. I keep meaning to look up when she died but I haven't had time.[paragraph break]'When people see her, she looks like a teenage girl in pigtails. I don't think she shows up covered in blood or anything like that; she's just a sad girl. Obviously a lot of the time she's invisible though.[paragraph break]'Some of the evidence that we have for her existence includes:[paragraph break]- sounds of footsteps and a girl crying when nobody else is in the church[line break]- cold spots in rooms, rooms that suddenly become cold for no reason[line break]- stuff goes missing, then shows up again[line break]- you see someone at the end of the hallway but when you get to the end of the hallway there's nobody there[line break]- basically everybody has seen her out of the corner of their eye at some point[paragraph break]'Debate continues on the subject of how Jennie died. Popular theories state that she jumped off of the church steeple, or she drank poison, or used a space heater to carbon monoxide herself. According to one explanation she died accidentally, because she was using a Ouija board, but that theory is idiotic. Jennie Bancroft definitely committed suicide, and it was definitely because of a boy she was in love with.'"

Rule for clarifying the parser's choice of Jennie Bancroft:
	do nothing.

Does the player mean querying about Jennie Bancroft:
	if the ghostly figure is on-stage:
		it is unlikely;
	otherwise:
		it is very likely.

Does the player mean querying vaguely about Jennie Bancroft:
	if the ghostly figure is on-stage:
		it is unlikely;
	otherwise:
		it is very likely.

Instead of consulting Casey's notes about "suicide":
	try querying the notes about Jennie instead.

Poison is a thing. The note of poison is "'So some people think Jennie poisoned herself. I think this idea mostly makes sense. I mean, on one hand, why would there be poison in a church? But on the other hand, she could easily have brought the poison to the church with her. On the first hand, though, why would she drink the poison in the church if the poison was somewhere else to begin with? But on the other hand, a church is a pretty dramatic location to kill yourself in, and I assume teenagers back then were about as dramatic as they are today.'"

Carbon monoxide is a thing. Understand "space heater" or "heater" or "carbon monoxide poisoning" or "asphyxiation" as carbon monoxide. The note of carbon monoxide is "'The other theory is that she asphyxiated herself with carbon monoxide from a space heater. I don't know how space heaters work, or whether there was anything like that in the church ever. The jury is out on this theory.'"

The Ouija board is a thing. The note of the Ouija board is "'At some point someone put forth the theory that Jennie Bancroft died because she used a Ouija board in a séance. I don't remember who said this or whether they supplied any other details. It seems like the kind of thing Patrick the youth leader would say, since he's always going on about how evil Ouija boards are, but why would Patrick the youth leader, who does not believe in ghosts, present a cautionary tale about Ouija boards as an explanation for the existence of a ghost? Also, how the heck does a Ouija board kill someone? It doesn't add up, Robin.[paragraph break]'It's a nonissue though because the truth is Jennie killed herself.'"


































[DON'T READ THIS PART, ALL THE STUFF BELOW HERE IS IMAGINARY]

































[SERIOUSLY GO AWAY]







































[NO NO NO NO NO NO NO NO NO NO NO NO NO NO NO NO NO STOP]





























[FINE]










Hazy Place is a room. [I've decided not to give it a room description.]

Hazy Place is privately-named.
Understand "hazy place" as the hazy place.

Instead of going nowhere in Hazy Place, say "There is nowhere to go."

Jennie is in Hazy Place.

Catwaiting is a number that varies. Catwaiting is 0.

Waiting is acting quietly.
Examining is acting quietly.
Looking is acting quietly.
Querying something about is acting quietly.
Querying vaguely about is acting quietly.
Listening is acting quietly.
Smelling is acting quietly.
Taking inventory is acting quietly.
Thinking is acting quietly.

Every turn when Hazy Place is not visited (this is the waiting for the cat rule):
	If the location is Boiler and the can of tuna is open and the can of tuna is in Boiler:
		if the current action is acting quietly:
			increment Catwaiting;
			if Catwaiting is 23:
				cause the cat to appear;
		otherwise:
			now Catwaiting is 0.
						
To cause the cat to appear:
	say "You hear an uncertain meow in some dark corner, and the cat trots out from behind the pipes. It only glances at you as it approaches the can of tuna and begins to chow down.[paragraph break]You crouch down to watch; it ignores you. When it's had its fill, it turns back around to its hiding place in the pipes. Just before it disappears, though, it looks back at you. Its eyes are very wide.[paragraph break]There's a space between the pipes to the north that wasn't there before.";
	now Hazy Place is mapped north of Boiler;
	move the cat to Hazy Place.



Rule for writing a paragraph about Jennie:
	say "A sad girl is here. [The cat] is cradled in her arms."

Rule for writing a paragraph about the cat when the location is Hazy Place:
	now the cat is mentioned.
	
Understand "girl" and "sad girl" as Jennie.
Understand "old-fashioned" and "clothes" as Jennie [when the location is Hazy Place].

Jennie is not proper-named.
The indefinite article of Jennie is "the".

Rule for printing the name of Jennie Bancroft:
	 if the location is the Hazy Place:
		say "sad girl";
	otherwise if the ghostly figure is seen:
		say "ghostly figure";
	otherwise:
		say "ghost".

The description of Jennie is "She must be about your age, but her clothes seem old-fashioned. Her hair is tied into pigtails. She's not crying, but you can tell she's sad."

Jennie can be transparent.
Jennie is transparent.

Jennie's pigtails are part of Jennie.
Jennie's pigtails are privately-named and plural-named.
Jennie's pigtails are not proper-named.
The printed name of Jennie's pigtails is "pigtails".
Understand "hair" and "pigtail" and "pigtails" as Jennie's pigtails.
The description of Jennie's pigtails is "Nobody wears pigtails anymore."

Instead of pulling Jennie's pigtails, say "You should try to be nicer."

Instead of taking Jennie's pigtails, try pulling the noun.

Instead of querying Casey's notes about Jennie's pigtails:
	try querying Casey's notes about Jennie.

Does the player mean querying about Jennie's pigtails:
	if Hazy Place is visited:
		it is likely;
	otherwise:
		it is very unlikely.

Rule for clarifying the parser's choice of Jennie's pigtails:
	do nothing.

Instead of chatty behavior when the location is Hazy Place:	
	say "You say something to the girl, and she says something back. And now you remember something...[paragraph break]***[paragraph break]";
	wait for any key;
	now Hazy Place is not mapped north of Boiler;
	say "Just as you caught up to Gideon in the steeple, he climbed out the window onto the roof. You pleaded at him to come back, but he ignored you.[paragraph break]So you climbed out after him. You called his name again and again, but he paid no mind. And then you had caught up to him, and you reached out to him, but your other hand lost its grip.[paragraph break]You were sliding off of the roof, you were floating in space[unicode 8212]then there was a loud noise, and you were on the ground.[paragraph break]Gideon jumped down after you, but he landed on his feet, of course, and when he saw that you weren't moving, he simply padded away.[paragraph break]";
	wait for any key;
	say "***[paragraph break]That's odd. You weren't standing here before. What happened? You remember you thought you saw something between the pipes...[paragraph break]...But you can't remember what it was, and there's certainly nothing there now.";
	remove the mass of tuna from play;
	remove the cat from play;
	now the cat is male;
	now the time since progress counter is 0; [we don't want the cat starting a cat-hinting scene immediately after this]
	move the player to Boiler.


