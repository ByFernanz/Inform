"Child's Play" by Stephen Granade

[The source code to Child's Play is licensed under a Creative Commons NonCommercial Sampling Plus 1.0 License. You are encouraged to take large chunks of the source and incorporate them into your games.]

Include Extended Banner by Stephen Granade. Include Plurality by Emily Short. Include Unicode Character Names by Graham Nelson.

The release number is 4. The story creation year is 2007. The story headline is "A child, a toy, and a rival". The story genre is "Fiction". The story copyright string is "2007-2008". The story rights statement is "All rights reserved." The story description is "It is playgroup day and playgroup day is normally a good day but ever since that little red-haired girl started coming she always wants your toys.

She shouldn't get your toys.

You tried telling the mom this but she doesn't understand you. She mostly ignored you but then she just shoved a pacifier in your mouth and changed your diaper."

VOLUME 0 - On I7 Extensions and Bug Workarounds

Section 0 - Compilation Smallerization (for Z-machine only)

[If we compile for z-code, we'll need memory economy turned on. However, by making this section z-code only, I can compile for Glulx when I need to troubleshoot and have all of my rule names and the like show up when I type RULES.]

Use memory economy.

Section 1 - Extensions & Workarounds

[Break out of a repeating loop.]
To stop repeating: (- break; -).

[How to find out if a current action is running silently.]
To decide if running silently: (- keep_silent == 1 -). To decide if not running silently: if running silently, decide no; decide yes.

[How to know if a testing command is executing (thanks to JDC)]
To decide if testing is in progress: (- (TestCheck()) -).
To decide if testing is not in progress: (- (~~TestCheck()) -).
Include (-
[ TestCheck ;
#IFDEF TEST_SCENARIOS_EXIST;
if (test_stack-->2 > 1) rtrue;
#ENDIF;
rfalse; ];
-).

[I use randomness in say phrases enough that I want a short form]
To decide if (x - a number) out of (y - a number):
	if a random chance of x in y succeeds, decide yes;
	decide no.

[And similarly a definition for two things being in the same room]
To decide whether (x - a thing) and (y - a thing) are co-located:
	if the location of x is the location of y, decide yes;
	decide no.

To decide whether (x - a thing) and (y - a thing) are not co-located:
	if x and y are co-located, decide no;
	decide yes.

Definition: a thing is present if the location of the player encloses it.

To decide if (x - a thing) is acted upon: if the noun is x or the second noun is x, decide yes; decide no.

To decide if (x - a thing) is not acted upon: if x is acted upon, decide no; decide yes.

[Copy rows from one table to another.]
Include
(-
[ CopyTableRows table1 table2 v0 i j;
	for (i=1: i<=TableRows(table1):i++)
		if (TableRowIsBlank(table1, i)==false) {
			v0 = TableLookUpEntry(table1, 1, i);
			j = TableBlankRow(table2);
			if (TableRowIsBlank(table2, j)==true)
				TableLookUpEntry(table2, 1, j, 1, v0);
		}
	rfalse;
];
-).
To copy rows from (t1 - a table-name) to (t2 - a table-name): (- CopyTableRows({t1}, {t2}); -).

[Remove a room from a backdrop's list of locations. We don't actually delete it -- we write over the value with the value of a placeholder called "MTRoom".]
MTRoom is a room.

Include (-
[ RemoveRoomFromBackdrop R B i k;
	i = B.#found_in;
	for (k=0: k<i/WORDSIZE: k++) {
		if (B.&found_in-->k == R) {
			B.&found_in-->k = (+ MTRoom +);
		}
	}
];
-).
To delete (R - a room) from (B - a backdrop): (- RemoveRoomFromBackdrop({R},{B}); -).

[The printed name of the player is "yourself". That works really well for messages like "You can't take yourself." It fails miserably for things like NPC reactions, where the NPC may be reacting to another NPC or to the player: "Jemison follows [the leader]." So: a workaround "say" phrase.]
To say name of (t - a thing):
	if t is the player begin;
		say "you";
		mark yourself in output;
	otherwise;
		say "[the t]";
	end if.

To say name of (t - a thing) is:
	if t is the player begin;
		say "you are";
		mark yourself in output;
	otherwise;
		say "[the t] is";
	end if.

[A lot of times, I7 is too smart and turns things like aliens' to aliens" when printed. This say statement lets me get around that.]
To say ': say unicode 39.

[Since you can't pass variables defined by "let" or phrase options to another phrase (i.e. I7 has no closures), this hacky global workaround.]
hacky global item is an object that varies.

VOLUME 1 - On New and Changed Kinds

Chapter 1 - New Kinds, Which Extend the Taxonomy of the World

[The active/passive dichotomy indicates whether or not their state machines are currently running.]
A person can be a baby or a parent. A person is usually a parent. A person can be calm or upset. A person is usually calm.

A playmate is a kind of person. A playmate is always a baby. The plural of playmate is playmates. The specification of a playmate is "Represents a child with whom the player can play, for varying values of 'play'." A playmate can be active or passive. A playmate is usually passive.

The player is a baby. The player is male. The player has a number called cry count. The cry count of the player is 0. The player is in Beneath the Covers.

Instead of examining the player, say "You are as [if the player is wearing the squid costume]powder blue and squiddy[otherwise]baby-like[end if] as ever[if the player is not wearing the squid costume][note baby-desc][end if]." Instead of taking the player, say "You do not need to pick yourself up, you already are all self-possessed." Instead of tasting the player, say "You give yourself a lick and I guess you taste okay." Check smelling the player: instead say "You are baby powder fresh."

[A one-time deal. Concept borrowed from zarf. NB that you cannot examine a one-time deal without affecting it all Schrodinger-like. The first time you query it, it is changed from not over to over.]
A one-time deal is a kind. A one-time deal can be over. The specification of a one-time deal is "Represents an event that should occur once and only once. You can see whether a one-time deal has occurred or not, but note that the first time you do so, the one-time deal is then considered to have occurred."
To decide if (d - a one-time deal) has occurred:
	if d is over, decide yes;
	now d is over;
	decide no.

To decide if (d - a one-time deal) has not occurred:
	if d has occurred, decide no;
	decide yes.

[Furniture is a kind of supporter with some wacky rules. Normally I would specify that furniture is suitable for pulling up on here, but I can't because I7 cares about definition order.]
Furniture is a kind of supporter.

Rule for deciding the concealed possessions of furniture:
	if the player is quarter-height-or-higher, no;
	if the particular possession is a person, no;
	[A teeny hack to allow the cup of cheerios to be seen and interacted with even from the ground]
	if the particular possession is the cup of Cheerios, no;
	otherwise yes.

[If the concealed possessions thing worked as I'd like, these next two rules would not be necessary.]
Rule for printing the name of furniture when the player is not standing:
	omit contents in listing;
	continue the action.

Rule for writing a paragraph about furniture (called the item) when the player is not standing:
	say "[the initial appearance of the item][line break]".

[This next rule should make it so things on scenery furniture aren't listed in the room description when the player isn't standing. Sadly, it doesn't.]
Rule for writing a paragraph about scenery furniture (called the item) when the player is not standing:
	now everything on the item is mentioned;
	now the item is mentioned.

Chapter 2 - Modified Existing Kinds, Which Allow for Richer Gameplay

[It is possible that I stole this from Emily Short's Complex Listing, though I admit nothing.]
A thing can be marked for special listing or unmarked for special listing. A thing is usually unmarked for special listing.

A room has some text called the faraway descriptor.

To say nearly-in-reach message for (n - a thing): say "You are much closer to [the n] but still not close enough to reach it."

Instead of putting something on a supporter (called s) when the player is not standing:
	if the player is on s, continue the action;
	if s is the short plastic block or s is the bugish plastic block or s is the tall plastic block, instead say "You could put things on [the s] but then you wouldn't be able to climb onto it so you refrain.";
	say "Your arms are too short for that."

Instead of pushing, pulling or waving a supporter (called the item) when the player is standing:
	if the player is not pulled up on the item, continue the action;
	say "You cannot really push or pull [the item] while you are pulled up on it, that would be a terrible tax on your sense of balance which is nearly nonexistent."

Instead of searching a supporter, say "It is not something you have to search."

[If you're on a supporter, >DOWN should get you down off of the supporter.]
Instead of going down when something (called platform) supports the player: try getting off the platform.

Instead of someone going down when something (called platform) supports the person asked: try the person asked getting off the platform.

Definition: a supporter is occupied if someone is on it.

Instead of entering an occupied supporter:
	if the player is on the noun, say "You are already there!";
	otherwise say "[The noun] doesn't have enough room as long as [a random person on the noun] is there."

Check someone entering an occupied supporter: stop the action.

VOLUME 2 - On Changes to the World Model

Chapter 1 - New and Changed Actions, Which Extend or Restrict a Player's Range of Action

Instead of climbing, say "You can pull up on stuff and all but you can't do that climbing thing. Stupid short stubby legs."

Instead of climbing an enterable supporter, try entering the noun.

Instead of jumping, say "There is no way you are going to jump, you can barely crawl."

Instead of burning, say "It is too bad Fisher Price doesn't make a baby flamethrower, you would be all on that."

Instead of cutting or tying it to or swinging or buying, say "You are no good at that kind of stuff."

Instead of waving hands when the player is carrying something, say "You are busy holding stuff."

Instead of waving hands, say "You wave your hands and whee! it is fun."

Instead of sleeping, say "You are too good for naps!"

Instead of waking up, say "You are already awake, do not worry."

Instead of thinking, say "Thinking is like all you're doing these days, it is your job."

Instead of throwing at, say "There is no way, you can barely pick stuff up and you are no Cy Young or whatever."

Instead of attacking something, say "That might make you feel better but that is not the true baby way."

[The "if a parent person is present" checks aren't part of the rule so that the rules are fairly unspecific and thus are sorted low in the Instead rulebook.]
Instead of chewing a playmate:
	if a parent person is present, say "That is so not a good thing to do right in front of a parent unless you absolutely have to, for some reason biting is like the ultimate baby evil and you would get in trouble and not get your toy.";
	[Let one of the other rules catch this.]
	otherwise continue the action.

Instead of attacking or pushing or pulling a playmate: 
	if a parent person is present, say "Hitting and pushing and pulling other babies when you're in front of parents is no good, they will take you away and you won't get your toy.";
	otherwise continue the action.

Instead of attacking or pushing or pulling or chewing a playmate:
	say "There is no parent around right now so you might get away with it at first but the parents would figure it out, if you want to hurt someone it really needs to be worth it and it needs to look like an accident."

Instead of tasting a playmate, say "From a distance parent's can't tell if you are planning on licking or biting another baby, it is best not to lick them at all."

Instead of attacking or pushing or pulling or chewing a parent person, say "It is no good kicking or biting a parent, it is better to rebel in less obvious ways."

Instead of tasting a parent person, say "Licking parents may seem like fun at first but soon they are telling their friends 'oh this is cute, watch this' and expecting you to do it on command."

Instead of pushing or pulling a playmate when the playmate is standing, say "That would certainly knock [the noun] over but that would get everyone in an uproar so it's best not to do it unless you really need to."

Instead of taking inventory when the player has no thing (this is the empty-handed inventory rule), say "Your hands are all empty."

Instead of taking inventory when the player is pulled up on something (called the item) (this is the pulled-up inventory rule), say "You are not carrying anything but you do have your hands full since you are pulled up on [the item]."

The empty-handed inventory rule is listed after the pulled-up inventory rule in the instead rulebook.

Instead of taking inventory when the player is gnawing something (called the masticated) and the player is pulled up on something (called the item), say "Your hands are full of [the item] and your mouth is full of [the masticated]."

A check kissing rule (this is the kissing other people rule):
	instead say "Your kisses are only for your family."
The kissing other people rule is listed before the block kissing rule in the check kissing rules.

Understand "squish [something]" or "smoosh [something]" as squeezing. Instead of squeezing something, say "It is hard enough to pick up things, squeezing is even harder and so don't worry about doing it, if it happens it happens."

Understand "lick [something]" as tasting. 
Check tasting:
	if the noun is a person, instead say "You should stick with tasting things that aren't all wiggly and alive, you can get in trouble for that.";
	instead say "[Cap it-they of the noun] [if the noun acts plural]don't[otherwise]doesn't[end if] taste strange or anything."

Understand the command "walk" as something new. Understand the command "run" as something new.

Walking is an action applying to nothing. Understand "walk" or "run" as walking. Report walking: say "That is a developmental milestone you will reach someday but not today."

Reading is an action applying to one visible thing. Understand the command "read" as something new. Understand "read [something]" as reading. Report reading:  say "It must be nice to read, the mom and the dad are always doing it, but you do not yet know how."

Wriggling is an action applying to nothing. Understand "wriggle" or "wiggle" or "squirm" as wriggling. Report wriggling: say "You wriggle around and that is probably cute and all but it is not really helpful."

Rolling over is an action applying to nothing. Understand "roll over" as rolling over. Report rolling over: say "Whatever, that may be impressive when you are five or six but you are like eleven or nearly twelve months old now."

Crawling aimlessly is an action applying to nothing. Understand "crawl" as crawling aimlessly.
Report crawling aimlessly: say "You can crawl around and all but to really get somewhere you have to pick a direction."

Understand "crawl [direction]" or "crawl to the [direction]" or "cruise [direction]" or "cruise to the [direction]" as going.

Understand "cruise [thing]" or "cruise to [thing]" as a mistake ("You can only cruise in a direction.")

Understand "get down" as exiting.

[Lump all actions that move the player together]
Going is being ambulatory. Entering something is being ambulatory. Exiting is being ambulatory. Getting off something is being ambulatory. Crawling aimlessly is being ambulatory. Rolling over is being ambulatory.

[Identify all actions that are talking and then prevent them.]
Talking to is an action applying to one visible thing. Understand "talk to [something]" as talking to.

Asking people for something is being talkative. Asking people about something is being talkative. Telling people about something is being talkative. Answering people that something is being talkative. Telling people about something is being talkative. Talking to something is being talkative. Saying yes is being talkative. Saying no is being talkative. Saying sorry is being talkative. Swearing obscenely is being talkative. Swearing mildly is being talkative. Singing is being talkative.

Instead of being talkative, say "You don't have speech down yet."

Instead of asking someone to try doing something (this is the can't command others rule), say "You don't have speech down yet."

[Pushing things around. Normally, pushing things doesn't print anything prior to the move, and then the pushed object isn't mentioned in the room description. This after rule, courtesy of Emily Short, fixes both of those problems.]
After going a direction (called way-pooshed) with something (called the pooshed-thing) (this is the pushing things to a new room rule): say "You shove and shove at [the pooshed-thing] [if the player is wearing the squid costume]with your floppy tentacles [end if]and eventually you get it moving to [the way-pooshed] and then you follow it."; move the pooshed-thing to the location; continue the action.

Instead of an actor going a direction with a pulled-up-on pushable between rooms thing (called the platform): if the actor is the player, say "That will not move as long as [if the player is pulled up on the platform]you are[otherwise]someone is[end if] all pulled up on it."; stop the action.

Instead of an actor going a direction with an occupied supporter (called the platform): if the actor is the player, say "That will not move as long as [if the player is on the platform]you are[otherwise]someone is[end if] all on it.";

Instead of pushing or pulling a pushable between rooms thing, say "[The noun] looks like it will move pretty far but you will have to say in what direction you want to push it."

Instead of taking a pushable between rooms supporter (this is the can't take pushable things rule), say "You tug on [the noun] but it is too big to take although it slides around just a little."

The can't take pushable things rule is listed before the can't take large things rule in the instead rulebook.

Understand "unfold [something]" as opening. Understand "fold [something]" as closing.

Understand "unzip [something]" as opening. Understand "zip [something]" as closing.

Understand "stick [other things] on/onto/to [something]" as putting it on.

[New commands]

Falling is an action applying to nothing. Understand "fall" or "fall down" or "fall over" as falling. Instead of falling, try going down.

Understand "shake [something]" or "rattle [something]" as waving.

Check an actor waving something carryable that is not held by the actor (this is the taking before shaking rule):
	if the actor is the player, say "(first taking [the noun])[command clarification break]";
	silently try the actor taking the noun;
	if the actor is not holding the noun, stop the action.

Check an actor waving something that is not carryable:
	if the actor is the player, say "[The noun] is big and so you cannot really shake [it-them].";
	stop the action.

After waving: say "You shake [the noun] and that is okay I guess."

Report someone waving something:
	if the person asked is present, instead say "[The person asked] shakes [the noun]."

Crying is an action applying to nothing. Understand "cry" or "scream" or "yell" or "wail" or "shriek" or "sob" or "fuss" or "pout" or "squall" as crying. Check crying: if the player is gnawing something, instead say player has a full mouth. Report crying: say "You make some noise but it doesn't really do anything for you right now."

Instead of crying when the player is in the baby-swing:
	say "You have already gotten in the baby swing, crying will do you no good now."

Instead of crying when Tabletime is happening, say "The mom says, 'I know you don't like them, but prunes are good for you.'"

Instead of crying when Zoe has your favorite toy, say "Crying to get the parents to take back the toy is a total Zoe trick, you will not stoop that low. Besides, you are so close to getting the toy it would be no good to have the mom take it away again."

Carry out crying when the baby-swing is in play and Picturetime is not happening:
	increment the cry count of the player by 1.

After crying when the cry count of the player is 1:
	say "You let out this sobbing wail and the mom says, 'Shh, it's okay.'"

After crying when the cry count of the player is 2:
	say "When you cry again the mom says, 'Shh. Calm down.'"

After crying when the cry count of the player is 3: say "This time you add a lot of tremolo to your crying and the mom says, 'You're not going to calm down, are you, kiddo? Do you need the swing?'"

[The bit with the dirty costume can also come up when upstanding using the mom when the player is wearing the squid costume and the squid costume is dirty. Synchronize the two rules in the event of changes.]
After crying when the cry count of the player is greater than 3:
	say "'Okay, kiddo, let's check you and make sure,' the mom says and scoops you up. 'Diaper's okay[run paragraph on]";
	if the player is wearing the squid costume and the squid costume is dirty begin;
		say ", though this costume is filthy.' She strips it from you, still talking, 'You're[run paragraph on]";
		record 5 points for "getting that evil squid costume off";
		remove the squid costume from play;
	otherwise;
		say ". You're[run paragraph on]";
	end if;
	say " not hungry. To the swing!' [run paragraph on]";
	if a playmate is in the baby-swing begin;
		say "Then she sees that [a random playmate in the baby-swing] is already in the swing and says 'I guess you'll have to hang on a sec' and she puts you back down.";
	otherwise;
		say "You're airlifted into the swing and the mom[if something is carried by the player] takes away [the list of things carried by the player] and[end if] straps you in. She turns it on, click click click, and then you are swinging[if the current speedness of the speed switch is hi] really high[end if].";
		now every thing that is carried by the player is in the location;
		if the player is gnawing something (called g), now g is not gnawed by anything;
		if the player is standing, now the player is not pulled up on anything;
		move the player to the baby-swing;
		now the baby-swing is on;
		now the cry count of the player is 0;
		the player stops swinging in six turns from now;
		if your favorite toy is not Parry-touched and the baby-swing is in the TKBUG Open Area and your favorite toy is in the bookcase, Parry potentially wanders by in four turns from now;
	end if.

At the time when Parry potentially wanders by:
	if your favorite toy is not Parry-touched begin;
		say "Parry comes wandering by with his water glass but he stops when he sees you swinging. He looks at your favorite toy. He looks at you. He looks at your toy[if Zoe and the player are co-located]. He looks at Zoe[end if]. He looks at how close you get to your favorite toy. When he is all done with his looking he casually leans against the bookcase and taps your toy. It totally falls a bit closer! He winks at you and heads on by[note Parry-knocks-the-toy].";
		now your favorite toy is Parry-touched;
	end if.

At the time when the player stops swinging:
	now the baby-swing is off;
	say "You look up and there is the mom, you are swinging toward her face and away, toward and away. She turns off the swing and gets you down and puts you on the floor before going back to her seat.";
	move the player to the location of the baby-swing;

Laughing is an action applying to nothing. Understand "laugh" or "coo" or "goo" or "smile" as laughing. Check laughing: if the player is gnawing something, instead say player has a full mouth. Report laughing: say "You smile and make a bubbly laugh noise, it is the thing you do that makes everyone say 'oh my goodness how cute!'"

Breathing is an action applying to nothing. Understand "breathe" as breathing. Report breathing: say "You do not have many things you can do, you are so young, but you don't have to think about breathing really, you do that automatically."

Breathing fast is an action applying to nothing. Understand "breathe fast" as breathing fast. Report breathing fast: say "You breathe fast for a minute and it makes you all light-headed."

Breathing slow is an action applying to nothing. Understand "breathe slow" or "breathe slowly" as breathing slow. Report breathing slow: say "You breathe slow but after a while you have to start breathing normally again."

[Adam Thornton made me add this]
Eliminating is an action applying to nothing. Understand "poop" or "pee" or "urinate" or "micturate" or "shit" as eliminating. Report eliminating: say "See for you to do that on command you'd have to be potty trained and you are not, that is just another way the man controls you."

Kicking is an action applying to nothing. Understand "kick" as kicking. Report kicking: say "You are all kick! kick! kick! but you don't have coordination or anything so it doesn't do much."

Kicking at is an action applying to one thing. Understand "kick [something]" or "kick at [something]" as kicking at. Report kicking at: say "You are way not coordinated enough to do that."

Playing with is an action applying to one thing. Understand "play with [something]" as playing with. Report playing with: say "That is way generic, you will need to be specific about what you want to do to play with something."

Flipping is an action applying to one thing. Understand "flip [something]" as flipping.
Check flipping:
	abide by the can't switch on unless switchable rule;
	if the noun is switched on, try switching off the noun instead;
	try switching on the noun instead.

Putting it under is an action applying to two things. Understand "put [other things] under [something]" or "push [other things] under [something]" or "wedge [other things] under [something]" as putting it under. Check putting it under: instead say "There is no real need to do that."

Rocking is an action applying to nothing. Understand "rock" as rocking. Report rocking: say "You are already totally rocking."

Spitting up is an action applying to nothing. Understand "spit" or "spit up" or "vomit" or "puke" or "slobber" or "drool" or "barf" or "ralph" as spitting up. Report spitting up: say "That would get your clothes all dirty and you do not need to do that right now."

Spitting up on is an action applying to one thing. Understand "spit up on [something]" or "spit on/in [something]" or "vomit on [something]" or "puke on [something]" or "slobber on [something]"or "drool on [something]"or "barf on [something]"or "ralph on [something]" as spitting up on. Report spitting up on: instead say "You do not need to get [the noun] all dirty right now."

Report spitting up on yourself: instead say "You do not need to get yourself all dirty right now."

Report spitting up on a person: instead say "It may be normal little baby behavior to drool and spit up all over parents and other babies but you are like eleven months old and now it would be unseemly."

Pushing it with is an action applying to two things. Understand "push [something] with [something preferably held]" or "lift [something] with [something preferably held]" or "raise [something] with [something preferably held]" or "hit [something] with [something preferably held]" as pushing it with.

[It's possible (pace Dan Shiovitz) that the player will type LIFT ME WITH FOOTSTOOL. Map that to upstanding with.]
Instead of pushing the player with something when the player's command includes "lift" or the player's command includes "raise", try upstanding using the second noun.

Check an actor pushing something with something carryable that is not held by the actor (this is the pick up objects before pushing with them rule):
	say "(first taking [the second noun])[command clarification break]";
	silently try taking the second noun;
	if the actor is not holding the second noun, stop the action.

Report pushing it with: say "Most of the time you can just push things without having to use something to do the pushing other than your little baby hands."

Speedness is a kind of value. The speednesses are lo, med, and hi.

Turning it to is an action applying to one thing and one speedness. Understand "turn [something] to [a speedness]" or "turn [something] on [a speedness]" or "flip [something] to [a speedness]" or "push [something] to [a speedness]" as turning it to. Check turning it to: say "You so cannot turn [the noun] like that."

[Get rid of the grammar "set x to y" (which matches the "setting it to" action) and attach it to the "turning it to" action.]
Understand the command "set" as something new. Understand "set [something] to [a speedness]" as turning it to.

[Remember where people were before they went somewhere, so we can report on it and can have a simple follow command.]
A person has a room called last location.

Carry out an actor going (this is the keeping track of people rule): change the last location of the actor to the location of the actor. The keeping track of people rule is listed first in the carry out going rulebook.

To decide if (youngster - a person) was recently present:
	if the last location of the youngster is the location, decide yes;
	decide no.

Following is an action applying to one visible thing. Understand "follow [any thing]" or "chase [any thing]" as following.

Check following when the noun is not a person (this is the can't follow inanimate objects rule): instead say "You really can only follow babies and also people."

Check following when the noun is the player (this is the can't follow yourself rule): instead say "That is easy, you are already here!"
Check someone following when the noun is the person asked: stop the action.

Check following when the noun is present (this is the can't follow someone present rule): if the actor is the player, say "That is easy, [the noun] is already here!"; stop the action.

Check someone following when the person asked and the noun are co-located: stop the action.

Check following when the last location of the noun is not the location (this is the can only follow at one remove rule): instead say "You can't remember where [the noun] went. Stupid little kid brain."

Check someone following when the last location of the noun is not the location of the person asked: stop the action.

Carry out an actor following:
	let the destination be the location of the noun;
	let aim be the best route from the location of the actor to the destination;
	if aim is not nothing begin;
		if the actor is the player begin;
			say "(heading [aim])[command clarification break]";
			try going aim;
		otherwise;
			[NPCs should go quietly]
			silently try the actor going aim;
		end if;
	end if.

Building is an action applying to one visible thing. Understand "build [any thing]" or "put together [any thing]" or "make [any thing]" as building.
Check building: instead say "You do not know how to build [the noun]."

[Fiddling reachability and the like when the player is held.]
A rule for reaching outside persons (this is the can't reach far-away objects when held rule):
	if the person reaching is the player, say "[The container in question] is holding you and so you are too far away.";
	deny access.

Instead of being ambulatory when the player is held by someone (called the holder), say "[The holder] is holding you and so you cannot really go anywhere from up here."

[If the player is held by someone and that someone has a "writing a paragraph about" rule, their paragraph won't be printed. This rule bypasses that as a default.]
A carry out looking rule (this is the room description paragraphs about enclosing people rule):
	if the player is enclosed by someone (called the holder), carry out the writing a paragraph about activity with the holder.

The room description paragraphs about enclosing people rule is listed after the room description paragraphs about objects rule in the carry out looking rulebook.

Chapter 2 - Printing the Description Of Activity, Which Allows Us to Tack Things Onto Descriptions

[I will note that after Emily Short and I discussed how to do this, she turned it into the Crusoe example in the I7 documentation.]
Printing the description of something is an activity.

This is the fancy examining rule:
	carry out the printing the description activity with the noun;

The fancy examining rule is listed instead of the standard examining rule in the carry out examining rulebook.

Rule for printing the description of something (called item) (this is the standard printing the description of rule):
	say "[description of item] [run paragraph on]".

[Ensure that we end our descriptions with a paragraph break]
Last after printing the description of something:
	say "[paragraph break]".

[To add a line to a description, use an 'After printing the description of...' rule. For example:
After printing the description of something that is damp:
	say "It is damp." instead.
N.B. that the "instead" is needed to prevent extra blank lines from appearing.]

Chapter 3 - Sizes, Which Have an Effect on Carrying Capacity

[Small things are things that a baby can carry in one hand, or in the mouth. Medium can only be carried in two hands, and restrict motion. Large can't be carried at all.]

A size is a kind of value. The sizes are small, medium and large. A thing has a size. Things are usually large. The specification of a size is "How large something is relative to what a small child can pick up."

[A player can carry a maximum of two small things or one medium thing. Any small item being chewed doesn't count towards the carry capacity. Any item being worn doesn't count towards the carry capacity.]
Definition: a baby person is burdened:
	if it is carrying more than zero medium things then decide yes;
	if it is carrying less than three small things and it is gnawing something that is held by it then decide no;
	if it is carrying more than one small thing then decide yes;
	if it is standing then decide yes;
	decide no.
Definition: a baby person is unburdened:
	if it is standing then decide no;
	if it is carrying exactly one small thing and it is not carrying a medium thing and it is gnawing something that is held by it then decide yes;
	if it is not carrying anything then decide yes;
	decide no.
Definition: a baby person is overburdened:
	if it is carrying more than two small things then decide yes;
	if it is carrying more than zero medium things and more than zero small things then decide yes;
	if it is standing then decide yes;
	decide no.
Definition: a thing is carryable if it is not large.

Instead of taking an Inform library animate object, say "You should only pick up things that are not so wiggly."

Instead of taking something large (this is the can't take large things rule), say "[The noun] [is-are] way big and there is no way you're picking [it-them] up or anything."

Check taking something medium when the player is not unburdened (this is the can't take medium things when burdened rule): instead say "[The noun] is big enough that you will have to drop everything to get [it-them] because it takes two hands[note inventory-limit]."

[This is a check rule so's I can handle the case where a player tries to take something they've already taken.]
Check taking a small thing when the player is burdened (this is the can't take small things when burdened rule):
	if the noun is not carried, instead say "Your hands are all full, maybe if you drop something you could get [the noun][note inventory-limit]."

Report taking a small thing: instead say "You pick [it-them of the noun] up."

Report taking a medium thing: instead say "You pick [it-them of the noun] up and it takes two hands."

[Variations on the above three rules for other people.]
Check someone taking a large thing (this is the other people can't take large items rule):
	if the person asked is a baby, stop the action.

Check someone taking a medium thing (this is the other people can't take medium items if holding anything rule):
	if the person asked is a baby and the person asked is not unburdened, stop the action.

Check someone taking a small thing (this is the other people can't take small items when burdened rule):
	if the person asked is a baby and the noun is not carried and the person asked is burdened, stop the action.

Chapter 4 - Heights, Which Allow Us to Roughly Judge How High Up Something Is

Section 1 - Height Definitions

[We define heights by a parent's height. Quarter-height is knee-high. Half-height is waist-height. We also define exact heights and inexact heights. For the latter, as long as you're that height or higher you're good.]

Definition: something is half-height:
	if the baby-swing encloses it and the current speedness of the speed switch is hi, decide yes;
	if the cabinet top encloses it, decide yes;
	decide no.

Definition: something is quarter-height:
	if the tall plastic block encloses it, decide yes;
	if furniture encloses it, decide yes;
	if it is standing, decide yes;
	if it is enclosed by a standing person, decide yes;
	if the baby-swing encloses it and the current speedness of the speed switch is not hi, decide yes;
	decide no.

Definition: something is eighth-height:
	if the short plastic block encloses it or the bugish plastic block encloses it, decide yes;
	decide no.

Definition: something is half-height-or-higher:
	if it is half-height, decide yes;
	decide no.

Definition: something is quarter-height-or-higher:
	if it is quarter-height or it is half-height-or-higher, decide yes;
	decide no.

Definition: something is eighth-height-or-higher:
	if it is eighth-height or it is quarter-height-or-higher then yes;
	decide no.

Definition: something is zeroth-height:
	if it is eighth-height-or-higher then no;
	decide yes.

[Comparing heights]
To decide if (x - a thing) is higher than (y - a thing):
	if x is zeroth-height and y is eighth-height-or-higher, decide no;
	if x is eighth-height and y is quarter-height-or-higher, decide no;
	if x is quarter-height and y is half-height-or-higher, decide no;
	decide yes.

[Reachableness: from the floor, you can't reach things that are quarter height or higher. From quarter height, you can't reach zeroth-height. From half-height, you can't reach eighth-height.]
To decide if (t - a thing) is reachable by (p - a person):
	if t is a parent person, decide yes;
	if p is zeroth-height and t is quarter-height-or-higher, decide no;
	if p is quarter-height and t is zeroth-height, decide no;
	if p is half-height and t is not quarter-height-or-higher, decide no;
	decide yes.

[A message for things being out of reach]
To say out-of-reach message for (n - a thing): say "You have short arms, you can't reach [the n] from [if the player is higher than n]up[otherwise]down[end if] here."

Section 2 - Height Checking Rules

[Height Reachability Rules determine whether or not something is reachable due to height restrictions. This rulebooks is called by a "before" rule. Success means the action's okay to continue. Failure means the action should be stopped. If a height reachability rule ends in failure, it should print out why the failure occurred.]

[In theory I could use reaching inside/reaching outside rules, but heights don't necessarily depend on things being inside or outside other things.]

Height Reachability Rules is a rulebook. The height reachability rules have outcomes stop action processing (failure), continue action processing (success), and no decision (no outcome - default).

[Since rules don't have access to rulebook variables in their when/while clauses -- and, in fact, don't have access to e.g. "actor" except inside the rules themselves, I have use global variables.]

[The actor whose reachability we're testing.
N.B. this isn't re-entrant.]
The reaching actor is a person that varies.

[The noun to be height-tested. Also not re-entrant.]
The reaching object is an object that varies.

[It turns out a lot of built-in talking actions require a touchable actor, so fix that oversight.]
A height reachability rule when the current action is being talkative (this is the height-ignoring actions rule):
	continue action processing.

[Getting things held by other people.]
A height reachability rule (this is the objects held by other people rule):
	if the reaching object is enclosed by a parent person (called p) begin;
		if the reaching actor is not enclosed by p begin;
			if the reaching actor is the player, say "[P] has [the reaching object] and so you can't reach it.";
			stop action processing;
		end if;
	end if.

[These next two rules aren't phrased as e.g. "A height reachability rule when the noun is not nothing" so that they're near the bottom of the rulebook.]
A height reachability rule (this is the standard height checking rule):
	if the reaching object is reachable by the reaching actor, continue action processing;
	if the reaching actor is the player, say out-of-reach message for the reaching object;
	stop action processing.

[The height reachability rulebook is triggered by a single rule that I shove into the action-processing rules. c.f. the Electrified example.]

This is the check-heights rule:
	if the action requires a touchable noun and the noun is not nothing begin;
		change the reaching actor to the actor;
		change the reaching object to the noun;
		consider the height reachability rules;
		if the outcome of the rulebook is the stop action processing outcome, stop the action;
	end if;
	if the action requires a touchable second noun and the second noun is not nothing begin;
		change the reaching actor to the actor;
		change the reaching object to the second noun;
		consider the height reachability rules;
		if the outcome of the rulebook is the stop action processing outcome, stop the action;
	end if.

The check-heights rule is listed before the basic accessibility rule in the action-processing rules.

Chapter 5 - Gnawing and Chewing, Which Allow Babies to Chew On Things

Section 1 - The Gnawing Relation and Dampness

Gnawing relates one thing to one thing. The verb to gnaw (he gnaws, they gnaw, it is gnawed, he is gnawing) implies the gnawing relation.

A thing is either damp or dry. A thing is usually dry. A thing can be chewable. A thing is usually chewable. A person is usually not chewable.

After printing the description of a damp thing: say "It is all damp from being chewed. [run paragraph on]"

Section 2 - Chewing and Stopping Chewing

[Defined actions for chewing and stopping chewing]
Chewing is an action applying to one thing. Understand "chew on [something]" or "chew [something]" or "gnaw on [something]" or "gnaw [something]" or "bite [something]" or "put [something] in mouth" as chewing.

Stopping chewing is an action applying to one thing. Understand "stop chewing on [something]" or "stop chewing [something]" or "stop gnawing on [something]" or "stop gnawing [something]" or "take [something] out of mouth" or "spit [something]" or "spit [something] out" or "spit out [something]" as stopping chewing.

[Changes to inventory due to chewing]
After printing the name of something (called the chewed-upon) which is gnawed by someone (called the chewer) while taking inventory:
	say " (being chewed)".

Instead of eating something that is not edible, try chewing the noun.
Instead of taking off something that is gnawed by someone, try stopping chewing the noun.
Instead of an actor taking something that is gnawed by the actor (this is the taking a chewed thing means not chewing it any more rule), try stopping chewing the noun.

[Action-handling rules for chewing and stopping chewing]
Before chewing something carryable that is not held:
	say "(first taking [the noun])[command clarification break]";
	silently try taking the noun;
	if the player is not holding the noun, stop the action.

[This one rule is a Before rule because of all of the Instead overrides I use, and I want to make sure the player's mouth isn't full before any other checks are carried out.]
Check chewing:
	if the noun is the player begin;
		if not running silently, say "You do not need to chew yourself, you are not some baby version of a cutter.";
		stop the action;
	otherwise if the player is gnawing the noun;
		if not running silently, say "You are already chewing on [the noun] so good news, it is like you've pre-succeeded.";
		stop the action;
	otherwise if the player is gnawing something;
		if not running silently, say player has a full mouth;
		stop the action;
	otherwise if the noun is a person;
		if not running silently, say "You have tried to chew on other babies and people before and it totally does not work.";
		stop the action;
	otherwise if the noun is not chewable;
		if not running silently, say "[The noun] is not chewable, which is weird, most everything is chewable.";
		stop the action;
	otherwise if the noun is not carryable;
		if not running silently, say "You give [the noun] a chew or two but you cannot carry [it-them] so you give up chewing it pretty soon.";
		stop the action;
	end if.

Check someone chewing something carryable that is not held by the person asked (this is the other people must hold small items to chew rule):
	silently try the person asked taking the noun;
	if the person asked is not holding the noun, stop the action.

Check someone chewing (this is the other people can't chew two things rule):
	if the person asked is gnawing something, stop the action.

Check someone chewing (this is the other people can't chew people or unchewable things rule):
	if the noun is not chewable or the noun is a person, stop the action.

Carry out an actor chewing:
	if the noun is carryable, now the actor is gnawing the noun;
	if the noun is small or the noun is medium, now the noun is damp.

Report chewing: say "You [if the noun is carryable]shove [the noun] in your mouth[otherwise]put your mouth on [the noun][end if] and start chewing on [it-them] and that [if the player is unburdened]leaves your hands free[note chewing-stuff][otherwise]feels good[end if]."

Report someone chewing: if the person asked is present, say "[The person asked] [if the noun is carryable]shoves [the noun] in [its-their of the person asked] mouth[otherwise]puts their mouth on [the noun][end if] and chews on [it-them of the noun]."

Check an actor stopping chewing:
	if the actor is not gnawing the noun begin;
		if the actor is the player, say "You are not chewing on that.";
		stop the action;
	end if.

Carry out an actor stopping chewing:
	now the noun is not gnawed by anything;
	if the actor is overburdened,
		silently try the actor dropping the noun;

Report stopping chewing:
	say "You pop [the noun] out of your mouth[if the noun is not held] and onto the floor[end if]."

Report someone trying stopping chewing:
	if the person asked is present, say "[The person asked] pops [the noun] out of [its-their of the person asked] mouth[if the noun is not held by the person asked] and onto the floor[end if].";
	stop the action.

[Side effects of chewing]
To pop (item - a thing that is gnawed by anything) out:
	if the item is gnawed by the player, say "(popping [the item] out of your mouth first)";
	now the item is not gnawed by anything; 

Carry out an actor dropping something that is gnawed by anything: pop the noun out.

Carry out an actor putting something that is gnawed by anything on something: pop the noun out.

Carry out an actor inserting something that is gnawed by anything into something: pop the noun out.

To say player has a full mouth: say "You would but your mouth is all full."

Instead of an actor drinking when the actor is gnawing something:
	if the actor is the player, say player has a full mouth.

Instead of an actor spitting up when the actor is gnawing something:
	if the actor is the player, say player has a full mouth.

Instead of an actor spitting up on when the actor is gnawing something:
	if the actor is the player, say player has a full mouth.

Chapter 6 - Clinging, Cruising, and Standing, Which Allows Babies To Pull Up On Items and Move Around

Section 1 - The Clinging Relation

[It would be nice if the verb could be "to cling to".]
Clinging relates various people to one thing. The verb to cling (he clings, they cling, it is clinged, he is clinging) implies the clinging relation. The verb to be pulled up on implies the clinging relation.

[5G67 introduced a bug where this "standing" definition clashed with the "standing using" action. Whoops. Rephrase.]
Definition: a person is standing if it is pulled up on something.
Definition: a thing is pulled-up-on if something is pulled up on it.

The previous-support is a thing that varies.

To seat (kid - a person):
	if the kid is pulled up on something (called item), change previous-support to item;
	now the kid is not pulled up on anything;

To let go of (furnishing - a thing):
	now the furnishing is not clinged by anyone;

[Changes to status line due to upstanding using]
When play begins:
	change the left hand status line to "[the player's surroundings][if the player is pulled up on something (called the support)] (pulled up on [the support])".

A thing can be suitable for pulling up on. A thing is usually not suitable for pulling up on. A thing can be suitable for cruising. A thing is usually not suitable for cruising.

Furniture is usually suitable for pulling up on. A woman is usually suitable for pulling up on. A man is usually suitable for pulling up on.

[A height reachability rule for the furniture on which you are pulled up.]
A height reachability rule when the reaching actor is pulled up on the reaching object: continue action processing.

Section 2 - Standing, Standing Using and Sitting Down

[I don't want "stand" or "stand up" to map to exiting, so I can give gentle guidance messages regarding being pulled up]
Understand the command "stand" or "stand up" as something new. Standing alone is an action applying to nothing. Understand "stand" or "stand up" as standing alone.

Check standing alone:
	if the player is standing, instead say "You know you are not any good at standing by yourself so you don't let go and instead you stay pulled up.";
	if the player is inside something, instead try exiting;
	instead say "You try that standing thing but you are still no good at it without holding onto something so you will have to pull up on something to stand up.";

[Ideally I'd do this next bit by saying "Understand 'stand down' or 'climb down' as going down." but the going action is weird and won't let me do that.]
Going-down-mapping is an action applying to nothing. Understand "stand down" or "climb down" as going-down-mapping. Instead of going-down-mapping, try going down.

[Defined actions for standing using and sitting down]
Understand "pull up" as a mistake ("You'll have to say what you want to pull up on.").

[5G67 introduced a bug where this "standing using" action clashed with the "standing" definition above. Whoops. Accordingly, I've jiggered the name of this action.]
Upstanding using is an action applying to one thing. Understand "pull up [something]" or "pull up on [something]" or "stand using [something]" or "stand on [something]" or "climb on [something]" or "pull up with [something]" or "lift me with [something]" or "lift myself with [something]" or "pull me with [something]" or "pull me on [something]" or "pull me up with [something]" or "pull me up on [something]" or "pull on [something]" as upstanding using.

Sitting down is an action applying to nothing. Understand "sit" or "sit down" or "let go" as sitting down.

[A command that sometimes maps to sitting down.]
Lying down is an action applying to nothing. Understand "lie" or "lie down" as lying down.

[Adding to the ambulatory definition.]
Standing alone is being ambulatory. Upstanding using is being ambulatory. Sitting down is being ambulatory. Lying down is being ambulatory.

[Mapping some commands to upstanding using and sitting down]
Instead of entering something that is suitable for pulling up on, try upstanding using the noun.
Instead of climbing something that is suitable for pulling up on, try upstanding using the noun.
Instead of rubbing a parent person, try upstanding using the noun.

Before dropping a suitable for pulling up on thing:
	if the player is pulled up on the noun, instead try sitting down.

[Action-handling rules for standing using and sitting down]
Check upstanding using:
	if the noun is the player, instead say "Pulling yourself up by your bootstraps is a metaphor, you can't really pull yourself up using yourself.";
	if the noun is not suitable for pulling up on, instead say "You can't pull up on [the noun] to stand.";
	if the player is on the noun, instead say "You are already on [the noun] and that is just as good if not better than pulling up on it.";
	if the player is on a supporter (called the platform) begin;
		if the platform is the noun, instead say "You cannot both pull up on something and be sitting on it at the same time.";
		if the noun is furniture, instead say "Even this high you would have a very hard time stepping from [the platform] to [the noun] without falling on your face so it is best that you not do that.";
		say "(first getting off of [the platform] and plopping on the ground)[line break]";
		silently try getting off the platform;
		if the player is on the platform, stop the action;
	otherwise if the player is pulled up on something (called the support);
		if the support is the noun, instead say "You are already all pulled up on [the support] and you can't go any higher.";
		say "(first letting go of [the support] and plopping on the ground)[line break]";
		let go of the support;
	otherwise;
		if the player is not unburdened begin;
			if the player is gnawing a medium thing (called g), instead say "Even though you are chewing on [the g] it is big enough that you still have to keep it in your hands and so you can't pull up while you have it, if only [the g] were smaller you would have your hands free.";
			instead say "You have stuff in your hands and you will need those hands to pull yourself up so drop something I guess.";
		end if;
	end if;

Check someone upstanding using:
	if the noun is not suitable for pulling up on, stop the action;
	if the person asked is pulled up on something (called the support) begin;
		if the support is the noun, stop the action;
		seat the person asked;
	otherwise;
		if the person asked is not unburdened, stop the action;
	end if;

Carry out an actor upstanding using (this is the standard upstanding using rule): now the actor is pulled up on the noun.

Report upstanding using:
	if a playmate is pulled up on the noun, say "You wriggle your way next to [the list of playmates pulled up on the noun] and pull up on [the noun] and get to your feet.";
	otherwise say "You pull up on [the noun] and get to your feet and you are all steady except for the occasional sway."

Report someone upstanding using:
	if the person asked is present, say "[The person asked] pulls up on [the noun]."

Check an actor sitting down:
	if the actor is not standing begin;
		if the actor is the player, say "Since you are already sitting you can't sit again or maybe it's like you've already succeeded at sitting.";
		stop the action;
	end if.

Carry out an actor sitting down: seat the actor.

Report sitting down (this is the standard report sitting down rule):
	say "You sit down on your diaper and it is good that it is all padded and stuff."

Report someone sitting down: if the person asked is present, say "[The person asked] lets go and sits down."

Before an actor lying down when the actor is standing: instead try the actor sitting down.

Check lying down: instead say "You pretty much alternate between lying down and sitting down and crawling, it is your natural state, so there is no need to specifically ask to lie down if you're not standing."

[Side effects of standing using]

[A global variable to hold an item on which a player was recently pulled up -- for one turn only.]
The just-released item is a thing that varies. An every turn rule (this is the clear the just-released item rule): Now the just-released item is nothing.

Before going down or exiting when the player is pulled up on something:
	try sitting down instead;

Instead of going up when the player is pulled up on something:
	say "This is as high as you are going to go, you can pull up but not really climb."

Before going a direction (called goal) when the player is pulled up on something (called support) (this is the sit down before moving in a non-cruising direction rule):
	if going goal while holding onto support involves cruising begin;
		do nothing;
	otherwise if goal is not up; [The previous rule handles going up]
		say "(first letting go of [the support] and plopping on the ground)[command clarification break]";
		let go of the support;
	end if.

Before pushing something to a direction when the player is pulled up on something (called support):
	say "(first letting go of [the support] and plopping on the ground)[command clarification break]";
	let go of support.

Before entering something when the player is pulled up on something (called support):
	say "(first letting go of [the support] and plopping on the ground)[command clarification break]";
	let go of support.

Before someone going down when the person asked is pulled up on something:
	try the person asked trying sitting down instead;

Check someone going a direction (called goal) when the person asked is pulled up on something (called support) (this is the others must sit before moving rule):
	if going goal while holding onto support involves cruising begin;
		do nothing;
	otherwise;
		seat the person asked;
		change the just-released item to support;
	end if.

[We should only reach these rules if the direction in which a character is going is a cruise direction.]

[The cruise the player rule is in part duplicated by the third Zoe stalks the player rule. Keep them synchronized.]
Instead of going a direction (called goal) when the player is pulled up on something (called support) (this is the cruise the player rule):
	let furnishing be the other end of support to the goal;
	cruise the player to the goal and hold the furnishing;

Carry out someone going a direction (called goal) when the person asked is pulled up on something (called support) (this is the others cruising rule):
	let furnishing be the other end of support to the goal;
	cruise the person asked to the goal and hold the furnishing;

A height reachability rule when the reaching actor is standing:
	if the current action is taking or pushing or attacking, continue action processing.

Instead of taking or pushing or attacking something when the player is standing:
	if the player is pulled up on the noun, say "Your hands are full of [the noun] since you are pulled up on it, but you cannot really take [the noun].";
	otherwise say "You cannot reach [the noun] because you are standing and if you let go with one hand you would just plop down before you could get it."

Instead of someone taking something when the person asked is standing, stop the action.

Instead of chewing something when the player is standing, say "You do not have enough hands and eyes and coordination to chew something new when you are all pulled up."

Instead of someone chewing something when the person asked is standing, stop the action.

Section 3 - Cruising

[When you're pulled up on some things, you can cruise along them -- slide along their length. Babies do this with couches and chairs and the like. We keep track of what you can cruise along using a table. It would be clever to use relations here, but we don't just relate one thing to another; we relate one thing to another via a direction.]

Table of Cruising Possibilities
Primary	Direction	Reciprocal
north pink striped loveseat	south	south pink striped loveseat
south pink striped loveseat	north	north pink striped loveseat
west grey couch	east	middle grey couch
middle grey couch	west	west grey couch
middle grey couch	east	east grey couch
east grey couch	west	middle grey couch

To decide if going (goal - a direction) while holding onto (support - a thing) involves cruising:
	if support is not suitable for cruising, decide no;
	repeat through the Table of Cruising Possibilities begin;
		if primary entry is support and direction entry is goal, decide yes;
	end repeat;
	decide no.

To decide what thing is the other end of (support - a thing) to the (goal - a direction):
	repeat through the Table of Cruising Possibilities begin;
		if primary entry is support and direction entry is goal, decide on reciprocal entry;
	end repeat;
	change hacky global item to the support;
	report bug "[The hacky global item] doesn't have that direction in the Table of Cruising Possibilities.";
	decide on support. 

To decide what thing is the first end of (support - a thing) from the (goal - a direction):
	repeat through the Table of Cruising Possibilities begin;
		if reciprocal entry is support and direction entry is goal, decide on primary entry;
	end repeat;
	change hacky global item to the support;
	report bug "The reciprocal [hacky global item] doesn't have that direction in the Table of Cruising Possibilities.";
	decide on support.

To cruise (kid - a person) to (goal - direction) and hold (support - a thing):
	if kid is the player, say "You slide along while moving your hands to keep steady.";
	now the kid is pulled up on the support;
	change the last location of the kid to the location of the kid;
	move the kid to the room goal from the location of the kid;

Chapter 7 - Fascination, Which Allows Babies to be Fixated on Objects

Fascination relates one thing to one person. The verb to fascinate (he fascinates, they fascinate, he fascinated, it is fascinated, he is fascinating) implies the fascination relation.

To decide if (victim - a person) is fixated on (focus - an object):
	if focus fascinates victim, decide yes;
	if focus is a person and focus encloses something that fascinates victim, decide yes;
	decide no.

To decide if (victim - a person) is not fixated on (focus - an object):
	if victim is fixated on focus, decide no;
	decide yes.

To say focus of (victim - a person):
	let f be a random thing that fascinates victim;
	say "[the f]".

Definition: a person is fixated if something fascinates it.

Chapter 8 - Scoping, Which is Fiddled Because in This Game Regions Are Really Rooms

After deciding the scope of the player when the location is regionally in Play Area:
	repeat with P running through the rooms in Play Area begin;
		place P in scope;
	end repeat.

After deciding the scope of the player when the location is regionally in Hidden Area:
	repeat with P running through the rooms in Hidden Area begin;
		place P in scope;
	end repeat.

A thing can be room-mentioning. A thing is usually room-mentioning.

To say the placement of (item - a thing):
	if the item and the player are not co-located begin;
		let place be the location of the item;
		let way be the best route from the location to the place;
		say "[is-are of item]";
		if item is inside a container (called c), say " inside [the c]";
		say "[if item is room-mentioning] [faraway descriptor of place][end if][if way is a direction] to the [way][note fiddled-scope]";
	end if.

After printing the description of something which is within a room which is not the location (this is the description of distant item location rule): say "[Cap it-they of the noun] [the placement of the noun]. [run paragraph on]".

Before doing something other than examining or following or building to something which is not enclosed by the location (this is the babies have stubby little arms rule):
	if the noun is a backdrop begin;
		if the noun is not in the location, instead say "[The noun] [is-are] way too far away for that[note fiddled-scope].";
	otherwise;
		instead say "[The noun] [the placement of the noun] and so [is-are of the noun] way too far away for that.";
	end if.

Does the player mean doing something other than examining or following something which is not enclosed by the location: it is very unlikely.

[I don't like how, because everything is effectively always in scope, you get clarification messages on doing things like pulling up on a loveseat. This next bit of wizardry prevents that from happening. TK This will need to be checked against every update of I7 ever, sigh.]
Include (-
[ CountPresentMatchedObjects i cnt;

	cnt = 0;
	for (i=0; i<number_matched; i++) {
		! This next test corresponds to "obj is in the location" in I7
		if (real_location == I7_InWhat(match_list-->i)) {
			cnt++;
			if (cnt > 1) { rtrue; }
		}
	}
	rfalse;
];
-).
To decide if only one matched object is present: (- (CountPresentMatchedObjects) -)

Rule for clarifying the parser's choice when only one matched object is present:
	do nothing.

Chapter 9 - Pre-writing a Paragraph About, Which is a New Activity To Allow Us to Collate Playmates' Paragraphs in Room Descriptions

[Originally this was to be a simple rule added to the carry out looking rulebook, but I7 doesn't handle things properly. I can't mark items that I print something about as already having been mentioned so the "writing a paragraph about" activity doesn't fire, among other problems.]

Pre-writing a paragraph about something is an activity.

A carry out looking rule (this is the room description pre-writing paragraphs about playmates rule):
	let c be the number of playmates in the location;
	if c is greater than 0 begin;
		if c is greater than 1 begin;
			let flag be 0;
			repeat with p running through playmates in the location begin;
				say "[if flag is not 0] and [end if][p]";
				increment flag by 1;
			end repeat;
			say " are here. [run paragraph on]";
		end if;
		repeat with p running through playmates in the location begin;
			carry out the pre-writing a paragraph about activity with p;
		end repeat;
		if c is greater than 1, say note collated-reports;
		say paragraph break;
	end if.

[Don't otherwise mention playmates.]
Rule for writing a paragraph about a playmate (called p):
	now p is mentioned;
	now p is not marked for listing.

The room description pre-writing paragraphs about playmates rule is listed after the room description body text rule in the carry out looking rulebook.

Chapter 10 - Visitation, Which Puts Hooks on the Player Entering Rooms

A room has a number called visitation count. The visitation count of a room is usually 0.

Visiting is an action applying to one visible thing. Carry out visiting: if the noun is a room, increase the visitation count of the noun by 1.

The last report going rule (this is the record the player's visitation rule): try visiting the location.

Chapter 11 - Noticeability, Which Keeps People from Seeing Things

[A mechanism for marking certain objects as not to be taken by playmates]
A thing can be noticeable by playmates. A thing is normally noticeable by playmates.
Definition: a thing is pickupable:
	if it is not noticeable by playmates then decide no;
	if it is carryable then decide yes;
	decide no.
Definition: a thing is worth noting:
	if it is noticeable by playmates then decide yes;
	decide no.

VOLUME 3 - On the Physical Environment

Chapter 0 - The Player's Stuff

Your diaper is part of the player. The description of your diaper is "The diaper covers you and contains any embarassing accidents or whatever." Your diaper is ambiguously plural. Understand "diapers" as your diaper.

Instead of dropping or taking off your diaper, say "There is no way you are reaching all the way around and taking off your diaper, you would have to be superbaby or something to do that." Instead of looking under or searching your diaper, say "That is all you in there."

Instead of doing something other than examining, looking under, searching, dropping, smelling or taking off your diaper, say "The diaper is always there, it's not like there's anything you can do about it."

Check smelling the diaper: try smelling the player instead.

Chapter 1 - Beneath the Covers

Beneath the Covers is a room. "It is dark and stuffy and you are covered over, something is all draped over you, it is enough to make you crazy." The printed name is "Smothered in Darkness".

[Without this rule, if players type something like >TAKE while under the covers, the game will assume they mean the blanket and print "(the blanket)". By explicitly assuming that they mean the blanket, the announcement isn't printed, and instead handling falls through to the "Instead of doing something..." rule below. Tah-dah!]
Rule for supplying a missing noun while in Beneath the Covers: change the noun to the blanket.

Instead of doing something other than looking, examining, crying, breathing, breathing fast, breathing slow, sleeping, or waiting in Beneath the Covers, say "Everything you try just winds you tighter in the darkness, this sucks."

Instead of examining in Beneath the Covers, say "It is too dark."

Instead of going a direction in Beneath the Covers, say "You flail around but you are wound up in darkness, it is like there is no escape."

Instead of breathing slow in Beneath the Covers, say "You wish you could, it's not that easy, maybe it gets easier when you get older."

Instead of sleeping in Beneath the Covers, say "It is tempting but no, naps just waste time."

Instead of crying in Beneath the Covers:
	now the player is upset;
	say "Suddenly the darkness is gone, and it is so light, you are blinded for just a second. You blink and blink and then you can see the mom holding the blanket she was covering you with. 'Oh, kid, I'm so sorry, I thought you were having fun,' she says. 'We'll do something else.'

That'll teach her[note crying-in-darkness].[banner text][paragraph break]";

[The darkness "instead" rule is to override the "Instead of examining" rule above. The "in Beneath the Covers" clause is needed to make it more specific than the above rule.]
The blanket is scenery in Beneath the Covers. Understand "cover" or "covers" or "something" or "thing" or "man" as the blanket. The darkness is scenery in Beneath the Covers. Understand "dark" or "darkness" as the darkness. Instead of examining the darkness in Beneath the Covers, say "It is so dark you cannot even see the darkness, that is how dark it is."

Chapter 2 - The Living Room

Section 1 - The Middle of the Living Room (Introduction scene only)
[We separate the Intro Middle from all other rooms because we make such large changes to it after the intro that it's easiest to replace it with an entirely new room.]

The Intro Middle is a room. "This is the middle of the living room, it is all big and vast. But it would be bigger and vaster if there weren't all these pillows and stuff in a big ring around you[if the ring of pillows is breached]. But there is a big gap in the ring of pillows now to the east[end if][if unvisited]. The mom is totally all paranoid and whatever, she is a new mom[end if]." The printed name of Intro Middle is "Middle of the Living Room".

In the Intro Middle is a squishy ball, a plush book, and your favorite toy. Your favorite toy and squishy ball are small. The plush book is medium.

The description of the squishy ball is "Blue and brown and white and it has teeth marks on it." Understand "blue", "brown" or "white" as the squishy ball. The squishy ball is pushable between rooms.

The teeth marks are part of the squishy ball. The description is "There are teeth marks in the squishy ball from you chomping on it."

The description of the plush book is "It is plush and the mom likes to read it to you." Instead of opening or closing the plush book, try reading the plush book.

The description of your favorite toy is "This is the thing your aunt gave you[note hhgttg] which is a super fun toy. It has places to chew and places that are shiny and it rattles when you shake it[if your favorite toy is in the bookcase]. Right now it is up on the bookcase, it is like nearly half a parent high[end if]." Understand "thing" or "favourite" as your favorite toy. Your favorite toy can be Parry-touched or not Parry-touched. Your favorite toy is not Parry-touched.

Instead of waving your favorite toy, say "Rattle rattle rattle."

Instead of inserting something into your favorite toy, say "You may be thinking about a different game."

[Ring of pillows]
The ring of pillows is scenery in the Intro Middle. The description is "The ring is a bunch of pillows and stuff piled around in a circle so you can't go crawling away when the mom is all distracted[if breached]. But that will not work this time because there is a big hole where Alfadog used to be[end if]. The whole thing is taller than you even when you are sitting." Understand "pillow" or "stuff" as the ring of pillows. The ring of pillows can be whole or breached. The ring of pillows is whole. Understand "hole" or "gap" as the ring of pillows when the ring of pillows is breached. Instead of taking the ring of pillows, say "It is true that there are a bunch of individual pillows in the ring of pillows but each one is like bigger than you are, so you aren't going to be able to take any of them. The mom and the dad must need a lot of softness in their lives." Instead of tasting the ring of pillows, say "The pillows taste kinda dusty."

Instead of pushing or pulling the ring of pillows, say "They are so soft and way bigger than you, and you have the proportional strength of one baby, so they do not move."

After examining the ring of pillows when Alfadog is in the location and Alfadog is scenery:
	say "Hey wait, what is Alfadog doing wedged in the pillows like that?";
	now Alfadog is not scenery.

Instead of searching the ring of pillows when Alfadog is in the location and Alfadog is scenery:
	say "Hey wait, what is Alfadog doing wedged in the pillows like that?";
	now Alfadog is not scenery.

Instead of going a direction (called d) in the Intro Middle:
	if d is up or d is down, continue the action;
	try exiting.

Instead of exiting in the Intro Middle:
	if the player is standing, continue the action;
	say "Those pillows are terribly high, you can't really climb over them, and they are too big to shove. Stupid small legs. Stupid small body."

Instead of going east in the Intro Middle when the ring of pillows is breached, move the player to the TV Room.

Instead of exiting in the Intro Middle, try going east.

Instead of upstanding using the ring of pillows, say "They are all smooshy and that makes them too hard to pull up on."

[Alfadog and alfaswitch]
Alfadog is scenery in the Intro Middle. The description of Alfadog is "It is Alfadog, your alphabet-knowing toy[if the ring of pillows is whole] and it is all wedged under some pillows[end if]. He is about as long as you and he is about as tall as you when you are crawling, it is as if he is your brother only he is a dog. He has this switch on him that makes him run and say the alphabet and such." Understand "alphadog" or "dog" or "him" as Alfadog. Instead of switching on Alfadog, try switching on the alfaswitch. Instead of switching off Alfadog, try switching off the alfaswitch. Instead of tasting Alfadog, say "Alfadog tastes of plastic and sweatshops."

A small device called the alfaswitch is part of Alfadog. The printed name is "switch on Alfadog". Understand "switch" as the alfaswitch.

Instead of turning or pushing or switching on the alfaswitch when Alfadog is in Intro Middle:
	say "You are all fumbly on the switch but then you get it turned on and look out! Alfadog lights up and his paws start digging in and he is barking out 'A! B! C! D! EFG!' It looks like he may be stuck under those pillows but then 'L! M! N! O! [bold type]P[roman type]!' pillows tumble out of his way and he is free, zipping off to the east! You hear him winding down, 'W X Y and Zeee.....' And now there is this big gap in the pillows to the east.

That Alfadog is very powerful, he should probably be recalled[note others-do-your-bidding].";
	now Alfadog is in the TV Room;
	now Alfadog is not scenery;
	now the ring of pillows is breached;
	record 5 points for "smashing open the pillows with Alfadog";

Instead of turning or pushing or switching on the alfaswitch, say "Alfadog does not light up, I guess he is all tired now. Maybe he needs a nap."

Instead of taking or pushing or pulling Alfadog when the ring of pillows is whole, say "It is hard enough to move Alfadog around when he is not all covered over with pillows, there is no way you are budging him yourself when he is wedged in there like that."

Before pushing Alfadog to a direction when the ring of pillows is whole, instead say "Alfadog may be an alfaman's best friend but it is also too large for you to really move."

Instead of taking or pushing or pulling Alfadog, say "Alfadog may be an alfaman's best friend but it is also too large for you to really move."

Before pushing Alfadog to a direction, instead say "Alfadog may be an alfaman's best friend but it is also too large for you to really move."

Instead of doing something other than examining in the presence of the mom when Alfadog is acted upon and Goading is happening, say "You make a move towards [the noun] and the mom grabs you and says, 'We're not playing with Alfadog right now, kiddo. We're working on pulling up.'"

Instead of doing something other than examining in the presence of the mom when the alfaswitch is acted upon and Goading is happening, say "You make a move towards [the noun] and the mom grabs you and says, 'We're not playing with Alfadog right now, kiddo. We're working on pulling up.'"

[Footstool]
The footstool is furniture in the Intro Middle. "There is a red footstool, it [if the player is pulled up on the footstool]is all close to you[otherwise if the footstool is in the West End of Couch or the footstool is in the Secret Space]is right next to the couch[otherwise]looms over you[end if][if some thing is on the footstool and the player is pulled up on the footstool]. [run paragraph on][otherwise]." The description is "[if the player is pulled up on the footstool]It is hard to get a good look at the footstool, your face is like inches away from it, but it is all rough under your hands[otherwise if the footstool is in the West End of Couch]The footstool is up in a corner, there is only a small gap between it and the couch with more couch past it to the west[otherwise if the footstool is in the Secret Space]It is hiding you from all of the other babies[otherwise]Tall and red fabric all over. It is kinda imposing really, all tall and everything[note relative-sizes][end if][if the folded paper is not handled and Snacktime is happening]. Beyond it past the gap between the footstool and the couch you can see this little piece of paper[end if].". Understand "tall", "red", "stool", or "fabric" as the footstool.

Report upstanding using the footstool when the footstool is supporting something:
	instead say "You pull yourself up and it is one hand and another hand which is hard because the footstool is all unstable and rocks some but then you are all on your feet so yay you! And now that you are up here you can see [the contents of the footstool] on the footstool."

Instead of pushing, pulling, attacking, or waving the footstool when the footstool is supporting something:
	say "You push on the footstool and it rocks and you pull on the footstool and it rocks and then ";
	if the mom is in the location begin;
		say "the mom puts a hand on the footstool and steadies it. 'Whoa there,' she tells you, 'careful or you'll knock off the toy.'";
	otherwise;
		now every thing which is on the footstool is in the TV Room;
		say "you hear stuff fall off it! But the stuff fell outside the ring of pillows[note pushing-footstool].";
		record 5 points for "knocking your favorite toy off the footstool";
	end if.

Instead of pushing, pulling or waving the footstool:
	say "You tug on the footstool and it rocks and rocks and then it stops rocking."

Instead of pushing, pulling or waving the footstool when the footstool is in the West End of Couch, say "It does not move, it is too close to the couch."

Instead of touching the footstool, say "Rough."

Section 2 - North End of Loveseat

Mentioning baby-gate-goneness is a one-time deal.

The North End of Loveseat is a room. "This is the part of the loveseat that is to the north. It goes on to the south and you could totally slip around the end of the loveseat to the west[if the plastic gate is in the North End of Loveseat] except that a baby gate is in the way[end if]. There is the rocking chair to the east as well, that is always fun.[if the plastic gate is not in the North End of Loveseat and mentioning baby-gate-goneness has not occurred][paragraph break]Hey, that baby gate that blocked your way west is all gone." The printed name is "Loveseat at the North". The faraway descriptor is "over by the north end of the loveseat".

The rug is a backdrop. It is in the North End of Loveseat and Behind the Loveseat. "[if the player is not zeroth-height]From up here the pattern on the rug doesn't make any more sense than it does when you are on the floor[otherwise]Maybe the pattern on the rug looks fine when you are all tall and parenty but when you are on your hands and knees it mainly makes your eyes water[end if]. There is this bump in it that runs [if the plastic gate is in the North End of Loveseat]right alongside the baby gate[otherwise]in a north-south line[end if]." Understand "pattern" as the rug. Instead of looking under the rug, say "It is too large for you to really move it and see under it."

The bump is part of the rug. The description is "The bump is not big but it is not small either, maybe there is an extension cord running under the rug there. OSHA would probably declare it a trip hazard." Instead of pushing or pushing or kicking at or squeezing or attacking the bump, say "You push on the bump but it is unyielding much like your iron will."

Instead of an actor going west with a pushable between rooms thing (called poosh) when the actor is in the North End of Loveseat and the plastic gate is not in the North End of Loveseat:
	if the actor is the player, say "There is this bump in the rug that totally catches [the poosh] and keeps you from pushing [it-them] west from here."

Instead of an actor going west in the North End of Loveseat when the plastic gate is in the North End of Loveseat:
	if the actor is the player, say "That baby gate is completely in your way.";
	stop the action.

The north pink striped loveseat is furniture in the North End of Loveseat. It is scenery and not room-mentioning. The description of the north pink striped loveseat is "The loveseat is pink and striped and tastes funny if you lick it. This is the north part of it and there is open space past it to the west[if the plastic gate is in the North End of Loveseat] that is blocked by a baby gate[end if]." The north pink striped loveseat is suitable for cruising. Rule for printing the name of the north pink striped loveseat while constructing the status line: say "loveseat" instead.

Instead of tasting the north pink striped loveseat, say "It has this funny zing to it like dust or stale cheerios."

Section 3 - Rocking Chair Room

By the Rocking Chair is east of the North End of Loveseat. "The gliding rocking chair is all tall and wooden. It is where the mom rocks you when she thinks you need a nap even though you do not need naps. The loveseat is west and there is a small open area to the east and a big open area in the middle of the room to the south." The faraway descriptor is "next to the gliding rocking chair".

The smooth rock is a small thing in By the Rocking Chair. The description is "The mom found this one one of your walks, well it wasn't really a walk, more like the mom driving you around in a stroller, anyway she found this rock and gave it to you." Understand "stone" or "pebble" as the smooth rock.

The gliding rocking chair is furniture in the By the Rocking Chair. It is not room-mentioning and scenery. The description is "The dad likes to sit in the chair and rock back and forth and back and forth and back and forth which is okay for napping (not that you need naps) but is not so good when you are trying to pull up on it." Understand "tall" or "wooden" or "rocker" as the gliding rocking chair. The gliding rocking chair is suitable for pulling up on. Rule for printing the name of the gliding rocking chair while constructing the status line: say "chair" instead. Instead of tasting the gliding rocking chair, say "Woody with a hint of smoke." Instead of pushing or pulling the rocking chair, say "You are too small and it is too big for you to push and pull it all that well."

Instead of pushing or pulling the rocking chair during Playtime, say "You stopping Parry from rocking by pushing on the chair is like ants stopping semi trucks from rolling."

[The normal description of distant item location rule will add "It is next to the gliding rocking chair" to the rocking chair's description, thanks to the room's faraway descriptor. Fix that.]
After printing the description of the gliding rocking chair when the gliding rocking chair is within a room which is not the location:
	if the gliding rocking chair and the player are not co-located begin;
		let place be the location of the gliding rocking chair;
		let way be the best route from the location to the place;
		say "It is [if way is a direction]to the [way] [end if]between the small open place and the loveseat. [run paragraph on]";
	end if;
	ignore the description of distant item location rule.

Instead of putting something under the gliding rocking chair, say "That would be a good way to stop the rocking chair from rocking but it is a gliding rocking chair and so even if you put something under there it could keep gliding."

After upstanding using the gliding rocking chair, say "It is hard to pull up on the rocker because it wants to be all wiggly but you do it anyway."

To decide if Parry is rocking: if someone is enclosed by Parry, decide no; decide yes.
To decide if Parry isn't rocking: if someone is enclosed by Parry, decide yes; decide no.

Getting points for J following you is a one-time deal. Mentioning stopped rocking is a one-time deal.

Instead of upstanding using the gliding rocking chair during Playtime:
	abide by the check upstanding using rulebook;
	if a person is held by Parry begin;
		consider the standard upstanding using rule;
		say "You pull up on the gliding rocking chair and Parry looks down at you and says 'Oh, uh, hang on just a second and I'll pick you up, too.'";
	otherwise;
		say "As soon as you have pulled up on the gliding rocking chair Parry looks down and says 'Hey, there, you want to be picked up?' and he lifts you into his lap and stops rocking[if mentioning stopped rocking has not occurred]. If you were on the ground you could get to the backpack now[end if][run paragraph on]";
		move the player to Parry, without printing a room description;
		Parry releases the player in three turns from now;
		if Jemison attempts to follow the player into Parry's lap begin;
			say ". And then Jemison pulls up on the gliding rocking chair and looks up at you and grunts softly.";
			if getting points for J following you has not occurred, record 5 points for "getting Jemison to pull up after you";
		otherwise;
			say ".";
		end if;
	end if.

Instead of someone (called the stander) trying upstanding using the gliding rocking chair during Playtime:
	if a person is held by Parry begin;
		consider the standard upstanding using rule;
		if the stander is present and not running silently, say "[The stander] pulls up on the gliding rocking chair and Parry looks down at [it-them of the stander] and says 'Oh, uh, hang on just a second and I'll pick you up, too.'";
	otherwise;
		let print-flag be 0;
		if the stander is present and not running silently, change print-flag to 1;
		if print-flag is 1, say "[The stander] pulls up on the gliding rocking chair and Parry looks down and says 'Hey, there, [if the stander is male]mister man[otherwise]sweetie[end if], do you want to be picked up?' and he scoops [it-them of the stander] up";
		move the stander to Parry;
		Parry releases in three turns from now;
		if Jemison attempts to follow the stander into Parry's lap and print-flag is 1, say ". Jemison is not going to let [the stander] get away, he pulls up on the gliding rocking chair and stares up at [the stander].";
		otherwise if print-flag is 1, say ".";
	end if.

[The following "at the time when..." rules deal with kids getting put down by Parry. The player can wiggle (by trying to go down or something similar) when held by Parry in order to get down sooner, bypassing these timed rules. If there was only one timed rule, and if Jemison then followed the player into Parry's lap, the single rule would fire and he'd be put down too early for the player to do anything about the backpack under the rocking chair. Since you can't create "at the time when..." rules that include arguments, I have two separate timed rules instead, one for the player, and one for random kids.]

At the time when Parry releases the player:
	if the player is enclosed by Parry, make Parry put the player down.

At the time when Parry releases:
	[In case the kid held by Parry is set down early because Marion gives the backpack to Parry:]
	if no person is enclosed by Parry, rule succeeds;
	make Parry put a random person held by Parry down.

To make Parry put (kid - a person) down:
	let print-flag be 0;
	move the kid to the location of the gliding rocking chair, without printing a room description;
	if kid is the player or kid is present, change print-flag to 1;
	if kid is Jemison begin;
		now Jemison is active;
		if Jemison is fixated on Parry begin;
			let f be a random thing that fascinates Jemison;
			if print-flag is 1, say "Parry says, 'Okay, down you go' to Jemison who scrabbles around and grabs [the f]. Parry gently lowers Jemison to the floor";
			move f to Jemison;
			now f does not fascinate Jemison;
		end if;
	otherwise if print-flag is 1;
		say "Parry says, 'Okay, down you go,' and he gently lowers [name of kid] to the floor";
	end if;
	if someone (called the stander) is pulled up on the gliding rocking chair begin;
		if print-flag is 1, say ". He then is all 'And now it's your turn' and he picks [name of stander] up[if stander is Jemison][note jemison-in-Parrys-lap][end if][run paragraph on]";
		now the stander is not pulled up on anything;
		if the stander is Jemison and Jemison is not fixated on Parry begin;
			if print-flag is 1, say " but Jemison makes a big fuss and Parry is all, 'Whoa! Whoa!' and [if 1 out of 4]Becca laughs and says, 'Jemison gets that way sometimes.' Parry smiles weakly and [end if]puts Jemison back down[if Jemison is fixated] and Jemison looks pointedly at [the random thing that fascinates Jemison][end if]. Then Parry starts rocking again.";
		otherwise;
			move the stander to Parry, without printing a room description;
			Parry releases in three turns from now;
			if Jemison attempts to follow the stander into Parry's lap and print-flag is 1, say " and Jemison is right behind [name of stander] and pulls up on the gliding rocking chair.";
			otherwise if print-flag is 1, say ".";
		end if;
	otherwise if print-flag is 1;
		say " and starts rocking again.";
	end if.

At the time when Marion almost removes the toy:
	if Marion encloses the backpack begin;
		say "There is this rattle rattle and your head snaps up and [if Marion is not present]you look way over at the loveseat and [end if]see Marion start to pull your favorite toy out of the backpack and then she looks at you and then she looks at Zoe and she puts the toy back in the backpack.";
		Marion looses the wild onesie in two turns from now;
	end if.

At the time when Marion looses the wild onesie:
	if Playtime is not happening, rule succeeds;
	if Marion does not enclose the backpack, rule succeeds;
	now the backpack is in the location of the gliding rocking chair;
	now the backpack is not handled;
	now the backpack is closed;
	now the plain white onesie is loose;
	remove the plain white onesie from play;
	remove your favorite toy from play;
	if Marion is present, say "Marion stands up and walks over to the gliding rocking chair and then she is back only now she doesn't have the backpack any more.";
	if Parry is present, say "A shadow falls over you and it is Marion with the backpack. [if Parry is rocking]Parry stops rocking and [end if]Marion shoves the backpack back under the gliding rocking chair."

At the time when Parry looses the wild onesie:
	if Playtime is not happening, rule succeeds;
	now the backpack is in the location of the gliding rocking chair;
	now the backpack is not handled;
	now the backpack is closed;
	now the plain white onesie is loose;
	remove the plain white onesie from play;
	remove your favorite toy from play;
	if Parry is present begin;
		say "Parry finally frees the onesie from the terrible clutches of the backpack and shoves it and your favorite toy back inside and ";
		if the player is held by Parry, say "holds you carefully while he ";
		otherwise if a person (called p) is held by Parry, say "holds [p] carefully while he ";
		say "puts the backpack back under the rocking chair. 'Got it!' he calls to Marion, who says, 'Thanks!' Zoe just looks disguted, it is like that is the only look she practices.";
	end if.

To decide if Jemison attempts to follow (leader - a person) into Parry's lap:
	if Jemison and the leader are not co-located, decide no;
	if Jemison is not fixated on the leader, decide no;
	if Jemison clears all inventory, do nothing;
	now Jemison is pulled up on the gliding rocking chair;
	now Jemison is passive;
	decide yes.

The backpack is an openable closed container in By the Rocking Chair. "A backpack with bears on and your favorite toy in it is shoved behind the rocking chair next to the wall[if open] and is all gaping open[end if][note take-all-from-backpack]." The description is "Zoe's backpack has all these cute bears sewed on it. Clearly her mom Marion picked it out, it does not reek of evil like it would if Zoe had chosen it. It is [if the backpack is open]all gaping open though the only thing you can see in it is this white onesie[otherwise]closed so you can't see what's in it[end if][if your favorite toy is not in play] though you know your toy is in there somewhere[end if]." Understand "bear" or "bears" or "with bears on" or "bag" as the backpack. The printed name is "backpack with bears on".

The zipper is part of the backpack. "The backpack's zipper is [if the backpack is open]down a ways but stuck[otherwise]zipped all the way up." Instead of opening or pulling the zipper, try opening the backpack. Instead of closing the zipper, try closing the backpack.

The faker-toy is part of the backpack. Understand "your" or "favorite" or "toy" as the faker-toy when your favorite toy is not in play and Playtime is happening. The printed name is "favorite toy". The indefinite article of the faker-toy is "your". Before doing something when the faker-toy is acted upon, instead say "You can't really do anything to your toy right now because it is in that backpack." Before examining the faker-toy, instead say "Your toy is inside Zoe's backpack so you cannot really see it."

Instead of doing something other than examining when the backpack is acted upon and Parry is rocking and the backpack is in By the Rocking Chair, say "You would but Parry is rocking, rocking, rocking, and you can't reach under the rocking chair to get to the backpack while he is rocking, rocking, rocking."

Instead of doing something other than examining when the backpack is acted upon and the backpack is held by someone (called the holder), say "[The holder] has the backpack right now."

[I have to trap taking, pushing, and pulling separately because of other rule interactions.]
Instead of taking or pushing or pulling the backpack when Parry is rocking and the backpack is in By the Rocking Chair, try opening the backpack instead. [The open rule gets caught by the above rule.]

Instead of taking or pushing or pulling the backpack when the backpack is held by someone, try opening the backpack.

Instead of pushing or pulling the backpack, say "It is way big and you can't really budge it."

A rule for reaching inside the backpack:
	if Parry is rocking and the backpack is in By the Rocking Chair begin;
		if the person reaching is the player, say "You would but Parry is rocking, rocking, rocking, and you can't reach under the rocking chair to get to the backpack while he is rocking, rocking, rocking.";
		deny access;
	end if.

Getting points for opening the backpack is a one-time deal.

After opening the backpack:
	move the plain white onesie to the backpack;
	say "When you open the backpack all you can see in it is this white onesie that is totally in your way.";
	if Zoe is carrying something (called the distractor) begin;
		silently try Zoe trying dropping the distractor;
		if Zoe is not carrying the distractor, say "Zoe sees you open that backpack and she gets agitated and tosses [the distractor] aside.";
	end if;
	if getting points for opening the backpack has not occurred, record 5 points for "opening the backpack";
	now Zoe is done acting.

After searching the backpack, say "You cannot find anything in the backpack, that stupid onesie is in the way."

Instead of opening the backpack when the backpack is open and the plain white onesie is stuck and the plain white onesie is in the backpack, say "That backpack is as open as it is going to be with that onesie all stuck in there."

Instead of closing the backpack when the backpack is open and the plain white onesie is stuck and the plain white onesie is in the backpack, say "You try and you try but that onesie is stuck in there and so you can't close the backpack."

Instead of removing the plain white onesie from the backpack, try taking the plain white onesie. Instead of pulling or pushing the plain white onesie, try taking the plain white onesie.

Instead of taking the plain white onesie when the plain white onesie is in the backpack:
	if the plain white onesie is loose begin;
		now the plain white onesie is stuck;
		change the act percentage of Zoe to 100;
		now Zoe is done acting;
		say "You pull on the onesie and it comes part of the way out of the backpack and then it gets hung on the backpack zipper. Great. Now it is blocking the backpack and it is also stuck[note stuck-onesie].";
	otherwise;
		say "You tug on that stupid onesie but it is stuck in the stupid backpack's stupid zipper.";
	end if.

Instead of looking under the plain white onesie when the plain white onesie is in the backpack:
	if the plain white onesie is loose, try taking the plain white onesie;
	otherwise say "You cannot see under the onesie, it is in your way and you cannot move it."

Instead of taking something that is in the backpack:
	say "That white onesie is in the way."

[These next two rules allow you to refer to the white onesie in the backpack with ALL but not anything else.]
Rule for deciding whether all includes the plain white onesie: it does.
Rule for deciding whether all includes something that is in the backpack: it does not.

[Zoe's toys from her backpack]

The plain white onesie is a small thing. The plain white onesie can be stuck or loose. The plain white onesie is loose. The description of the plain white onesie is "That onesie is [if loose]totally in the way[otherwise]all stuck in the backpack's zipper[end if]."

Does the player mean taking the plain white onesie when the backpack is open: it is very likely.
Does the player mean pulling the plain white onesie when the backpack is open: it is very likely.
Does the player mean looking under the plain white onesie when the backpack is open: it is very likely.

Table of Stuff Originally In the Backpack
Stuff
a thing
with 6 blank rows

The soft foam block, the plastic cup, the teething ring, the much-loved kangaroo, the teeny squeezy book, and the little doll are small things in the backpack. Understand "roo" as the much-loved kangaroo.

When play begins (this is the filling the table of backpack stuff rule):
	repeat with t running through things in the backpack begin;
		if the number of blank rows in the Table of Stuff Originally In the Backpack is 0 begin;
			report bug "Too few rows in the Table of Stuff Originally In the Backpack";
			stop repeating;
		otherwise;
			choose a blank row in the Table of Stuff Originally In the Backpack;
			change stuff entry to t;
		end if;
	end repeat.

The description of the soft foam block is "Magenta and soft and fun to chew. It is like one hand wide on a side." Understand "magenta" as the soft foam block.

The description of the plastic cup is "This is one cup that originally came with a bunch of other cups and they were all different sizes so you could stack them up into a giant tower of cups. But there is only one cup, that is no good for stacking." Instead of inserting something into the plastic cup, say "It is small and can't really hold all that much, besides the cup is for stacking not holding."

The description of the teething ring is "Ribbed for your chewing pleasure."

The description of the much-loved kangaroo is "This stuffed kangaroo has been loved and chewed and now it has some stuffing showing." Understand "stuffed" or "stuffing" as the much-loved kangaroo. Instead of kissing the much-loved kangaroo, say "You hug and kiss on the kangaroo."

The description of the teeny squeezy book is "This is not really a book, it is more like a very small toy that looks kinda like a book." Instead of opening or closing the teeny squeezy book, try reading the teeny squeezy book.

The description of the little doll is "It has red hair like Zoe but presumably it is not evil like Zoe."

Section 4 - Small Open Area

[This was the "Small Open Area" but that then obliterates the value "small".]
[Also, there is undoubtedly a more clever way of mentioning your favorite toy when it's on the bookcase than how I'm doing it in the room description, but I never can figure out how the printing activities of I7 work.]
The TKBUG Open Area is east of By the Rocking Chair. "You are east of the gliding rocking chair that is fun to ride on and north of the big TV that Sesame Street comes from. On the wall is a big bookcase full of chewy books. There is a way into the kitchen to the north but one of those baby gates is there, that is no fair[if your favorite toy is in the bookcase].[paragraph break]About half a parent high in the bookcase is your favorite toy[end if]." The printed name is "Small Open Area". The faraway descriptor is "in that small open area".

[Handle taking your favorite toy when it's above your head]
Before taking or attacking your favorite toy when your favorite toy is in the bookcase and the TKBUG Open Area encloses the player:
	if the player is half-height-or-higher begin;
		now your favorite toy is handled;
		if your favorite toy is not Parry-touched, instead say "You stretch for the toy but it is just barely out of reach[if 1 out of 5], grf, that is very frustrating[end if].";
		now your favorite toy is in the northeast-playpen;
		say "You stretch your arms but they are still too much like teeny T. Rex arms so then you scrunch down in your swing and stretch out a leg and as you swoop up again you just are able to kick the toy! It flies into the air and onto your lap! But as you swing back ";
		if Zoe is on the tall plastic block, say "Zoe leans over from the tall block and swats at the toy and you swat at her and the upshot is the toy goes flying again";
		otherwise say "the toy starts to slide out of your lap and you try to grab it but you have the reflexes of a sloth on methamphetamines or something, your arm just jerks and sends the toy flying again";
		say " and it goes kerplunk into the playpen.

This is the worst playdate ever.";
		now the baby-swing is temporarily muffled;
		now Zoe is passive;
		Zoe reactivates in zero turns from now;
		instead record 5 points for "accidentally getting your toy in the playpen";
	otherwise if the player is in the baby-swing;
		instead say "The swing gets you close to the toy but even [if your favorite toy is Parry-touched]though Parry got the toy a little closer to you, [end if]at the top of the swing arc you are not close enough to grab it, you need the swing to go higher.";
	otherwise if the player is eighth-height-or-higher;
		instead say nearly-in-reach message for your favorite toy;
	otherwise;
		instead say "You have short arms, you can't reach your favorite toy from down here.";
	end if.

Before pushing your favorite toy with the poosh stick when your favorite toy is in the bookcase:
	if the player holds the poosh stick, instead say "[If the player is quarter-height-or-higher]You are much closer to the toy but t[otherwise]T[end if]he long stick is hard to control and the shelves keep getting in the way."

At the time when Zoe reactivates:
	now Zoe is active.

The bookcase is a scenery container in the TKBUG Open Area. The description is "Books sleep here when they are not being read by the parents. The books do not really taste all that good and besides you cannot reach any of them [if the player is in the baby-swing]strapped into the swing[otherwise]now that the bottom bookcase shelves are all empty[end if][if the player is not quarter-height-or-higher]. From down here it is even hard to see the books, they are way up there[end if][if your favorite toy is in the bookcase]. Your favorite toy is up next to the books about a half a parent high[end if]." Instead of tasting the bookcase, say "The parents must have cleaned it recently because it has an oil taste." Instead of upstanding using the bookcase, say "You would think there would be good handholds but there aren't, and the wood is all slick."

Some out-of-reach books are scenery in the TKBUG Open Area. The description is "These are books but boring and with no pictures. They are all probably post-modern stories of longing and loss and existential angst and whatever because books about Maisy totally are better." Instead of doing something other than examining when the noun is the out-of-reach books or the second noun is the out-of-reach books, say "They are completely out of reach even though there used to be ones in reach before the parents moved them. Whatever. They don't even taste good anyway. They are all like musty with metaphor." Understand "book" as the out-of-reach books. The printed name is "books".

Instead of an actor going north in the TKBUG Open Area when the plastic gate is in the TKBUG Open Area:
	if the actor is the player, say "That baby gate is completely in your way.";
	stop the action.

The plastic gate is a backdrop. The description is "Plastic and it is wedged in there so you cannot go anywhere which is totally not right." It is in the TKBUG Open Area, the Behind the Loveseat, and the North End of Loveseat. Understand "baby" as the plastic gate. It is suitable for pulling up on. Instead of taking the plastic gate, say "It is stuck in place possibly by magic." Instead of tasting the plastic gate, say "Chewy under your new teeth."

[The playpen is large enough to go across two rooms, and is represented by two different objects. I have a single routine to describe the playpens, in order to save on string space. This will end up being a Glulx game, so that's silly, but oh well.]

A playpen is a kind of container. Playpens are transparent and enterable.

A rule for reaching inside a playpen when the top oval is closed:
	say "The playpen opening is [if the top oval is nearly in reach]still [end if]way too high for you to reach inside.";
	deny access.

A height reachability rule when the reaching object is a playpen (this is the playpen can be reached from all of the blocks rule):
	if the reaching actor is on a pushable between rooms supporter, continue action processing.

Instead of upstanding using a playpen when the top oval is closed, say "The mesh gives too much for you to pull yourself up on it."

Instead of upstanding using a playpen when the top oval is open, say "Now that the playpen is open there is really nothing to pull up on."

Instead of inserting something into a playpen when the top oval is closed, say "The opening is way too high for you to put anything in the playpen, it's a shame you can't throw things with any accuracy or anything."

Instead of inserting something into a playpen when the top oval is closed and the top oval is nearly in reach, say "Putting [the noun] into the playpen won't do you much good and you won't be able to get at it again so you should hold onto [it-them of the noun]."

Instead of inserting something into a playpen when the top oval is open, say "You have been working hard to get things out of the playpen, don't spend time putting things back in."

Instead of entering a playpen when the top oval is closed, say "The mesh is all in the way and besides it is no fun being in the playpen so why would you go in there voluntarily?"

Instead of entering a playpen when the top oval is closed and the top oval is nearly in reach, say "You might be able to tip yourself over the top oval and into the playpen but the fall would be baby-crushing so no thank you."

Instead of opening a playpen, try opening the top oval. Instead of closing a playpen, try closing the top oval.

Instead of pushing a playpen with the poosh stick, try pushing the top oval with the poosh stick. Instead of unlocking a playpen with the poosh stick, try pushing the top oval with the poosh stick.

Rule for reaching outside a playpen:
	say "You will have to get out of the playpen.";
	deny access.

Rule for writing a paragraph about a playpen (called p):
	say "[the initial appearance of p][if the top oval is open] The playpen is all open.[run paragraph on][end if]";
	if something is in p, say " [if the top oval is open]In the playpen[otherwise]Through the mesh[end if] you can see [a list of things in p].";
	otherwise say line break.

Mentioning deathtrapness is a one-time deal.

To say playpen-description about (p - a playpen):
	say "This portable playpen is all old, probably it should be recalled except that the parents are way cheap. It is this frame made of metal bars and it has mesh sides and a floor for babies to play on. The bars go around in ovals at the top and bottom. [if the top oval is open]The hinged half of the top oval is down so the playpen is all open[otherwise]It should be recalled because the top oval is hinged and you can lower one side of the playpen by pushing the top bar up and then letting go so that the mesh comes down[end if][if mentioning deathtrapness has not occurred], that is a total deathtrap[end if]";
	if something is in p, say ". [if the top oval is open]In the playpen[otherwise]Through the mesh[end if] you can see [a list of things in p]";
	say ". "

The top oval is a backdrop. It is in the TKBUG Open Area and the Middle of the Living Room. The description is "The top oval is [if the top oval is open]half hanging down so that the playpen is all open[otherwise]way up there on the playpen. It must be like two or three babies high above you[end if]." Understand "metal", "bar", "bars", "hinge", "hinges", "opening", or "ovals" as the top oval. The top oval can be open or closed. The top oval is closed and not chewable. The top oval is not noticeable by playmates.

To decide if the top oval is nearly in reach:
	if the player is eighth-height-or-higher, decide yes;
	decide no.

To decide if the top oval is too high:
	if the player is quarter-height-or-higher, decide no;
	if the second noun is the poosh stick, decide no;
	decide yes.

A height reachability rule when the reaching object is the top oval and the top oval is open (this is the no use doing anything to the open top oval rule):
	if the reaching actor is the player, say "Now that you have lowered the oval there is no need to do anything more with it.";
	stop action processing.

A height reachability rule when the reaching object is the top oval and the top oval is too high (this is the top oval is too far away rule):
	if the reaching actor is the player begin;
		if the top oval is nearly in reach, say nearly-in-reach message for the top oval;
		otherwise say "You have short arms, you can't reach the top oval from down here.";
	end if;
	stop action processing.

A height reachability rule when the reaching object is the top oval and the reaching actor is in the baby-swing (this is the top oval is unreachable from the baby swing rule):
	abide by the baby-swing specific noun reachability rule.

A height reachability rule when the reaching object is the top oval (this is the top oval is now in reach rule): continue action processing.

Before pushing the top oval to up, instead try pushing the top oval.

Before inserting something into the top oval when the top oval is closed:
	let target be a random playpen in the location;
	instead try inserting the noun into target.

Instead of opening the top oval when the top oval is open, say "You have already opened the playpen."

Instead of opening the top oval when the top oval is closed, try pushing the top oval.

Instead of closing the top oval when the top oval is closed, say "It is already all closed."

Instead of closing the top oval when the top oval is open, say "It was too much work to open in the first place!"

Instead of unlocking the top oval with the poosh stick, try pushing the top oval with the poosh stick.

Instead of pushing the top oval with the poosh stick when the poosh stick is carried by the player:
	say "You get the poosh stick into position under the oval and push the oval up with it and it goes up a bit and clicks but when you let go it drops back into place. I guess you need to push up the oval at both ends of the playpen at the same time.";
	see if Zoe gets excited again.

Instead of pushing the top oval:
	say "You push on the side of the oval closest to you and it goes up a bit and clicks but when you let go it drops back into place. I guess you need to push up the oval at both ends of the playpen at the same time.";
	see if Zoe gets excited again.

To see if Zoe gets excited again:
	if Dejectiontime is happening begin;
		say "[line break]Zoe coughs and you look at her and she looks at you and you both look at the top oval of the playpen. It is like you are communicating telepathically and that communication is 'I may despise you and all that you stand for but I will work with you in order to lift the oval and free the toy but after that it is every baby for him or herself.' So you nod and she nods and it is a deal[note Zoe-cooperates].

Becca says, 'That's a bad cough Zoe has. Is she getting sick?' but Marion says, 'No, she coughs like that sometimes to get attention.'";
		change (machine corresponding to a baby name of Zoe in the Table of Baby State Machines) to ZK Cooperating Rules;
	end if.

Instead of pushing the top oval with the poosh stick when Cooperationtime is happening and Zoe is on the tall plastic block and the location is the Middle of the Living Room:
	if the poosh stick is not held by the player, abide by the pick up objects before pushing with them rule;
	say "You push on the oval with the poosh stick and at the same time Zoe pushes her end of the oval and it clicks up and then half of it falls down. That leaves the playpen all gaping open!";
	now the top oval is open;
	record 5 points for "opening the playpen".

Instead of pushing the top oval with the poosh stick when Cooperationtime is happening and the location is the Middle of the Living Room:
	if the poosh stick is not held by the player, abide by the pick up objects before pushing with them rule;
	say "You push the oval up with the poosh stick and it goes up a bit and clicks but Zoe is not yet in place to push the other side as well so nothing much happens."

Instead of pushing the top oval with the poosh stick when Cooperationtime is happening and the location is the TKBUG Open Area:
	if the poosh stick is not held by the player, abide by the pick up objects before pushing with them rule;
	say "You move the stick into position to push the top oval and ";
	if Zoe is on the tall plastic block, say "nearly hit Zoe on the nose, she looks down at you as if you are an idiot both for nearly hitting her and for pushing the oval up on the side where she already is.";
	otherwise say "Zoe just looks at you as if to say 'this is the side of the oval I will be pushing, get your own side.'"

The northeast-playpen is a playpen in the TKBUG Open Area. "Part of your playpen is here, there is more of it to the southwest." The description is "[playpen-description about northeast-playpen]". The printed name is "northeast part of the playpen". Understand "northeast", "part", "playpen", "frame", or "mesh" as the northeast-playpen.

Instead of going southwest when the player is in the northeast-playpen:
	say "You move to the other part of the playpen.";
	move the player to the southwest-playpen.

Section 5 - South End of Loveseat

The South End of Loveseat is south of the North End of Loveseat and southwest of By the Rocking Chair. "This is the south end of the loveseat, there is more loveseat to the north and a big play area to the east and to the south is the couch that is bigger than the loveseat." The printed name is "Loveseat at the South". The faraway descriptor is "by the south end of the loveseat".

The south pink striped loveseat is furniture in the South End of Loveseat. It is not room-mentioning, scenery and suitable for cruising. The description of the south pink striped loveseat is "The loveseat is pink and striped and tastes funny if you lick it. This is the south part of it."

Instead of tasting the south pink striped loveseat, say "It has this funny zing to it like dust or stale cheerios."

The rattly car is a pushable between rooms small thing in the south end of loveseat. The description of the rattly car is "It is all rattly, I guess that makes it more fun or something." Instead of pushing the rattly car, say "You push it and it rolls and rattles some." Instead of waving the rattly car, say "It goes rattle rattle."

Section 6 - Middle of the Living Room

The Middle of the Living Room is east of the South End of Loveseat and southeast of the North End of Loveseat and south of By the Rocking Chair and southwest of the TKBUG Open Area. "This is the middle of the living room, it is all big and vast. This is where everybody plays because it is so big. But when you get bored there is a bunch of other stuff you can go to like the TV to the east and the loveseat to the west and the couch to the south and the gliding rocking chair to the north." The faraway descriptor is "in the middle of the living room".

The southwest-playpen is a playpen in the Middle of the Living Room. "Part of your playpen is here, there is more of it to the northeast." The description is "[playpen-description about southwest-playpen]". The printed name is "southwest part of the playpen". Understand "southwest", "part", "playpen", "frame", or "mesh" as the southwest-playpen.

Instead of going northeast when the player is in the southwest-playpen:
	say "You move to the other part of the playpen.";
	move the player to the northeast-playpen.

Section 7 - TV Room

The TV Room is east of the Middle of the Living Room and southeast of By the Rocking Chair and south of the TKBUG Open Area. "The entertainment center here holds the TV all towering above you and it has these glass doors beneath the TV that you can see stereo equipment through. You are near the couch to the south and the middle of the living room to the west and there is a small open area to play in to the north[if Introduction is happening][note introcomp][end if]." The printed name is "In Front of TV". The faraway descriptor is "over by the TV".

The entertainment center is scenery in the TV Room. The description is "The entertainment center is tall and has some doors beneath it but the important thing is that the TV is on it[if the television is off]. Not that the TV is on right now, who knows what Blue's Clues or Sesame Street episodes you are missing?[otherwise]. Right now the TV is showing [tv screen display].[end if]". Understand "door" or "doors" or "glass" as the entertainment center.

Instead of tasting the entertainment center, say "The wood is all varnishy and the glass is cold."

Instead of opening or pushing or pulling the entertainment center, say "You push on the doors to the entertainment center but the parents have them all fixed in place."

Instead of closing the entertainment center, say "The doors to the entertainment center are already closed."

Instead of unlocking the entertainment center with, say "You do not know how to unlock the doors."

Instead of upstanding using the entertainment center, say "Its surface is sleek and modern and your hands slide right off if it so you cannot get enough purchase to pull up on it."

Instead of searching the entertainment center, say "There is stereo equipment behind the glass doors and there is a big TV all tall up there on the entertainment center."

The stereo equipment is scenery in the TV Room. The description is "It is all made by Aiwa[note no-aiwa], can't the parents buy real equipment?" Instead of doing something other than examining when the stereo equipment is acted upon, say "It is behind the doors to the entertainment center."

The television is scenery in the TV Room. The description is "The TV is [if the baby-swing is in the TV room and the player is not in the baby-swing]blocked by this baby swing that is in the way[otherwise if off]dark and that makes you sad[otherwise]showing [tv screen display][end if]." Understand "tv" or "telly" or "screen" as the television. The television is not room-mentioning. The television can be on or off. The television is off. The television has a number called screen turn count. The screen turn count of the television is 1.

Instead of an actor doing something other than examining or switching on or switching off when the television is acted upon:
	if the actor is the player begin;
		if the player is in the baby-swing, say "With you strapped into the baby swing you cannot really reach the TV.";
		otherwise say "It is all far away and if you could stand up and were taller then you could reach it but you aren't so you can't.";
	end if.

Instead of switching on or switching off the television when the player is carrying the remote control, say "Even though you have the remote control it is beyond your baby powers to work it, it is so complex."

Instead of switching on or switching off the television, say "You do not have the remote control that the parents use to make the TV turn on or off or show different things like Sesame Street or Baby Feynman."

Instead of going somewhere from the TV Room during Introduction, say "Not without your favorite toy, [if your favorite toy is on the footstool]you should get it from the footstool.[otherwise]it was too hard to get it down in the first place."

Instead of going west from the TV room during Introduction, move the player to the Intro Middle.

To say tv screen display:
	let row-count be the screen turn count of the television plus 2;
	now row-count is row-count divided by 3;
	say On Screen in row row-count of the Table of Television Messages.

Every turn when the television is on:
	if the screen turn count of the television is greater than the number of rows in the Table of Television Messages times 3, now the screen turn count of the television is 1;
	if the player is in the TV room begin;
		let row-count be the screen turn count of the television plus 2;
		if the remainder after dividing row-count by 3 is 0 begin;
			now row-count is row-count divided by 3;
			say "[Change Message in row row-count of the Table of Television Messages][paragraph break]";
		end if;
	end if;
	now the screen turn count of the television is the screen turn count of the television plus 1.

Table of Television Messages
Change Message	On Screen
"The screen goes dark and then light. There is this big ball with a ring around it on screen and some words or something across it. The TV then says 'SPACE' in this weird drugged voice."	"a big ball with a ring around it"
"Swooping music plays from the TV and all of these yellow balls start bouncing around on screen."	"all these yellow balls"
"The bouncing balls go away on screen and a big yellow flower shows up, it is all curvy."	"a big yellow flower"
"The yellow flower is replaced by this drawing of a yellow disk and it has what looks like flames coming off of it and the calm announcer voice says, 'SUN.'"	"a drawing of a yellow disk"
"Now the yellow things are gone from the TV, instead there are all these crescent shapes drifting down in some thick liquid inside a cylinder. Maybe it is a toy or something."	"a bunch of crescent shapes"
"The drifting crescent shapes drift off screen and now there are crescent cookies being baked and turning into golden brown crescents."	"all these tasty-looking crescent cookies"
"In the TV you see a big white thing, it glows bright. And then the TV says 'MOON'."	"a big white glowing thing"
"It looks like the TV is done with white things, now there is this big row of balls starting with this huge yellow one and then a smaller one and a little bigger one and so on. There are a lot of those balls."	"a row of different-sized balls"
"In the TV the row of balls all get kicked by tiny kids except for this one and it is blue and white."	"this single blue and white ball"
"A kid on TV takes the blue and white ball and throws it around and then there is a crumpled blue and white ball. You hear this poof poof poof noise and the ball starts inflating and getting less crumply."	"a crumpled blue and white ball"
"The blue and white crumply ball on the TV is now not crumply at all, it is very smooth and shiny. That calm announcer voice says 'EARTH'. A giant baby face appears and starts chewing on the Earth ball."	"a smooth and shiny Earth ball"
"The giant baby face on the TV goes away in this star-shaped wipe and then a hand shows up on screen with a piece of chalk."	"a hand holding a piece of chalk"
"There is this hand on TV holding chalk, it starts drawing star shapes. More and more and more star shapes fill the screen."	"bunches and bunches of star drawings"
"On TV the star shapes turn into these tiny glowing points of light that go twinkle! Twinkle! Twinkle!"	"twinkling points of light"
"Points of light on the TV fade away and now there is just one big point of light and the announcer says 'STARS'."	"a big point of light"
"Now on TV all of these stars are gathering together, it is like they are having a big star party."	"a big star party"
"The star party on the TV screen gets even wilder as more and more stars come rushing in. It is like a rave only no one needs glow sticks because everyone already glows."	"a giant glowing star party"
"'GALAXIES' says the TV announcer and I guess that is what a star party is called."	"a huge star party"
"Whoosh! goes the TV and there is this side wipe and bubbles starts drifting across the screen."	"bubbles"
"There are bubbles on the TV screen, they start popping and then there is this kid, she's probably three, and the wind is blowing her hair all around."	"a kid with her wind-blown hair"
"The TV's whooshing sound dies down and there are these puffy things all over the screen like onesie lint. 'CLOUDS' says the announcer."	"puffy clouds"
"'Pfff pfff pff' says the TV and this deflated balloon starts getting bigger on screen."	"a partially-deflated balloon"
"The balloon on TV is all filled up and it is bright blue. Then a bunch more balloons start falling down."	"a bunch of balloons"
"There are so many balloons on the TV screen that it is like a riot of colors."	"a bunch of brightly-colored balloons"
"These babies start crawling through the TV balloons and hitting them and knocking them everywhere and the announcer says 'BALLOONS'."	"babies kicking balloons"

Section 8 - West End of Couch

The West End of Couch is south of the South End of Loveseat and southwest of the Middle of the Living Room. "The couch is very long and this is the west part of it. There is a loveseat to the north and more couch to the east." The printed name is "Couch at the West". The faraway descriptor is "by the west end of the couch".

After deciding the scope of the player when Snacktime is happening and the location is the West End of Couch and the folded paper is not handled: place the folded paper in scope.

A big couch is a kind of furniture. Rule for printing the name of a big couch while constructing the status line: say "couch" instead. Instead of tasting a big couch, say "The rolled edge of the cushions is funny on your tongue." A big couch is not room-mentioning, scenery and suitable for cruising.

The west grey couch is a big couch in the West End of Couch. Understand "gray" or "cushion" or "cushions" as the west grey couch. The description is "Grey and short and it is soft when the mom or the dad put you on it. This part is all to the west and there is more couch to the east[if the footstool is in the West End of Couch] and a tiny piece of it past the footstool to the west[end if]."

Instead of going west in the West End of Couch when the footstool is in the West End of Couch, say "Crawling like you are there is no way you will fit in the narrow gap between the footstool and the couch[if 1 out of 3], it is because you have the shoulders of a very small linebacker[end if]."

[This has to be a before rule, since the before rule that makes you let go when trying to go in a non-cruising direction would override an instead rule.]
Before going west in the West End of Couch when the footstool is in the West End of Couch and the player is pulled up on the footstool or the player is pulled up on the west grey couch:
	if the thing gone with is nothing begin;
		say "There is just enough space for you to slide between the footstool and the couch so you cruise along the couch past the footstool and into the tiny space back there[note squeeze-behind-footstool].";
		if the player is pulled up on the west grey couch, now the player is pulled up on the hidden grey couch;
		change the last location of the player to the location of the player;
		move the footstool to the Secret Space;
		move the player to the Secret Space;
		stop the action;
	end if.

After visiting the West End of Couch when the visitation count of West End of Couch is 1 and Snacktime is happening, say "You can just see some paper beyond the footstool."

Section 9 - Middle of Couch

The Middle of Couch is east of the West End of Couch and southeast of the South End of Loveseat and south of the Middle of the Living Room and southwest of the TV Room. "Here is where the middle of the couch is. If you want to play you should totally go north to the middle of the room where there is a lot of space. There is more couch to the east and west." The printed name is "Couch in the Middle". The faraway descriptor is "at the middle of the couch".

The middle grey couch is a big couch in the Middle of Couch. Understand "gray" or "cushion" or "cushions" as the west grey couch. The description is "Grey and short and it is soft when the mom or the dad put you on it. This part is in the middle and to the east there is more couch and to the west there is more couch."

Section 10 - East End of Couch

The East End of Couch is east of the Middle of Couch and southeast of the Middle of the Living Room and south of the TV Room. "You are where the couch ends here at the east, at least where it ends at one end. North is the TV and west is more couch." The printed name is "Couch at the East". The faraway descriptor is "by the east end of the couch".

The east grey couch is a big couch in the East End of Couch. Understand "gray" or "cushion" or "cushions" as the west grey couch. The description is "Grey and short and it is soft when the mom or the dad put you on it. This part is the east part. The rest of the couch is west."

Section 11 - Behind the Loveseat

Behind the Loveseat is west of the North End of Loveseat. "Back here is the back of the loveseat casting a shadow over you, it is kind of cold. You can go south and east. You could go west but there is totally a door in your way. You could go north in the hall but there is a baby gate in the way there." The faraway descriptor is "behind the loveseat".

Instead of going north in Behind the Loveseat when the plastic gate is in the Behind the Loveseat, say "That baby gate is completely in your way."

The shadow is scenery in Behind the Loveseat. The description is "The shadow makes you all cold."

The front door is scenery in Behind the Loveseat. The description is "It is big and wood and goes outside." Instead of going west in Behind the Loveseat, say "The front door is all in your way." Instead of opening or closing or pushing or pulling the front door, say "You can open and close some things but not people-sized doors."

The back loveseat is a backdrop. It is in Behind the Loveseat and By the Cabinet. Understand "pink" or "striped" or "seat" as the back loveseat. The description is "Pink and striped and very tall above you." Instead of upstanding using the back loveseat, say "You try but the back is all slick and you totally cannot get enough grip to pull up back here."

Section 12 - By the Cabinet

By the Cabinet is south of Behind the Loveseat. "The cabinet that is also like an endtable for the loveseat and the couch is here. You can go north back to behind the loveseat[if something is on the cabinet top].[paragraph break]Way up on top of the cabinet you see [a list of things on the cabinet top][end if][if unvisited].[paragraph break]You can't believe the mom just shoved your favorite toy in that cabinet so you couldn't have it[end if]." The faraway descriptor is "next to the cabinet".

The wood cabinet is an openable closed scenery container in By the Cabinet. Understand "endtable" or "table" as the wood cabinet. The description is "The cabinet is wood like a lot of other furniture in here because the parents like wood or something, maybe they are tree huggers but only if the trees have been cut down and planed and shaped. Anyway it has this door in it which is [if the wood cabinet is open]standing open[otherwise]closed[end if][if something is on the cabinet top] and on top is [a list of things on the cabinet top][end if]." Instead of tasting the wood cabinet, say "It is waxy and maybe a bit lemony." Instead of putting something under the wood cabinet, say "There is not really enough room under there for anything thicker than a piece of paper." Instead of putting the folded paper under the wood cabinet, say "I guess it is really hard to put the paper under there because you can't get it to go under there again." Instead of looking under the wood cabinet, say "There is no real space under there for you to look in."

Instead of opening the wood cabinet when the player is standing (this is the player can't open the cabinet while standing rule), instead say "That is a good idea but you cannot really do that while you are all pulled up without falling down on your diaper instead, and besides the baby lock is so high you still couldn't reach it."

The player can't open the cabinet while standing rule is listed before the player opens the cabinet with jangly zoe rule in the instead rulebook.

Instead of opening the wood cabinet when the player is on a pushable between rooms enterable supporter (called the platform) (this is the player can't open the cabinet while on a block rule):
	[This shouldn't happen if the paper's been played with.]
	if the folded paper is handled, continue the action;
	say "That is a clever idea but your ideas are more agile than you are. Even though [the platform] makes you taller, opening the cabinet and pushing down on the baby lock and not falling on your face and hurting yourself is like two more things than you can do at one time. You should find something else to push down on that lock for you."

Mentioning the source of the coaster is a one-time deal.

To have the coaster bounce off the lock:
	say ". [if mentioning the source of the coaster has not occurred]This paper coaster that must have been under the tumbler[otherwise]The paper coaster[end if] comes sliding off the cabinet and hits the baby lock! But the coaster is really light and so it bounces off without opening the lock and lands on the floor[note coaster-falls]";
	move the coaster to By the Cabinet.

To have the coaster almost bounce off the lock:
	say ". [if mentioning the source of the coaster has not occurred]This paper coaster that must have been under the tumbler[otherwise]The paper coaster[end if] slides forward like it's about to fall off the cabinet".

Instead of opening the wood cabinet when Jemison is present and Zoe is present and Zoe has the velcro jangly balls (this is the player opens the cabinet with jangly zoe rule):
	say "You reach for the door and Zoe moves to stop you but Jemison gets in front of Zoe and reaches for the jangly balls[note baby-cancels-baby]. While she is all dealing with him you tug on the door and the baby lock catches";
	if the folded paper is handled begin;
		if the heavy tumbler is on the cabinet top begin;
			say ", making the entire cabinet rock towards you. And as it does so the tumbler tumbles, hitting the baby lock and pushing it down before springing off of the lock and over your head. The cabinet door pops open and your favorite toy rolls out[if the player is zeroth-height] and into your lap[end if]!";
			move your favorite toy to the player;
			stop the action;
		end if;
		say " and the entire cabinet rocks towards you now that there is no paper under it[if your favorite toy is in the wood cabinet] and your favorite toy rattles around in there";
		if the coaster is on the cabinet top, have the coaster bounce off the lock;
	otherwise if your favorite toy is in the wood cabinet;
		say " and you get a glimpse of your favorite toy";
	end if;
	instead say ".[paragraph break]Sadly your baby reflexes are not up to doing anything more than opening the door and so you then let go and the cabinet door closes[if the paper is handled] and the cabinet rocks back again[end if][note believable-baby-action]."

Instead of opening the wood cabinet when the player has the velcro jangly balls and Jemison is present:
	instead say "You go for the door but then Jemison is in front of you reaching for the jangly balls and they go jingle jangle when you pull away from him."

Together-playing is a one-time deal.

Instead of opening the wood cabinet when the folded paper is handled:
	say "You tug on the door and the baby lock on the door catches and the entire cabinet rocks towards you now that there is no paper under it[if the heavy tumbler is on the cabinet top] so that the heavy tumbler tips forward like it's going to fall[end if][if your favorite toy is in the wood cabinet] and your favorite toy rattles around in there[end if][note believable-baby-action]";
	if the coaster is on the cabinet top begin;
		if Zoe is present, have the coaster almost bounce off the lock;
		otherwise have the coaster bounce off the lock;
	end if;
	say ".[paragraph break]Then [if Zoe is present]Zoe shoves the door[otherwise]your baby reflexes are not up for more so you let go[end if] and the cabinet rocks back [if the coaster is on the cabinet top]making the coaster slide back fully on top of the cabinet [end if]and the door closes[if Zoe is present][note Zoe-pushes-door][end if][if zoe is present and together-playing has not occurred].[paragraph break]Lisa looks over the couch at you and at Zoe and she says, 'Awwww, they're playing together! How cute!'[otherwise].[end if]"

Instead of opening the wood cabinet:
	say "You tug on the door but it has one of those baby lock things on it that you have to push down to open the door and you cannot reach it at all so the door opens a tiny bit[if your favorite toy is in the wood cabinet] and you can barely see your favorite toy in there[end if][note believable-baby-action].[paragraph break]Then [if Zoe is present]Zoe falls forward and pushes against the door and it[otherwise]your reflexes are not up for more so you let go and the door[end if] slams shut[if Zoe is present][note Zoe-pushes-door][end if][if zoe is present and together-playing has not occurred].[paragraph break]Lisa looks over the couch at you and at Zoe and she says, 'Awwww, they're playing together! How cute!'[otherwise].[end if]"

Instead of pushing or pulling the wood cabinet, say "It is pretty firmly on the ground right now."

Instead of pushing or pulling the wood cabinet when the folded paper is handled (this is the push the wood cabinet rule), consider the make the cabinet rock rule.

This is the make the cabinet rock rule: say "It goes rock rock rock now that there is no paper wedged under it[if the heavy tumbler is on the cabinet top] and the tumbler tilts like it might fall forward[end if]."

A thing called the cabinet door is part of the wood cabinet. The description is "The door to the cabinet is [if the wood cabinet is open]open.[otherwise]closed." Before opening the cabinet door: try opening the wood cabinet instead. Before closing the cabinet door: try closing the wood cabinet instead. Before pulling the cabinet door: try opening the wood cabinet instead.

The cabinet top is a supporter. The cabinet top is part of the wood cabinet. Instead of examining the cabinet top, try examining the wood cabinet.

The fake baby lock is scenery in By the Cabinet. The printed name is "baby lock". The description is "You cannot really see the baby lock when the cabinet door is closed but you know what it looks like, it is a plastic latch way up at the top of the door. If you push down on the plastic latch then you can open the door which is a laugh because you are way short and it is way high." Instead of doing something other than examining when the fake baby lock is acted upon, say "You can't see the baby lock with the cabinet door closed but even if you could it is way high and out of reach. Not even pulling up would let you reach it unless you pulled up and then stood there for a year while you grew taller." Understand "latch" as the fake baby lock.

Section 13 - Secret Space Behind the Footstool

Secret Space is a room. "There is not a lot of room back here behind the footstool, you fill most of the space between the footstool and the couch and this wood cabinet that is acting like an endtable." The printed name is "Secret Space by the Couch". The faraway descriptor is "tucked behind the footstool".

The hidden grey couch is a big couch in the Secret Space. Understand "gray" or "cushion" or "cushions" as the hidden grey couch. The description is "Grey and short and it is soft when the mom or the dad put you on it. There's not a lot of couch here on account of the footstool blocking you to the east." The printed name is "grey couch".

The hidden wood cabinet is scenery in Secret Space. Understand "endtable" or "table" as the hidden wood cabinet. The description is "It is the back of a wood cabinet that is also functioning as an endtable, I guess the parents are too cheap to buy one piece of furniture that is an endtable and another piece of furniture that is a cabinet." The printed name is "wood cabinet". Instead of tasting the hidden wood cabinet, say "It is waxy and maybe a bit lemony." Instead of opening the hidden wood cabinet, say "The door must be on the other side of it." Instead of putting something under the hidden wood cabinet, say "There is not really enough room under there for anything thicker than a piece of paper." Instead of putting the folded paper under the hidden wood cabinet, say "I guess it is really hard to put the paper under there because you can't get it to go under there again."

Instead of pushing or pulling the hidden wood cabinet, say "It is pretty firmly on the ground right now."

Instead of pushing or pulling the hidden wood cabinet when the folded paper is handled, consider the make the cabinet rock rule.

Instead of going nowhere from the Secret Space, say "Really there is only one way out of this teeny space and that is to pull up and cruise on out to the east."

The folded paper is a small thing in the Secret Space. "There is this tiny folded piece of paper shoved under the cabinet." The description is "It is a tiny folded piece of paper[if not handled and the location of the player is the location of the folded paper] that is all shoved under the wood cabinet[end if]." The printed name is "folded piece of paper". Understand "tiny" or "piece of" as the folded paper.

Instead of pulling or opening the folded paper when the folded paper is not handled, try taking the folded paper.

After taking the folded paper when the folded paper is not handled (this is the pull out the folded paper rule):
	say "You tug and tug and tug and then you see Parry's hand reach over you and remove the tumbler from on top of the cabinet where it was and pop! the folded paper comes out from under the wood cabinet and the cabinet goes rock rock rock. It is a good thing Parry was thirsty right now.";
	move the heavy tumbler to West End of Couch;
	move the coaster to the cabinet top;
	record 5 points for "making the cabinet unsteady";
	stop the action.

Rule for implicitly taking the folded paper when the folded paper is not handled:
	try taking the folded paper.

Instead of opening the folded paper, say "It is really folded and creased from being under the cabinet and your fingers are too stubby." Instead of closing the folded paper, say "It is already all folded."

Before going east in the Secret Space when the player is pulled up on the footstool or the player is pulled up on the hidden grey couch:
	say "You slide back out into the big wild world.";
	if the player is pulled up on the hidden grey couch, now the player is pulled up on the west grey couch;
	change the last location of the player to the location of the player;
	move the footstool to the West End of Couch;
	move the player to the West End of Couch;
	stop the action.

Section 14 - Regions

The Outer Ring is a region. The TKBUG Open Area, the TV Room, the West End of Couch, the Middle of Couch, and the East End of Couch are in the Outer Ring.

The Middle Ring is a region. By the Rocking Chair, the South End of Loveseat, and the Middle of the Living Room are in the Middle Ring.

The Play Area is a region. The North End of Loveseat, the Middle Ring, and the Outer Ring are in the Play Area.

The Hidden Area is a region. Behind the Loveseat and By the Cabinet are in the Hidden Area.

VOLUME 4 - On Other People

To decide if (kid - a person) is alone:
	if a playmate in the location of the kid is not the kid, decide no;
	decide yes.

To decide if (kid - a person) is not alone: if kid is alone, decide no;
	decide yes.

[When a kid wanders, there's a chance that they'll wander back towards each other.]
To have (kid - a person) wander:
	let position be the location of the kid;
	if the kid is alone and 2 out of 3 begin;
		let leader be a random playmate who is not the kid;
		let goal be the location of leader;
	otherwise;
		let goal be a random room which is adjacent to position;
	end if;
	let way be the best route from the position to the goal;
	if way is not nothing, try kid going way;

After printing the name of something which is gnawed by someone (called the chewer) while examining: say " (which [it-they of the chewer] [if the chewer is the player]are[otherwise][is-are][end if] totally chewing on)".

To say drop: say "[one of]get rid of[or]drop[or]let go of[purely at random]".

To say drops: say "[one of]gets rid of[or]drops[or]lets go of[purely at random]".

To say takes: say "[one of]takes[or]grabs[or]snatches[purely at random]".

Chapter 1 - Adults

Section 0 - Chatter Rules

[A hacky no-value table]

Table of Empty
MT
"-"

Semaphore is a kind of value. The semaphores are ready to act, busy acting, done acting, and not acting.

[For chatter status, "ready to act" means we'll allow the adults to talk, "busy acting" means the adults are talking, "done acting" means the adults are finished with their conversation, and "not acting" means the adults won't talk at all.]
The chatter turn count is a number that varies. The chatter turn count is 0. The chatter count is a number that varies. The chatter count is 1. The chatter list table is a table-name that varies. The chatter list table is the Table of Empty. The current chatter table is a table-name that varies. The current chatter table is the Table of Empty. The chatter status is a semaphore that varies. The chatter status is not acting.

To have adults chatter:
	if the chatter list table is the Table of Empty begin;
		report bug "Tried to have the adults chatter despite the chatter list table being Nothing.";
		change the chatter status to not acting;
		rule succeeds;
	end if;
	if the current chatter table is the Table of Empty begin;
		if the number of filled rows in the chatter list table is 0 begin;
			change the chatter status to not acting;
			rule succeeds;
		end if;
		choose a random row in the chatter list table;
		change the current chatter table to the convo entry;
		change the chatter count to 1;
		blank out the whole row;
	end if;
	choose row chatter count in the current chatter table;
	say "[chatter entry][note parent-convo][paragraph break]";
	increment chatter count by 1;
	if chatter count is greater than the number of rows in the current chatter table begin;
		change chatter status to done acting;
		change current chatter table to the Table of Empty;
	end if.

Every turn when the chatter status is ready to act (this is the start the chatter rule):
	increment the chatter turn count by 1;
	if the chatter turn count is greater than 10 plus a random number from 1 to 4 begin;
		change the chatter status to busy acting;
		change the chatter turn count to 0;
	end if.

Every turn when the chatter status is busy acting (this is the chatter control rule):
	have adults chatter;
	if the chatter status is done acting, change the chatter status to ready to act.

To restart chatter:
	change chatter status to ready to act;
	change the chatter turn count to 0;
	change current chatter table to the Table of Empty.

To cut chatter short:
	if chatter status is busy acting, change current chatter table to the Table of Empty;
	change the chatter status to not acting.

Section 1 - The Mom

The mom is a woman in the Intro Middle. "The mom[note the-mom] is playing with you and is all excited about it." The indefinite article is "the". The description is "[if Tabletime is happening]She is hovering in this 'oh I hope you will eat' kind of way[otherwise if Picturetime is happening]She is trying to herd babies to get a picture and that is like herding fish[otherwise]When the mom is all sitting down she does not tower so much over you but she is still pretty big[note relative-sizes][end if]." Understand "momma", "mama", "mommy", "leg", "legs", "parent", or "farina" as the mom.

Rule for writing a paragraph about the mom during Tabletime: say "The mom is hovering around you."

Rule for writing a paragraph about the mom during Picturetime: say "The mom is trying to get all of you together for a picture."

Rule for writing a paragraph about the mom: if Playtime is happening or Snacktime is happening or Costumetime is happening or Climaxtime is happening, say "Those are the mom's legs there by the couch."

Rule for writing a paragraph about the mom when the player is standing: if Playtime is happening or Snacktime is happening or Costumetime is happening or Climaxtime is happening, say "The mom is sitting on the couch."

Instead of giving or showing something to the mom, say "She is too busy with talking and adults and other stuff."

Instead of kissing the mom, say "You would but if you start loving on her spontaneously like this she will come to expect it and then that is one less emotional lever for you to use on her."

Instead of crying in the presence of the mom during Goading, say "You have tried that crying thing before to get out of doing stuff but it totally leads to worse things, so it is best to save the crying in case you really need it later."

Instead of laughing in the presence of the mom during Goading, say "You smile and then you laugh and the mom laughs and you laugh more and the mom laughs more and it is so easy to manipulate her."

Instead of giving or showing something to the mom during Goading:
	say "The mom takes [the noun] from you and says 'Good! But now try pulling up!' Then she drops [the noun] and hey, if she was going to do that, she should have let you keep [it-them]!";
	now the noun is in the location of the player.

After upstanding using the mom during Goading:
	say "You grab the mom's clothes and pull up and she laughs, 'That wasn't what I had in mind, but it'll do.' So you give her a real cute look and she laughs again.";

Instead of upstanding using the mom:
	if Goading is happening, continue the action;
	abide by the check upstanding using rulebook;
	say "The mom is distracted by everyone else being here so she just gently lowers you back to the floor instead of playing with you."

Section 2 - Marion Kinmundy

[Insert misogynist joke about women never being transparent here.]
Marion is a transparent woman in North End of Loveseat. The description is "Grr, Marion Kinmundy. She is Zoe's mom. Grr, Zoe. What is weird is that Marion is all nice while Zoe is pure evil." Understand "kinmundy", "parent", "leg" or "legs" as Marion. Marion is suitable for pulling up on.

Rule for writing a paragraph about Marion during Picturetime: say "Marion is calm in the face of all this chaos."

Rule for writing a paragraph about Marion during Playtime: say "The legs of Marion dangle from the loveseat."

Rule for writing a paragraph about Marion when the backpack is held by Marion during Playtime: say "Marion is on the loveseat with Zoe's backpack and your toy."

Rule for writing a paragraph about Marion when the player is standing during Playtime: say "Marion is sitting there on the loveseat."

Rule for writing a paragraph about Marion when Snacktime is happening or Costumetime is happening: say "The legs of Marion dangle from the sofa."

Rule for writing a paragraph about Marion when the player is standing and (Snacktime is happening or Picturetime is happening): say "Marion is sitting all comfortable on the couch."

Instead of upstanding using Marion:
	abide by the check upstanding using rulebook;
	say "Marion [one of]laughs[or]smiles at you[or]says 'You go play with the others now'[purely at random] and takes you and puts you back on the ground[if 1 out of 4] with a pat on your back[end if]."

Instead of upstanding using Marion when Marion encloses the backpack:
	abide by the check upstanding using rulebook;
	say "Marion says, 'Sorry, I've got my hands full with this backpack,' and gently lowers you back to the ground[if 1 out of 4] with a pat on your back[end if]."

Instead of Zoe upstanding using Marion:
	abide by the check upstanding using rulebook;
	if Zoe is present, say "Zoe pulls up on Marion and Marion gives her a quick hug before putting her back down."

Instead of a playmate (called p) upstanding using Marion:
	abide by the check upstanding using rulebook;
	if p is present, say "[P] pulls up on Marion and she [one of]laughs[or]smiles at [P][or]says 'You go play with the others now'[purely at random] and puts [it-them] back down[if 1 out of 4] with a pat on [its-their] back[end if]."

Instead of giving or showing something to Marion, say "She is too busy with talking and adults and other stuff."

Section 3 - Lisa Sandoval

Lisa is a woman in West End of Couch. The description is "Lisa Sandoval is okay, you guess, but she is all the time watching over Cassie and that is a full-time job." Understand "sandoval", "parent", "leg" or "legs" as Lisa. Lisa is suitable for pulling up on.

Rule for writing a paragraph about Lisa during Picturetime: say "Lisa is staying near Cassie in case she makes a break for it."

Rule for writing a paragraph about Lisa during Playtime: say "Lisa's legs are hanging off of the couch."

Rule for writing a paragraph about Lisa when the player is standing during Playtime: say "Lisa is sitting there on the couch."

Rule for writing a paragraph about Lisa during Snacktime: say "Lisa's legs are hanging off of the loveseat."

Rule for writing a paragraph about Lisa when the player is standing during Snacktime: say "Lisa is all lounging on the loveseat."

Rule for writing a paragraph about Lisa when Parry is puzzle-allowing during Snacktime: say "Lisa is cuddling Cassie and trying to calm her down."

Rule for writing a paragraph about Lisa during Costumetime: say "Lisa's legs are hanging off of the couch."

Rule for writing a paragraph about Lisa when the player is standing during Costumetime: say "There is Lisa sitting on the couch."

Instead of upstanding using Lisa:
	abide by the check upstanding using rulebook;
	say "You don't even get both hands on Lisa before she pushes you away."

Instead of Cassie upstanding using Lisa:
	abide by the check upstanding using rulebook;
	if Cassie is present, say "Cassie pulls up on Lisa who says, 'Oh, oh, Cassie, be careful' before putting her back down."

Instead of a playmate (called p) upstanding using Lisa:
	abide by the check upstanding using rulebook;
	if p is present, say "[P] tries to pull up on Lisa but [it-they] doesn't even get [its-their] hands on Lisa before she pushes [P] away."

Instead of giving or showing something to Lisa, say "She is too busy with talking and adults and other stuff."

Section 4 - Parry Mason

Parry is a transparent man. The description is "That is Parry Mason, he is Watson Mason's dad. Normally Watson's mom Shari is here but she must be somewhere else so that is why Parry is here[if Parry encloses the backpack]. Right now he has the backpack and your favorite toy in his lap[end if]." Understand "mason", "leg", "legs", "parent", "foot" or "feet" as Parry. Parry is suitable for pulling up on. Parry can be puzzle-blocking or puzzle-allowing. Parry is puzzle-blocking.

Rule for writing a paragraph about Parry during Playtime:
	say "Parry's feet ";
	if someone (called the victim) is held by Parry, say "are flat on the floor and he is not rocking right now because he is holding [the victim] all gingerly.";
	otherwise say "keep leaving the floor as he rocks."

Rule for writing a paragraph about Parry when the player is standing during Playtime:
	say "Parry ";
	if someone (called the victim) is held by Parry, say "sits still in the rocking chair and holds [the victim] all carefully as if [it-they of the victim] might explode.";
	otherwise say "rocks slowly in the rocking chair."

Rule for writing a paragraph about Parry when the player is held by Parry during Playtime:
	say "Parry holds you gingerly high above the floor, wow you are tall."

Rule for writing a paragraph about Parry when Parry encloses the backpack during Playtime:
	say "Parry is trying to get the white onesie free of the backpack and he has your favorite toy in his lap."

Rule for writing a paragraph about Parry during Snacktime: say "You can see Parry or at least his legs over by the couch."

Rule for writing a paragraph about Parry when the player is standing during Snacktime: say "There is Parry on the couch."

Rule for writing a paragraph about Parry when Parry is puzzle-allowing during Snacktime: say "Parry is hovering over Lisa and Cassie and trying to be all helpful."

Rule for writing a paragraph about Parry during Picturetime: say "Parry is standing back a bit watching the picturetime commotion."

Rule for writing a paragraph about Parry during Costumetime: say "Those are Parry's feet dangling from the loveseat."

Rule for writing a paragraph about Parry when the player is standing during Costumetime: say "There is Parry on the loveseat."

Instead of upstanding using Parry:
	abide by the check upstanding using rulebook;
	say "Parry lets you get to your feet and then he puts you back on the floor and looks a bit guilty."

Instead of an actor upstanding using Parry during Playtime: try the actor upstanding using the gliding rocking chair.

Instead of Watson upstanding using Parry:
	abide by the check upstanding using rulebook;
	if Watson is present, say "Watson pulls up on Parry and Parry smiles down at Watson before reluctantly putting him back on the floor."

Instead of a playmate (called p) upstanding using Parry:
	abide by the check upstanding using rulebook;
	if p is present, say "[P] pulls up on Parry who puts [it-them] back down but looks guilty about it[if 1 out of 3], he is a big old pushover[end if]."

Instead of giving or showing something to Parry, say "He is too busy with talking and adults and other stuff."

[Change how things Parry carries are listed when he's holding the player.]
[Taken from http://groups.google.com/group/rec.arts.int-fiction/msg/2e13119bd6ea5580?dmode=source]
Rule for listing nondescript items of Parry when the player is enclosed by Parry:
        say "Parry has [a list of marked for listing things which are carried by Parry] in his lap."

Carry out stopping chewing when the player is held by Parry during Playtime: move the noun to Parry.
Report stopping chewing when the player is held by Parry during Playtime: instead say "You spit out [the noun] with a 'pbuh' sound and since you are held by Parry it falls in his lap."

Carry out dropping when the player is held by Parry during Playtime: move the noun to Parry.
[This next rule is here solely because Jota tried filling Parry's lap with stuff.]
After dropping when the player is held by Parry during Playtime:
	let flag be 0;
	repeat with p running through the things which are carried by Parry begin;
		if p is not the noun and p is not the player begin;
			if flag is 0 begin;
				change flag to 1;
				say "You drop [the noun] into Parry's lap and he absentmindedly brushes off [the p]";
			otherwise;
				say " and [the p]";
			end if;
			move p to the location of Parry;
		end if;
	end repeat;
	if flag is 1 begin;
		say " onto the floor.";
		stop the action;
	end if;
	continue the action.
Report dropping when the player is held by Parry during Playtime: instead say "[The noun] falls into Parry's lap."

Instead of showing or giving something to Parry when the player is held by Parry during Playtime: say "You show [the noun] to Parry who is kinda distracted and so [it-they] fall[s] into Parry's lap."; move the noun to Parry; if the player is gnawing the noun, now the noun is not gnawed by anything.

Instead of going down or crying or wriggling or kicking or exiting when the player is held by Parry during Playtime: say "You wriggle and fuss a bit and [run paragraph on]"; make Parry put the player down.

The paper coaster is a small thing. The description is "A light spongy paper coaster for soaking up liquid from glasses." Understand "light" or "spongy" or "round" as the coaster.

Instead of an actor chewing the paper coaster:
	if the actor is the player begin;
		say "You shove the coaster into your mouth and it may be absorbent but it is no match for your drool. It disintegrates!";
	otherwise if the actor is present;
		say "[The actor] starts chewing on the paper coaster but its absorbency is no match for baby drool and it disintegrates!";
	end if;
	remove the paper coaster from play.

Instead of eating the paper coaster, try chewing the paper coaster.

The heavy tumbler is a container. The description is "The tumbler is thick glass and it holds water, it is left over from Parry snacking[if the tumbler is not on the cabinet top]. I guess Parry took it off the cabinet to take a drink from it and then forgot to put it back on the cabinet[otherwise]. It is way up on the cabinet[end if][if some thing is in the heavy tumbler]. Floating in the water you see [the contents of the heavy tumbler][end if]." Understand "water" or "thick" or "glass" or "cup" as the heavy tumbler. Rule for printing room description details of the heavy tumbler: stop.

To decide if the heavy tumbler is interactable: if Parry is puzzle-allowing and the tumbler is not on the cabinet top, decide yes; decide no.

Mentioning no mouth is a one-time deal.

To block messing around with the tumbler: say "You move towards the tumbler but Parry picks it up and says 'No, that's my glass[if mentioning no mouth has not occurred].' And then the mom chimes in, 'Hey, kiddo, no mouth! No mouth[end if].' Once you back off Parry puts the tumbler back down."

Before doing something other than examining when the heavy tumbler is acted upon and Parry is puzzle-blocking and the tumbler is not on the cabinet top (this is the Parry protects the tumbler rule), instead block messing around with the tumbler.

Instead of taking the heavy tumbler when the heavy tumbler is interactable, say "There is no way you're lifting it, it is really heavy."

Instead of pushing or pulling the heavy tumbler when the heavy tumbler is interactable, say "That thing is so thick on the bottom it is like eighty pounds and doesn't tip at all, the adults must all be weightlifters."

Before pushing the heavy tumbler to a direction when the heavy tumbler is interactable, instead say "That thing is so thick on the bottom it is like eighty pounds and doesn't tip at all, the adults must all be weightlifters."

Instead of inserting a small thing into the heavy tumbler when the heavy tumbler is interactable and the player is holding the noun:
	if the noun is the velcro jangly balls, instead say "Those velcro jangly balls are too useful to shove in there and probably lose them.";
	say "You shove [the noun] into the tumbler and it goes ploop into the water[if the noun is the paper coaster] and luckily enough it holds together instead of disintegrating[end if].";
	if the player is gnawing the noun, now the noun is not gnawed by anything;
	move the noun to the tumbler.

Instead of inserting something into the heavy tumbler when the heavy tumbler is interactable, say "[The noun] won't fit inside it."

Instead of inserting something into the heavy tumbler when the heavy tumbler is on a supporter and the player is standing, say "That would take more coordination than you have, you are no Kurt Thomas."

Instead of removing something from the heavy tumbler when the heavy tumbler is interactable, say "Now that [the noun] is wet you can't really get hold of it."

Instead of taking something that is in the heavy tumbler when the heavy tumbler is interactable, say "Now that [the noun] is wet you can't really get hold of it."

[Eating requires you take the tumbler, which won't happen, so I re-route using this before rule]
Before eating the heavy tumbler when the heavy tumbler is interactable and the player is not gnawing something, instead try drinking the heavy tumbler.

Instead of drinking or tasting or chewing or spitting up on the heavy tumbler when the heavy tumbler is interactable and the player is not gnawing something:
	say "You put your mouth on the edge of the glass but there is no way you will tip it enough to drink but at least you slobbered all over it.";
	now the tumbler is damp.

Section 5 - Becca Thorsby

Becca is a woman. The description is "Becca is Jemison's mom, she is as loud as Jemison is quiet and that is very loud. Or very quiet. Anyway, that is Becca." Understand "thorsby", "pants", "parent", "leg" or "legs" as Becca.

Rule for writing a paragraph about Becca during Playtime: say "Those are Becca's legs by the loveseat."

Rule for writing a paragraph about Becca during Picturetime: say "Becca is all ordering everyone around."

Rule for writing a paragraph about Becca when the player is standing during Playtime: say "Becca is sitting on the loveseat."

Rule for writing a paragraph about Becca:
	if Snacktime is happening or Costumetime is happening or Climaxtime is happening, say "There are Becca's legs, they are by the rocking chair."

Rule for writing a paragraph about Becca when the player is standing:
	if Snacktime is happening or Costumetime is happening or Climaxtime is happening, say "Becca is sitting on the rocking chair though she is not rocking."

Instead of upstanding using Becca the first time: if the player is wearing the squid costume, continue the action; abide by the check upstanding using rulebook; say "Becca laughs this booming laugh and says 'Whoa, Farina, have you ever got a climber here!' and then she puts you back on the floor." Instead of upstanding using Becca: abide by the check upstanding using rulebook; say "Becca says 'Careful with the fabric! These are new pants!' and she laughs and puts you back on the floor."

Instead of Jemison upstanding using Becca:
	abide by the check upstanding using rulebook;
	if Jemison is present, say "Jemison pulls up on Becca who rubs Jemison's fuzz of hair and laughs and puts him back down on the floor."

Instead of a playmate (called p) upstanding using Becca:
	abide by the check upstanding using rulebook;
	if p is present, say "[P] pulls up on Becca who says 'Watch the pants, kid!' and she laughs and puts [it-them] back on the floor.";

Instead of giving or showing something to Becca, say "She is too busy with talking and adults and other stuff."

Chapter 2 - Playmates

[Since babies are essentially state machines in real life, we model them that way, too. The state machines are contained in rulebooks, with an every turn rule functioning as dispatch. The babies' active/passive state controls whether or not their state machine runs.]

A playmate has a number called act percentage. A playmate has a number called act increment. A playmate has a number called base act percentage. A playmate has a semaphore.

[Set babies back to their ground state -- no gnawing, fascination, or pulling up on, and no inventory]
To set the kids to a ground state:
	[There are "now..." statements that can handle this, such as "now every thing is not gnawed by anything" and "now every thing fascinates nothing". But that loops through every thing in the game, so no thanks, not when we can just explicitly loop through playmates.]
	repeat with k running through playmates begin;
		if k is fixated begin;
			let f be a random thing that fascinates k;
			now f does not fascinate k;
		end if;
		if k is gnawing something (called g), now g is not gnawed by anything;
	end repeat;
	now every thing that is carried by a playmate is in the TKBUG Open Area;
	now every thing that is carried by the player is in the TKBUG Open Area;
	now every playmate is not pulled up on anything;
	if the player is gnawing something (called g), now g is not gnawed by anything;
	if the player is standing, now the player is not pulled up on anything.

[I keep track of the last time a playmate moves. In theory I could skip this and use an "if Cassie has been in the location of Cassie for <x> turns" rule, but that doesn't work properly.]
A playmate has a time called last wander time. A person has a room called watched location.

Every turn:
	repeat with toddler running through playmates begin;
		if location of toddler is not the watched location of toddler begin;
			now watched location of the toddler is the location of toddler;
			change the last wander time of toddler to the time of day;
		end if;
	end repeat.

To decide if (youngster - a playmate) is ready to leave:
	if the time of day is after one minute after the last wander time of the youngster, decide yes;
	decide no.

To reset (youngster - a playmate): now the act percentage of the youngster is the base act percentage of the youngster.

To decide if (youngster - a playmate) acts with a bonus of (B - a number):
	if youngster is not ready to act, decide no;
	let probability be B plus the act percentage of the youngster;
	if a random chance of the probability in 100 succeeds begin;
		reset the youngster;
		decide yes;
	end if;
	now the youngster is not acting;
	increase the act percentage of the youngster by the act increment of the youngster;
	decide no.

To decide if (youngster - a playmate) acts:
	if youngster acts with a bonus of 0, decide yes;
	decide no.

To decide if (youngster - a person) clears all inventory:
	now every thing is unmarked for special listing;
	while youngster is carrying something (called the troublesome item) begin;
		silently try youngster dropping the troublesome item;
		if youngster is carrying the troublesome item, decide no;
		now the troublesome item is marked for special listing;
	end while;
	decide yes.

Table of Baby State Machines
Baby Name	Machine
Zoe	ZK fussing rules
Cassie	CS hint-giving rules
Watson	WM hint-giving rules
Jemison	JT state rules

An every turn rule (this is the babies doing stuff rule):
	[In theory this could be "...running through active ready to act playmates" but their status might change mid-loop if the kid acts during another kid's turn.]
	repeat with npc running through active playmates begin;
		if npc is ready to act and the npc acts begin;
			now the npc is busy acting;
			consider the machine corresponding to a baby name of npc in the Table of Baby State Machines;
			now the npc is done acting;
		end if;
	end repeat;

The last every turn rule (this is the reset all babies rule):
	now every playmate is ready to act;

To decide if (youngster - a person) announces a report in (t - a table-name):
	if the youngster is a baby name listed in t begin;
		consider the report rulename entry;
		decide yes;
	end if;
	decide no.

Table of Taking Report Rules
baby name	report rulename
Cassie	CS taking report rule
Watson	WM taking report rule
Jemison	JT taking report rule
Zoe	ZK taking report rule

Report a playmate (called the youngster) taking something:
	if the youngster is present and the youngster announces a report in the table of Taking Report Rules, say paragraph break;
	stop the action.

Table of Dropping Report Rules
baby name	report rulename
Cassie	CS dropping report rule
Watson	WM dropping report rule
Zoe	ZK playtime dropping report rule

Report a playmate (called the youngster) dropping something:
	if the youngster is present and the youngster announces a report in the table of Dropping Report Rules, say paragraph break;
	stop the action.

A playmate has a direction called the direction tried. Before a playmate (called the youngster) going a direction (called goal), change the direction tried of youngster to goal.

Table of Going Report Rules
baby name	report rulename
Cassie	CS going report rule
Watson	WM going report rule
Zoe	ZK going report rule

Report a playmate (called the youngster) going a direction (called goal):
	if the youngster announces a report in the table of Going Report Rules begin;
		if the youngster is present or the youngster was recently present, say paragraph break;
	end if;
	stop the action.

Table of Sitting Down Report Rules
baby name	report rulename
Cassie	CS sitting down report rule

Report a playmate (called the youngster) sitting down:
	if the youngster is present and the youngster announces a report in the table of Sitting Down Report Rules, say paragraph break;
	stop the action.

Section 1 - Zoe Kinmundy

Zoe is a playmate in the By the Rocking Chair. Zoe is female. The description is "Grr, Zoe Kinmundy. That little red-haired girl[note red-haired girl] has this [if zoe is wearing the ladybug costume]ladybug costume on, it is supposed to be cute but you are not fooled[otherwise]sundress-like jumper thing, it's not like you know fashion or anything so you're not sure exactly what it is but it leaves her arms and legs bare for speed and evil[end if]. Her red hair is in pigtails, like that will fool anyone into thinking she is nice[if Zoe is carrying something]. She has a death-grip on [a list of things carried by Zoe][end if]." The base act percentage is 20. The act percentage is 20. The act increment is 10. Understand "Kinmundy", "little", "red-haired", "girl", "baby", "pigtail" or "pigtails" as Zoe.

Check smelling Zoe: instead say "You would think she would smell all evil but instead she smells like baby wash."

The fog of grump is scenery in By the Rocking Chair. The description is "Boy, Zoe is grumpy." Instead of doing something other than examining to the fog of grump, say "The fog of grump is more a metaphor than a real thing you can do anything with."

Zoe wears a sundress-like jumper. The description of the sundress-like jumper is "All yellow and it has flowers." Understand "sundress" as the sundress-like jumper.

Rule for pre-writing a paragraph about Zoe when Zoe is carrying your favorite toy: say "Zoe has your toy! ZOE HAS YOUR TOY! [run paragraph on]";

Rule for pre-writing a paragraph about Zoe when Zoe is carrying something (called the item):
	say "Zoe has hold of [a item] [one of]all sadly[or]but she looks like she doesn't like [it-them][or]loosely[purely at random]. [run paragraph on]";

Rule for pre-writing a paragraph about Zoe during Playtime:
	say "Zoe is sitting in a fog of grump on the floor. [run paragraph on]";

Rule for pre-writing a paragraph about Zoe during Snacktime:
	say "Zoe has her eye on you[if 1 out of 8] (not literally)[end if]. [run paragraph on]";

Rule for pre-writing a paragraph about Zoe during Tabletime: rule succeeds.

Rule for pre-writing a paragraph about Zoe when Zoe is wearing the ladybug costume: say "Zoe hunkers in a ladybug costume like a wolf in cute sheep's clothing. [run paragraph on]";

To deal with Zoe and the backpack:
	move the backpack to Parry;
	move your favorite toy to Parry;
	if Parry is present begin;
		say "'Parry, here,' and shoves the backpack at Parry";
		if someone (called the kid) is held by Parry, say ", who says 'Oh!' and puts down [the kid] and takes the backpack";
		otherwise say " who takes it";
		say ". 'Can you get that onesie out of the way?' Parry just nods and digs in the backpack and pulls out your favorite toy and dumps it in his lap! Then he starts tugging the onesie this way and that to free it from the implacable jaws of the backpack zipper. Meanwhile Marion shushes Zoe before sitting back on the loveseat[note parry-opens-backpack].";
	end if;
	if getting points for Parry removing the toy has not occurred, record 5 points for "getting Parry to take the toy out of the backpack";
	if a person (called the kid) is held by Parry, move the kid to the location of the gliding rocking chair, without printing a room description;
	Parry looses the wild onesie in four turns from now.

Instead of taking something held by Zoe during Playtime:
	say "Zoe shrieks at you and yanks back [the noun]. Marion sees that and she reacts in a flash and says ";
	if Marion encloses the backpack, deal with Zoe and the backpack;
	otherwise say "'No, Zoe, no!' and stops doing whatever parent thing she was doing and jumps up and pulls Zoe away from you. 'I'm sorry, Farina,' she tells the mom."

[Like the tearing-yourself-away-from-Cheerios message, I only want the following message to fire once, so I use a global flag for that.]

The Zoe-cant-flee-message-flag is a number that varies.

An every turn rule: change the Zoe-cant-flee-message-flag to 0.

[There is a bug here that I'm not going to bother fixing: this message fires even if you try a command like >DROP TOY when you don't have the toy.]
Before doing something when your favorite toy is held by Zoe and your favorite toy is acted upon (this is the Zoe tries to flee rule):
	if Zoe flees from the player begin;
		say "Zoe sees you coming for the toy! She will not let you have it!";
		have Zoe flee from the player;
		stop the action;
	otherwise if the Zoe-cant-flee-message-flag is 0;
		change the Zoe-cant-flee-message-flag to 1;
		say "Zoe tries to crawl away, but there are babies and Cheerios and it is a big train-wreck and she cannot get away!";
	end if.

The Zoe tries to flee rule is listed after the Cheerios message rule in the Before rules.

Instead of taking your favorite toy when your favorite toy is held by Zoe (this is the snatch the toy from Zoe rule):
	[Run through some of the check taking rules. We can't use the entire rulebook, since the can't take people's possessions rule would kick in, and besides, we only care about the size rules]
	abide by the can't take small things when burdened rule;
	say "You lunge forward and yank on the toy and Zoe is all distracted by Cheerios and the toy pops out of her evil sweaty grasp! You have your toy!";
	record 10 points for "getting your favorite toy for the last time";
	[Normally I would move the toy to the player and trigger the scene end on that, but then you can do like Admiral Jota did and do things like GIVE TOY TO CASSIE that trigger an implicit TAKE. By removing the toy from play, the implicit take stops short.]
	remove your favorite toy from play.

[Get rid of the (first taking your toy) bit from an implicit take of your favorite toy.]
Rule for implicitly taking your favorite toy when your favorite toy is held by Zoe:
	instead try silently taking the noun.

Instead of attacking or pushing or pulling or chewing Zoe, say "There is no way the parents will believe you had nothing to do with hurting her, you'd better leave her alone for now."

Instead of attacking or pushing or pulling or chewing Zoe when your favorite toy is held by Zoe:
	if the pile of Cheerios is present, instead say "Then she would cry and the parents would see you and you would look like the bad baby, you should just grab the toy instead.";
	say "Zoe sees you coming and she runs like the toyful coward she is!";
	have Zoe flee from the player;

Instead of attacking or chewing Zoe when Marion encloses the backpack:
	say "You wait until no one is looking and then attack Zoe, it is a little hand-to-hand attack that even a level 1 wizard could do better. Zoe shrieks and hits back and of course Marion sees that and she reacts in a flash and says ";
	deal with Zoe and the backpack.

Instead of pushing or pulling Zoe when Marion encloses the backpack:
	say "You give Zoe this hard shove when no one is looking and Zoe shrieks and hits back. Marion sees that and she reacts in a flash and says ";
	deal with Zoe and the backpack.

Instead of showing or giving something to a playmate (called p) when the player is held by Parry during Playtime (this is the player is too high to show things to people rule):
	say "Parry has you so high that [p] can't see [the noun]."

Instead of showing or giving something to Zoe during Playtime:
	move the noun to the location;
	if the player is gnawing the noun, now the noun is not gnawed by anything;
	say "Zoe shrieks at you and shoves [the noun] out of your hands and hits at you. Marion sees that and she reacts in a flash and says ";
	if Marion encloses the backpack, deal with Zoe and the backpack;
	otherwise say "'No, Zoe, no!' and stops doing whatever parent thing she was doing and jumps up and pulls Zoe away from you. 'I'm sorry, Farina,' she tells the mom."

Instead of someone showing or giving something to Zoe during Playtime:
	if Zoe is present, say "[The person asked] offers [the noun] to Zoe and Zoe shrieks and shoves and kicks at [the noun] and at [the person asked][note baby-state-machines]. Marion sees that and she reacts in a flash and says, ";
	if Marion encloses the backpack begin;
		deal with Zoe and the backpack;
	otherwise if Zoe is present;
		say "'No, Zoe, no!' and stops doing whatever parent thing she was doing and jumps up and pulls Zoe away from [the person asked]. 'Sorry about Zoe's behavior,' she says all apologetically.";
	end if.

Instead of showing or giving the poosh stick to Zoe during Cooperationtime, say "Zoe sniffs, it is clear she has no interest in doing any pooshing with the stick."

Instead of showing or giving something to Zoe, say "Zoe eyes you coldly, no way she is taking anything from you[if Dejectiontime is happening] even if she is all dejected[end if]."

Instead of someone showing or giving something to Zoe:
	if Zoe is present, say "[The person asked] tries to give [the noun] to Zoe but she will have none of it[if 1 out of 6], she is so unfriendly[end if]."

This is the ZK taking report rule:
	say "Zoe [one of]snatches[or]grabs[or]totally takes[purely at random] [the noun]. [run paragraph on]";

This is the ZK playtime dropping report rule:
	let switch-value be a random number from 1 to 3;
	if switch-value is 1, say "Zoe drops [the noun] and [one of]shoves[or]pushes[purely at random] [it-them] away from her";
	if switch-value is 2, say "Zoe tosses [the noun] aside";
	if switch-value is 3, say "Zoe takes [the noun] and [one of]tosses [it-them] aside[or]shoves [it-them] away[or]drops [it-them][purely at random]";
	say ". [run paragraph on]".

This is the ZK dropping report rule:
	let switch-value be a random number from 1 to 3;
	if switch-value is 1, say "Zoe drops [the noun] like it is on fire";
	if switch-value is 2, say "Zoe lets [the noun] fall from her hands";
	if switch-value is 3, say "Zoe tosses [the noun] aside";
	say ". [run paragraph on]".

This is the ZK going report rule:
	if Zoe is present, say "[Zoe possibly making noise when moving]Zoe stalks in from [the opposite of the direction tried of Zoe] and settles near you. [run paragraph on]";
	if Zoe was recently present, say "[Zoe possibly making noise when moving]Zoe [if the just-released item is not nothing]lets go of [the just-released item] and [end if]stalks off to [the direction tried of Zoe]. [run paragraph on]".

This is the ZK fleeing report rule:
	if Zoe is present, say "Zoe comes in from [the opposite of the direction tried of Zoe][if Zoe carries your favorite toy] with your toy[end if]! [run paragraph on]";
	if Zoe was recently present, say "Zoe [if the last location of Zoe is the location of the pile of Cheerios]somehow ignores the Cheerios and [end if][if a not-Zoe-baby playmate is in the last location of Zoe]pushes her way past babies [end if][if the last location of Zoe is the location of the pile of Cheerios and a not-Zoe-baby playmate is in the last location of Zoe]who don't move very fast because of the Cheerios [end if][if a not-Zoe-baby playmate is in the last location of Zoe]and [end if]runs off to [the direction tried of Zoe][if Zoe carries your favorite toy] with your toy[end if]! [if 1 out of 4]SHE HAS YOUR TOY! [end if][run paragraph on]".

[There is this subtil bug in I7 where, if you push one of the blocks around and Zoe stalks after you, the following rule gets triggered. So I hack around the report by not having it fire during Snacktime.]
After Zoe going a direction (called way-pooshed) with something (called the pooshed-thing) when Snacktime is not happening (this is the Zoe pushing things to a new room rule):
	if Zoe is present, say "You see [the pooshed-thing] coming towards you from [the opposite of way-pooshed], it is alive! But no, it is just Zoe pushing it around.";
	if Zoe was recently present, say "Zoe pushes and pushes at [the pooshed-thing] until it and she goes off to [the way-pooshed]."

[Have Zoe stalk the player during Snacktime]
After going a direction during Snacktime (this is the Zoe stalks the player rule):
	consider the report going rules;
	consider the stalking Zoe rule;

After going a direction with something during Snacktime (this is the second Zoe stalks the player rule):
	consider the stalking Zoe rule;
	continue the action.

The second Zoe stalks the player rule is listed after the pushing things to a new room rule in the after rulebook.

[This next rule in part duplicates the actions of the cruise the player rule. Keep them synchronized.]
Instead of going a direction (called goal) when the player is pulled up on something (called support) during Snacktime (this is the third Zoe stalks the player rule):
	if going goal while holding onto support involves cruising begin;
		let furnishing be the other end of support to the goal;
		cruise the player to the goal and hold the furnishing;
		consider the stalking Zoe rule;
	otherwise;
		continue the action;
	end if.

This is the stalking Zoe rule:
	let flag be 0;
	if the location is in the Middle Ring and the location of Zoe is in the Outer Ring, change flag to 1;
	if the location is the North End of Loveseat begin;
		if the location of Zoe is in the Outer Ring or the location of Zoe is in the Middle Ring, change flag to 1;
	end if;
	if the location is in the Hidden Area and the location of Zoe is not in the Hidden Area, change flag to 1;
	if the location is By the Cabinet and the location of Zoe is not By the Cabinet, change flag to 1;
	if flag is 1 begin;
		if Zoe clears all inventory begin;
			if Zoe is present and some thing is marked for special listing, say "Zoe tosses [a list of things that are marked for special listing] aside.";
			let way be the best route from the location of Zoe to the location;
			if way is a direction, try Zoe going way;
		end if;
		now every thing is unmarked for special listing;
	end if.

ZK Fussing Rules is a rulebook.

A ZK fussing rule when Zoe is carrying anything (this is the Zoe fusses with inventory rule):
	if 3 out of 4 begin;
		if Zoe is not present, rule succeeds;
		let target be a random thing carried by Zoe;
		let switch-value be a random number from 1 to 10;
		if target is chewable and switch-value is less than 4, say "Zoe [one of]gives [the target] a brief chew[if 1 out of 3] or two[end if][if 2 out of 7] before spitting it out with a 'buh' noise[end if][or]puts [the target] in her mouth and gnaws [it-them] some but then she lets [it-them] fall out of her mouth[purely at random].";
		otherwise if switch-value is less than 6, say "Zoe [one of]looks[or]stares[purely at random] at [the target] and she sighs.";
		if switch-value is greater than 5 begin;
			if 1 out of 2, say "Zoe pulls at [the target] but then she stops[if 1 out of 4], she is so bored with [it-them][end if].";
			otherwise say "Zoe [one of]shakes [the target][or]takes [the target] and shakes [it-them][purely at random] half-heartedly[if 1 out of 4], she is so bored with [it-them][end if].";
		end if;
		rule succeeds;
	end if.

A ZK fussing rule when Zoe is carrying anything (this is the Zoe disgustedly drops inventory rule):
	try Zoe dropping a random thing carried by Zoe;
	rule succeeds.

Fuss count is a number that varies. Fuss count is 0.

To say Marion asking Parry to stop rocking:
		let switch-value be a random number from 1 to 3;
		if switch-value is 1, say ". That makes Marion [one of]sigh[or]laugh[or]smile[purely at random] and she says, 'Parry, if you don't mind?' So";
		if switch-value is 2, say ". Marion says 'Okay, Zoe, okay' and laughs and says 'Excuse me, Parry, I need to get a toy from Zoe's backpack. Again.' So";
		if switch-value is 3, say ". 'I know, Zoe,' Marion says[if 1 out of 5], she is laughing as she says it[end if]. So";
		say " [if Parry is rocking]Parry stops rocking long enough for Marion to reach[otherwise]Marion reaches[end if] the backpack under the rocking chair ".

Getting points for Parry removing the toy is a one-time deal.

Mentioning not-fave-toyness is a one-time deal.

A ZK fussing rule (this is the Zoe grumbling rule):
	if Zoe is present begin;
		if the backpack is open and the backpack is in By the Rocking Chair and Parry does not enclose the backpack begin;
			say "Zoe shrieks angrily and bounces up and down and flails her arms in the general direction of the backpack that you opened";
			increase fuss count by 4;
		otherwise;
			let switch-value be a random number from 1 to 3;
			if switch-value is 1, say "Zoe makes this sort of [one of]coughing[or]grumping[purely at random] [one of]hiccuping[or]grunting[purely at random] half-[one of]sob[or]cry[purely at random][if 1 out of 5], it is so annoying[end if]";
			if switch-value is 2, say "Zoe clenches her tiny fists and [one of]waves them while complaining[or]complains loudly[purely at random] about life";
			if switch-value is 3, say "Zoe [one of]scrunches[or]screws[purely at random] up her face and bursts into wails[if 1 out of 8], those are so baby crocodile tears[end if]";
		end if;
	end if;
	increment fuss count by 1;
	if fuss count is greater than 2 plus a random number from 1 to 2 begin;
		now fuss count is 0;
		remove the plain white onesie from play; [so Marion can't grab it]
		let item be a random thing in the backpack;
		let item-in-backpack-flag be 1;
		[If the backpack is empty for some reason, have Marion grab something else instead. This shouldn't happen, but apparently some times it does.]
		if item is nothing begin;
			repeat through the Table of Stuff Originally In the Backpack begin;
				if the stuff entry is not carried begin;
					change item to the stuff entry;
					change item-in-backpack-flag to 0;
					stop repeating;
				end if;
			end repeat;
		end if;
		move the item to Zoe;
		if the item is not carried by Zoe begin;
			change hacky global item to the item;
			report bug "Tried to move [the hacky global item] to Zoe but failed for some reason.";
		end if;
		if the backpack is open and the plain white onesie is stuck begin;
			[Put the white onesie back]
			move the plain white onesie to the backpack;
			move the backpack to Marion;
			now the backpack is handled;
			if Zoe is present begin;
				say Marion asking Parry to stop rocking;
				say "and she sees the white onesie stuck in the backpack. 'Geez,' she says and [if item-in-backpack-flag is 0]grabs [a item] from the floor[otherwise]pulls out [a item][end if] and dumps it in Zoe's lap. Zoe gets all upset but Marion says, 'Shh, Zoe, be calm, I can't fix the backpack if you're fussing.' Parry says, 'Here, I'll empty out the backpack and fix that for you' but Marion says 'No, thanks,' and she sits down and starts trying to get the onesie out of the backpack";
			otherwise;
				say "You can hear Zoe squalling all loudly by the gliding rocking chair";
				if Marion is present, say " and Marion stands up and in a moment comes back with the backpack";
			end if;
			Marion almost removes the toy in one turn from now;
		otherwise;
			if Zoe is present begin;
				say Marion asking Parry to stop rocking;
				say "and then she digs around and [if item-in-backpack-flag is 0]there's nothing in there so she grabs [a item] from the floor[otherwise]pulls out [a item][end if] and closes the backpack and she gives [the item] to Zoe[if 1 out of 4] and says 'Here you go!' [otherwise]. [end if]Then Zoe [if mentioning not-fave-toyness has not occurred]sees that it's not your favorite toy, ha ha to her, and she [end if][one of]squalls[or]shrieks[or]fusses[purely at random] and tries to shove [the item] away but Marion says, 'Shh, Zoe, be calm,' and makes Zoe take [it-them of the item]";
			otherwise;
				say "You can hear Zoe squalling all loudly by the gliding rocking chair";
				if Marion is present, say " and Marion stands up and goes to quiet her and then comes back in a second";
				say ".";
			end if;
		end if;
		if no thing is in the backpack begin;
			if Zoe is present, say ".[paragraph break]Marion also spends some time grabbing a bunch of discarded toys and throwing them back in the backpack[run paragraph on]";
			repeat through the Table of Stuff Originally In the Backpack begin;
				if the stuff entry is not carried, move the stuff entry to the backpack;
			end repeat;
		end if;
		if the backpack is open and the backpack is not held by Marion, now the backpack is closed;
	end if;
	if Zoe is present, say ".";
	rule succeeds.

ZK Stalking Rules is a rulebook.

A ZK stalking rule when Zoe is carrying some thing (this is the Zoe dumping inventory rule):
	try Zoe dropping a random thing carried by Zoe;
	rule succeeds.

A ZK stalking rule (this is the Zoe picking up stuff rule):
	if 3 out of 5 begin;
		let position be the location of Zoe;
		if a pickupable thing is in position begin;
			let target be a random pickupable thing in position;
			try Zoe taking target;
			rule succeeds;
		end if;
	end if.

A ZK stalking rule (this is the Zoe wanders rule):
	if Zoe is ready to leave begin;
		let position be the location of Zoe;
		if the location is By the Cabinet, rule fails;
		if the location is in the Hidden Area, rule fails;
		if the location is the North End of Loveseat begin;
			if position is By the Cabinet begin;
				try zoe going north;
			otherwise;
				let way be the best route from position to By the Cabinet;
				if way is a direction, try zoe going way;
			end if;
			rule succeeds;
		end if;
		if the location is in the Middle Ring begin;
			[This is a compromise solution that I'm not really happy with]
			let goal be a random room which is adjacent to position;
			while goal is in the Outer Ring begin;
				now goal is a random room which is adjacent to position;
			end while;
			if goal is not position begin;
				let aim be the best route from the location of Zoe to goal;
				if aim is a direction, try zoe going aim;
			end if;
			rule succeeds;
		end if;
		have Zoe wander;
		rule succeeds;
	end if.

[A rulebook to get Zoe to push the blocks into the TKBUG Open Area, and rules to keep the player from messing with blocks when Zoe's around]

Instead of going a direction with a pushable between rooms enterable supporter (called the platform) when Zoe is present and (your favorite toy is in the bookcase or Cooperationtime is happening):
	if Zoe is zeroth-height begin;
		say "You start pushing on [the platform] but then Zoe slithers to the other side and keeps you from moving it.";
	otherwise if Zoe is on the platform;
		say "Zoe just looks at you with this satisfied smile since you cannot push [the platform] while she is on it.";
	otherwise;
		if the platform is the short plastic block and Zoe is on the tall plastic block begin;
			say "Zoe takes this crazy leap from the tall block onto the short one and sticks the landing! That is all impressive even though it keeps you from pushing the short block around.";
		otherwise if the platform is the tall plastic block and Zoe is on the short plastic block;
			say "When you start pushing on [the platform] Zoe slides off of the short block and moves to the other side of [the platform] so you can't make it go anywhere.";
			instead move Zoe to the location of the platform;
		otherwise;
			say "You start pushing on [the platform] but Zoe moves onto it so you can't move it, grr.";
		end if;
		move Zoe to the platform;
	end if.

Zoe can be up-climbing or down-climbing. Zoe is up-climbing.

[TK BUG: I'd like to use a rulebook variable here, but it looks like I can't then refer to that rulebook variable in the when/while clauses of the rulebook's rules' premises themselves.]
ZK Climbing Rules is a rulebook. [TK NO GOOD The ZK climbing rules rulebook has an object called the push-block.] The push-block is an object that varies.

The first ZK climbing rule (this is the determine Zoe's block of choice rule):
	[During block pushing and climbing, Zoe moves fast]
	change the base act percentage of Zoe to 100;
	change the act percentage of Zoe to 100;
	if the short plastic block is not in the TKBUG Open Area begin;
		change the push-block to the short plastic block;
	otherwise if the bugish plastic block is not in the TKBUG Open Area;
		change the push-block to the bugish plastic block;
	otherwise if the tall plastic block is not in the TKBUG Open Area;
		change the push-block to the tall plastic block;
	otherwise;
		[In case Zoe goes wandering away from the blocks for whatever reason, use the Zoe heads for the push-block rule to bring her back]
		change the push-block to the tall plastic block;
		ignore the Zoe pushes the push-block rule;
		[If the player is in the baby swing, make Zoe head up]
		if the player is in the baby-swing, now Zoe is up-climbing;
	end if.

A ZK climbing rule when Zoe and the push-block are not co-located (this is the Zoe heads for the push-block rule):
	let way be the best route from the location of Zoe to the location of the push-block;
	if way is a direction, try Zoe going way;
	rule succeeds.

A ZK climbing rule when Zoe and the push-block are co-located (this is the Zoe pushes the push-block rule):
	let way be the best route from the location of Zoe to the TKBUG Open Area;
	if way is a direction, try Zoe pushing the push-block to the way;
	rule succeeds.

Mentioning suck-it-ness is a one-time deal.

To say Zoe's annoyance at player thwarting her climb (d - a direction) onto (b - a thing):
	say "Zoe starts to climb [d] onto [the b] but then since you are on the [if d is up]higher[otherwise]lower[end if] blocks she looks [d] at you and makes this annoyed face[if mentioning suck-it-ness has not occurred], ha ha, she can suck it[end if][if the player is on the bugish plastic block]. Clearly she is waiting for you to go down directly from the medium block[end if].".

A ZK climbing rule when Zoe is zeroth-height and Zoe is up-climbing (this is the Zoe starts climbing blocks rule):
	if the player is on the short plastic block, say Zoe's annoyance at player thwarting her climb up onto the short plastic block;
	otherwise try Zoe entering the short plastic block;
	rule succeeds.

A ZK climbing rule when Zoe is on the short plastic block and Zoe is up-climbing (this is the Zoe climbs up from the short block rule):
	if the player is on the bugish plastic block or the player is on the tall plastic block, say Zoe's annoyance at player thwarting her climb up onto the bugish plastic block;
	otherwise try Zoe entering the bugish plastic block;
	rule succeeds.

A ZK climbing rule when Zoe is on the bugish plastic block and Zoe is up-climbing (this is the Zoe climbs up from the bugish plastic block rule):
	if the player is on the tall plastic block begin;
		say Zoe's annoyance at player thwarting her climb up onto the tall plastic block;
	otherwise;
		try Zoe entering the tall plastic block;
		if Cooperationtime is happening and Zoe is on the tall plastic block and Zoe and the player are not co-located, say "You hear this cough from Zoe[note stranding-zoe].";
	end if;
	rule succeeds.

Making a use-everything-in-the-inventory joke is a one-time deal.

A ZK climbing rule when Zoe is on the tall plastic block and Zoe is up-climbing (this is the Zoe can't reach the toy on the bookcase rule):
	if the player is in the baby-swing begin;
		say "Zoe watches you swing back and forth, back and forth.";
	otherwise if 1 out of 2;
		if the player and Zoe are co-located begin;
			if Zoe is carrying something (called the carried item), say "Zoe [one of]waves [the carried item] at your favorite toy[or]pokes [the carried item] towards your favorite toy[or]looks like she wants to throw [the carried item] at your favorite toy but can't[or]lifts [the carried item] into the air as if it will magically get her your favorite toy[purely at random][if 1 out of 2 and making a use-everything-in-the-inventory joke has not occurred], she is doing the baby equivalent of randomly using everything in your inventory on an object[note meta-joke][end if].";
			otherwise say "Zoe [one of]reaches for the toy in the bookcase but can't reach it[or]makes sad moon-eyes at your favorite toy[or]is all sad that she can't reach the toy[purely at random].";
		end if;
		now Zoe is down-climbing;
	end if;
	rule succeeds.

A ZK climbing rule when Zoe is on the tall plastic block and Zoe is down-climbing (this is the Zoe climbs down from the tall plastic block rule):
	if the player is on the bugish plastic block, say Zoe's annoyance at player thwarting her climb down onto the bugish plastic block;
	otherwise try Zoe entering the bugish plastic block;
	rule succeeds.

A ZK climbing rule when Zoe is on the bugish plastic block and Zoe is down-climbing (this is the Zoe climbs down from the bugish plastic block rule):
	if the player is on the short plastic block, try Zoe going down;
	otherwise try Zoe entering the short plastic block;
	rule succeeds.

A ZK climbing rule when Zoe is on the short plastic block and Zoe is down-climbing (this is the Zoe climbs down from the short plastic block rule):
	try Zoe going down;
	rule succeeds.

A ZK climbing rule when Zoe is zeroth-height and Zoe is down-climbing (this is the Zoe contemplates her options rule):
	if the player and Zoe are co-located, say "Zoe [run paragraph on]";
	let target be a random small carryable thing in the location of Zoe;
	if Zoe is carrying something (called the carried item) begin;
		silently try Zoe dropping the carried item;
		if the carried item is not carried by Zoe and (the player and Zoe are co-located), say "[one of]drops[or]tosses aside[or]discards[purely at random] [the carried item] and she [run paragraph on]";
	end if;
	if target is not nothing begin;
		silently try Zoe taking the target;
		if the target is carried by Zoe and (the player and Zoe are co-located), say "[one of]grabs[or]picks up[purely at random] [a target].";
	otherwise;
		if the player and Zoe are co-located, say "[one of]contemplates the toy[or]is clearly thinking about how to get that toy[or]nudges one of the blocks thoughtfully[purely at random].";
	end if;
	now Zoe is up-climbing;
	rule succeeds.

ZK Moping Rules is a rulebook.

[Borrow the rules for Zoe climbing down from the blocks.]
The Zoe climbs down from the tall plastic block rule is listed in the ZK Moping Rules.
The Zoe climbs down from the bugish plastic block rule is listed in the ZK Moping Rules.
The Zoe climbs down from the short plastic block rule is listed in the ZK Moping Rules.

A ZK moping rule when Zoe is carrying something (called the carried item) (this is the Zoe discards whatever she was fiddling with rule):
	try Zoe dropping the carried item.

The Zoe discards whatever she was fiddling with rule is listed after the Zoe climbs down from the short plastic block rule in the ZK Moping Rules.

[If Zoe's not next to the player and the player's in a room w/a playpen, move Zoe.]
A ZK moping rule when Zoe and the player are not co-located (this is the Zoe mopes around the player rule):
	if the location is the Middle of the Living Room or the location is the TKBUG Open Area begin;
		move Zoe to the location;
		say "Zoe comes in [one of]all sadly[or]slowly[or]dejectedly[or]and sighs heavily[or]and looks sadly at the playpen[purely at random].";
		rule succeeds;
	end if.

The Zoe mopes around the player rule is listed after the Zoe discards whatever she was fiddling with rule in the ZK Moping Rules.

A ZK moping rule:
	if 1 out of 6 begin;
		if Zoe is present, say "Zoe [one of]pokes dejectedly at the playpen[or]looks at the playpen[or]looks at you and does this sad little shrug[or]hunches her shoulders sadly[or]sighs[or]frowns[or]looks around listlessly[purely at random].";
		rule succeeds;
	end if.

ZK Cooperating Rules is a rulebook.

The determine Zoe's block of choice rule is listed first in the ZK Cooperating Rules.
The Zoe heads for the push-block rule is listed in the ZK Cooperating Rules.
The Zoe pushes the push-block rule is listed in the ZK Cooperating Rules.
The Zoe starts climbing blocks rule is listed in the ZK Cooperating Rules.
The Zoe climbs up from the short block rule is listed in the ZK Cooperating Rules.
The Zoe climbs up from the bugish plastic block rule is listed in the ZK Cooperating Rules.

A ZK cooperating rule when Zoe is on the tall plastic block:
	if 1 out of 4 begin;
		if Zoe is present, say "Zoe looks at you and coughs pointedly.";
		otherwise say "You hear this cough, it is Zoe trying to get your attention.";
		rule succeeds;
	end if.

ZK Toy-Grabbing Rules is a rulebook.

A ZK toy-grabbing rule when Zoe is on the tall plastic block (this is the Zoe scampers down from the tall block rule):
	try Zoe entering the bugish plastic block;
	rule succeeds.

A ZK toy-grabbing rule when Zoe is on the bugish plastic block (this is the Zoe scampers down from the bugish plastic block rule):
	try Zoe going down;
	rule succeeds.

[This next rule shouldn't be needed, but just in case...]
A ZK toy-grabbing rule when Zoe is not carrying your favorite toy and Zoe and your favorite toy are not co-located (this is the Zoe goes towards the toy rule):
	let way be the best route from the location of Zoe to the location of your favorite toy;
	if way is a direction, try Zoe going way;
	rule succeeds.

A ZK toy-grabbing rule when Zoe is not carrying your favorite toy (this is the Zoe ganks the toy rule):
	try Zoe taking your favorite toy;
	rule succeeds.

[I'll borrow Fuss Count, a global variable that Zoe uses when she fusses in the first major scene, to let Zoe flee only after one turn.]

A ZK toy-grabbing rule when Zoe is present (this is the Zoe flees from you with the toy rule):
	if fuss count is 1 begin;
		have Zoe flee from the player;
		rule succeeds;
	otherwise;
		increment fuss count by 1;
	end if.

A ZK toy-grabbing rule when Zoe is not present (this is the Zoe plays with your toy rule):
	if 1 out of 5, say "You hear this rattle jingle, it is Zoe playing with your toy!";
	rule succeeds.

[Another hack to let me pick a non-Zoe playmate.]
Definition: a person is not-Zoe-baby:
	if it is the player, decide yes;
	if it is a playmate and it is not Zoe, decide yes;
	decide no.

[There are two routines here. The first defines whether or not Zoe will flee successfully (which she will do as long as she's not in the room with the pile of Cheerios and with some playmate who's not her). The second forces Zoe to flee.]
To decide if Zoe flees from the player:
	if Zoe and the pile of Cheerios are co-located begin;
		if a not-Zoe-baby playmate is in the location of Zoe, decide no;
	end if;
	decide yes.

To have Zoe flee from the player:
	let destination be a random room which is adjacent to the location of Zoe;
	[Make Zoe head for the pile of Cheerios if they're around]
	if the pile of Cheerios is in play and the pile of Cheerios and Zoe are not co-located, change destination to the location of the pile of Cheerios;
	let way be the best route from the location of Zoe to the destination;
	if way is a direction, try Zoe going way;
	now fuss count is 0.

Section 2 - Cassie Sandoval

Cassie is a playmate in the East End of Couch. Cassie is female. The description is "That is Cassie Sandoval, she is wearing a [if Cassie is wearing the pumpkin costume]costume that makes her look like a pumpkin[otherwise]white dress that is all bright against her dark skin and her hair is captured with pink clips[end if]. Her arms are scraped up and she has some bruises on her legs[if Cassie is carrying something]. She is carrying [a list of things carried by Cassie][end if]." The base act percentage is 20. The act percentage is 20. The act increment is 15. Understand "Sandoval" or "baby" as Cassie.

Cassie wears a white dress and some pink hair clips. The description of the white dress is "It is so bright white, her mom must launder it every ten minutes." The description of some pink hair clips is "Wow those are pink." The white dress and some pink hair clips are small.

Check smelling Cassie: instead say "She smells all speedy."

Rule for pre-writing a paragraph about Cassie during Tabletime: rule succeeds.

Rule for pre-writing a paragraph about Cassie when Cassie is wearing the pumpkin costume: say "Cassie is dressed in this pumpkin costume, it is adorable. [run paragraph on]"

Rule for pre-writing a paragraph about Cassie when Parry is puzzle-allowing during Snacktime: say "Cassie is sprawled in Lisa's arms all sniffly. [run paragraph on]"

Rule for pre-writing a paragraph about Cassie when Cassie is pulled up on something (called the support):
	say "Cassie is all pulled up on [the support]. [run paragraph on]";

Rule for pre-writing a paragraph about Cassie when Cassie is gnawing something (called the mastication):
	say "Cassie has [the mastication] in her mouth. [run paragraph on]";

Rule for pre-writing a paragraph about Cassie when Cassie is carrying something (called the item):
	say "Cassie has hold of [the item] and keeps [one of]fiddling with[or]shaking[purely at random] [it-them]. [run paragraph on]";

Rule for pre-writing a paragraph about Cassie:
	say "Cassie is looking around for mischief to make. [run paragraph on]";

Instead of Cassie giving something to the player: if Cassie is present, say "Cassie offers you [the noun] but pfft, like you want to [one of]be in her debt[or]owe her anything[purely at random]."

Instead of Cassie giving something to a playmate: if Cassie is present, say "Cassie tries giving [the noun] to [the second noun] but [the second noun] ignores the offer."

Instead of showing or giving something to Cassie when Cassie is carrying something (called the item) and Cassie is active:
	say "You show [the noun] to Cassie who looks at [it-them of the noun] and then shows you [the item] in response."

Instead of showing or giving something to Cassie when Cassie is active:
	say "You show [the noun] to Cassie and she laughs happily."

[Start with Cassie pulled up on the east grey couch so the player can see her cruise and know that such an action is possible.]
Cassie is pulled up on the east grey couch.

After visiting the East End of Couch for the first time:
	if Cassie is in the East End of Couch begin;
		cruise Cassie to the west and hold the middle grey couch;
		announce Cassie cruising to the west letting go of the east grey couch and holding the middle grey couch;
		now Cassie is active;
	end if.

To announce Cassie cruising to (goal - a direction) letting go of (the old support - a thing) and holding (the new support - a thing):
	if Cassie was recently present, say "Cassie [one of]cruises[or]moves[purely at random] hand over hand along [the old support] to [the goal] and is gone[note kid-hint-giving].";
	if Cassie is present, say "Cassie comes cruising in from [the opposite of the goal] holding on to [the new support]."

This is the CS taking report rule:
	if 1 out of 6, say "Cassie sees [the noun] and decides she has to have [it-them] and so she takes [it-them]";
	otherwise say "Cassie [takes] [the noun][if 1 out of 3] off the floor[otherwise if 1 out of 3] excitedly[end if]";
	if 1 out of 6, say ", she is so grabby";
	say ". [run paragraph on]";

This is the CS dropping report rule:
	let switch-value be a random number from 1 to 3;
	if switch-value is 1, say "Cassie drops [the noun] forcefully";
	if switch-value is 2, say "Cassie throws [the noun] at the ground[if 1 out of 3] and unsurprisingly she hits her target";
	if switch-value is 3, say "Cassie [one of]throws[or]tosses[purely at random] [the noun] up [if 1 out of 3]in the air [end if]and [it-them] falls[if 1 out of 4] and Cassie all laughs";
	say "[note randomness-sux]. [run paragraph on]";

This is the CS going report rule:
	if Cassie was recently present or Cassie is present begin;
		let p be a random parent person in the last location of Cassie;
		[This next statement is to hack around a bug: if you print the name of the mom, it's printed as "mom" instead of "the mom"]
		if p is the mom, now p is nothing;
		if 1 out of 2 and Cassie was recently present begin;
			say "[Cassie possibly making noise when moving]Cassie [if the just-released item is not nothing]lets go of [the just-released item] and [end if][if 1 out of 3 and p is not nothing]looks up at [the p] to orient herself and [end if]falls forward onto her hands and then she is [if 1 out of 3]all [end if]crawling off to [the direction tried of Cassie][if 1 out of 4] and is gone[end if]";
		otherwise;
			say "[Cassie possibly making noise when moving]Cassie [if Cassie is not present and the just-released item is not nothing]lets go of [the just-released item] and [end if][if 1 out of 3 and p is not nothing and Cassie is not present]looks up at [p] to orient herself and [end if][one of]zips[or]zooms[or]crawls[purely at random] ";
			if Cassie is present, say "in from [the opposite of the direction tried of Cassie][if 1 out of 4] and sits down all pleased";
			otherwise say "off to [the direction tried of Cassie]";
		end if;
		say "[note randomness-sux]. [run paragraph on]";
	end if.

This is the CS sitting down report rule:
	let switch-value be a random number from 1 to 3;
	if switch-value is 1, say "Cassie lets go of [the previous-support] and falls backwards and before she hits the ground she bends her knees and sits";
	if switch-value is 2, say "Cassie lets go of [the previous-support] and falls onto her diaper and is all [one of]'eee ba ba ba'[or]chortling[purely at random] as she does so";
	if switch-value is 3, say "Cassie has this shifty grin [if 1 out of 6]as if to say 'watch this!' [end if]and she lets go of [the previous-support] and sits";
	say "[note randomness-sux]. [run paragraph on]";

Report Cassie chewing: if Cassie is present, say "Cassie [one of]shoves [the noun] in her mouth[or]chews on [the noun][purely at random][if 1 out of 5] with gusto[end if][note randomness-sux]."; stop the action.

Report Cassie stopping chewing:
	if Cassie is not present, stop the action;
	if 3 out of 5, say "Cassie [one of]yanks[or]pops[purely at random] [the noun][if 1 out of 5] that she was chewing on[end if] out of her mouth";
	otherwise say "Cassie stops chewing [if 1 out of 4]on [end if][the noun] and spits it into her hands[if 1 out of 5], I guess she used up all of the flavor[end if]";
	say "[note randomness-sux]."

Report Cassie upstanding using:
	if Cassie is not present, stop the action;
	let switch-value be a random number from 1 to 3;
	if switch-value is 1, say "Cassie grabs [if 1 out of 4]hold of [end if][the noun] and pulls herself up[if 1 out of 3], then she [one of]smiles[or]babbles happily[purely at random][end if]";
	if switch-value is 2, say "Cassie pulls [if 1 out of 3]herself [end if]up on [the noun][if 1 out of 6] and then tries to keep climbing up but she is now as high as she can go[end if]";
	if switch-value is 3, say "Cassie goes hand over hand up [the noun] until she is standing";
	if 1 out of 6, instead say " and Lisa calls out, 'Oh, honey, be careful, you don't want to fall and hurt yourself. Please?'";
	otherwise instead say "[note randomness-sux]."

Report Cassie entering:
	if Cassie is not present, stop the action;
	instead say "Cassie climbs onto [the noun]."

Report Cassie exiting:
	if Cassie is not present, stop the action;
	say "Cassie climbs down off of [the noun]";
	if Cassie is on something (called the platform), say " and onto [the platform]";
	instead say "."

CS hint-giving rules is a rulebook.

A CS hint-giving rule when Cassie is standing:
	try Cassie sitting down;
	move the squishy ball to Cassie;
	change the base act percentage of Cassie to 100;
	change the act percentage of Cassie to 100;
	rule succeeds.

A CS hint-giving rule when Cassie and Zoe are not co-located:
	let destination be the location of Zoe;
	let aim be the best route from the location of Cassie to destination;
	if aim is a direction, try Cassie going aim.

A CS hint-giving rule when Cassie and Zoe are co-located:
	if Cassie is present begin;
		try Cassie giving the squishy ball to Zoe;
		choose row with a baby name of Cassie in the Table of Baby State Machines;
		change machine entry to CS State Rules;
		change the base act percentage of Cassie to 20;
		change the act percentage of Cassie to 20;
	end if;
	rule succeeds.

CS state rules is a rulebook.

[Ew, this is nasty. But it lets me hack around "a random playmate in the location of Cassie who is not Cassie" not working.]
Definition: a person is not-Cassie-baby:
	if it is the player, decide yes;
	if it is a playmate and it is not Cassie, decide yes;
	decide no.

A CS state rule when Cassie is on the tall plastic block (this is the Cassie does a death-defying leap rule):
	if Cassie is present, say "Cassie giggles and leaps off of the tall block and that would be okay if she didn't then smack her face into the floor. [run paragraph on]";
	otherwise say "You suddenly hear this heavy thud which must be Cassie leaping off the tall plastic block. [run paragraph on]";
	have Cassie get hurt;
	rule succeeds.

A CS state rule when Cassie is on the tall plastic block (this is the Cassie climbs down gently from the tall plastic block rule):
	if Cassie and the bugish plastic block are co-located begin;
		try Cassie entering the bugish plastic block;
		now the act percentage of Cassie is 100;
	end if;
	rule succeeds.

A CS state rule when Cassie is on the bugish plastic block (this is the Cassie climbs up or down the medium block rule):
	[Don't let Cassie climb up if the player's on a taller block]
	if Cassie and the tall plastic block are co-located and the player is not on the tall plastic block begin;
		try Cassie entering the tall plastic block;
		now the act percentage of Cassie is 100;
		rule succeeds;
	otherwise if Cassie and the short plastic block are co-located;
		try Cassie entering the short plastic block;
		now the act percentage of Cassie is 100;
		rule succeeds;
	otherwise;
		try Cassie getting off the bugish plastic block;
		rule succeeds;
	end if;

A CS state rule when Cassie is on the short plastic block (this is the Cassie climbs up or down the short block rule):
	[Don't let Cassie climb up if the player's on a taller block]
	if Cassie and the bugish plastic block are co-located and the player is not on the bugish plastic block and the player is not on the tall plastic block begin;
		try Cassie entering the bugish plastic block;
		now the act percentage of Cassie is 100;
		rule succeeds;
	otherwise;
		try Cassie getting off the short plastic block;
		rule succeeds;
	end if.

A CS state rule when Cassie and the short plastic block are co-located during Snacktime (this is the Cassie climbs up on the short block rule):
	try Cassie entering the short plastic block;
	now the act percentage of Cassie is 100;
	rule succeeds.

The Cassie climbs up on the short block rule is listed after the Cassie climbs up or down the short block rule in the CS state rulebook.

A CS state rule when Cassie is carrying anything (this is the Cassie shares the wealth rule):
	if 2 out of 10 and Cassie is not alone begin;
		let item be a random thing carried by Cassie;
		let target be a random not-Cassie-baby person in the location of Cassie;
		if target is Zoe and Marion encloses the backpack begin;
			[Don't let Cassie solve this puzzle for the player by accident]
			do nothing;
		otherwise;
			try Cassie giving item to target;
			rule succeeds;
		end if;
	end if.

A CS state rule when Cassie is carrying anything (this is the Cassie drops her inventory rule):
	if 2 out of 8 begin;
		try Cassie dropping a random thing carried by Cassie;
		rule succeeds;
	end if.

A CS state rule when Cassie is carrying anything (this is the Cassie stops chewing rule):
	if Cassie is gnawing anything (called the mastication) begin;
		try Cassie stopping chewing the mastication;
		rule succeeds;
	end if.

A CS state rule when Cassie is carrying anything (this is the Cassie chews on her inventory rule):
	if 2 out of 6 begin;
		try Cassie chewing a random thing carried by Cassie;
		rule succeeds;
	end if.

A CS state rule when Cassie is carrying anything (this is the Cassie plays with her inventory rule):
	if Cassie is not present, rule succeeds;
	let target be a random thing carried by Cassie;
	if 2 out of 5 begin;
		if 1 out of 2, say "Cassie gives [the target] a [if 1 out of 6]good hard [end if]shake.";
		otherwise say "Cassie shakes [the target].";
	otherwise;
		let switch-value be a random number from 1 to 3;
		if switch-value is 1, say "Cassie takes [the target] and kinda [one of]fiddles[or]plays[purely at random] with it.";
		if switch-value is 2, say "Cassie plays with [the target].";
		if switch-value is 3, say "Cassie holds [the target] first in one hand and then in the other hand.";
	end if;
	rule succeeds.

A CS state rule when Cassie is standing (this is the Cassie cruises or climbs rule):
	if 1 out of 2 and Cassie is pulled up on something (called the support) begin;
		if 3 out of 4 and the support is suitable for cruising begin;
			sort the Table of Cruising Possibilities in random order;
			choose row with a Primary of support in the Table of Cruising Possibilities;
			cruise Cassie to the Direction entry and hold the reciprocal entry;
			announce Cassie cruising to the Direction entry letting go of the primary entry and holding the reciprocal entry;
		otherwise;
			if Cassie is present, say "Cassie tries to [one of]go[or]climb[purely at random] higher up [the support] but she is already standing and can't go any higher[if 1 out of 6], maybe if she stays there long enough she will grow taller[end if].";
		end if;
		rule succeeds;
	end if.

A CS state rule when Cassie is standing (this is the Cassie lets go of a supporter rule):
	try Cassie sitting down;
	rule succeeds.

A CS state rule (this is the Cassie pulls up on parents during Playtime rule):
	if a parent person (called p) is in the location of Cassie and Playtime is happening and 1 out of 3 begin;
		[Don't let a kid solve the Parry rocking puzzle]
		if p is not Parry or Playtime is not happening begin;
			try Cassie upstanding using p;
			rule succeeds;
		end if;
	end if.

A CS state rule (this is the Cassie pulls up or climbs rule):
	if a random supporter is in the location of Cassie begin;
		let target be a random supporter in the location of Cassie;
		if target is the gliding rocking chair and Playtime is happening begin;
			do nothing;
		otherwise if target is the footstool and Snacktime is happening;
			do nothing;
		otherwise if target is the short plastic block or target is the bugish plastic block or target is the tall plastic block;
			do nothing;
		otherwise if 3 out of 10 and target is not nothing;
			if target is suitable for pulling up on begin;
				try Cassie upstanding using the target;
			otherwise if Cassie is present;
				say "Cassie tries to climb [the target] but there are not really any handholds[if 1 out of 6] and so she gives up[end if].";
			end if;
			rule succeeds;
		end if;
	end if.

A CS state rule (this is the Cassie picks up stuff rule):
	if 3 out of 7 begin;
		let position be the location of Cassie;
		if a pickupable thing is in position begin;
			let target be a random pickupable thing in position;
			try Cassie taking target;
			rule succeeds;
		end if;
	end if.

A CS state rule when Cassie and the short plastic block are not co-located during Snacktime (this is the Cassie heads for the short block rule):
	if Cassie is ready to leave begin;
		let way be the best route from the location of Cassie to the location of the short plastic block;
		if way is a direction, try Cassie going way;
		rule succeeds;
	end if.

The Cassie heads for the short block rule is listed after the Cassie picks up stuff rule in the CS state rulebook.

A CS state rule (this is the Cassie wanders rule):
	if 2 out of 4 and Cassie is ready to leave begin;
		have Cassie wander;
		rule succeeds;
	end if.

A CS state rule (this is the Cassie eyeing the plastic block rule):
	if Cassie and the tall plastic block are co-located begin;
		if Cassie is present, say "Cassie looks at the tall plastic block and then she sidles away from it.";
		rule succeeds;
	end if.

A CS state rule (this is the Cassie babbling rule):
	if Cassie is present begin;
		if 1 out of 3, say "Cassie says '[one of]B-b-b-b-b-buh buh buh.'[or]Eeeeeee eeee ee-ah!'[or]Pbbbbbth.'[purely at random]";
		otherwise say "Cassie [one of]makes these noises like a wounded pteranodon and smiles[or]babbles happily[purely at random].";
	end if;
	rule succeeds.

The short plastic block is a pushable between rooms enterable supporter. The description is "It is red and it is about as wide as you are and as tall as your head is high when you are crawling around." Understand "red" or "small" as the short plastic block.

[Can't call this the medium plastic block -- that would interfere with the medium size designation.]
The bugish plastic block is a pushable between rooms enterable supporter. The description is "It is green and it is about as wide as you are and as tall as your head is high when you are sitting down." The printed name is "medium plastic block". Understand "medium" or "green" as the bugish plastic block.

The tall plastic block is a pushable between rooms enterable supporter. The description is "It is blue and it is about as wide as you are and tall as your head is high when you are pulled up." Understand "blue" or "large" as the tall plastic block. The tall plastic block is suitable for pulling up on and not suitable for cruising.

After dropping when the player is on the short plastic block or the player is on the bugish plastic block or the player is on the tall plastic block: move the noun to the location of the player; continue the action.

Instead of jumping when the player is on the short plastic block or the player is on the bugish plastic block: say "Jumping would probably hurt you, so instead you climb down."; silently try exiting; try looking.

Instead of jumping when the player is on the tall plastic block, say "Wow that would end with your face all planted and full of pain, let's not do that."

[For players' convenience]
Before going a direction (called way) when the player is on the short plastic block:
	if way is not down and way is not up begin;
		say "(first climbing off of the short block)[command clarification break]";
		silently try exiting;
		if the player is on the short plastic block, stop the action;
	end if.

[Access rules for the blocks]

A height reachability rule when the reaching object is the short plastic block (this is the short block reachability rule):
	if the reaching actor is zeroth-height, continue action processing;
	if the reaching actor is on the short plastic block, continue action processing;
	if the reaching actor is on the bugish plastic block, continue action processing;
	if the reaching actor is the player, say "You have short arms, you can't reach the short plastic block from [if the player is quarter-height-or-higher]up[otherwise]down[end if] here.";
	stop action processing.

A height reachability rule when the reaching object is the bugish plastic block (this is the bugish block reachability rule):
	if the reaching actor is zeroth-height, continue action processing;
	if the reaching actor is on the short plastic block, continue action processing;
	if the reaching actor is on the bugish plastic block, continue action processing;
	if the reaching actor is on the tall plastic block, continue action processing;
	if the reaching actor is the player, say "You have short arms, you can't reach the medium plastic block from [if the player is quarter-height-or-higher]up[otherwise]down[end if] here.";
	stop action processing.

A height reachability rule when the reaching object is the tall plastic block (this is the tall block reachability rule):
	if the reaching actor is zeroth-height, continue action processing;
	if the reaching actor is on the bugish plastic block, continue action processing;
	if the reaching actor is on the tall plastic block, continue action processing;
	if the reaching actor is pulled up on the tall plastic block, continue action processing;
	if the reaching actor is the player, say "You have short arms, you can't reach the tall plastic block from [if the player is half-height-or-higher]up[otherwise]down[end if] here.";
	stop action processing.

[This rule is to get around the "Instead of entering/climbing something suitable for pulling up on" rules]
Instead of entering the tall plastic block:
	abide by the check entering rules;
	abide by the carry out entering rules;
	if the actor is present, abide by the report entering rules.

Instead of upstanding using the short plastic block, try entering the short plastic block.

Instead of climbing or upstanding using the tall plastic block when the player is on the short plastic block or the player is on the bugish plastic block or the player is on the tall plastic block, try entering the tall plastic block instead.

Instead of entering the bugish plastic block when the player is not on the short plastic block and the player is not on the tall plastic block and the player is not on the bugish plastic block, say "The block is just short enough that you cannot pull yourself up on it and tall enough that you cannot really climb up on it from the floor."

Instead of upstanding using the bugish plastic block, try entering the bugish plastic block.

Instead of entering the tall plastic block when the player is not on the bugish plastic block and the player is not on the tall plastic block (this is the tall block is way too tall to enter rule), say "The block is so tall and you are not high enough to get up on it."

Instead of entering the tall plastic block when the player is on the short plastic block (this is the tall block versus the short block rule), say "Since you are on the short plastic block you are at this weird height where the tall block is too tall to climb up on but it is too short to really pull up on."

The tall block versus the short block rule is listed before the tall block is way too tall to enter rule in the instead rulebook.

Report entering the short plastic block for the first time: instead say "You start out pulling up on the block but it is so short you fall forward onto it and then you are sitting on it a little ways above the ground[note those-annoying-blocks]."

Report entering the bugish plastic block: instead say "You carefully climb onto the medium plastic block, it is a ways off the ground."

Report entering the tall plastic block: instead say "You clamber up onto the tall plastic block, you are getting so high."

[For some reason, I7's "standard report entering rule" fires whether or not the player is actually in the room. Fix that for the plastic blocks.]
Report someone entering a supporter:
	if the person asked is not present, stop the action;
	continue the action.

[An "instead of getting off the tall plastic block" rule will block the player from *ever* getting off the tall plastic block. I hack around that with a flag.]

The tall plastic block can be climb-blocking or climb-allowing. The tall plastic block is climb-blocking.

Before entering the bugish plastic block when the player is on the tall plastic block: now the tall plastic block is climb-allowing.

After entering the bugish plastic block: now the tall plastic block is climb-blocking; continue the action.

Instead of getting off the tall plastic block when the tall plastic block is climb-blocking, say "The tall block is way tall and you would hurt yourself if you tried to get down from it directly."

Instead of getting off the tall plastic block when the tall plastic block is climb-blocking and the bugish plastic block is present:
	silently try the player entering the bugish plastic block;
	say "The tall block is way tall and you would hurt yourself if you tried to get down from it directly[if the player is on the bugish plastic block] so instead you climb onto the medium block[end if].";
	if the player is not on the bugish plastic block, now the tall plastic block is climb-blocking.

Report someone getting off the short plastic block:
	if the person asked is present, say "[The person asked] climbs down off of the short plastic block.";
	rule succeeds.

Report someone getting off the bugish plastic block:
	if the person asked is present, say "[The person asked] climbs down off of the medium plastic block.";
	rule succeeds.

Instead of going up when the player is on the short plastic block and the bugish plastic block is present, try entering the bugish plastic block.

Instead of going up when the player is on the bugish plastic block and the tall plastic block is present, try entering the tall plastic block.

Instead of pushing or pulling Cassie when Cassie is standing and Snacktime is happening:
	if a parent person is present, continue the action; [Let the default rule handle this]
	say "You look around and there are no parents right here so you shove Cassie and she topples over with a squeak of surprise. Her head goes all bounce on the floor. [run paragraph on]";
	have Cassie get hurt.

To have Cassie get hurt:
	say "Cassie shrieks, it is the loudest you have ever heard her be. I guess she is not the super-tough baby she thinks she is[note Cassie-falls-down]. Anyway Lisa leaps up and runs to Cassie and Parry leaves his tumbler and runs to Cassie too, they scatter the plastic blocks around the room in their haste. 'Baby, baby, it's okay, shh,' you hear Lisa saying and then Parry is [if Cassie is present]also checking Cassie over and shining this little flashlight in her eyes and [end if]saying, 'I think she's okay, she's just scared. Her pupillary response is just fine.'";
	[If adults are talking, cut their convo short]
	cut chatter short;
	[Scatter the blocks]
	let block-room be the location of the tall plastic block;
	let end-spot be a random room adjacent to block-room;
	if no person is on the short plastic block, move the short plastic block to end-spot;
	let end-spot be a random room adjacent to block-room;
	if no person is on the bugish plastic block, move the bugish plastic block to end-spot;
	now Cassie is in the location of the tall plastic block;
	now Cassie is passive;
	now Lisa is in the location of Cassie;
	now Parry is in the location of Cassie;
	Lisa soothes more in one turn from now;
	now Parry is puzzle-allowing.

At the time when Lisa soothes more:
	say "Lisa is still saying 'It'll be okay, Cassie, it'll be okay' and Parry is saying 'Breathe, Cassie, breathe' and Cassie is now taking big hiccuping breaths.";
	Cassie is okay in one turn from now.

At the time when Cassie is okay:
	say "Cassie finally is all better and is laughing some and Lisa says, 'Thanks, Parry, I appreciate your help' and Parry says, 'No problem.' And they go back to their seats";
	if the heavy tumbler is in the West End of Couch begin;
		if the heavy tumbler is damp or something (called floater) is in the heavy tumbler begin;
			if the location is the West End of Couch, say ". You see Parry pick up his tumbler for a drink and he lifts it to his lips and says ";
			otherwise say ". Then you hear Parry say ";
			say "'Ugh! [if something is in the heavy tumbler]There's [a floater] in my tumbler[otherwise]My tumbler's damp[end if]!' and everyone laughs at Parry. Becca says, 'That's why you don't put things where the kids can [if something is in the heavy tumbler]get to[otherwise]chew all over[end if] them!' And Parry sighs";
			if the location is the West End of Couch or the location is By the Cabinet, say " and you see him put";
			otherwise say " and you hear the wooden thunk of Parry putting";
			say " the tumbler way up on the wood cabinet in the corner between the couch and the loveseat and definitely out of your reach[note ruined-tumbler]";
			now the heavy tumbler is on the cabinet top;
			if the coaster is on the cabinet top, remove the coaster from play;
			record 5 points for "getting the tumbler on the cabinet";
		end if;
	end if;
	say ".";
	now Cassie is active;
	now Lisa is in the North End of Loveseat;
	now Parry is in the West End of Couch;
	now Parry is puzzle-blocking;
	change the chatter status to ready to act.

Section 3 - Watson Mason

Watson is a playmate. Watson is male. The description is "Watson Mason has this big round face that is perfect for commercials and baby food labels, only his face is always tear-streaked so maybe he is not cut out for show-biz. Maybe he is sad because he is dressed [if Watson is wearing the bat costume]in this costume that is supposed to be a scary bat but instead is like a sad dumpy bat[otherwise]like a teeny tiny engineer in his polo onesie with a collar and because he smells like baby powder[end if][if Watson is carrying something]. He is clutching [a list of things carried by Watson][end if]." The base act percentage is 20. The act percentage is 20. The act increment is 10. Understand "Mason" or "baby" as Watson.

Check smelling Watson: instead say "He's all baby powder."

Watson wears a polo onesie. The description of the polo onesie is "White with this collar that says 'some day I will be middle management.'" The polo onesie is small.

Rule for pre-writing a paragraph about Watson during Tabletime: rule succeeds.

Rule for pre-writing a paragraph about Watson when Watson is wearing the bat costume:
	say "Watson is gloomy in his sad homemade bat costume. [run paragraph on]";

Rule for pre-writing a paragraph about Watson when Watson is carrying something (called the item):
	say "Watson is holding [the item][if 1 out of 3] like it is all dangerous or something[end if]. [run paragraph on]";

Rule for pre-writing a paragraph about Watson when Watson is upset:
	say "Watson sits on the floor and his face is all screwed up and red. [run paragraph on]";

Rule for pre-writing a paragraph about Watson:
	say "Watson sits all sad and dumpy on the floor. [run paragraph on]";

[When the player first sees Watson and Jemison, have Watson take the rattly car so Jemison becomes fixated and the player sees how that works.]
After visiting the South End of Loveseat for the first time: try Watson taking the rattly car; now Watson is active; now Jemison is active.

This is the WM taking report rule:
	say "Watson [if 3 out of 4]gingerly [end if][one of]picks up[or]takes[purely at random] [the noun][note randomness-sux]. [run paragraph on]";

This is the WM dropping report rule:
	say "Watson puts [one of]down [the noun][or][the noun] down[purely at random][note randomness-sux]. [run paragraph on]"

This is the WM going report rule:
	if Watson is present, say "[Watson possibly making noise when moving]Watson wanders in from [the opposite of the direction tried of Watson]. [run paragraph on]";
	if Watson was recently present, say "[Watson possibly making noise when moving]Watson [if the just-released item is not nothing]lets go of [the just-released item] and [end if]wanders off to [the direction tried of Watson]. [run paragraph on]".

[Watson reacting to other people]
To say Watson flinches: say "That makes Watson [one of]flinch[or]be all flinchy[or]give a little cry and cover his eyes[purely at random][note reacting-to-others].";

After someone (called sitter) sitting down:
	if the sitter is Watson, continue the action;
	if Watson and the sitter are not co-located, continue the action;
	if the sitter is present and not running silently begin;
		if the sitter announces a report in the Table of Sitting Down Report Rules begin;
			if 1 out of 6, say Watson flinches;
			otherwise say paragraph break;
		otherwise;
			report bug "Missing sitting report when Watson reacting.";
		end if;		
	end if.

After sitting down when Watson is present:
	if 1 out of 6, say "When you sit, Watson [one of]flinches[or]is all flinchy[or]gives a little cry and covers his eyes[purely at random][note reacting-to-others].";
	otherwise consider the standard report sitting down rule.

After Cassie dropping something when Watson and Cassie are co-located and not running silently (this is the Watson flinching at Cassie dropping rule):
	if Cassie is present and 1 out of 6 begin;
		if Cassie announces a report in the Table of Dropping Report Rules, say Watson flinches;
		otherwise report bug "Missing Cassie dropping report when Watson flinching.";
	otherwise;
		continue the action;
	end if.

Instead of showing or giving something to Watson:
	say "You show [the noun] to Watson and he kinda flinches and looks away."

WM hint-giving rules is a rulebook.

A WM hint-giving rule when Watson is carrying anything (this is the Watson sluffs off stuff in preparation rule):
	try Watson dropping a random thing carried by Watson;
	[Speed Watson up for this next bit]
	change the base act percentage of Watson to 100;
	change the act percentage of Watson to 100;
	rule succeeds.

A WM hint-giving rule (this is the Watson pulls up on a parent rule):
	let position be the location of Watson;
	if a parent person is in position begin;
		if Watson is present begin;
			let target be a random parent person in position;
			try Watson upstanding using target;
			change (machine corresponding to a baby name of Watson in the Table of Baby State Machines) to WM State Rules;
			change the base act percentage of Watson to 20;
			change the act percentage of Watson to 20;
		end if;
	otherwise;
		report bug "Watson isn't in the same room as a parent like he should be.";
	end if;
	rule succeeds.

WM state rules is a rulebook.

A WM state rule when Watson is carrying anything (this is the Watson plays with his inventory rule):
	if 1 out of 2 begin;
		if Watson is present begin;
			let target be a random thing carried by Watson;
			let switch-value be a random number from 1 to 4;
			if switch-value is 1, say "Watson [one of]gives [the target] a hug[or]hugs [the target][purely at random][if 1 out of 4], he is such a wuss[end if]";
			if switch-value is 2, say "Watson [one of]fiddles[or]plays[purely at random] with [the target]";
			if switch-value is 3, say "Watson tugs at [the target] as if he wants to take [it-them] apart";
			if switch-value is 4, say "Watson holds [the target] at arm's length as if it might bite him";
			say "[note randomness-sux].";
		end if;
	otherwise;
		try Watson dropping a random thing carried by Watson;
	end if;
	rule succeeds;

A WM state rule (this is the Watson pulls up on parents rule):
	if a parent person (called p) is in the location of Watson and 1 out of 4 begin;
		[Don't let a kid solve the Parry rocking puzzle]
		if p is not Parry or Playtime is not happening begin;
			try Watson upstanding using p;
			rule succeeds;
		end if;
	end if.

A WM state rule (this is the Watson picks up stuff rule):
	let position be the location of Watson;
	[It would be nice if this could be "if Watson is in the presence of a carryable thing"]		if a pickupable thing is in position and 2 out of 3 begin;
		let target be a random pickupable thing in position;
		try Watson taking target;
		rule succeeds;
	end if;

A WM state rule (this is the Watson wanders rule):
	if Watson is ready to leave, have Watson wander;

WM fussing rules is a rulebook.

[I'll borrow Fuss Count, a global variable that Zoe uses when she fusses in the first major scene, to keep track of when Watson should be picked up.]

A WM fussing rule (this is the Watson fussing to get in the swing rule):
	if Watson is present, say "Watson [one of]thumps his fists on the floor[or]grimaces horribly[or]holds his breath and his eyes bug out[purely at random] and then he gives a loud cry";
	otherwise say "You hear Watson cry loudly[if 1 out of 3], boy is he angry[end if]";
	[Don't let Watson go to the swing while Parry's tending Cassie (indicated by her being passive)]
	if Cassie is active and a random number from 1 to 10 is less than 2 plus fuss count begin;
		now the baby-swing is in the TV room;
		say ". Then Parry says, 'Farina, I'm going to borrow your swing and see if that calms Watson down.' He [if Parry is present]stands up and strides off[otherwise]goes clomping around[end if] and you can see him come back with a baby swing. He [if the baby-swing is present]comes over to you and Watson and the TV and puts the swing down and picks up Watson. He straps Watson in the swing and turns it on before he goes back[otherwise]goes over by the TV with it and then there is the picking up of Watson and the strapping in of Watson and the swinging of Watson[end if], I hope that makes Watson feel better";
		now Watson is in the baby-swing;
		now the baby-swing is on;
		[Reset Watson's state machine and let him sit passively]
		choose row with a baby name of Watson in the Table of Baby State Machines;
		change machine entry to WM State Rules;
		now Watson is passive;
		now Watson is calm;
		Watson stops swinging in four turns from now;
	otherwise;
		increase fuss count by 2;
	end if;
	say "."

At the time when Watson stops swinging:
	[Don't let Parry do anything with Watson while he's dealing with Cassie (indicated by Cassie being passive)]
	if Cassie is passive begin;
		Watson stops swinging in two turns from now;
	otherwise;
		now Watson is active;
		now the baby-swing is off;
		now Watson is in the TV room;
		if the baby-swing is present, say "A shadow falls on you, it is Parry. He turns off the swing and he unstraps Watson and puts Watson on the floor by you and then Parry goes back to sit down. Watson looks all calm again, that is good.";
		otherwise say "[if Parry is present]A shadow falls on you, it is Parry standing up. He[otherwise]You see Parry stand up and he[end if] goes over to where the TV is and the swing is and he does something that probably involves Watson before he sits down again.";
	end if.

Instead of giving a blox-thing to Watson:
	move the noun to Watson;
	abide by the Watson disassembles a held thing rule.

WM building rules is a rulebook.

A WM building rule when Watson and the brix blox bucket are not co-located:
	let way be the best route from the location of Watson to the location of the brix blox bucket;
	if way is a direction, try Watson going way;
	rule succeeds.

A WM building rule when the brix blox bucket is upright:
	if Watson is present, say "Watson [one of]gazes at the brix blox bucket[or]nudges the brix blox bucket thoughtfully[or]stares at the brix blox bucket[or]tries to look through the brix blox bucket to the brix blox inside it[or]is all lost in thought about the brix blox bucket[purely at random].";
	rule succeeds.

A WM building rule when the booklet is not in the location of the brix blox bucket:
	if Watson is present, say "Watson [one of]pokes at the pile of blox[or]puts a sun yellow blox and a rain forest green blox together but then takes them apart sadly[or]shoves some of the blox around[or]take a blox and chews it and puts it back down[purely at random], he looks like he is not sure what he wants to do.";
	rule succeeds.

A WM building rule when the booklet is not open:
	try Watson opening the booklet;
	rule succeeds.

A WM building rule when a blox-thing (called disassemblee) is held by Watson (this is the Watson disassembles a held thing rule):
	if Watson is present, say "Watson takes [the disassemblee] apart and dumps the brix into the big pile.";
	remove the disassemblee from play;
	rule succeeds.

A WM building rule when a blox-thing (called disassemblee) is in the location of Watson:
	if disassemblee is not held begin;
		if Watson is present, say "Watson picks up [the disassemblee] and takes it apart and dumps the brix into the big pile.";
		remove the disassemblee from play;
		rule succeeds;
	end if.

A WM building rule when a blox-thing is in play:
	if Watson is present, say "Watson tries to build a [page in row page-index of the Table of Instruction Pages] but he doesn't have enough blox[if 1 out of 4], he gets all grunty about that[end if].";
	rule succeeds.

Mentioning larping is a one-time deal.

A WM building rule:
	choose row page-index in the Table of Instruction Pages;
	if Watson is present, say "Watson looks at the instruction booklet and his hands move so fast they are almost blurry and the dayglo blox go together and suddenly Watson has built a [page entry]. He looks at it and then drops it[if 1 out of 3 and mentioning larping has not occurred], he is so going to grow up to be an engineer and like online games and in his spare time I bet he will LARP[end if].";
	move the associated object entry to the location of Watson;
	rule succeeds.

Section 4 - Jemison Thorsby

Jemison is a playmate. Jemison is male. The description is "Jemison Thorsby's name is not one you would want, that is for sure. He is [if Jemison is wearing the bumblebee costume]wearing this bumblebee costume though he is not flying around or anything[otherwise]stylish in his blue onesie[end if] and he has a small mouth and these big eyes that are watching, always watching, it is kinda creepy[if Jemison is carrying something]. He is carrying [a list of things carried by Jemison][end if][if Jemison is fixated]. He is clearly all interested in [focus of Jemison][end if]." The base act percentage is 10. The act percentage is 10. The act increment is 10. Understand "Thorsby" or "baby" as Jemison.

Check smelling Jemison: instead say "Jemison smells like zinc oxide."

Jemison can be susceptible to fixation or impervious to fixation. Jemison is susceptible to fixation.

Jemison wears a blue onesie. The description of the blue onesie is "It is the color of the endless sky." The blue onesie is small.

Some velcro jangly balls are a small wearable thing. The description is "They are all silver and jangly with velcro on the end of them[if someone has the velcro jangly balls and the player does not have the jangly balls] and they dangle off of [a random jangly person][end if]." The velcro jangly balls are ambiguously plural. Understand "silver" or "ball" as the velcro jangly balls.

Instead of waving the velcro jangly balls, say "They go jangle jangle jangle."

A report going rule when the player has the velcro jangly balls:
	say "You [one of]are all jangle jangle jangle as you leave[or]jangle your way out of the room[purely at random]."

To say (youngster - a person) possibly making noise when moving:
	if youngster has the velcro jangly balls begin;
		if youngster is present, say "You hear this jangle jangle jangle and then [run paragraph on]";
		if youngster was recently present, say "There is this jangle jangle jangle and [run paragraph on]";
	end if.

Definition: a playmate is jangly if it has the velcro jangly balls.

After taking off the velcro jangly balls when the player is overburdened:
	silently try dropping the velcro jangly balls;
	if the player is gnawing the velcro jangly balls, now the velcro jangly balls are gnawed by nothing;
	continue the action.

After taking off the velcro jangly balls:
	say "The bells go rrrrrrrrip and pull off[if the velcro jangly balls are not held] and you let them fall to the floor[end if].";
	if the player is gnawing the velcro jangly balls, now the velcro jangly balls are gnawed by nothing.

After someone (called ripper) taking off the velcro jangly balls:
	if the ripper and the player are co-located, instead say "[The ripper] pulls on the jangly balls until they come off."

Instead of taking the velcro jangly balls when the velcro jangly balls are worn by someone (called jangler) (this is the taking the balls rule):
	abide by the can't take small things when burdened rule;
	consider the carry out taking rulebook;
	say "You tug on the velcro jangly balls and you tug some more and they come off of [the jangler] with a rrrrrrrip sound";
	if jangler is Jemison and Jemison is fixated on the velcro jangly balls, say " and Jemison looks at you with sad sad eyes and then stares at the velcro balls[note Jemisons-bells]";
	say ".";
	if someone is gnawing the velcro jangly balls, now the velcro jangly balls are gnawed by nothing.

Instead of removing the velcro jangly balls from someone, try taking the velcro jangly balls.

[Make sure all of our "instead" rules for taking are consulted before the "taking the balls" rule]
The taking the balls rule is listed after the can't take large things rule in the instead rulebook.

Instead of putting the velcro jangly balls on the player, try wearing the velcro jangly balls.

Instead of putting the velcro jangly balls on something worn by someone (called target), try putting the velcro jangly balls on target.

Making Zoe jingle-jangle is a one-time deal.

Instead of putting the velcro jangly balls on a playmate (called target):
	abide by the can't put what's not held rule;
	say "You shove the velcro end of the balls against [the target] high on [its-their of target] back [if the target is not Jemison]where [it-they] can't reach [end if]and the velcro sticks!";
	move the velcro jangly balls to target;
	now the velcro jangly balls are worn by the target;
	if someone is gnawing the velcro jangly balls, now the velcro jangly balls are gnawed by nothing;
	if target is Zoe and making Zoe jingle-jangle has not occurred, record 5 points for "putting Jemison's velcro jangly balls on Zoe".

Instead of putting the velcro jangly balls on a parent person, say "That is a good way to get those taken away, you'd better not."

Instead of putting the velcro jangly balls on the diaper, try wearing the velcro jangly balls.

Instead of putting the velcro jangly balls on something worn by someone (called p), try putting the velcro jangly balls on p.

Instead of putting the velcro jangly balls on something, say "The velcro jangly balls are for putting on people."

After pre-writing a paragraph about a jangly playmate (called jangler): say "The velcro jangly balls hang off of [it-them of the jangler]. [run paragraph on]".

[Every time Jemison takes the balls, he wears them.]
After Jemison taking the velcro jangly balls:
	if the velcro jangly balls are carried by Jemison, now the velcro jangly balls are worn by Jemison;
	continue the action.

Rule for pre-writing a paragraph about Jemison during Tabletime: rule succeeds.

Rule for pre-writing a paragraph about Jemison when Jemison is wearing the bumblebee costume:
	say "Jemison is all dressed like a bumblebee. [run paragraph on]".

Rule for pre-writing a paragraph about Jemison when Jemison is pulled up on something (called the support):
	say "Jemison holds on to [the support] tenaciously";
	if something (called the focus) fascinates Jemison begin;
		if the location of Jemison encloses the focus, say " though he keeps staring at [the focus][if the focus is held by the player] that you are all holding";
	end if;
	say ". [run paragraph on]".

Rule for pre-writing a paragraph about Jemison when something (called the focus) fascinates Jemison:
	if the location of Jemison encloses the focus begin;
		say "Jemison is all staring at [the focus]";
		if the focus is carried by someone (called the carrier), say " which [name of carrier is] holding";
		say ". [run paragraph on]".
	otherwise;
		say "Jemison is looking around sadly for [the focus]. [run paragraph on]";
	end if.

Rule for pre-writing a paragraph about Jemison when Jemison is gnawing something (called the mastication):
	say "Jemison is all chewing on [the mastication]. [run paragraph on]";

Rule for pre-writing a paragraph about Jemison when Jemison is carrying something (called the item):
	say "Jemison is [one of]playing[or]toying[purely at random] with [the item]. [run paragraph on]";

Rule for pre-writing a paragraph about Jemison:
	say "Jemison watches you watching him. [run paragraph on]";

Instead of giving something to Jemison when Jemison is fixated:
	if the noun fascinates Jemison begin;
		move the noun to Jemison;
		say "You give [the noun] to Jemison and he grabs [it-them of the noun] out of your hands";
		if the noun is the velcro jangly balls begin;
			say " and sticks them onto his onesie";
			now Jemison wears the noun;
		otherwise;
			now the noun does not fascinate Jemison;
		end if;
		say ".";
	otherwise if Jemison is susceptible to fixation;
		instead try showing the noun to Jemison;
	otherwise;
		continue the action;
	end if.

Instead of someone giving something to Jemison when Jemison is fixated:
	if the noun fascinates Jemison begin;
		if Jemison is present, say "[The person asked] shows [the noun] to Jemison and he grabs [it-them of the noun] without hesitation[if the noun is the velcro jangly balls] and sticks them onto his onesie[end if].";
		move the noun to Jemison;
		if the noun is the velcro jangly balls,
			now Jemison wears the velcro jangly balls;
		otherwise now the noun does not fascinate Jemison;
	otherwise;
		continue the action;
	end if.

Instead of showing something to Jemison when Jemison is fixated:
	if the noun fascinates Jemison begin;
		say "He makes this motion towards [the noun] and you pull [it-them of the noun] away, ha ha, this is like taking candy away from a you.";
	otherwise if Jemison is susceptible to fixation;
		if Jemison clears all inventory begin;
			now the noun fascinates Jemison;
			say "Jemison gets all excited and [if some thing is marked for special listing][drops] [a list of things that are marked for special listing] and [end if]starts watching you and [the noun].";
		otherwise;
			report bug "Jemison should switch fixation to [the noun] but didn't.";
		end if;
		now every thing is unmarked for special listing;
	otherwise;
		continue the action;
	end if.

Instead of showing something to Jemison when Jemison is not fixated and Jemison is susceptible to fixation:
	if Jemison clears all inventory begin;
		now the noun fascinates Jemison;
		say "Jemison gets all excited and [if some thing is marked for special listing][drops] [a list of things that are marked for special listing] and [end if]starts watching you and [the noun].";
	otherwise;
		report bug "Jemison should be all interested in [the noun] but isn't.";
	end if;
	now every thing is unmarked for special listing.

To check on Jemison following (youngster - a person):
	if Jemison and the pile of Cheerios are not co-located begin;
		if Jemison is ready to act and Jemison acts with a bonus of 20 begin;
			now Jemison is busy acting;
			try Jemison following youngster;
			now Jemison is done acting;
			now every thing is unmarked for listing;
		end if;
	end if.

Instead of showing something to Jemison when Jemison is not fixated and Jemison is not susceptible to fixation: say "Jemison doesn't really pay attention to [the noun], I guess he has had his fill of being obsessed about stuff."

To decide if Jemison snatches (item - a thing) that (youngster - a person) discarded:
	[If we're running silently, don't have Jemison grab the object.]
	if running silently, decide no;
	if Jemison and the pile of Cheerios are co-located, decide no;
	if Jemison is ready to act and Jemison acts with a bonus of 20 begin;
		now Jemison is busy acting;
		if Jemison is pulled up on something (called the support) begin;
			silently try Jemison sitting down;
			if Jemison is standing, decide no;
		end if;
		if Jemison clears all inventory begin;
			silently try Jemison taking item;
			if Jemison is carrying the item and Jemison is present begin;
				if 1 out of 3, say "Jemison sees [name of youngster] [drop] [the item] and is all excited[if 1 out of 3], at least as excited as he gets,[end if] and ";
				otherwise say "When Jemison sees [name of youngster] [drop] [the item] he ";
				if the just-released item is not nothing, say "lets go of [the just-released item] and ";
				if some thing is marked for special listing, say "[drops] [a list of things that are marked for special listing] and takes [the item] instead";
				otherwise say "takes [it-them of the item]";
				if the item is worn, say " and sticks [it-them of the item] on his onesie";
				say "[note reacting-to-others].";
				[leave this next statement here so we don't double-print the list of dropped items]
				now every thing is unmarked for special listing;
			end if;
		end if;
		if some thing is marked for special listing begin;
			if Jemison is present, say ". Jemison [if the just-released item is not nothing]lets go of [the just-released item] and [end if][drops] [a list of things that are marked for special listing].";
			now every thing is unmarked for special listing;
		otherwise if the just-released item is not nothing and Jemison is present;
			say ". Jemison lets go of [the just-released item] and eases to the floor.";
		end if;
		now Jemison is done acting;
		reset Jemison;
		decide yes;
	end if;
	decide no.

This is the JT taking report rule:
	say "Jemison ";
	if the just-released item is not nothing, say "lets go of [the just-released item] and eases to the floor and ";
	if some thing is marked for special listing begin;
		say "[drops] [a list of things that are marked for special listing] and ";
		now every thing is unmarked for special listing;
	end if;
	say "[if 1 out of 4]carefully [end if]picks up [the noun][if 1 out of 4] and blinks at it[otherwise if 1 out of 4], he is all deliberate about it[end if][if the noun is worn] and then sticks [the noun] onto his onesie[end if]. [run paragraph on]"

After Jemison taking something that fascinates Jemison:
	if not running silently and Jemison is present, say "Jemison suddenly sees that [the noun] is available and he scoops [it-them of the noun] up[if the noun is worn] and sticks [it-them] on his onesie[end if].";
	if the noun is not the velcro jangly balls, now the noun does not fascinate Jemison.

Report Jemison dropping: if Jemison is present, say "Jemison [drops] [the noun][if 1 out of 5] and it is now on the ground[end if][note randomness-sux]."; stop the action.

Report Jemison chewing: if Jemison is present, say "Jemison [one of]puts his mouth all over [the noun][or]chews on [the noun][purely at random][if 1 out of 5]. He looks pretty thoughtful when he does that[end if][note randomness-sux]."; stop the action.

Report Jemison stopping chewing: if Jemison is present, say "Jemison [one of]takes [the noun] that is in his mouth and pulls it[or]pulls [the noun][purely at random] out of his mouth[note randomness-sux]."; stop the action.

Report Jemison going a direction (called goal):
	if Jemison was recently present, say "[Jemison possibly making noise when moving]Jemison [if the just-released item is not nothing]lets go of [the just-released item] and [end if][one of]goes off[or]crawls[purely at random] to [the goal].";
	if Jemison is present, say "[Jemison possibly making noise when moving]Jemison [one of]comes[or]crawls[purely at random] in from [the opposite of the goal].";
	stop the action.

[Jemison reacting to other people]
After dropping a thing (called item) when Jemison is present and Jemison is not fixated:
	if Jemison snatches the item that the player discarded, do nothing;
	otherwise continue the action.

After someone (called dropper) dropping a thing (called item) when Jemison is not fixated:
	if the dropper is Jemison, continue the action;
	if Jemison and the dropper are not co-located, continue the action;
	if the dropper is present and not running silently begin;
		if the dropper announces a report in the table of Dropping Report Rules begin;
			do nothing;
		otherwise;
			change hacky global item to dropper;
			report bug "[The hacky global item] is missing a dropping report rule when Jemison trying to take something someone just dropped.";
		end if;
	end if;
	if Jemison snatches the item that dropper discarded begin;
		do nothing;
	otherwise if the dropper is present and not running silently;
		say paragraph break;
	end if.

After someone (called leader) going a direction (called goal) when Jemison is not fixated:
	if the leader is Jemison, continue the action;
	if the location of Jemison is not the last location of the leader, continue the action;
	if not running silently begin;
		if leader is present or leader was recently present begin;
			if leader announces a report in the table of Going Report Rules begin;
				say paragraph break;
			otherwise;
				change hacky global item to the leader;
				report bug "Missing going report rule when Jemison trying to follow [hacky global item].";
			end if;
		end if;
	end if;
	check on Jemison following leader.

Before Jemison following someone:
	if Jemison clears all inventory begin;
		do nothing;
	otherwise;
		if some thing is marked for special listing begin;
			if Jemison is present, say "Jemison [drops] [a list of things that are marked for special listing].";
			now every thing is unmarked for special listing;
		end if;
		stop the action;
	end if.

After Jemison following someone (called leader) when not running silently: 
	if Jemison is present begin;
		say "Jemison comes in after [name of leader]";
		if something (called the focus) fascinates Jemison and the leader carries the focus, say " and settles close by [it-them] and looks pointedly at [the focus]";
		say ".";
	otherwise if Jemison was recently present;
		if some thing is marked for special listing begin;
			say "First Jemison [drops] [a list of things that are marked for special listing]. Then he[run paragraph on]";
			now every thing is unmarked for special listing;
		otherwise;
			say "Jemison[run paragraph on]";
		end if;
		say " [one of]follows[or]trails after[purely at random] [name of leader].";
	end if;
	reset Jemison.

[Jemison being fixated on something rules]
After taking something when Jemison is susceptible to fixation and Jemison is present:
	if the noun is your favorite toy, continue the action;
	if the noun fascinates Jemison, continue the action;
	if Parry encloses Jemison, continue the action;
	if Jemison clears all inventory begin;
		say jemison enspelled by player taking the noun;
		now the noun fascinates Jemison;
	otherwise;
		consider the standard report taking rule;
		if some thing is marked for special listing begin;
			say "Jemison [drops] [a list of things that are marked for special listing].";
		end if;
	end if;
	now every thing is unmarked for special listing;
	reset Jemison;
	now Jemison is done acting.

After someone (called taker) taking when Jemison is susceptible to fixation and Jemison is not fixated:
	let print-flag be 0;
	if the taker is Jemison, continue the action;
	if the taker and Jemison are not co-located, continue the action;
	if the taker is present begin;
		if the taker announces a report in the table of Taking Report Rules begin;
			change print-flag to 1;
		otherwise;
			change hacky global item to the taker;
			report bug "[The hacky global item] is missing an entry in the Table of Taking Report Rules";
		end if;
	end if;
	now Jemison is busy acting;
	if Jemison clears all inventory begin;
		if Jemison is present, say Jemison enspelled by the taker taking the noun;
		now every thing is unmarked for special listing;
		now the noun fascinates Jemison;
	otherwise if some thing is marked for special listing;
		if Jemison is present, say "[if print-flag is 1][paragraph break][end if]Jemison [drops] [a list of things that are marked for special listing].";
		now every thing is unmarked for special listing;
	otherwise if print-flag is 1;
		say paragraph break;
	end if;
	now Jemison is done acting;
	reset Jemison.

After dropping something that fascinates Jemison (this is the Jemison grabbing dropped items that fascinate rule):
	if Jemison is not present, continue the action;
	if Jemison clears all inventory begin;
		silently try Jemison taking the noun; [Jemison taking will reset the fascination.]
		if Jemison is not holding the noun begin;
			abide by the report dropping rulebook;
		otherwise if Jemison is present;
			say "When you drop [the noun] Jemison is all over that and he ";
			if some thing is marked for special listing begin;
				say "[drops] [a list of things that are marked for special listing] and snatches up [the noun].";
			otherwise;
				say "snatches [it-them of the noun] up";
				if the noun is the velcro jangly balls begin;
					say " and sticks the jangly balls to his onesie";
					now the noun is worn by Jemison;
				end if;
				say ".";
			end if;
		end if;
		now every thing is unmarked for special listing;
		reset Jemison;
	otherwise;
		report bug "Jemison wasn't able to clear inventory to pick up [the noun].";
	end if.

After someone (called the dropper) dropping something that fascinates Jemison (this is the Jemison grabbing NPC-dropped items that fascinate rule):
	if running silently, continue the action;
	if the dropper encloses the noun, continue the action;
	if the dropper and Jemison are not co-located, continue the action;
	if the dropper is present begin;
		if the dropper announces a report in the table of Dropping Report Rules begin;
			do nothing;
		otherwise;
			change hacky global item to the dropper;
			report bug "[The hacky global item] is missing an entry in the Table of Dropping Report Rules";
		end if;
	end if;
	now Jemison is busy acting;
	if Jemison clears all inventory begin;
		try Jemison taking the noun;
	otherwise if the dropper is present;
		say paragraph break;
	end if;
	now Jemison is done acting;
	reset Jemison.

[Don't let Watson reacting to Cassie interfere with fixation.]
The Watson flinching at Cassie dropping rule is listed after the Jemison grabbing dropped items that fascinate rule in the after rulebook.

After going a direction (called goal) when Jemison is fixated:
	if the location of Jemison is not the last location of the player, continue the action;
	if Jemison is not fixated on the player, continue the action;
	consider the report going rulebook;
	if Snacktime is happening, consider the stalking Zoe rule;
	try Jemison trying following the player;
	reset Jemison.

After someone (called the leader) going a direction (called goal) when Jemison is fixated:
	if the location of Jemison is not the last location of the leader, continue the action;
	if Jemison is not fixated on the leader, continue the action;
	if the leader is present or the leader was recently present begin;
		if the leader announces a report in the table of Going Report Rules begin;
			say paragraph break;
		otherwise;
			change hacky global item to the leader;
			report bug "[The hacky global item] is missing a report in the Table of Going Report Rules.";
		end if;
	end if;
	try Jemison following the leader;
	reset Jemison.

To say Jemison enspelled by (taker - a person) taking (item - a thing):
	say "When Jemison sees [name of taker] [one of]take[or]pick up[purely at random] [the item], ";
	if 3 out of 4, say "he gets all excited and ";
	otherwise say "his eyes light up [if 1 out of 8](metaphorically, it's not like his eyes are flashlights or anything) [end if]and ";
	if something (called the focus) fascinates Jemison, say "he forgets all about [the focus] and ";
	say "he [if some thing is marked for special listing][drops] [a list of things that are marked for special listing] and [end if]starts watching [the item] and thus [the taker][note jemison-fascinated].".

JT state rules is a rulebook.

A JT state rule when the velcro jangly balls fascinate Jemison and Jemison has the velcro jangly balls (this is the Jemison plays with his jangly balls rule):
	if Jemison is present, say "Jemison [one of]jiggles the jangly balls[or]reaches around to the jangly balls and pats at them[or]pats the jangly balls[purely at random] and [one of]they jangle quietly[or]they go jangle jangle jangle[purely at random][if 1 out of 5] and Jemison is all smiles[end if].";
	rule succeeds.

A JT state rule when the velcro jangly balls fascinate Jemison and Jemison does not have the velcro jangly balls and Jemison and the velcro jangly balls are co-located (this is the Jemison plays with someone else's jangly balls rule):
	if 1 out of 3 begin;
		if Jemison is present begin;
			if someone (called p) has the velcro jangly balls and a random jangly person is eighth-height-or-higher, say "Jemison looks sadly at the velcro jangly balls, they are out of his reach.";
			otherwise say "Jemison reaches out and [one of]bats at[or]hits[or]pokes at[purely at random] the velcro jangly balls[if someone has the velcro jangly balls and the player does not have the jangly balls] that are hanging off of [a random jangly person][end if].";
		end if;
		rule succeeds;
	end if.

A JT state rule when something (called the focus) fascinates Jemison (this is the Jemison follows fixated objects rule):
	let origin be the location of Jemison;
	if the location of the focus is not the origin begin;
		let the destination be the location of the focus;
		let aim be the best route from the origin to the destination;
		if aim is a direction, try Jemison going aim;
		rule succeeds;
	end if.

A JT state rule when something (called the focus) fascinates Jemison (this is the Jemison takes fixated objects rule):
	if the focus and Jemison are co-located and the focus is not had by someone begin;
		try Jemison taking the focus;
		rule succeeds;
	end if.

A JT state rule when something (called the focus) fascinates Jemison (this is the Jemison stares at fixated objects rule):
	if Jemison is present and the location of Jemison encloses the focus begin;
		if the focus is had by someone (called the carrier) begin;
			if 2 out of 3, say "Jemison cannot look away from [name of carrier] and [the focus].";
			otherwise say "Jemison stares at [name of carrier] and waits for [it-them] to drop [the focus].";
		otherwise;
			say "Jemison stares at [the focus] with this eerie fixated gaze.";
		end if;
	end if;
	rule succeeds.

A JT state rule when Jemison is carrying anything (this is the Jemison playing with inventory rule):
	if 1 out of 3 begin;
		if Jemison is gnawing anything (called the mastication) begin;
			try Jemison stopping chewing the mastication;
		otherwise;
			try Jemison chewing a random thing carried by Jemison;
		end if;
	otherwise if 1 out of 2;
		if Jemison is present, say "Jemison stares at [a random thing carried by Jemison], he is all fascinated with it[note randomness-sux].";
	otherwise;
		try Jemison dropping a random thing carried by Jemison;
	end if;
	rule succeeds;

A JT state rule (this is the Jemison picking up or chewing on stuff rule):
	if 2 out of 3 begin;
		let position be the location of Jemison;
		[It would be nice if this could be "if...Jemison is in the presence of a chewable thing"]
		if 1 out of 2 and a chewable worth noting thing is in position begin;
			let target be a random chewable worth noting thing in position;
			try Jemison chewing the target;
			rule succeeds;
		[It would be nice if this could be "if Jemison is in the presence of a carryable thing"]
		otherwise if a pickupable thing is in position;
			let target be a random pickupable thing in position;
			try Jemison taking target;
			rule succeeds;
		end if;
	end if;
			
A JT state rule (this is the Jemison wanders rule):
	if Jemison is ready to leave, have Jemison wander;

VOLUME 5 - On Scenes

Chapter 0 - Suffocation

Suffocation is a scene. Suffocation begins when play begins. Suffocation ends happily when the number of filled rows in the Table of Suffocating Messages is 0. Suffocation ends badly when the player is upset.

Every turn during Suffocation:
	repeat through the Table of Suffocating Messages
	begin;
		say "[message entry][paragraph break]";
		blank out the whole row;
		rule succeeds;
	end repeat.

Table of Suffocating Messages
Message
"You are trying not to breathe fast, it is hard when you are so closed in."
"The darkness is all scratchy on your skin."
"This is oppressive, you are being held down by the man."
"Suddenly the darkness is gone, and it is so light, you are blinded for just a second. You blink and blink and then you can see the mom holding the blanket she was covering you with. 'Peekaboo!' she shouts and she is all grinning.

So I guess you were being held down by the woman.[line break][banner text]"

When Suffocation ends:
	now the player is calm;
	move the player to Intro Middle;

Chapter 1 - Goading

Goading is a scene. Goading begins when Suffocation ends. Goading ends when the player is pulled up on the footstool or the player is pulled up on the mom.

When Goading begins:
	say "You eye your favorite toy and the mom sees you doing it and scoops it up. 'No, no, we're going to try pulling up!' She is all the time trying to build your character or something. She waves the toy at you. 'Look! Look where I'm putting it!' She drops the toy on the footstool where you can't see it unless you're all pulled up, that is just no fair[note pulling-up].";
	now your favorite toy is on the footstool.

The introduction turn count is a number that varies. The introduction turn count is 0.

Every turn during Goading:
	increment the introduction turn count by 1;
	if the introduction turn count is less than 3, rule succeeds;
	change the introduction turn count to 0;
	choose row chatter count in the Table of Encouraging Messages;
	say "[message entry][paragraph break]";
	increment the chatter count by 1;
	if the chatter count is greater than the number of rows in the Table of Encouraging Messages
	begin;
		change the chatter count to 1;
		sort the Table of Encouraging Messages in random order;
	end if;

Table of Encouraging Messages
Message
"The mom sits there looking at you all expectantly."
"The mom points to the footstool. 'Give it a try! Go ahead! Pull up!'"
"The mom sighs and is all 'You were pulling up just yesterday[note disappointments].'"
"The mom mimes holding onto the footstool and lifting herself with it."

When Goading ends:
	say "The mom applauds, 'Way to go!' She looks at her watch and is all 'Whoops! Playgroup's about to arrive. You hang out here while mom goes and gets ready.' She stands up [if the player is pulled up on the mom]and dumps you on your diaper [end if]and steps over all of those pillows and you are stuck here now by yourself[note baby-containment].";
	if the player is pulled up on the mom begin;
		let go of the mom;
		record 5 points for "pulling up on the mom";
	otherwise;
		record 5 points for "pulling up on the footstool";
	end if;
	remove the mom from play;

Chapter 2 - Introduction

Introduction is a scene. Introduction begins when Goading ends. Introduction ends when the player has your favorite toy.

When Introduction ends:
	record 10 points for "getting your favorite toy the first time";
	say "There is a ding-dong and the mom comes clattering in all 'Hang on! I'm coming!' She opens the door and there is Marion Kinmundy here for playgroup. She is carrying that little red haired girl Zoe who started coming to playgroup a couple of whiles ago. Zoe sees your favorite toy and her eyes light up.

The mom must be psychic or something because she whirls around and snatches up your favorite toy. 'Oh, no,' she says with a little laugh, 'we're not having you two fight over this thing again. I'll tuck it away where it'll be safe.' And Marion laughs too, which is cool, that way they don't notice you and the little red haired girl glaring at each other.

She wants your toy? And the mom wants to lock the toy away? Bring it on[note antagonist].";
	if testing is not in progress, wait for any key.

Chapter 3 - Playtime

Playtime is a scene. Playtime begins when Introduction ends. Playtime ends when the player has your favorite toy.

When Playtime begins:
	display chapter head "Chapter 1" with subhead "Playtime";
	now Zoe is active;
	say "Right behind Marion and Zoe the little red haired girl is Lisa who is Cassie's mom. Lisa is carrying Cassie all carefully but Cassie is squirming to get down. 'Hi Farina, hi Marion--hang on Cassie, mommy's going to put you down,' Lisa says and then she steps over the baby gate and dumps Cassie by the couch to the south of you.

Meanwhile the mom is putting all the pillows back on the couch and loveseat. As she finishes she asks Marion, 'Can I put this toy in Zoe's backpack for now?' Marion is all 'Sure!' as she puts Zoe on the floor over by the rocking chair and sits on the loveseat.";
	if the player is gnawing something (called g), now g is not gnawed by anything;
	remove your favorite toy from play;
	remove the squishy ball from play; [Cassie will get this in a bit.]
	if the squishy ball is in the Intro Middle, move the squishy ball to the Middle of the Living Room;
	if the plush book is in the Intro Middle, move the plush book to the Middle of the Living Room;
	change the chatter list table to the Table of Playtime-Chatter.

After going a direction when the mom is not in play during Playtime:
	consider the report going rulebook;
	now the mom is in the Middle of Couch;
	now Becca is in South End of Loveseat;
	now Parry is in By the Rocking Chair;
	now Watson is in the South End of Loveseat;
	now Jemison is in South End of Loveseat;
	say "There is a ding-dong! and the mom goes and opens the front door. It is Becca and her son Jemison and some strange guy is there holding Watson Mason. 'Come on in, everyone,' the mom says. She turns to the strange guy, 'I'm glad you could make it, Parry[note parry-entering].'

Parry shrugs and smiles and says, 'I always wondered what everyone did during playgroup.'

'Drink mimosas[note drink-mimosas]!' Becca says over her shoulder to Parry and there is polite laughter and Parry smiles like he is kinda unsure about all of this.

'Why don't you put Watson in here with the others?' the mom asks and Parry nods. He ends up putting Watson by Jemison over by the loveseat. Then all the standing adults sit down [if the mom and the player are co-located]with the mom sitting near you [otherwise if Parry and the player are co-located]with Parry sitting near you [end if]so now I guess they are sitting adults.";
	change the chatter status to ready to act.

When Playtime ends:
	record 10 points for "getting your favorite toy the second time";
	say "Parry says, 'Okay, down you go,' and he gently lowers you to the floor, right next to Zoe. Zoe looks over and sees what you have in your hands and she goes totally nuts, she is shrieking and waving her fists and if she had Force powers you would totally be choked right now.

And of course Zoe acting out gets Marion and the mom to pay attention and Marion is all 'Hey, hey, Zoe, calm down!' and the mom is all 'Oh, gosh, they managed to get that toy out of the backpack.' So the jig is up where by 'the jig' I mean the toy and by 'up' I mean going away.";
	cut chatter short;
	[Reset Cassie's state machine, in case she never gave her hint]
	choose row with a baby name of Cassie in the Table of Baby State Machines;
	change machine entry to CS State Rules;
	change the base act percentage of Cassie to 20;
	change the act percentage of Cassie to 20;
	if testing is not in progress, wait for any key.

Table of Playtime-Chatter
Convo
Table of Parry-Name-Chatter
Table of Shari-Chatter
Table of Mimosa-Chatter
Table of Breastfeeding-Chatter
Table of Watson-Rash-Chatter

Table of Parry-Name-Chatter
Chatter
"'Hey, I just realized,' Becca says all loud[if Becca is present] above you[end if], 'Your name is Parry Mason!'"
"'Um, yes?' Parry says."
"'Cool, just like the detective!' Becca says."
"Parry [if the player is enclosed by Parry]blushes red and [end if]says, 'My name's actually spelled--'[paragraph break]'No, I think the original Perry Mason was a lawyer,' Lisa says."
"Parry clears his throat. 'The lawyer spells his name with an e--'[paragraph break]'Right, lawyer, that's what I meant,' Becca tells Lisa."
"'Let Parry finish,' the mom says[if the mom is present] from above[end if], but Parry just goes, 'No, that's okay.'"

Table of Shari-Chatter
Chatter
"Marion asks Parry, 'How is Shari doing?'"
"'Oh, my God, Parry and Shari!' laughs Becca. 'I hadn't realized that--that's just too precious.'"
"'She's feeling better,' Parry says. 'She's keeping food down now, at least.'"
"Marion asks, 'Oh, are you two expecting?'"
"Parry is all, 'Oh, no, no, no. No. It's just the flu.'"
"Becca goes, 'You two should give Watson a brother. Toughen him up.'"
"'Can you imagine having two?' Lisa laughs, and it's not a calm laugh, more a desperate laugh, 'I'd go crazy.'"

Table of Mimosa-Chatter
Chatter
"Becca says to the mom, 'Nice job on those mimosas.'[paragraph break]'Thanks,' the mom replies."
"Lisa says, 'It's nice, they help me calm down.'"
"'And they're good for you,' Marion says."
"'I'm not sure they're that good for you,' Parry says."
"Marion says, 'They do have orange juice in them.'"
"'Besides, if it tastes good, it's good for you,' says Becca."

Table of Breastfeeding-Chatter
Chatter
"Lisa all of a sudden busts out with 'I know I asked this last time but does anyone else's boobs hurt? Because here lately feeding Cassie is murder.'"
"'Mine don't,' Parry says quietly, but Becca is all 'That's why I got Jemison on solids as soon as I could.'"
"The mom asks Lisa, 'Have you tried lanolin?'"
"'You and your remedies, Farina,' Lisa says. 'Lanolin this and cabbage leaves that.'"
"'It makes me glad Zoe was so good at latching on,' Marion says. Grr, Zoe."
"Lisa sighs and says, 'Maybe I'll try the lanolin,' and the mom says, 'Let us know how it works.'"

Table of Watson-Rash-Chatter
Chatter
"'Say, how's Watson's rash doing?' Lisa asks Parry.[paragraph break]Parry clears his throat. 'He's much better, thanks.'"
"The mom says, 'Did you use zinc oxide cream?'"
"'In a roundabout way,' Parry says, 'We used Boudreaux's butt paste[note butt-paste].'"
"'Ew,' Becca laughs, 'that is the grossest name for a product.'"
"Marion tells Becca, 'Given what we have to deal with from our children, a name like that is the least of our worries.'"

Chapter 4 - Tabletime

Tabletime is a scene. Tabletime begins when Playtime ends. Tabletime ends when the number of filled rows in the Table of Tabletime-Cheerio-Reaching is 0.

When Tabletime begins:
	[Clear out everyone's stuff]
	set the kids to a ground state;
	now every playmate is passive;
	now every playmate is in At the Table;
	now every parent person is in At the Table;
	remove the backpack from play;
	remove the plain white onesie from play;
	remove the fog of grump from play;
	display chapter head "Chapter 2" with subhead "Snacktime";
	sort the Table of Tabletime-Baby-Chatter in random order;
	change chatter count to 0;
	say "And it's just like you expected. The mom scoops you and the toy up and the two of you dangle in mid-air while she removes the baby gate by the loveseat and goes around behind the loveseat. There's a cabinet back there, she pushes down the baby lock at the top of the cabinet door and opens the door and in goes your toy.

You would be all into getting the toy back except everyone decides now is the time to have a snack so a bunch of toys get piled in that small open area in the living room and you and Zoe and Cassie and Watson and Jemison are dragged into the kitchen and out comes the food.";
	move the player to At the Table.

When Tabletime ends:
	change chatter count to 0;
	say "Finally all of this feeding is over and the Cheerios get put away and everyone gets wiped up with baby wipes before getting dumped back in the living room."

At the Table is a room. "Your high chair is not way super high since it only gets you up to the level of the kitchen table but that is still pretty high. It lets you see around the table which is better than when you are on the floor and cannot even see the top of the table[note purpose-of-interludes]."

Instead of rocking in At the Table, say "You rock and rock and try to tip your chair over but it is too safely designed. The mom must read Consumer Reports or something."

Instead of waving the high chair, try rocking.

Instead of going or entering or exiting or getting off in At the Table, say "You are all strapped into your high chair so you are going nowhere."

Instead of doing something other than examining or smelling to something during Tabletime:
	if the noun is the player or the high chair or the food spoon, continue the action;
	if the player encloses the noun, continue the action;
	say "With you strapped into your high chair you cannot reach [the noun]."

[All playmates have a pre-writing a paragraph rule that does nothing during Tabletime. By hooking onto the end of Jemison's rule, we can describe them all.]
After pre-writing a paragraph about Jemison during Tabletime: say "They are all being fed. [run paragraph on]"

Your high chair is scenery in At the Table. The description is "The high chair is what holds you up when it is time to eat and it has straps that hold you in." Understand "strap" or "straps" as your high chair. Instead of opening your high chair, say "The chair straps are too complex for your tiny fat fingers." Instead of climbing your high chair, try exiting. Before taking off your high chair, try opening your high chair instead.

Prune-tasting is a one-time deal.

The food spoon is in At the Table. "In front of you is this spoon full of food that the mom wants you to eat." The description is "The spoon [if prune-tasting is not over]looks like it [end if]is all full of chopped [if prune-tasting is not over]nastiness. Prunes, maybe[otherwise]prunes. Bleeeeeeeach[end if]." Understand "prune" or "prunes" as the food spoon.

Instead of smelling the food spoon, say "It has this fruity smell to it." Instead of pushing or pulling or taking or attacking or touching the food spoon, say "You reach for it but the mom moves it away and says 'Ah ah ah! No hands! Eat it, don't play with it!'" [This next bit needs to be "before" to override the taking requirements.] Before eating or chewing or tasting the food spoon: say "You open your mouth and the mom spoons in the prunes[if 1 out of 3] and they slide down your throat[end if]. Yeuch. And if that isn't bad enough, the spoon goes away and comes back with more prunes[if prune-tasting is not over]! You are just like Sisyphus here.[otherwise]![end if]"; now prune-tasting is over; stop the action.

Instead of spitting up when the player is in At the Table, say "You start to spit up but the mom is right there wiping up your spit."

Instead of spitting up on something when the player is in At the Table, try spitting up.

The food-bite is scenery in At the Table. Setting action variables for doing something to the food-bite when not taking: change the noun to the food spoon. Before taking the food-bite, try eating the food spoon instead. Understand "bite" as the food-bite.

The kitchen table is scenery in At the Table. "It is round and wooden." Understand "round" and "wooden" as the kitchen table.

The pile of Cheerios is a thing. "Those sure are a bunch of Cheerios spilled everywhere." The description is "Oaty o's of pure goodness." Understand "cheerio" as the pile. Instead of smelling the pile of Cheerios, say "The Cheerios smell like awesomeness incarnate."

Before eating or tasting the pile of Cheerios when Tabletime is not happening: instead say "NOM NOM NOM."

Instead of taking the pile of Cheerios: abide by the check taking rulebook; say "You take a Cheerio but you cannot stop yourself, you have to eat it NOM NOM NOM.";

Cheerio-message-flag is a number that varies.

An every turn rule: now cheerio-message-flag is 0.

[Sometimes this rule can fire multiple times in a turn as commands are converted to other commands. Avoid that by using a flag that is set when the message is printed and reset at the beginning of every turn.]
Before doing something other than waiting or visiting when the pile of Cheerios is not acted upon and the pile of Cheerios is present (this is the Cheerios message rule):
	if your favorite toy is not acted upon and cheerio-message-flag is 0 begin;
		say "(tearing yourself away from the Cheerios)[command clarification break]";
		now cheerio-message-flag is 1;
	end if.

[A report going rule when the player is in the presence of a playmate and the player is in the presence of the pile of Cheerios:]
A report going rule when the pile of Cheerios is in the last location of the player and a playmate is in the last location of the player:
	say "You have to push your way past [a list of playmates in the last location of the player] thanks to those Cheerios and that slows you down."

Every turn during Tabletime:
	if the pile of Cheerios is not in At the Table begin;
		[chatter count is 0 to begin with so we skip the first turn of Tabletime]
		if chatter count is not 0 begin;
			choose row chatter count in the Table of Tabletime-Parent-Chatter;
			say "[chatter entry][if chatter count is less than the number of rows in the Table of Tabletime-Parent-Chatter] Meanwhile, [otherwise][paragraph break]";
			if chatter count is less than the number of rows in the Table of Tabletime-Parent-Chatter begin;
				choose row chatter count in the Table of Tabletime-Baby-Chatter;
				say "[chatter entry][paragraph break]";
			otherwise;
				move the pile of Cheerios to At the Table;
			end if;
		end if;
		increment chatter count by 1;
	otherwise;
		choose a random row in the Table of Tabletime-Cheerio-Reaching;
		say "[chatter entry][paragraph break]";
		blank out the whole row;
	end if.

Table of Tabletime-Parent-Chatter
Chatter
"Becca says, 'Farina, I know you're not feeding that child chopped-up people food.'"
"'It's no different than canned baby food,' the mom tells Becca."
"'Really?' Lisa says. 'I'd be worried that I'd give Cassie something that'd hurt her.'"
"'Kids can eat nearly anything you can if it's pureed,' says Marion."
"'Weird,' Becca says. 'Then why buy special baby food at all?'"
"'Well,' says Parry, 'Sometimes it's really convenient to have a jar for Mason[note mason-jar] so--whoops!' And then you stop paying attention to him because he spilled a teeny bowl of Cheerios and you and all the other babies are all staring at the spray of Cheerios."

Table of Tabletime-Baby-Chatter
Chatter
"Jemison is taking bits of food and dropping them over his high chair and then watching them hit the floor."
"Cassie is trying to climb out of her chair so she can fall to the floor and Lisa is restraining her."
"Watson is spitting up and Parry is looking sad that he's going to have to change Watson's clothes."
"Zoe is demanding more food. Grr, Zoe."
"the mom is saying, 'Here! Take a bite!'"

Table of Tabletime-Cheerio-Reaching
Chatter
"Jemison reaches for the Cheerios but they are way too far away."
"Cassie rocks hard in her seat, maybe she thinks she can tip over to get to the Cheerios."
"Watson looks at the Cheerios all sadly."

Chapter 5 - Snacktime

Snacktime is a scene. Snacktime begins when Tabletime ends. Snacktime ends when the player has your favorite toy.

When Snacktime begins:
	remove alfadog from play; [aww]
	delete North End of Loveseat from the plastic gate;
	change the visitation count of the West End of Couch to 0;
	move your favorite toy to the wood cabinet;
	move the footstool to the West End of Couch;
	now every thing in the backpack is in the TKBUG Open Area;
	[Get the parents and the kids in their new places]
	now Parry is in the West End of Couch;
	now Lisa is in the North End of Loveseat;
	now Becca is in the By the Rocking Chair;
	now Marion is in the East End of Couch;
	now the mom is in the Middle of Couch;
	now Zoe is in the TKBUG Open Area;
	now Cassie is in the West End of Couch;
	now Watson is in the TV room;
	now Jemison is in the Middle of Couch;
	[Prepare the baby swing and Watson]
	choose row with a baby name of Watson in the Table of Baby State Machines;
	change machine entry to WM Fussing Rules;
	now Watson is upset;
	now fuss count is 0;
	[In case Watson never got out of his hint-giving rules:]
	change the base act percentage of Watson to 20;
	change the act percentage of Watson to 20;
	[Prepare the tumbler puzzle]
	move the heavy tumbler to the cabinet top;
	move the short plastic block to the TKBUG Open Area;
	move the bugish plastic block to the South End of Loveseat;
	move the tall plastic block to the Middle of the Living Room;
	[Change Zoe's reports]
	choose row with a baby name of Zoe in the Table of Dropping Report Rules;
	change report rulename entry to ZK dropping report rule;
	choose row with a baby name of Zoe in the Table of Baby State Machines;
	change machine entry to ZK Stalking Rules;
	[Adjust Jemison's fixation]
	now Jemison is impervious to fixation;
	move the velcro jangly balls to Jemison;
	now Jemison wears the velcro jangly balls;
	now Jemison is fascinated by the velcro jangly balls;
	[Move unused conversations into the table of snacktime chatter]
	copy rows from Table of Playtime-Chatter to Table of Snacktime-Chatter;
	change the chatter list table to the Table of Snacktime-Chatter;
	restart chatter;
	now every playmate is active;
	move the player to the Middle of the Living Room.

When Snacktime ends:
	say "Then you hear the crying. When you turn you see a very wet Zoe and a very wet Jemison and the empty tumbler is next to them. Jemison takes it like a toddler, he is fine, but Zoe is angry like a wet evil cat. Heads pop over the top of the loveseat staring down at all of you, one of them is Parry and he looks really embarassed[note tumbler-falls].

You're probably not going to get to keep the toy.";
	cut chatter short;
	record 10 points for "getting your favorite toy the third time";
	[Evil people like Admiral Jota might pile things into the secret space. Clear out the Secret Space.]
	now every small thing in Secret Space is in the West End of Couch;
	now every medium thing in Secret Space is in the West End of Couch;
	if testing is not in progress, wait for any key.

Table of Snacktime-Chatter
Convo
Table of Birth-Experience-Chatter
Table of Babysitter-Chatter
Table of Preschool-Chatter
Table of Kids-Love-Toys-Chatter
Table of Kid-Development-Chatter
with 5 blank rows

Table of Birth-Experience-Chatter
Chatter
"Becca is still all sipping on a mimosa and she says, 'Hey, hospitals should give us mimosas to go with our epidurals.'"
"The mom laughs, 'Becca, you didn't even get an epidural.'"
"Becca just smiles while Lisa says, 'I still can't believe you did that.'"
"'It wasn't too hard,' Becca says. 'I just screamed a lot and hit Kurt when the pain got really bad.'"
"'The trick,' Marion says, 'is to relax into the pain instead of anticipating it and tensing up.'"
"Lisa sighs and says, 'My epidural stopped working.'"
"'What?' Becca asks. 'I hadn't heard that.'"
"'Yeah', says Lisa, 'I got my epidural and then they turned me on my side. The next thing I knew I was feeling everything, and I mean everything, on my left side.'"
"Parry pipes up, 'You know, it's a shame that the anesthesiologist makes you hunch forward so he can slide the needle into your spine just when contractions make you want to flatten out.'"
"All the other parent people stare and Parry and he sits back quietly."

Table of Babysitter-Chatter
Chatter
"The mom asks the other parent people, 'Do any of you have a suggestion for a baby sitter?' Wait, what the hey?"
"The mom goes on, 'We've got a Christmas party to go to in a month or so, and we've never used a babysitter before.' No, really--what the hey???"
"Marion says, 'Didn't you have luck with Susan, Lisa?' Susan, that's a name that sounds okay."
"Lisa says, 'I've used Susan two--no, three times. She said Cassie was great.' Lisa pauses all thoughtful like. 'Maybe she was lying to get a bigger tip.'"
"'I didn't think she was all that,' Becca says. 'I had her keep Zoe once. Susan and Zoe didn't get along at all.' This Susan is sounding better all the time."
"Marion says, 'You could have Susan visit ahead of time to look for any problems.'"
"'I'll give her a call, then,' the mom says."

Table of Preschool-Chatter
Chatter
"'You've all picked out preschools, right?' Becca says."
"Parry is all, 'Not really.'"
"'Oh my God, it's almost too late!' Lisa says, and Becca chimes in, 'Competition for the good preschools is fierce.'"
"Parry says, 'We've looked at some, but really we're just after somewhere where Watson can have more socialization.'"
"'Cassie is in preschool one day a week,' says Lisa. 'I can already tell that she's learning faster because of it.'"
"Becca agrees, 'I'm going to make sure Jemison has three or four legs up educationally.'"
"You can totally hear Marion's frown when she says, 'I'm not interested in pushing Zoe like that.'"
"'It's not pushing,' Becca sniffs. 'It's making sure our kids live up to their potential.'"
"'Still,' Marion says, and the conversation kinda peters out."

Table of Kids-Love-Toys-Chatter
Chatter
"Parry says to Marion and the mom, 'Your two kids really love that one toy.'"
"The mom laughs. 'Ever since Zoe started coming it's been that way.'"
"'I always thought that kind of competition wouldn't happen until Zoe was two or so,' Marion says."
"Becca says, 'Jemison doesn't really care about other kids['] toys,' she must be smoking something and it is not tobacco."
"'I don't know what I'll do if Cassie ends up grabbing toys from other kids,' Lisa says."
"Marion says, 'You'll watch for it and redirect Cassie and the other kid. When Cassie's older, you'll punish her.'"
"'Oh,' Lisa says, 'I couldn't spank Cassie.'"
"'You don't have to spank her,' the mom says and Marion chimes in, 'Farina's right. You'll figure out what Cassie's levers are and you'll pull them.'"
"Lisa sounds all puzzled, 'What do you mean?'"
"Marion says, 'As outgoing as Cassie is, you can try sending her to her room, or you can take away something she likes or likes to do. Figure out what she values. That's where you apply pressure.'"
"'Oh, okay,' Lisa says, but you can tell she is not convinced."

Table of Kid-Development-Chatter
Chatter
"'Cassie is really pulling up well,' the mom tells Lisa."
"'Oh, thanks,' Lisa says. 'She's probably too good at pulling up and climbing, to tell you the truth.'"
"'We've been working on pulling up,' the mom says, boy is she going to be surprised by all the practice you're getting today."
"Becca snorts. 'I love how all of you take credit for your kids['] development. Like you're the one pulling up.'"
"Lisa says, 'You're just annoyed that Jemison just sits there--' but Becca keeps on talking, 'So your kid pulls up or cruises or whatever. Big deal!'"
"Parry says, 'Kids develop at different speeds. It's not as if I'll still be helping Watson pull up when he goes to college.'"
"'As long as Zoe doesn't fall too far behind, I'm not worried,' Marion says."
"'Yeah,' Parry says. 'It won't bother me if Watson is like many boys and is slower to talk.'"
"'As long as Cassie is ahead of where she's supposed to be, I won't worry either,' Lisa says."

The baby-swing is a fixed in place container. The description of the baby-swing is "It is this a-frame thing with a swing in the middle and a speed switch[if the player is in the baby-swing] and from up here in it you are all tall[otherwise] way up there[end if]. [if the current speedness of the speed switch is hi and the player is in the baby-swing]Now that[otherwise]When[end if] the speed switch is all the way up boy does that swing go high, nearly high enough to kick a parent in the head if you were that kind of baby. It is supposed to be this cheerful baby plaid but instead it is this weird lumberjack-like plaid[contents-and-speed of the swing in description]." The printed name is "baby swing". Understand "baby" or "swing" or "plaid" or "frame" or "a-frame" as the baby-swing. The baby-swing can be on or off. The baby-swing is off. The baby-swing can be temporarily muffled.

Instead of jumping when the player is in the baby-swing, say "You are strapped into the swing and that keeps you from jumping to your pain if not death."

Instead of inserting something into the baby-swing when the player is not in the baby-swing, say "The baby swing is [if the player is not quarter-height-or-higher]too high and is also [end if]not really for putting things other than babies in it."

Instead of entering or upstanding using the baby-swing when the player is not in the baby-swing, say "[if the player is eighth-height-or-higher]Even though you are taller right now y[otherwise]Y[end if]ou cannot get in the baby swing yourself, that is something the parents do for you when they are bored of you or you are upset."

Instead of being ambulatory when the player is in the baby-swing, say "You are way high in the baby swing and are all swinging, you are not some stunt baby who is used to taking falls."

Instead of pushing or pulling the baby-swing when the player is not in the baby-swing, say "The frame part is too heavy to move and the swing part is too high[if the baby-swing is on] and besides it is already swinging[end if]." Before pushing the baby-swing to a direction when the player is not in the baby-swing, instead say "It is too heavy for you to move."

A height reachability rule when the reaching actor is in the baby-swing (this is the baby-swing specific noun reachability rule):
	if the reaching object is not nothing begin;
		if the baby-swing encloses the reaching object or the reaching object is your favorite toy, continue action processing;
		if the reaching actor is the player, say "With you strapped into the swing you cannot reach [the reaching object].";
		stop action processing;
	end if;
	continue action processing.

Every turn when the baby-swing is on and the baby-swing is present:
	if the baby-swing is temporarily muffled begin;
		now the baby-swing is not temporarily muffled;
	otherwise;
		say "The baby swing clicks and clacks as it swings[if the current speedness of the speed switch is hi] really high[end if]";
		if the player is in the baby-swing, say ", and the whole world tilts and turns as you swing with it[if the current speedness of the speed switch is hi and the location of the baby-swing is TKBUG Open Area and your favorite toy is not in a playpen]. At the top of the swing you are way close to your toy";
		say ".";
	end if.

To say contents-and-speed of the swing in description:
	if a playmate is in the baby-swing begin;
		say ". [A random playmate in the baby-swing] is all dangly from the swing";
		if the baby-swing is on begin;
			say " as it is swings";
			if the current speedness of the speed switch is hi, say " really high";
		end if;
	otherwise if the baby-swing is on;
		say ". The baby swing is all swinging[if the current speedness of the speed switch is hi] really high";
	end if.

To say contents-and-speed of the swing in room:
	if a playmate is in the baby-swing begin;
		say " and [a random playmate in the baby-swing] is all dangly from it";
		if the baby-swing is on begin;
			say " as it is swings";
			if the current speedness of the speed switch is hi, say " really high";
		end if;
	otherwise if the baby-swing is on;
		say " and it is all swinging[if the current speedness of the speed switch is hi]really high";
	end if.

Rule for writing a paragraph about the baby-swing: say "There is a big old baby swing here[contents-and-speed of the swing in room]."

Rule for writing a paragraph about the baby-swing when the player is in the baby-swing: say "The baby swing is all holding you up."

Rule for writing a paragraph about the baby-swing when the baby-swing is in the TV room: say "There is this baby swing right in front of the TV[contents-and-speed of the swing in room]."

Rule for writing a paragraph about the baby-swing when the baby-swing is in the TKBUG Open Area: say "There is a big old baby swing all shoved up against the bookcase[contents-and-speed of the swing in room]."

Instead of turning the baby-swing to, try turning the speed switch to the speedness understood.

A thing called the speed switch is part of the baby-swing. The description is "That is the switch that controls how high the swing goes, it can be set to lo, med, or hi. Right now it is on [current speedness of the speed switch]." The speed switch has a speedness called the current speedness. The current speedness of the speed switch is lo.

Instead of turning the speed switch to (this is the change speed switch speeds rule):
	if the speedness understood is the current speedness of the speed switch begin;
		say "The switch is already set to [speedness understood].";
	otherwise;
		change the current speedness of the speed switch to the speedness understood;
		say "You push on the switch [if the player carries the poosh stick]with the poosh stick [end if]and turn it to [speedness understood].";
	end if.

Instead of flipping or turning or attacking or pushing or pulling the speed switch (this is the block flipping or turning or pushing the speed switch rule), say "The speed switch has three settings, lo, med, or hi, so you will have to turn it to a specific speed."

The block flipping or turning or pushing the speed switch rule is listed after the change speed switch speeds rule in the instead rulebook.

Instead of pushing the speed switch with the poosh stick, try pushing the speed switch.

A height reachability rule when the reaching object is the speed switch and the reaching actor is in the baby-swing (this is the speed switch is way high even when in the baby swing rule):
	if the reaching actor is the player, say "In one way you are close to the speed switch now that you are in the baby swing but in another way you are not that close because the swing straps hold you in, you will have to change the speed switch from outside the swing.";
	stop action processing.

The speed switch is way high even when in the baby swing rule is listed before the baby-swing specific noun reachability rule in the height reachability rulebook.

A height reachability rule when the reaching object is the speed switch and (the reaching actor is quarter-height-or-higher or the poosh stick is carried by the reaching actor) (this is the speed switch is finally in reach rule):
	continue action processing.

A height reachability rule when the reaching object is the speed switch and the reaching actor is on a pushable between rooms enterable supporter (called the platform) (this is the speed switch is still way high rule):
	if the reaching actor is the player, say "Even on [the platform] you are not tall enough to reach the speed switch.";
	stop action processing.

A height reachability rule when the reaching object is the speed switch (this is the speed switch is way high rule):
	if the reaching actor is the player, say "You have short arms, you can't reach the speed switch from [if the player is half-height-or-higher]up[otherwise]down[end if] here.";
	stop action processing.

Chapter 6 - Picturetime

Picturetime is a scene. Picturetime begins when Snacktime ends. Picturetime ends when the number of filled rows in the Table of Picturetime-Chatter is 0.

When Picturetime begins:
	[Clear out everyone's stuff]
	set the kids to a ground state;
	now every playmate is passive;
	now every playmate is in Onna Couch;
	now every parent person is in Onna Couch;
	now Jemison is impervious to fixation;
	remove the velcro jangly balls from play;
	remove the heavy tumbler from play;
	display chapter head "Chapter 3" with subhead "Picturetime";
	change chatter count to 0;
	change cry count of the player to 0;
	[Time for costumes!]
	now the squid costume is worn by the player;
	now the ladybug costume is worn by Zoe;
	now the bumblebee costume is worn by Jemison;
	now the pumpkin costume is worn by Cassie;
	now the bat costume is worn by Watson;
	say "It is like you are psychic because you don't get to keep the toy. The mom storms around the couch and scoops you up and grabs the toy. 'It's okay, Farina,' Marion says, 'accidents happen. Zoe and Jemison don't look hurt, just wet.' Several of the other parents are glaring at Parry, he looks all sheepish.

'Since the kids have to be changed anyway, let's take pictures!' Marion says.

'Sure, works for me,' Lisa says, and just like that everyone is getting their onesies taken off and instead they are being put in these Halloween costumes, and that would be okay except your squid costume is totally restrictive[note stupid-costumes].";
	move the player to Onna Couch.

When Picturetime ends:
	change chatter count to 0.

The squid costume is a wearable medium thing. The squid costume can be dirty or clean. The squid costume is clean. The description of the squid costume is "It is like a squid if a squid was all plush and powder blue and didn't have any suckers[if the player is wearing the squid costume]. You are wearing it and its tentacles cover your arms and hands[end if][if the squid costume is dirty]. It is all icky down the front from where you spit up on it[end if]." Understand "plush" or "powder" or "blue" or "tentacles" as the squid costume. Instead of taking off the squid costume, say "You cannot see how it comes off, maybe you will be stuck in it forever." Instead of chewing the squid costume, say "You gnaw on one of the tentacles a bit but it is not very satisfying and eating your way out of your costume is too impractical."

The costume-hint count is a number that varies. The costume-hint count is 0.

To check on giving a costume hint:
	increase the costume-hint count by 1;
	if the costume-hint count is 5, say ". You really have to get someone to take this costume off for you".

Instead of taking something when the player is wearing the squid costume (this is the squid costume lacks suckers for taking things rule):
	say "With this stupid costume on you cannot take anything, the fake tentacles are all in the way[note squid-costume]";
	check on giving a costume hint;
	say "."

Instead of upstanding using something when the player is wearing the squid costume (this is the squid costume lacks suckers for upstanding using rule):
	say "You would but the tentacles of your stupid squid costume are too slick[note squid-costume]";
	check on giving a costume hint;
	say "."

Instead of upstanding using a parent person (called target) when the player is wearing the squid costume (this is the parents find the squid costume cute rule), say "You can't pull up in the squid costume but you rub up against the legs of [the target] and get a look and a smile in response."

Instead of spitting up when the player is wearing the squid costume:
	if the squid costume is dirty begin;
		say "That is enough spitting up for now, the squid costume is good and dirty.";
	otherwise;
		say "You reach deep down inside you and bring up some of those stupid prunes the mom was feeding you and spit them up on the squid costume, making it all dirty[note spitting-up].";
		now the squid costume is dirty;
	end if.

Instead of spitting up on the squid costume, try spitting up.

Instead of showing or giving the squid costume to a parent person (called target) when the player is wearing the squid costume and the squid costume is dirty: try upstanding using the target.

[The squid costume can also come up when you cry when the cry count of the player is greater than 3. Synchronize the two rules in the event of changes.]
Instead of upstanding using the mom when the player is wearing the squid costume and the squid costume is dirty:
	say "You rub up against the legs of the mom and she looks down and says, 'Oh, oh, your costume is all dirty, kiddo.' She lifts you up and the next thing you know you are back on the ground and free of that evil squid costume.";
	record 5 points for "getting that evil squid costume off";
	remove the squid costume from play.

Instead of upstanding using a parent person (called target) when the player is wearing the squid costume and the squid costume is dirty:
	say "You rub up against the legs of [the target], who sees that you are dirty and says 'Uh, Farina,' and hands you over to the mom. The mom takes you and says, 'Oh, oh, your costume is all dirty, kiddo,' and the next thing you know you are back on the ground and free of that evil squid costume.";
	record 5 points for "getting that evil squid costume off";
	remove the squid costume from play.

The ladybug costume is a wearable medium thing. The description of the ladybug costume is "Black and red and cute all over, though it is not so cute when Zoe is wearing it." Understand "bug" as the ladybug costume.

The bumblebee costume is a wearable medium thing. The description of the bumblebee costume is "The tiny antennas and tiny wings totally make the costume." Understand "antenna" or "wing" as the bumblebee costume.

The pumpkin costume is a wearable medium thing. The description of the pumpkin costume is "Round but not so round that Cassie cannot zoom around in it."

The bat costume is a wearable medium thing. The description of the bat costume is "You can see how someone would think this was a fun idea but man, it just makes Watson look like a giant emo cupid."

Onna Couch is a room. "You don't get up on the couch all that much but now you are up here with everyone else so you can have your pictures taken." The printed name is "On the Couch".

Before being ambulatory in Onna Couch, instead say "You start to move and then there is the mom, she pushes you back in place. 'Whoa, kiddo, we've got to get a picture of everyone.'"

Instead of crying during Picturetime, say "The mom says, 'We're just taking a couple of pictures, kiddo. Hang in there.'"

Instead of crying when the chatter count is 9 during Picturetime, say "The mom says, 'We're about done!'"

The fake-couch is scenery in Onna Couch. Understand "gray" or "grey" or "couch" as the fake-couch. The description is "Grey and soft under you." The printed name is "grey couch".

Instead of spitting up when the player is in Onna Couch (this is the no spitting up during Picturetime rule): say "While it is sometimes fun to make the mom's life hard, all that would happen is that you would spit up and your costume would get dirty and the mom would try to clean up your costume and then she would put you back down on the couch for pictures and really who wants this picture taking to take any longer than it has to? After the parents have taken pictures, then you can go crazy with the spitting up."

Every turn during Picturetime:
	[chatter count is 0 to begin with so we skip the first turn]
	if chatter count is not 0 begin;
		choose row chatter count in the Table of Picturetime-Chatter;
		say "[chatter entry][paragraph break]";
		blank out the whole row;
	end if;
	increment chatter count by 1.

Before looking or examining or looking under or searching or reading or taking inventory when the chatter count is 8 during Picturetime:
	if the noun is not the white-and-red-spots, instead say "All you can see are these white-and-red spots."

[Since I don't have a good way to move items in and out through the chatter daemon, I'll fake the funk using a conditional Understand rule.]
The white-and-red-spots are scenery in Onna Couch. The description is "The spots are white and red and like you looked at the sun." Understand "white and red" or "white" or "red" or "spot" or "spots" or "sun" or "suns" as the white-and-red-spots when the chatter count is 8 and Picturetime is happening.

[The above rule about vision depends on the camera flashes going off on line 7 of the table. If you change its position, change the above rule.]
Table of Picturetime-Chatter
Chatter
"The mom says, 'That's an interesting costume Watson's wearing.' Watson must know he's being talked about, he ducks his head and looks sad."
"'We made it ourselves,' Parry replies."
"Becca laughs all loud. 'It's not very cheerful--whoops, hold on there Jemison.' Zoe must've pushed Jemison because he starts to topple sideways before Becca rescues him."
"Parry is all huffy, 'We wanted something different from the usual.'"
"'Does that thing have buttons? That'll make it fun to take off if he spits up on it,' Becca laughs again."
"Cassie bounces up and down next to you and you try to back away from her but your tentacles get in your way. 'Keep the kids together!' Lisa calls, pointing a small camera at all of you."
"Parry and the mom get you all in place on the couch. The mom dangles your favorite toy in front of all of you! You and Zoe stare raptly at it and then Parry says 'Now!' and it is like a thousand suns explode in front of you and all you can see are these white and red spots."
"As the spots clear you see Watson has pitched face first into the couch. Zoe is busy tugging on Jemison's wings and Cassie is chewing on one of your tentacles. 'Hope no one wanted another picture!' Becca says."
"The parents give up and take you all off the couch. As the mom puts you down she stops by the bookcase and puts your toy up there. 'Maybe next year they'll sit still,' Lisa sighs, and Parry just snorts."

Chapter 7 - Costumetime

Costumetime is a scene. Costumetime begins when Picturetime ends. Costumetime ends when Cooperationtime ends.

When Costumetime begins:
	move your favorite toy to the bookcase;
	now your favorite toy is not handled;
	remove the footstool from play;
	remove the folded paper from play;
	change cry count of the player to 0;
	[Get the parents and the kids in their new places]
	now Parry is in the North End of Loveseat;
	now Lisa is in the West End of Couch;
	now Becca is in the By the Rocking Chair;
	now Marion is in the East End of Couch;
	now the mom is in the Middle of Couch;
	now Zoe is in the TKBUG Open Area;
	now Cassie is in the West End of Couch;
	now Watson is in the South End of Loveseat;
	now Jemison is in the Middle of Couch;
	[Set up the blocks for Zoe]
	now the short plastic block is in the TV Room;
	now the bugish plastic block is in the TKBUG Open Area;
	now the tall plastic block is in the North End of Loveseat;
	[Change Zoe's state machine]
	choose row with a baby name of Zoe in the Table of Baby State Machines;
	change machine entry to ZK Climbing Rules;
	change the base act percentage of Zoe to 100;
	change the act percentage of Zoe to 100;
	[Set up the swing puzzle]
	now the remote control is in the North End of Loveseat; [i.e. next to Parry]
	[Set up Watson and his pile of brix]
	choose row with a baby name of Watson in the Table of Baby State Machines;
	change machine entry to WM Building Rules;
	move the brix blox bucket to the Middle of Couch;
	[Move unused conversations into the table of costumetime chatter]
	copy rows from the Table of Snacktime-Chatter to the Table of Costumetime-Chatter;
	change the chatter list table to the Table of Costumetime-Chatter;
	restart chatter;
	now every playmate is active;
	move the player to the Middle of the Living Room.

Table of Costumetime-Chatter
Convo
Table of Kids-Are-Like-Pets-Chatter
Table of Biting-Chatter
Table of TV-Chatter
Table of Spoiling-Grandparents-Chatter
with 10 blank rows

Table of Kids-Are-Like-Pets-Chatter
Chatter
"The mom says, 'The other day when we were out, I saw a mom with her toddler on her leash.'"
"'What?' Becca says, and the mom is all, 'I know! The toddler was wearing this monkey backpack and the tail unspooled into a leash that the mom held.'"
"Parry says, 'I don't know, that sounds like a clever idea.'"
"The mom is indignant, 'My child is not a dog to be on a leash!' Good for her, there is no way you'd wear a leash."
"Lisa says, 'When you get down to it, babies are about as smart as pets. Cassie might as well be a hyperactive Yorkie.'"
"'Lisa!' Marion says. 'That's horrible!'"
"'It's true!' Lisa says. 'They make messes on the floor and can't talk and you can classically condition them.'"
"'You know,' Parry says, 'A combination playpen and Skinner box might sell really well[note favorite-conversation].'"
"Becca says, 'That is the kind of thing people call Child Protective Services about.'"
"'I'm not sure Child Services gets called that often to rescue kids from Skinner boxes,' Parry says."
"The mom is still all huffy, 'Kids aren't pets.'"

Table of Biting-Chatter
Chatter
"'Oh my God, you guys,' Lisa says, 'I almost forgot to tell you! This kid in Cassie's class bit her yesterday!'"
"Becca says, 'I was always worried that Jemison would be--' and here her voice gets real quiet, 'a biter.' Then she gets loud again, 'But he never was.'"
"'Mine was a biter for a little while,' the mom says, that was so three months ago, and besides, those kids totally deserved it."
"Parry is all, 'What do you do if your child does bite other children?'"
"'At this age, there's not much you can do,' Marion says, 'other than removing them from the situation.' The moms are all silent for a moment, they're probably thinking of Allan. He used to be in the playgroup but then he started biting people and now he is an unperson like Nikolai Yezhov."
"'Can you keep them from even biting in the first place?' Lisa asks, and Parry follows up with, 'It would be nice if the parents were controlling their child better.'"
"'It's not a matter of control,' Marion says, 'you can't really control a ten-month old.'"
"The mom says, 'I'm just glad we aren't still dealing with this problem.'"

Table of TV-Chatter
Chatter
"'Did anyone see last night's episode of 'Beat'?' Parry says."
"'Is that the Boston cop show?' Becca asks."
"'Oh my God, who has time for TV?' Lisa asks. 'Besides, I can't watch something that violent when Cassie's up, even if she'd let me leave her alone.'"
"The mom says, 'I don't have a lot of time for TV either. Besides Sesame Street, that is,' and the parents all laugh, why are they dissing the Street?"
"Becca sighs. 'I always feel guilty when I set Jemison in front of the TV so I can get some work done.'"
"'I don't allow Zoe to watch much television,' Marion says, 'though she does watch some.'"
"Parry says, 'We did consider not letting Watson see any TV until he's two or so.'"
"'You're not one of those no-TV nuts, are you?' Lisa says all sharpishly."
"'They're not nuts,' Parry says, you can tell he's annoyed. 'In fact, they make a lot of sense.'"
"'Whatever,' Lisa sniffs, and the mom says, 'Remember, watch what you say about religion, politics, and TV.'"

Table of Spoiling-Grandparents-Chatter
Chatter
"'Hey, Parry,' Lisa says, 'Watson seems a little out-of-sorts today. I saw you had to put him in the swing when he was crying.'"
"Parry sighs. 'Shari's parents were here this weekend.'"
"'Let me guess,' says the mom, 'they carried Watson everywhere and played with him and wouldn't leave him alone, and now he's upset you're not doing the same.'"
"'Exactly!' Parry exclaims. 'How'd you know?'"
"'Detox!' Lisa and Becca and Marion chorus."
"'You must not talk to many other parents,' Becca says. 'It's nice when the grandparents visit and give you a break, but when they leave, oh boy!'"
"Parry says, 'I can't believe the same thing happens to all of you! How do you keep it from happening?'"
"'You can keep your parents from visiting Watson,' Marion says. 'Otherwise, no, there's no real prevention.'"
"The mom says, 'Be glad they love Watson as much as they do.'"
"'Yeah. Yeah!' Parry says."

The remote control is a small thing. "Shoved half-under the loveseat is a remote control." The remote control is not noticeable by playmates. The description is "This is the remote control that lets parents turn on the TV as if they have big stretchy arms that can reach the television from anywhere in the room. You could use it to turn on the TV if you were ancient, like three years old or something." Instead of pushing or switching on or switching off the remote control, say "The remote control is too advanced for you, it is the kind of thing adults use." Understand "button" or "buttons" as the remote control.

Before showing the remote control to a parent person, instead try giving the remote control to the second noun.

Instead of giving the remote control to a parent person (called victim) during Costumetime:
	let flag be 0;
	if the player is not gnawing the remote control begin;
		change flag to 1;
		silently try the player chewing the remote control;
		if the player is not gnawing the remote control, instead say "The way to do that is to put it in your mouth and pull up on [the victim] but your mouth is all full right now.";
		say "You put the remote control in your mouth and ";
	end if;
	say "[if flag is 0]You [end if]go to pull up on [the victim] to get [its-their of the victim] attention. [run paragraph on]";
	try upstanding using victim.

Instead of upstanding using a parent person (called victim) when the player is gnawing the remote control (this is the parents take the remote control rule):
	abide by the check upstanding using rulebook;
	if victim is Parry begin;
		say "Parry looks down and sees you with the remote in your mouth and his eyes get a little wide. 'Where'd you get that?' he asks, taking it out of your mouth. Then he murmurs, 'That reminds me.' He looks up and is all, 'Say, Farina, ";
	otherwise if the victim is the mom;
		say "The mom laughs and says, 'Where did you find that?' as she pulls the remote out of your mouth.

Then Parry sees you and her with the remote and says, 'That reminds me, ";
	otherwise;
		say "[The victim] looks down at you and gently pries the remote control out of your mouth. 'Hey, Farina,' [the victim] says, 'look what I found in someone's mouth.' Farina sees and laughs.

Parry sees it too and says, 'That reminds me, ";
	end if;
	say "I'd like to put in a Baby Feynman video for Watson.' There is then this big pregnant pause before Parry says, 'Uh, if that's okay.'

The mom is quiet, she is probably communicating with the other moms using only her eyes and mom-telepathy[note mom-telepathy] because wasn't Parry anti-TV for kids before? And then she says, 'Sure!' So Parry heads over to the TV and drags the baby swing out of the way of the TV and over by the bookcase and turns the TV on.";
	now the remote control is not gnawed by the player;
	remove the remote control from play;
	now the television is on;
	now the baby-swing is in TKBUG Open Area.

The brix blox bucket is a container. The description is "It is a tall cube that is open on top only the corners are all rounded so no babies are harmed in the extracting of the blox from the bucket and it is also wider at the top than at the bottom. It is [if the brix blox bucket is tipped over]lying on its side[otherwise]full of these lame faker Lego blocks[end if]." The brix blox bucket can be upright or tipped over. The brix blox bucket is upright. Rule for printing room description details of the brix blox bucket: stop. Understand "bricks" as the brix blox bucket.

Rule for deciding the concealed possessions of the brix blox bucket:
	if the brix blox bucket is tipped over, no;
	otherwise yes.

Rule for printing the name of the brix blox bucket when the brix blox bucket is upright:
	omit contents in listing;
	continue the action.

Before pushing the brix blox bucket to a direction, instead try pushing the brix blox bucket.

Instead of opening the brix blox bucket, say "The bucket doesn't have its lid on so it is already open."

Instead of searching the brix blox bucket when the bucket is upright, say "The bucket's opening is too high to look into."

Instead of pushing or pulling or turning the brix blox bucket when the brix blox bucket is tipped over, say "It has already fallen over, it cannot fall over any more than it already has."

Instead of inserting something into the brix blox bucket when the brix blox bucket is upright, say "The bucket is [if the player is pulled up on the brix blox bucket]still [end if]too tall for you to put stuff into it."

Instead of upstanding using the brix blox bucket when the brix blox bucket is tipped over, say "It is made of this slick plastic and you are no gecko[if the player is wearing the squid costume], you are a squid with no suckers[end if]."

Instead of upstanding using the brix blox bucket, try pushing the brix blox bucket.

Instead of pushing or pulling or turning the brix blox bucket:
	say "You put your [if the player is wearing the squid costume]tentacles[otherwise]hands[end if] on the bucket and it wobbles and then it wobbles more and look out! it comes crashing down, spilling these dayglo blox everywhere and dumping an instruction booklet out[note instruction-book].";
	now the brix blox bucket is tipped over;
	move the pile of dayglo blox to the location of the brix blox bucket;
	move the booklet to the location of the brix blox bucket.

The pile of dayglo blox are in the brix blox bucket. The description is "A big pile of dayglo blox from the brix blox bucket. Some are one hand wide and some are two hands wide and some are three hands wide and I guess they go together in various ways but whatever, you are no engineer." The pile of dayglo blox are fixed in place and ambiguously plural. Understand "blocks" as the pile of dayglo blox.

[Make the player refer to the pile of blox by default.]
Does the player mean doing something with the pile of dayglo blox: it is very likely.

Instead of searching the pile of dayglo blox, say "It is a big pile of random blox, it's not like you're going to find something unexpected. It's how they all go together that's important."

Instead of taking the pile of dayglo blox, say "You would take a blox or two but they are annoying to deal with and their edges are just sharp enough to be annoying and besides, the mom might see you with them and then she'd expect you to play with them. And blox are boring."

The building instructions booklet is in the brix blox bucket. The booklet can be open or closed. The booklet is closed and medium and not noticeable by playmates. The description is "The booklet tells you how to build stuff out of the blox, you've never really used it. It is [if the booklet is open]flopped open right now and is showing this [current booklet page] on the page[otherwise]closed[end if]." Understand "page" or "pages" as the booklet.

[Conditional "understand"s for the booklet]
Understand "helicopter" as the booklet when the page-index is 1 and the booklet is open and the helicopter is not in play.
Understand "rolly" or "car" or "thing" as the booklet when the page-index is 2 and the booklet is open and the rolly car is not in play.
Understand "nuclear" or "reactor" or "model" as the booklet when the page-index is 3 and the booklet is open and the nuclear reactor model is not in play.
Understand "wall" as the booklet when the page-index is 4 and the booklet is open and the wall is not in play.
Understand "poosh" or "stick" as the booklet when the page-index is 5 and the booklet is open and the poosh stick is not in play.

[Since the building action can apply to non-visible things, it bypasses the visibility check for the booklet. Re-do that check by hand.]
Before building the booklet when the booklet is closed:
	if the booklet is not visible, instead say "You can't see any such thing.";
	say "(opening the booklet)";
	silently try opening the booklet;
	if the booklet is closed, stop the action.

Instead of building the booklet, say "You look at the instructions but man, they are way complex. This is the kind of thing you hire people to do instead of bothering yourself."

Instead of following the booklet, try building the booklet.

To say current booklet page:
	choose row page-index in the Table of Instruction Pages;
	say "[page entry]. [description entry]".

The page-index is a number that varies. The page-index is 1.

[NB: if you alter this table, alter the conditional understand statements above.]

Table of Instruction Pages
Page	Description	Associated Object
"helicopter"	"It involves these complex blades and gears and stuff"	helicopter
"rolly car thing"	"It is all wheels and roundy bits"	rolly car
"nuclear reactor model"	"This probably needs special expensive uranium and graphite blox to work and the parents are too cheap to buy that kind of stuff"	nuclear reactor model
"wall"	"Sweet Christmas but this thing is so easy even you could build it though then the parents would make you play with the blox all the time, no thank you"	wall
"poosh stick"	"I guess they think kids need to build things to push things around with"	poosh stick

After printing the name of the booklet when the booklet is open:
	say " (open to some [page in row page-index of the Table of Instruction Pages] instructions)".

After an actor taking the booklet when the booklet is open:
	now the booklet is closed;
	if the actor is the player begin;
		say "You scoop up the booklet and it flops all closed.";
	otherwise if the actor and the player are co-located;
		say "[The actor] scoop[s] up the booklet and it flops all closed.";
	end if;
	stop the action.

Instead of an actor opening the booklet when the booklet is open, if the actor is the player, say "It is already open, maybe you want to turn the page instead?"
Instead of an actor opening the booklet when the booklet is carried, if the actor is the player, say "You cannot open the booklet as long as it's being carried."

Instead of an actor opening the booklet:
	if the actor is the player begin;
		say "You scrabble at the pages and the booklet falls open to this page that shows you how to build a [page in row page-index of the Table of Instruction Pages].";
	otherwise if the actor is present;
		say "[The actor] flops the instruction booklet open and it opens to a page that shows you how to build a [page in row page-index of the Table of Instruction Pages].";
	end if;
	now the booklet is open.

Instead of an actor closing the booklet when the booklet is closed, if the actor is the player, say "It is already closed."

Instead of an actor closing the booklet:
	if the actor is the player begin;
		say "You push the booklet closed.";
	otherwise if the actor is present;
		say "[The actor] closes the instruction booklet.";
	end if;
	now the booklet is closed.

Instead of turning or flipping the booklet when the booklet is closed, try opening the booklet.

Instead of turning or flipping the booklet:
	increment page-index by 1;
	if page-index is greater than the number of rows in the Table of Instruction Pages, change page-index to 1;
	say "You flip the pages by scrabbling at them [if the player is wearing the squid costume]with your tentacles [end if]until you are looking at this blocky diagram of a [page in row page-index of the Table of Instruction Pages]."

A blox-thing is a kind of thing. A blox-thing is usually small. A blox-thing is usually not noticeable by playmates.

Instead of building a blox-thing:
	if the noun is in play, instead say "[The noun] is already built so hooray, less work for you.";
	if the associated object in row page-index of the Table of Instruction Pages is the noun, instead try building the booklet;
	instead say "The instruction booklet doesn't show you how to build [the noun] but even if it did you probably could not follow the directions."

The helicopter is a blox-thing. The description is "You are small and all but this is even smaller and it is a shame you cannot get in it and fly it around."

The rolly car is a pushable between rooms blox-thing. The description is "Should cars have five wheels? Because this one totally does." Understand "wheel" or "wheels" as the rolly car. Instead of pushing the rolly car, say "You push the car around some but that gets old quick."

The nuclear reactor model is a blox-thing. The description is "The little scoop in the back for loading in the enriched uranium is a nice touch." Understand "little" or "scoop" as the nuclear reactor model.

The wall is a blox-thing. The description is "Man what? This is the stupidest blox thing ever. Who needs a tiny portable wall that is like six inches tall? You can't even stop someone with it, even clumsy Jemison would crawl right over it and knock it over, and keeping orcs from overrunning Helm's Deep? Forget it."

The poosh stick is a medium blox-thing. The description is "The parents call this a poosh stick because they think you are stupid and need baby talk, it is really a push stick. It is a stick for pushing things around. I guess the blox makers wanted easier and easier things to make and someone said, 'Hey, I know what would be easy to build, a long stick.' The stick is nearly two babies long, it's amazing that it holds together."

Check pushing something with the poosh stick:
	if the noun is not the top oval, instead try pushing the noun.

Section 1 - Dejectiontime

Dejectiontime is a scene. Dejectiontime begins when your favorite toy is in a playpen. Dejectiontime ends when the machine corresponding to a baby name of Zoe in the Table of Baby State Machines is ZK Cooperating Rules.

When Dejectiontime begins:
	change (machine corresponding to a baby name of Zoe in the Table of Baby State Machines) to ZK Moping Rules;
	now Zoe is down-climbing.

Section 2 - Cooperationtime

Cooperationtime is a scene. Cooperationtime begins when Dejectiontime ends. Cooperationtime ends when the top oval is open.

When Cooperationtime begins:
	now Zoe is up-climbing.

Chapter 8 - Climaxtime

Climaxtime is a scene. Climaxtime begins when Costumetime ends. Climaxtime ends when your favorite toy is not in play.

When Climaxtime begins:
	change (report rulename corresponding to a baby name of Zoe in the Table of Going Report Rules) to ZK fleeing report rule;
	change (machine corresponding to a baby name of Zoe in the Table of Baby State Machines) to ZK Toy-Grabbing Rules;
	now fuss count is 0;
	Parry munches Cheerios in one turn from now.

When Climaxtime ends:
	say "Zoe screws up her face and bursts into these loud wails, they are so terribly loud. She shoves at [a random not-Zoe-baby playmate in the location] who also starts crying and soon just about everyone is crying except you, ha ha, they are such babies.

Marion says, 'I was afraid we were staying too late past Zoe's naptime. Farina, everyone, it's been fun, and glad you could come, Parry. See everyone next week.' Hands come down and the squalling Zoe zips up into the air and into Marion's arms. Soon all of the babies are going skywards, parentwards, and out-the-doorwards.

With them all gone the air kinda leaks out of you, it has been a long playdate. The mom gathers you and your favorite toy up. 'C'mon, kid, let's get you to bed before you completely pass out.' But you do not need naps, does she not understand that?

Though your crib is nice and soft and your favorite toy chimes a bit as you curl around it and your teeny eyelids are so heavy.

So...heavy....";
	end the game in victory.

The cup of Cheerios is a thing. "It is a little plastic cup full of Cheerios." The description is "Cheerios! In a cup! What a totally portable idea."

To spill the Cheerios:
	say "It is as if the oxygen in the room goes foom like there was a flash fire and suddenly there are all of these babies headed your way[note babies-love-cheerios].";
	remove the cup of Cheerios from play;
	move the pile of Cheerios to the location;
	change (machine corresponding to a baby name of Cassie in the Table of Baby State Machines) to Baby Attraction Rules;
	change (machine corresponding to a baby name of Watson in the Table of Baby State Machines) to Baby Attraction Rules;
	change (machine corresponding to a baby name of Jemison in the Table of Baby State Machines) to Baby Attraction Rules;
	change the base act percentage of Cassie to 100;
	change the act percentage of Cassie to 100;
	change the base act percentage of Jemison to 100;
	change the act percentage of Jemison to 100;
	change the base act percentage of Watson to 100;
	change the act percentage of Watson to 100.

At the time when Parry munches Cheerios:
	say "Lisa suddenly says, 'Parry! Are you eating Cheerios?'

Parry sounds all sheepish, 'Yeah.'

Becca is all 'I hope you don't mind crumbs all over your loveseat, Farina,' and everyone laughs but Parry.";
	move the cup of Cheerios to the north pink striped loveseat.

A height reachability rule when the reaching object is the cup of Cheerios and Climaxtime is happening:
	if the reaching actor is quarter-height-or-higher, continue action processing;
	if the poosh stick is carried by the reaching actor and the current action is pushing the cup of Cheerios with, continue action processing;
	if the reaching actor is the player, say out-of-reach message for the cup of Cheerios;
	stop action processing.

Instead of pushing the cup of Cheerios with the poosh stick:
	say "You reach up with the stick and whack the cup and all of these Cheerios shower down around you in a big pile.[paragraph break]";
	spill the Cheerios.

Instead of pushing or attacking or taking the cup of Cheerios when the player is quarter-height-or-higher:
	if the player is standing, continue the action;
	say "You carefully reach over and bat at the cup and all of these Cheerios shower down onto the floor in a big pile.[paragraph break]";
	spill the Cheerios.

Baby Attraction Rules is a rulebook.

The current actor is a person that varies.

The first baby attraction rule (this is the decide what baby is acting rule):
	change the current actor to a random busy acting playmate.

A baby attraction rule when the current actor is standing (this is the babies let go of stuff to head for the Cheerios rule):
	try Cassie sitting down;
	rule succeeds.

A baby attraction rule when the current actor is carrying something (this is the babies stop carrying things to head for the Cheerios rule):
	if the current actor clears all inventory begin;
		if the current actor is present and some thing is marked for special listing, say "[The current actor] drops [a list of things that are marked for special listing].";
		rule succeeds;
	end if.

A baby attraction rule when the current actor and the pile of Cheerios are not co-located (this is the babies crawl towards Cheerios rule):
	let way be the best route from the location of the current actor to the location of the pile of Cheerios;
	if way is a direction, try the current actor going way;
	rule succeeds.

A baby attraction rule (this is the babies scarf down Cheerios rule):
	if 1 out of 5 and the current actor is present begin;
		if the current actor is Cassie, say "Cassie [takes] [one of]some[or]a handful of[purely at random] Cheerios and [one of]eats them[or]inhales them[or]shoves them in her mouth[purely at random][if 1 out of 4] and grins around the mushy oatiness of them[end if].";
		if the current actor is Watson, say "Watson [one of]gingerly[or]carefully[purely at random] [one of]picks up[or]takes[purely at random] a single Cheerio and [one of]sniffs at it cautiously before finally eating[or]then he eats[purely at random] it.";
		if the current actor is Jemison, say "Jemison [if 1 out of 4]considers the pile of Cheerios and then he [end if][one of]takes[or]picks up[purely at random] [one of]a few[or]some[purely at random] Cheerios and munches on them.";
		rule succeeds;
	end if.

VOLUME 6 - On Directives That Make the Game Pretty or More Entertaining

Chapter 1 - Miscellaneous Options

Use American dialect, the serial comma, and full-length room descriptions.

Release along with cover art, a "Modern-cplay" website, and the source text.

When play begins:
	change the right hand status line to "Score: [score]".

Use undo at death translates as (- Constant DEATH_MENTION_UNDO; -).
Use undo at death.

Chapter 2 - Screen Effects

[Print a chapter heading in the game]
To display chapter head (H - text):
	say "[bold type][H][roman type][line break][line break]".

To display chapter head (H - text) with subhead (SH - text):
	say "[bold type][H][line break][SH][roman type][line break][line break]".

To report bug (B - text):
	say "[bold type][unicode left square bracket]BUG[unicode right square bracket] [B][roman type]".

[Don't print the banner text the first time.]
Banner printing is a one-time deal.

First for printing the banner text rule:
	if banner printing has occurred, continue the action.

Rule for printing the banner text (this is the new extended banner rule):
	say "[bold type][story title][roman type][line break][extended story headline] by [story author][line break]Copyright [unicode copyright sign] [story copyright string] by [story author][line break][story rights statement][line break]Type ABOUT for more information.[run paragraph on][line break]Release [release number] / Serial number [story serial number] / Inform 7 build [I7 version number][line break]" instead.

The new extended banner rule is listed before the extended banner rule in the for printing the banner text rules.

Chapter 3 - Random Commands

[The player may be very old-school despite also being a baby.]

Waxing nostalgic is an action applying to nothing. Understand "xyzzy" or "plugh" or "plover" as waxing nostalgic.

Report waxing nostalgic: say "Supposedly saying [player's command] will do magical things and whatever, you can't talk yet."

[A way of giving comments]
Commenting is an action out of world applying to one topic. Understand "! [text]" as commenting. Carry out commenting: say "Noted."

Chapter 4 - Scoring

The maximum score is 100.

Table of Achievements
Points	Citation
a number	text
with 16 blank rows;

To record (P - a number) points for (T - text):
	if the number of blank rows in the Table of Achievements is less than 1
	begin;
		report bug "There are not enough blank rows in the Table of Achievements.";
	otherwise;
		choose a blank row in the Table of Achievements;
		change the points entry to P;
		change the citation entry to T;
	end if;
	award P points;

Requesting the full score is an action out of world. Understand "full" or "full score" or "fullscore" as requesting the full score.

Carry out requesting the full score: consider the print the full score rule.

This is the print the full score rule:
	say "You have like [the score] points out of [maximum score]";
	if the score is greater than 0
	begin;
		say ". Here is how you got them:[paragraph break]";
		repeat through the Table of Achievements begin; 
			say "     [points entry] for [citation entry][line break]";
		end repeat;
	otherwise;
		say ".";
	end if;

The print the full score rule is listed instead of the print final score rule in the for printing the player's obituary rulebook.

[You can't throw in an "after requesting the score" rule because, for whatever reason, the requesting the score rulebook appears to stop the action after the carry out actions. So instead I replace that rule.]
The first carry out requesting the score rule: say "You have like [the score] points out of [maximum score], in [turn count] turns.[run paragraph on]"

The announce the score rule is not listed in the carry out requesting the score rulebook.

[Another oddity: if I just have the first carry out rule above and the rule immediately following w/o running the paragraph on in both, I get an extra paragraph break when you request your score for the second time.]
Carry out requesting the score for the first time: say " (There's a fuller list of your accomplishments, you can see it by typing FULL SCORE.)[run paragraph on]"

The last carry out requesting the score rule: say line break.

Chapter 5 - Commentary Track

Include Footnotes by Stephen Granade.

[Instead of re-writing the game-ending restore-restart-undo-quit code to shove in mention of the commentary track, mention it in the AMUSING text.]

Rule for amusing a victorious player: say "If you'd like more information about the game and its design, you can play through the game with the commentary track on. At any time in the game, type COMMENTARY ON to turn on the commentary track."

Understand "commentary" as giving the footnote state. Understand "commentary on" as turning on footnotes. Understand "commentary off" as turning off footnotes.

The first report turning on footnotes rule: instead say "Commentary track on[note commentary-intro]. To read commentary track notes, type NOTE #."

The first report turning off footnotes rule: instead say "Commentary track off."

The first report giving the footnote state rule: instead say "The commentary track is currently [if footnotes are on]playing[otherwise]off[end if]. You can turn the commentary track [if footnotes are on]off[otherwise]on[end if] using the command COMMENTARY [if footnotes are on]OFF[otherwise]ON[end if]."

Table of Footnotes (continued)
Name	Note	Index	Read
commentary-intro	"Welcome to the commentary track of Child's Play! I'm your author and host, Stephen Granade. In this track, I'll be talking about design decisions I made, some of my goals in writing the game, and fun things to try."
baby-desc	"The player description doesn't mention the player's diaper or other clothes that are presumably being worn, though the diaper does exist, and is searchable."
pulling-up	"The major conceit of the game is that the player is a baby, and must interact with the world as a baby. That leaves the player with a limited range of actions. Sadly, unless you've become a parent recently, you've probably forgotten what it's like to be a baby and what a baby does. That's why the game has to re-teach you babies['] limited actions, like pulling up[note wait-on-mom]."
crying-in-darkness	"Crying is a great baby way of manipulating parents."
the-mom	"The protagonist doesn't really think of the parents in a possessive way. They're just people who provide food and toys."
wait-on-mom	"By the way, you should wait for a while and frustrate the mom."
relative-sizes	"A challenge in showing the world through a baby's eyes is portraying how big everything is in relation to you. As Dan Shiovitz pointed out, I needed to emphasize how you're looking up at the world most of the time."
pushing-footstool	"Babies are notorious for trying to pull up on things and instead accidentally pushing them or knocking them over. This initial footstool puzzle serves to introduce the push-on-things mechanic in a setting where there's little else to do."
disappointments	"Sometimes it seems that kids exist to prove their parents don't know what they're talking about."
baby-containment	"One of the fun things about parenthood is figuring out how to contain your kids so you can run off and do chores or other necessary things like World of Warcraft."
hhgttg	"Later your aunt will give you a thing you don't know what it is."
others-do-your-bidding	"A theme of the game is the PC getting others, both animate and inanimate, to do his or her bidding."
no-aiwa	"Clearly the parents haven't bought stereo components since the 1990s."
kid-hint-giving	"To help teach the player how to act like a baby, I have the other playmates in the playgroup model ideal baby behavior."
drink-mimosas	"While the playmates['] parents aren't based on specific people, the bit about mimosas is based on an actual playgroup Misty went to."
jemison-fascinated	"Babies are all the time becoming fascinated by what other babies do. Stupid babies."
inventory-limit	"Babies have a built-in inventory limit. I, of course, use this for puzzles."
chewing-stuff	"Babies are all the time chewing on things. They also -- and I swear this is true -- chew on things to get around their inventory limit."
red-haired girl	"I'm pretty sure that, when she's older, Zoe is the little red-haired girl Charlie Brown is in love with. I'm also convinced that Zoe isn't really all that bad."
butt-paste	"http://www.buttpaste.com/. Ask for it by name!"
stuck-onesie	"Babies have this unerring sense of what they shouldn't be playing with and a nearly-unstoppable drive to mess with things they shouldn't be touching."
take-all-from-backpack	"Admiral Jota is a madman and managed to break the backpack puzzle by the judicious use of TAKE ALL, requiring me to fiddle the rules regarding what's included in ALL."
antagonist	"I like games with an antagonist. Wandering around exploring is fine, but it's nice to be working against someone. With that said, I wanted Zoe to start out passively working against you, in effect pursuing her own separate agenda, only working directly against you as the game went on."
parent-convo	"All of the parents['] conversations are exaggerated versions of conversations and arguments that parents really have."
parry-opens-backpack	"Babies are masters at destroying the flow of your actions and plans. Eventually you learn how to break your plans up into tiny chunks separated by what you have to do for your kid."
purpose-of-interludes	"Interludes like this one help delineate the three-act structure and give clues to later behavior that you'll need to exhibit as the player."
coaster-falls	"Several beta-testers had issues with this puzzle, and weren't sure what to do. Dan Shiovitz suggested adding something that would demonstrate what you were trying to do without accidentally solving the puzzle for you."
squeeze-behind-footstool	"Having seen a baby cruise her way into a space she couldn't have gotten into, I knew I wanted to use it as a puzzle."
parry-entering	"Parry's the foil to the rest of the group. He's an outsider and can thus make comments that are probably closer to your reactions to the groups['] comments. Plus he does things the more experienced moms don't."
jemison-in-Parrys-lap	"This is the first example of the ongoing theme of using other people to do your bidding (assuming Alfadog doesn't count)."
ruined-tumbler	"Poor Parry. It's like he's never been around babies before[note spoiled-by-watson]. He's fallen prey to the common misconception that, if you keep pushing babies away from something they're interested in, eventually they'll grow bored and wander off."
spoiled-by-watson	"More likely, it's that Watson's so low-key that Parry is lax in his baby defense."
Cassie-falls-down	"Take that, overactive baby."
baby-cancels-baby	"It takes a baby to stop a baby, clearly. Since Zoe has upped the ante by blocking your actions directly, you get to stop her by using Jemison."
Zoe-pushes-door	"Zoe is now actively thwarting your plans, raising the stakes."
those-annoying-blocks	"Man, these blocks. They seemed like such a good idea at the time. I wanted a way to get you off the ground and give you access to higher-up things. In this game, which has a new and brittle world model, that's like giving you a rope that burns things. I didn't think through all of the ramifications of the blocks -- as Sam Ashwell pointed out, logically you should be able to climb onto the blocks and then onto furniture. I dealt with access cases in a piecemeal fashion before finally grafting on a more systematic model of heights."
Jemisons-bells	"I get tired of games where you're introduced to a certain game mechanic that you then never use again, so I deliberately re-used mechanics like Jemison's fascination with stuff throughout."
tumbler-falls	"Poor Parry. He's responsible for you getting the toy once again."
stupid-costumes	"So much of baby life consists of being dressed by your parents in uncomfortable or embarrassing outfits."
squid-costume	"You know what's more fun in IF than only being able to carry a few things? Being able to carry [italic type]nothing[roman type]."
spitting-up	"Isn't it nice that the solution to your problem makes more work for the mom?"
parry-knocks-the-toy	"Since Parry had helped you accidentally in the first two sections, I wanted him to help you directly in the third. Plus I figured he'd be on the side of truth and justice -- yours."
believable-baby-action	"Another theme of the game is you getting around adults['] attempts to control you in ways that you would (mostly) believe a baby could do. The results are all attributable to random baby actions, so that the adults have no idea it's all a part of your scheming."
Zoe-cooperates	"I like you having to team up with your antagonist, even if only for a little while. Though it's not a perfect arrangement: try pushing the oval with the poosh stick on the side where Zoe is."
babies-love-cheerios	"True story: at my son's first birthday party, he knocked over a small cup of Cheerios, spilling them near my feet. Instantly, every baby at the party crawled towards us, swarming around me.

Cheerios are baby crack."
baby-state-machines	"I modeled the babies as state machines, which they essentially are in real life, and implemented the state machines using rulebooks, with one master daemon to trigger the rulebooks. That turned out to be an excellent design decision, as it allowed me to swap rules in and out on the fly, share sets of behavior among babies, and slot in some hint-giving rules like this one."
meta-joke	"Some day I will get tired of meta-jokes about interactive fiction, but clearly today is not that day."
fiddled-scope	"Lucian Smith suggested that you be able to examine things in other 'rooms' within the large living room. It was an extremely handy addition to the game, since then I could see where things were and go to them. Shame it was such a headache to implement. I managed to get it working for this small game, but I'd hate to try it for a larger game."
introcomp	"I entered this first part of the game in Introcomp 2006. The competition helped, as it really made me think about what I wanted the player to see in the first few moves. I wanted to have a few simple puzzles that nevertheless depended on baby-like actions, introduce the writing style I'd chosen, and have a very clear direction. The last was critical -- without that throughline, players are increasingly likely to type QUIT."
instruction-book	"Originally, when you pushed the blox bucket over, the instruction book stayed inside. You had to look into the bucket to find it, remove it from the bucket, drop it, and then open it. About my third time playing through the section I realized how totally un-fun that was and changed it so the booklet comes falling out."
favorite-conversation	"This is my favorite of all of the parents['] conversations, mainly for this line right here."
mason-jar	"Perhaps a Mason jar. Ha! I crack myself up."
mom-telepathy	"As best I can tell, mom telepathy comes from moms having had a large shared pool of experiences when it comes to babies."
collated-reports	"Many turns there's a heck of a lot of text printed as parents talk and playmates romp around. To cut down on how many paragraphs are printed, I collated the descriptions of the playmates into a single paragraph."
randomness-sux	"I randomized the babies['] behavior and action descriptions to keep things fresh. It also made debugging a nightmare. NPCs suck; randomized NPCs suck more."
reacting-to-others	"It's nice how the NPCs occasionally react to what you or other NPCs do."
stranding-zoe	"So, funny story. My beta-testers pounded on the bit where Zoe cooperates with you to get the toy. They did all kinds of things to trip her up. When they finished and I'd fixed the bugs they found, I released the game.

It turned out you could push the climbing blocks away from Zoe while she was cooperating with you and she'd be perfectly happy. Of course, once the playpen was open, she was then stranded, letting you get the toy easily. Both Jonathan Blask and Emily Boegheim told me about the problem."

Chapter 6 - Menu System

[Rather than using the Basic Help Menu extension, I ganked the code from that extension and re-wrote everything.]

Include Menus by Emily Short.

To say bold story title: say "[bold type][story title][roman type]".

Table of Basic Help Options
title	subtable	description	toggle
"About [story title]"	a table-name	"[bold story title][paragraph break][story description]"	a rule
"How to Play"	--	"There is a good set of instructions up at http://brasslantern.org/players/howto/beginnersguide.html that will help you learn how to play, you could try there. But if you are getting stuck then there are two things you can do before you go looking for a walkthrough. One is you can go back through the game and read carefully, there are clues in the text and especially  in the things people say. The other is you should watch other babies, they may give you ideas."
"Special Commands"	--	"There is really only one special command, it is COMMENTARY ON and it will turn on the special commentary track. But it has spoilers so be careful!"
"Credits"	--	"[story title] was made using Inform 7, it is by Graham Nelson and it is so easy that even a baby can make a game with it. And the manual has all these great examples that I stole, so thanks Graham and Emily Short.

Dan Shiovitz and Lucian Smith tested this thing from start to finish a zillion times while I was writing it and they did a lot to make it super awesome. Along with them Admiral Jota and Sam Ashwell and Adam Thornton did final beta-testing, they gave great input too and you should totally pester them to test your games.

It turns out the game had bugs even after I released it and people told me about those bugs, people like Admiral Jota and Wendymoon and Jason Brown and Katzy and Jonathan Blask and Emily Boegheim. The ClubFloyd playthrough on January 6 2008 also turned up a bunch of stuff so thanks everyone who took part in that.

People also tested the Introcomp version of this and they are Dan Shiovitz and Sean Barrett and Lucian Smith and Admiral Jota."
"Tiny Print"	--	"[bold type]Copyright Information[roman type][line break]
[bold story title] is copyright [unicode copyright sign] [story copyright string] by [story author]. [story rights statement]

[bold type]Licensing Information[roman type][line break]
I place the following terms and conditions on copying and distributing [bold story title]: You may copy and distribute [bold story title] in whatever format you choose, as long as it (and 
especially its copyright information) is not altered. You may not distribute this game for a fee which is greater than the cost of distribution. Distribution of this game constitutes acceptance of this license.

[bold type]A Legal Dodge[roman type][line break]
Because this game is offered free of charge, there is no warranty, express or implied, attached to it."	--
"Getting Ahold of the Author"	--	"Comments are neat, you can send them to stephen@granades.com."	--

Understand "help" or "hint" or "hints" or "about" or "info" as asking for help.
Asking for help is an action out of world.
Carry out asking for help (this is the help request rule):
	change the current menu to the Table of Basic Help Options;
	carry out the displaying activity;
	clear the screen;
	try looking.

Chapter 7 - Re-written Messages

Include Modified Again Default Messages by David Fisher.

Table of custom library messages (continued)
Message ID	Message Text
LibMsg <unimportant object>	"[Cap that-those of the main object] are totally unimportant and you don't need to refer to them in this game."
LibMsg <empty line>	"You stare off into space in that timeless baby way that makes you look all smart or constipated or something.[paragraph break]"
LibMsg <oops no arguments>	"No problem or anything.[paragraph break]"
LibMsg <cannot do again>	"You totally can't repeat that.[paragraph break]"
LibMsg <command incomplete>	"You didn't say enough or anything![paragraph break]"
LibMsg <object not held>	"You totally aren't holding that![paragraph break]"
LibMsg <verb cannot have inanimate object>	"You can only do that to something that is all alive.[line break]"
LibMsg <which disambiguation>	"So which do you mean, "
LibMsg <single object disambiguation>	"Sorry but you can only use one item at a time here so which exactly?[paragraph break]"
LibMsg <cannot take possessions of others>	"[The main object] has a grip that is like steel.[paragraph break]"
LibMsg <cannot take scenery>	"[Cap that-those of the main object] [is-are] so not takeable.[paragraph break]"
LibMsg <cannot remove from closed containers>	"[Cap it-they of the main object] [is-are] so closed.[paragraph break]"
LibMsg <cannot insert into something not a container>	"[Cap that-those of the main object] so can't contain stuff.[paragraph break]"
LibMsg <cannot put something not held>	"To put [it-them of the main object] on something else you so need to be holding [the main object].[paragraph break]"
LibMsg <cannot put onto something being carried>	"Your coordination is really bad, you can't do that.[paragraph break]"
LibMsg <need to empty into a container>	"[The main object] so can't hold stuff.[paragraph break]"
LibMsg <empty something into itself>	"That like does nothing.[paragraph break]"
LibMsg <cannot give to yourself>	"You already have [the main object] so it is like you're already done.[paragraph break]"
LibMsg <block showing>	"[The main object] [is-are] so unimpressed.[paragraph break]"
LibMsg <cannot enter something carried>	"You so can't get into something you're holding.[paragraph break]"
LibMsg <cannot go that way>	"You so can't go that way.[line break]"
LibMsg <cannot search unless container or supporter>	"You don't find anything.[paragraph break]"
LibMsg <look under>	"You find nothing.[paragraph break]"
[LibMsg <report player touching self>	"That doesn't help.[paragraph break]"
LibMsg <report player touching other people>	"Do not be all touching everyone else![paragraph break]"]
LibMsg <top line what in>	" ([if main object is a person]held by[otherwise]in[end if] [the main object])"
LibMsg <block pushing in directions>	"You would but that ends up totally not working.[paragraph break]"

Report an actor touching (this is the CP report touching yourself rule):
	if the noun is the actor begin;
		if the actor is the player, say "That doesn't help.";
		otherwise say "[The actor] touches [if the actor is female]her[otherwise]him[end if]self.";
		stop the action;
	end if;
	continue the action.
The CP report touching yourself rule is listed before the report touching yourself rule in the report touching rulebook. The report touching yourself rule is not listed in the report touching rulebook.

Report an actor touching (this is the CP report touching other people rule):
	if the noun is a person begin;
		if the actor is the player, say "Do not be all touching everyone else!";
		otherwise say "[The actor] touches [if the noun is the player]you[otherwise][the noun][end if].";
		stop the action;
	end if;
	continue the action.
The CP report touching other people rule is listed before the report touching other people rule in the report touching rulebook. The report touching other people rule is not listed in the report touching rulebook.


Volume 7 - On Things That Are Not For General Consumption - Not for release

Chapter 0 - Debugging Actions and Rules

[Let me command other babies while I'm testing.]
The can't command others rule is not listed in any rulebook. Persuasion rule for asking people to try doing something: persuasion succeeds.

Orderizing is an action out of world. Understand "orderize" as orderizing. Carry out orderizing: seed the random-number generator with 31337.

Summoning is an action applying to one visible thing. Understand "summon [any thing]" as summoning. Carry out summoning: now the noun is in the location. Report summoning: say "[The noun] appears."

Passivating is an action applying to one visible thing. Understand "passivate [any person]" as passivating. Carry out passivating: now the noun is passive.

Passivating-all is an action applying to nothing. Understand "passiv-all" as passivating-all. Carry out passivating-all: now all playmates are passive.

Activating is an action applying to one visible thing. Understand "activate [any person]" as activating. Carry out activating: now the noun is active.

Activating-all is an action applying to nothing. Understand "activ-all" as activating-all. Carry out activating-all: now all playmates are active.

Burden-testing is an action applying to one visible thing. Understand "burden test [any person]" as burden-testing. Carry out burden-testing: say "Burdened? [if the noun is burdened]Yes![otherwise]No![end if]Unburdened? [if the noun is unburdened]Yes![otherwise]No![end if]Overburdened? [if the noun is overburdened]Yes![otherwise]No![end if]".

Fascination-testing is an action applying to one visible thing. Understand "fascinate test [any person]" as fascination-testing. Carry out fascination-testing: try the noun going west.

Present-testing is an action applying to one visible thing. Understand "present test [any person]" as present-testing. Carry out present-testing: say "[The noun] is [if the noun is present]totally[otherwise]not[end if] present."

Height-testing is an action applying to one visible thing. Understand "height test [any thing]" as height-testing. Carry out height-testing: say "[The noun] is - "; if the noun is zeroth-height, say "zeroth height - "; if the noun is eighth-height, say "eighth height - "; if the noun is quarter-height, say "quarter height - "; if the noun is half-height, say "half height - "; if the noun is eighth-height-or-higher, say "eighth height or higher - "; if the noun is quarter-height-or-higher, say "quarter height or higher - "; if the noun is half-height-or-higher, say "half height or higher - "; say paragraph break.

Deleting is an action applying to one visible thing. Understand "delete [any thing]" as deleting. Carry out deleting: remove the noun from play. Report deleting: say "You make [the noun] vanish."

[Get around the odd scoping that I'm doing.]
A procedural rule when summoning or passivating or activating or burden-testing or fascination-testing or present-testing or height-testing: ignore the babies have stubby little arms rule; ignore the check-heights rule.

Pinging is an action applying to nothing. Understand "ping" as pinging.
Carry out pinging:
	repeat with toddler running through the playmates begin;
		say "[The toddler] is ";
		if the toddler is active, say "active and "; otherwise say "passive and ";
		if the toddler is pulled up on something (called the support), say "pulled up on [the support] ";
		if the toddler is gnawing something (called the mastication), say "chewing on [the mastication] ";
		if the toddler is carrying something, say "holding [a list of things carried by the toddler] ";
		if the toddler is fixated, say "fascinated by [focus of toddler] ";
		say "in [location of the toddler]. [run paragraph on]";
	end repeat;
	say paragraph break.

Chapter 1 - Full Tests

[Strictly speaking, the Test actions aren't included in a release version, but I list them here to keep things tidy.]

Test me with "test intro / test playtime / test tabletime / test snacktime / test couchtime".

Chapter 2 - Scene Tests

Test intro with "orderize / z / l / z / z / x mom / z / z / z / z / z / z / x footstool / pull up on footstool / x footstool / l / d / push footstool / x alfadog / x alfaswitch / get alfadog / turn on alfadog / e / x alfadog / x toy / s / get toy".

Test tabletime with "push spoon / eat spoon / z / z / z / z / eat cheerios / get cheerios / z".

Test snacktime with "s / get balls / n / put balls on zoe / ne / push short block sw / nw / w / s / x tumbler / get tumbler / get balls / open cabinet / drop balls / open cabinet / get balls / put balls on zoe / open cabinet / n / e / s / s / x paper / w / pull up on couch / w / get paper / d / pull paper / drop all / e / pull up on footstool / e / x tumbler / eat tumbler / n / push block e / sw / z / z / eat tumbler / drink tumbler / chew tumbler / eat tumbler / drink tumbler / chew tumbler / n / n / w / s / open cabinet"

Test couchtime with "d / remove costume / spit up / z / z / z / z / z / z".

[Split picturetime test so we can go partway through]

Test pt-1 with "nw / spit up / pull up on parry / get remote / give remote to parry / e / e / z / climb short / z / z / z / climb medium / climb tall / turn switch to hi / cry / g / g / g / z / z / get toy / g / g / z / sw / s / push bucket".

Test pt-2 with "open booklet / turn page / g / g / g / z / z / z / z / z / z / take stick / n / push oval with stick / z / z / z / push oval with stick".

Test pt-3 with "ne / drop stick / get toy / get stick / w / w / push cheerios with stick".

Test picturetime with "test pt-1 / test pt-2".

Section 1 (for Z-Machine only)

Test playtime with "nw / get rock / pull up on chair / drop rock / pull up on chair / l / d / chew rock / sw / show rock to jemison / s / ne / n / pull up on chair / spit out rock / z / z / open backpack / get onesie / get cup / show cup to zoe / drop all / pull up on chair / get toy"

Section 2 (for Glulx only)

Test playtime with "nw / get rock / pull up on chair / drop rock / pull up on chair / l / d / chew rock / sw / show rock to jemison / s / ne / n / pull up on chair / spit out rock / z / z / open backpack / get onesie / get kangaroo / show kangaroo to zoe / drop all / pull up on chair / get toy"

Chapter 3 - Action Tests

Test chewing with "cry / get squishy ball / x ball / i / chew ball / x ball / i / relations / chew book / stop chewing ball / x ball / i / x ball / relations".
Test pullup with "cry / l / get favorite toy / pull up on footstool / l / get favorite toy / d".
Test cruising with "test intro / s / pull up on couch / relations / w / relations / w / relations / e / relations / d / relations".
Test burdening with "test intro / passiv-all / summon rolly car / summon squishy ball / summon rock / summon plush book / burden test me / get ball / burden test me / get rock / burden test me / get car / chew rock / burden test me / get car / burden test me / drop car / drop ball / burden test me / get book / burden test me / spit out rock / burden test me / drop book".
Test npc-setup with "test intro / s / gonear east couch / passiv-all / summon cassie / summon jemison / summon squishy ball / summon plush book / summon smooth rock / cassie, sit".
Test following with "test npc-setup / cassie, w / follow cassie / follow cassie / follow me / w / follow cassie".
Test other-following with "test npc-setup / cassie, w / jemison, follow cassie / w".
Test other-chewing with "test npc-setup / cassie, chew couch / cassie, chew ball / x cassie / l / relations / cassie, chew book / cassie, stop chewing ball / cassie, chew book / x cassie / cassie, drop book / relations / l".
Test other-cruising with "test npc-setup / cassie, pull up on couch / relations / cassie, d / relations / cassie, pull up on couch / relations / cassie, w / relations / w / cassie, n / relations / n".
Test fascination with "test npc-setup / cassie, take ball / cassie, n / n / w / fascinate test cassie / l / cassie, drop ball / jemison, drop ball / take ball / l / x jemison / e / x jemison / drop ball".
Test block-climbing with "test intro / summon short block / summon medium block / summon tall block / pull up on short block / d / climb short / climb medium / d / climb medium / climb short / climb medium / climb tall / d / climb tall / climb short / climb tall / d / climb short / u / u".

